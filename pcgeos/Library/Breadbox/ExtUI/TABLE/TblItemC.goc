@include <StdApp.goh>
@include "Table/TblItemC.goh"
@include "ExtUI/Table.goh"


/**************************************************************************
*       Class Declaration
**************************************************************************/

@classdecl TableItemClass ;

/*-----------------------------------------------------------------------*/

@method TableItemClass, MSG_META_START_SELECT {

    Rectangle rect ;

    if(!@call oself::MSG_GEN_GET_ENABLED()) {
    
       @callsuper() ;
       
       return ; 
    }

    retVal->flags = 0 ;

    @call oself::MSG_VIS_GET_BOUNDS(&rect) ;

    @call @genParent::
            MSG_EXTUI_TABLE_LINE_START_SELECT(
                                pself->GII_identifier,
                                retVal, xPosition - rect.R_left, yPosition - rect.R_top,
                                inputState) ;
                                
    if(!(retVal->flags & MRF_PROCESSED)) {

        @callsuper() ;
    }
}

/*-----------------------------------------------------------------------*/

@method TableItemClass, MSG_GEN_ITEM_SET_IDENTIFIER {

    @callsuper() ;

    @send @genParent::
            MSG_EXTUI_TABLE_LINE_SET_IDENTIFIER(identifier) ;
}

/*-----------------------------------------------------------------------*/

@method TableItemClass, MSG_META_LOST_FOCUS_EXCL {

    @callsuper() ;

    @send oself::MSG_VIS_REDRAW_ENTIRE_OBJECT() ;
}

/*-----------------------------------------------------------------------*/

@method TableItemClass, MSG_TABLE_ITEM_SET_ATTRS {

    pself->TII_attrs |= set ;
    pself->TII_attrs &= (~clear) ;
}

/*-----------------------------------------------------------------------*/

@method TableItemClass, MSG_TABLE_ITEM_GET_ATTRS {
    
    return(pself->TII_attrs) ;
}


/*************************************************************************/
