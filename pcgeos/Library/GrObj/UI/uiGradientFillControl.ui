/*
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	PC GEOS 
 * MODULE:	
 * FILE:	uiGradientFillControl.ui
 *
 * AUTHOR:	Jon Witort
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	jon	10 jun 1992	initial perversion
 *
 * DESCRIPTION:
 *	UI description for GrObjGradientFillControl stuff
 *
 * 	$Id: uiGradientFillControl.ui,v 1.1 97/04/04 18:06:08 newdeal Exp $
 *
 */

#include "Art/mkrGradients.ui"

start	GrObjGradientFillControlUI, notDetachable;

GrObjGradientTypeList = GenItemGroup {
	genStates = default -usable;
	behaviorType = exclusive;
	children =
		NoGradientItem,
		HorizontalGradientItem,
		VerticalGradientItem,
		RadialRectGradientItem,
		RadialEllipseGradientItem;
	applyMsg = MSG_GOGFC_SET_GRADIENT_TYPE;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_ITEM_GROUP_TOOLBOX_STYLE,
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

NoGradientItem = GenItem {
	moniker = " No Gradient ";
	identifier = GOGT_NONE;
	hints = {
		HINT_CENTER_MONIKER,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_GADGET_BACKGROUND_COLORS {
			byte 0xea, 0xea, 0xea, 0xea
		}
	}
}

HorizontalGradientItem = GenItem {
	moniker = list {
		HorizontalGradientColorMoniker,
		HorizontalGradientMonoMoniker
	}
	identifier = GOGT_LEFT_TO_RIGHT;
}

VerticalGradientItem = GenItem {
	moniker = list {
		VerticalGradientColorMoniker,
		VerticalGradientMonoMoniker
	}
	identifier = GOGT_TOP_TO_BOTTOM;
}

RadialRectGradientItem = GenItem {
	moniker = list {
		RadialRectGradientColorMoniker,
		RadialRectGradientMonoMoniker
	}
	identifier = GOGT_RADIAL_RECT;
}

RadialEllipseGradientItem = GenItem {
	moniker = list {
		RadialEllipseGradientColorMoniker,
		RadialEllipseGradientMonoMoniker
	}
	identifier = GOGT_RADIAL_ELLIPSE;
}

GrObjNumGradientIntervalsValue = GenValue {
	genStates = default -usable;
	moniker = 'N', "Number of Intervals";
	value = "DEFAULT_NUMBER_OF_GRADIENT_INTERVALS";
	minimum = 1;
	maximum = 255;
	applyMsg = MSG_GOGFC_SET_NUMBER_OF_GRADIENT_INTERVALS;
	destination = "TO_OBJ_BLOCK_OUTPUT";
}

end	GrObjGradientFillControlUI;

start GrObjControlUIStrings, data;

chunk GOGFCName = "Object Gradient Fill";
chunk HorizontalGradientName = "Horizontal Gradient Fill";
chunk VerticalGradientName = "Vertical Gradient Fill";
chunk RadialRectGradientName = "Radial Rectangle Gradient Fill";
chunk RadialEllipseGradientName = "Radial Ellipse Gradient Fill";
chunk NumGradientIntervalsName = "Number of Gradient Intervals";

end GrObjControlUIStrings;
