/*
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	PC GEOS 
 * MODULE:	
 * FILE:	uiRotateControl.ui
 *
 * AUTHOR:	Jon Witort
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	jon	24 feb 1992	initial perversion
 *
 * DESCRIPTION:
 *	UI description for GrObjSkewControl stuff
 *
 * 	$Id: uiSkewControl.ui,v 1.1 97/04/04 18:06:02 newdeal Exp $
 *
 */

#include "Art/mkrSkew.ui"

start	GrObjSkewControlUI, notDetachable;

GrObjSkewLeftTrigger = GenTrigger {
	genStates = default -usable;
	moniker = MonikerSkewLeft;
	action = MSG_GOSC_SKEW_HORIZONTALLY, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	0
			word	45
		}
	}
}

visMoniker MonikerSkewLeft = data {
#ifdef DO_DBCS
	byte	16
	wchar	"Skew 45", C_DEGREE_SIGN, " to the Left", 0;
#else
	char	16, "Skew 45", C_DEGREE, " to the Left", 0;
#endif
}

GrObjSkewRightTrigger = GenTrigger {
	genStates = default -usable;
	moniker = MonikerSkewRight;
	action = MSG_GOSC_SKEW_HORIZONTALLY, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	0
			word	-45
		}
	}
}

visMoniker MonikerSkewRight = data {
#ifdef DO_DBCS
	byte	16
	wchar	"Skew 45", C_DEGREE_SIGN, " to the Right", 0;
#else
	char	16, "Skew 45", C_DEGREE, " to the Right", 0;
#endif
}

GrObjSkewUpTrigger = GenTrigger {
	genStates = default -usable;
	moniker = MonikerSkewUp;
	action = MSG_GOSC_SKEW_VERTICALLY, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	0
			word	45
		}
	}
}

visMoniker MonikerSkewUp = data {
#ifdef DO_DBCS
	byte	9
	wchar	"Skew 45", C_DEGREE_SIGN, " Upwards", 0;
#else
	char	9, "Skew 45", C_DEGREE, " Upwards", 0;
#endif
}

GrObjSkewDownTrigger = GenTrigger {
	genStates = default -usable;
	moniker = MonikerSkewDown;
	action = MSG_GOSC_SKEW_VERTICALLY, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	0
			word	-45
		}
	}
}

visMoniker MonikerSkewDown = data {
#ifdef DO_DBCS
	byte	9
	wchar	"Skew 45", C_DEGREE_SIGN, " Downwards", 0;
#else
	char	9, "Skew 45", C_DEGREE, " Downwards", 0;
#endif
}

GrObjCustomSkewInteraction = GenInteraction {
	genStates = default -usable;
	moniker = 'C', "Custom Skew";
	children =	GrObjCustomSkewValueGroup,
			GrObjCustomSkewReplyApply;
	type = command;
	visibility = dialog;
	hints = {
			HINT_CENTER_CHILDREN_HORIZONTALLY,
			ATTR_GEN_HELP_CONTEXT {
				char "dbCustomSkew", 0
			}
	}
}

GrObjCustomSkewValueGroup = GenInteraction {
	children =	GrObjCustomSkewHMonikerAndValue,
			GrObjCustomSkewVMonikerAndValue;
	hints = {
			HINT_ORIENT_CHILDREN_VERTICALLY,
			HINT_RIGHT_JUSTIFY_CHILDREN
	}
}

GrObjCustomSkewReplyApply = GenTrigger {
	moniker = "Skew";
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {word IC_APPLY}
	}
	action = MSG_GOSC_CUSTOM_SKEW, "TO_OBJ_BLOCK_OUTPUT";

}
		   
GrObjCustomSkewHMonikerAndValue = GenInteraction {
	children = GrObjCustomSkewHMoniker, GrObjCustomSkewHValue;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_VERTICALLY
	}
}

GrObjCustomSkewHMoniker = GenGlyph {
	moniker = list {
		SkewHCMoniker,
		SkewHMMoniker
	}
}

GrObjCustomSkewHValue = GenValue {
	moniker = "Degrees";
	minimum = -89;
	maximum = 89;
	increment = 1;
	displayFormat = integer;
	hints = {
		HINT_PLACE_MONIKER_TO_RIGHT
	}
}

GrObjCustomSkewVMonikerAndValue = GenInteraction {
	children = GrObjCustomSkewVMoniker, GrObjCustomSkewVValue;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_VERTICALLY
	}
}

GrObjCustomSkewVMoniker = GenGlyph {
	moniker = list {
		SkewVCMoniker,
		SkewVMMoniker
	}
}

GrObjCustomSkewVValue = GenValue {
	moniker = "Degrees";
	minimum = -89;
	maximum = 89;
	increment = 1;
	displayFormat = integer;
	hints = {
		HINT_PLACE_MONIKER_TO_RIGHT
	}
}

end	GrObjSkewControlUI;

start GrObjControlUIStrings, data;

chunk GrObjSkewControlName = "Skew";
chunk GrObjSkewControlLeftName = "45 Degrees Left";
chunk GrObjSkewControlRightName = "45 Degrees Right";
chunk GrObjSkewControlUpName = "45 Degrees Up";
chunk GrObjSkewControlDownName = "45 Degrees Down";
chunk GrObjSkewControlCustomName = "Custom Skew";

end GrObjControlUIStrings;
