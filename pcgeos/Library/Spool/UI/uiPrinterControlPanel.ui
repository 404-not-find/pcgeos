/*****************************************************************************
 * Project:	PC GEOS
 * Module:	Spool
 * File:	UI/uiPrinterControlPanel.ui
 *
 * Author:	Steve Scholl
 *
 *
 *	$Id: uiPrinterControlPanel.ui,v 1.1 97/04/07 11:10:21 newdeal Exp $
 *
 *****************************************************************************/

#if	_CONTROL_PANEL
start		SpoolAppUI;

PrinterControlPanel	= GenInteraction {
	moniker = PanelTitle;
	children = 
		PrinterListGroup,
		JobList,
		FrontJobTrigger,
		BackJobTrigger,
		CancelJobTrigger,
		CloseTrigger;
	type = multipleResponse;
	visibility = dialog;
	attributes = default +sysModal;
	hints = {
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		ATTR_GEN_HELP_TYPE {
			byte HT_SYSTEM_MODAL_HELP
		},
		ATTR_GEN_HELP_CONTEXT {
			char	"dbPrinterCP", 0
		}
	}
}

#if	_NDO2000
visMoniker	PanelTitle	= "Printer Control Panel";
#else
visMoniker	PanelTitle	= "Printer Status";
#endif
visMoniker	PanelTitleShort	= "Printer Status";

end		SpoolAppUI;
#endif


#if	_CONTROL_PANEL
start		PrinterControlPanelUI;

/*************************************
 * Control Panel Printer List
 *************************************/

PrinterListGroup = GenInteraction {
	moniker = "Printer:";
	children =
		PrinterList;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

PrinterList = GenDynamicList {
	queryMsg = MSG_SPOOL_PANEL_REQUEST_PRINTER_MONIKER;
	destination = process;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ITEM_GROUP_MINIMIZE_SIZE,
		HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,
		HINT_FIXED_SIZE {
			SpecWidth   <>
			SpecHeight  <SST_LINES_OF_TEXT, 1>
			word	1
		},
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word	MSG_SPOOL_PANEL_SHOW_PRINTER_QUEUE
		}
	}
}


#if 0
PrinterTextInteraction = GenInteraction {
	children =
		PrinterTextDisplay,
		NextPrinterTrigger;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

PrinterTextDisplay = GenText {
	genAttributes = readOnly;
	text = printerText;
	textDisplayAttributes = default +singleLineText;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

chunk printerText = "";

NextPrinterTrigger = GenTrigger {
	moniker = "Other Printer(s)";
	action = MSG_SPOOL_PANEL_NEXT_PRINTER,process;
}
#endif


/*************************************
 * Control Panel Jobs List
 *************************************/

JobList = GenItemGroup {
	moniker = "";
	applyMsg = MSG_SPOOL_PANEL_JOB_LIST;
	destination = process;
	hints = {
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_PLACE_MONIKER_ABOVE,
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_FIXED_SIZE {
			SpecWidth  <SST_AVG_CHAR_WIDTHS, 60>
			SpecHeight <SST_LINES_OF_TEXT, 3>
			word	3
		}
	}
}


/*************************************
 * Control Panel Reply Bar
 *************************************/

FrontJobTrigger = GenTrigger {
	moniker = "Make Next";
	action = MSG_SPOOL_PANEL_MAKE_JOB_FIRST, process;
	hints = { HINT_SEEK_REPLY_BAR }
}

BackJobTrigger = GenTrigger {
	moniker = "Make Last";
	action = MSG_SPOOL_PANEL_MAKE_JOB_LAST, process;
	hints = { HINT_SEEK_REPLY_BAR }
}

CancelJobTrigger = GenTrigger {
 	moniker = "Cancel Print Job";
	action = MSG_SPOOL_PANEL_CANCEL_QUEUED_JOB, process;
	hints = { HINT_SEEK_REPLY_BAR }
}

CloseTrigger = GenTrigger {
	CLOSE_TRIGGER_VALUES
	genAttributes = signalInteractionComplete;
	action = MSG_SPOOL_PANEL_CLOSE,process;
	hints = { HINT_SEEK_REPLY_BAR }
}


/*************************************
 * Print From File dialog box
 *************************************/

end		PrinterControlPanelUI;
#else
start		PrinterControlPanelUI;
end		PrinterControlPanelUI;
#endif
