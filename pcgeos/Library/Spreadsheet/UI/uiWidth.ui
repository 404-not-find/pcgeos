/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	PCGEOS
 * MODULE:	uiWidth.ui
 * FILE:	uiWidth.ui
 *
 * AUTHOR:  	Gene Anderson: Jul  6, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	7/ 6/92	  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI for Column Width / Row Height controller
 *
 * 	$Id: uiWidth.ui,v 1.1 97/04/07 11:12:33 newdeal Exp $
 *
 ***********************************************************************/
start SSColumnWidthUI, notDetachable;

/****************************************************************************
 *	Column Width controller
 ****************************************************************************/

ColumnNarrowerTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'N', "Narrower";
	kbdAccelerator = control '1';
	action = MSG_SSCWC_CHANGE_COLUMN_WIDTHS, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {word -SS_COLUMN_WIDTH_INC}
	}
}

ColumnWiderTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'W', "Wider";
	kbdAccelerator = control '2';
	action = MSG_SSCWC_CHANGE_COLUMN_WIDTHS, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {word +SS_COLUMN_WIDTH_INC}
	}
}

ColumnBestFitTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'F', "Best Fit";
	action = MSG_SSCWC_SET_COLUMN_WIDTH, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {word 0,COLUMN_WIDTH_BEST_FIT},
		HINT_SEEK_REPLY_BAR
	}
}

ColumnWidthDB = GenInteraction {
	genStates = default -usable;
	moniker = 'C', "Column Width";
	children =
		CWRange;
	type = properties;
	visibility = dialog;
	hints = {
	    	HINT_CENTER_WINDOW,
		ATTR_GEN_HELP_CONTEXT {
			char "dbColWidth", 0
		}
	}
}

CWRange = GenValue {
	moniker = "Width:";
	minimum = SS_COLUMN_WIDTH_MIN;
	maximum = SS_COLUMN_WIDTH_MAX;
	increment = SS_COLUMN_WIDTH_INC;
	displayFormat = points;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	applyMsg = MSG_SSCWC_SET_COLUMN_WIDTH;
}

end SSColumnWidthUI;


/****************************************************************************
 *	Strings for controllers
 ***************************************************************************/

start ControlStrings, data;

chunk SSWCName = "Column Width";

chunk SSWCNarrowerName = "Column Narrower";
chunk SSWCWiderName = "Column Wider";
chunk SSWCBestFitName = "Best Fit";
chunk SSWCCustomName = "Custom Width";

end ControlStrings;
