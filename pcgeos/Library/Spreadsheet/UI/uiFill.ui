/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  uiFill.ui
 * FILE:	  uiFill.ui
 *
 * AUTHOR:  	  Gene Anderson: Aug  6, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	8/ 6/92	  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI for SSFillControl
 *
 *
 * 	$Id: uiFill.ui,v 1.1 97/04/07 11:13:08 newdeal Exp $
 *
 ***********************************************************************/

start SSFillUI, notDetachable;

/******************************************************************************
 *	"Fill Right"
 *****************************************************************************/

FillRightTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'R', "Fill Right";
	action = MSG_SSFC_DO_FILL, "TO_OBJ_BLOCK_OUTPUT";
	kbdAccelerator = control shift 'r';
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA { byte 0 }
	}
}

/******************************************************************************
 *	"Fill Down"
 *****************************************************************************/

FillDownTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'D', "Fill Down";
	kbdAccelerator = control shift 'd';
	action = MSG_SSFC_DO_FILL, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA { byte mask SSFF_ROWS }
	}
}

/******************************************************************************
 *	"Fill Series" DB
 *****************************************************************************/

FillSeriesDB = GenInteraction {
	genStates = default -usable;
	moniker = 'S', "Fill Series";
	visibility = dialog;
	type = command;
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
		HINT_CENTER_WINDOW,
		ATTR_GEN_HELP_CONTEXT {
			char "dbFillSeries", 0
		}
	}
	children =
		FillSeriesStuff;
}

FillSeriesStuff = GenInteraction {
	children =
		FillByList,
		FillTypeList,
		FillStepList,
		FillStepValue,
		FillApplyTrigger;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
	}
}

FillApplyTrigger = GenTrigger {
	action = MSG_SSFC_DO_SERIES, "TO_OBJ_BLOCK_OUTPUT";
	moniker = 'F', "Fill";
	hints = {
		HINT_SEEK_REPLY_BAR,
		HINT_DEFAULT_DEFAULT_ACTION,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND { word IC_APPLY }
	}
}

FillByList = GenItemGroup {
	moniker = "Fill By:";
	children =
		FillByRowsEntry,
		FillByColumnsEntry;
	selection = "mask SSFF_ROWS";
	numSelections = 1;
}

FillByRowsEntry = GenItem {
	moniker = 'R', "Rows";
	identifier = "mask SSFF_ROWS";
}

FillByColumnsEntry = GenItem {
	moniker = 'C', "Columns";
	identifier = "0";
}

FillTypeList = GenItemGroup {
	moniker = 'U', "Units:";
	children =
		FillNumberEntry,
		FillDayEntry,
		FillWeekdayEntry,
		FillMonthEntry,
		FillYearEntry;
	selection = "SSFT_NUMBER";
	destination = "TO_OBJ_BLOCK_OUTPUT";
	applyMsg = MSG_SSFC_SET_UNITS;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}

FillNumberEntry = GenItem {
	moniker = 'N', "Number";
	identifier = "SSFT_NUMBER";
}

FillDayEntry = GenItem {
	moniker = 'D', "Day";
	identifier = "SSFT_DAY";
}

FillWeekdayEntry = GenItem {
	moniker = 'W', "Weekday";
	identifier = "SSFT_WEEKDAY";
}

FillMonthEntry = GenItem {
	moniker = 'M', "Month";
	identifier = "SSFT_MONTH";
}

FillYearEntry = GenItem {
	moniker = 'Y', "Year";
	identifier = "SSFT_YEAR";
}

FillStepList = GenItemGroup {
	moniker = "Progression:";
	children =
		LinearEntry,
		GeometricEntry;
	selection = "0";
}

LinearEntry = GenItem {
	moniker = 'L', "Linear";
	identifier = "0";
}

GeometricEntry = GenItem {
	moniker = 'G', "Geometric";
	identifier = "mask SSFF_GEOMETRIC";
}

FillStepValue = GenText {
	moniker = 'S', "Step:";
	text = "1";
	textDisplayAttributes =
		singleLineText,
		useTabForNavigation;
	maxLength = "MAX_DIGITS_FOR_NORMAL_NUMBERS+MAX_CHARS_FOR_EXPONENT-1";
	hints = {
		ATTR_GEN_TEXT_SIGNED_DECIMAL,
		HINT_FIXED_SIZE {
			SpecWidth <SST_PIXELS, 1*72>
			SpecHeight <>
		}
	}
}

end SSFillUI;

/****************************************************************************
 *	Strings for controllers
 ***************************************************************************/

start ControlStrings, data;

chunk SSFCName = "Spreadsheet Fill";

chunk SSFCFillRightName = "Fill Right";
chunk SSFCFillDownName = "Fill Down";
chunk SSFCFillSeriesName = "Fill Series";

end ControlStrings;
