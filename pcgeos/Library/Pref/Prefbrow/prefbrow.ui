/***********************************************************************
 *
 *	Copyright (c) MyTurn.com 2001.  All rights reserved.
 *	MYTURN.COM CONFIDENTIAL
 *
 * PROJECT:	  GlobalPC
 * MODULE:	  User level pref module
 * FILE:	  prefbrow.ui
 *
 * AUTHOR:  	  Brian Chin, Mar 30, 2001
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	brianc  3/30/01   	Initial version
 *
 * DESCRIPTION:
 *
 *	UI for browser module of Preferences
 *
 * 	$Id$
 *
 ***********************************************************************/

#include "generic.uih"
#include "config.uih"

class PrefbrowDialog = PrefDialog {
}

/**************************************************************************
 *	Monikers
 **************************************************************************/ 

start	PrefbrowMonikers, data;

#include "Art/mkrPrefbrow.ui"

visMoniker PrefbrowTextMoniker = "Internet";

visMoniker PrefbrowMonikerList = list {
		PrefbrowTextMoniker,
		PrefbrowMoniker
}

end	PrefbrowMonikers;

/**************************************************************************
 *	Main UI
 **************************************************************************/ 

start	PrefbrowUI;

PrefbrowTitle = TitledGlyph {
	moniker = list {
		PrefbrowTextMoniker,
		PrefbrowMoniker
	}
}

PrefbrowTitleGroup = GenInteraction {
	children =
		PrefbrowTitle,
		BrowserGlyph;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

PrefbrowRoot = PrefbrowDialog {
	moniker = "Global Internet Settings";
	children = 
		PrefbrowTitleGroup,
		HomePageGroup,
		BrowserChange,
	        BrowserDontChange;
	attributes = default +modal, +notUserInitiatable;
	type = properties;
	visibility = dialog;
	genStates = default -usable;
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
		HINT_INTERACTION_SIMPLE_PROPERTIES,
	}
}


BrowserChange = GenTrigger {
	moniker = 'C', "Change";
	genAttributes = signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word IC_APPLY
		}
	}
}

BrowserDontChange = GenTrigger {
	moniker = 'D', "Don't Change";
	genAttributes = signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word IC_DISMISS
		}
	}
}

BrowserGlyph = GenText {
	genAttributes = default +readOnly;
	text = "Enter the page that you want Global Internet to start with:";
}

HomePageGroup = PrefInteraction {
	children = HomePageInput;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

HomePageInput = PrefText {
        moniker = "URL:";
        textDisplayAttributes   = default +singleLineText;
	maxLength = 256;  /* could be longer */
	hints = {
	        HINT_PLACE_MONIKER_ABOVE,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 50>
			SpecHeight <>
		},
		ATTR_GEN_INIT_FILE_CATEGORY {
		        char "htmlview",0
		},
		ATTR_GEN_INIT_FILE_KEY {
		        char "home",0
		}
	}
}

end	PrefbrowUI;
