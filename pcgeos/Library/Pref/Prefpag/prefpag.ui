/*
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * Project:	PC GEOS
 * Module:	UI Specification
 * File:	prefpag.ui
 *
 * Author:	Jennifer Wu, Mar 10, 1993
 *
 * Revision History:
 *	Name	Date		Description
 *	----	----		-----------
 *	jwu	3/10/93		Initial revision
 *
 * Description:
 *	UI for Pager preferences module.
 *
 *	$Id: prefpag.ui,v 1.1 97/04/05 01:29:45 newdeal Exp $
 *
 */

#include "generic.uih"
#include "config.uih"
#include "Art/mkrPrefPag.ui"

class PrefPagDialog = PrefDialog {
}

class PrefPagDynamicList = GenDynamicList{
	array = nptrComp : null;
}
		
/**************************************************************************
 		UI FOR PAGER
**************************************************************************/
start PrefPagUI;

PrefPagRoot = PrefPagDialog {
	attributes = default +notUserInitiatable;
	genStates = default -usable;
	type = organizational;
	children = PrefPagTitleGroup,
		PrefPagActionGroup,
		PrefPagRemoveErrorBox,
		PrefPagErrorBox;
	hints = {
		INIT_FILE_CATEGORY(pager),
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}

PrefPagTitleGroup = GenInteraction {
	children = PrefPagTitle,
		PrefPagHelp;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

PrefPagTitle = TitledGlyph {
	moniker = list {
		PrefPagTextMoniker,
		PrefPagLCMoniker,
		PrefPagLMMoniker,
		PrefPagLCGAMoniker
	}
}

PrefPagHelp = GenText {
	genAttributes = readOnly;
	text = "Use these options to select the way pages are handled.";
	hints = {
		HINT_FIXED_SIZE {
			SpecWidth <SST_PIXELS, 132>
			SpecHeight <>
		}
	}
}

/*******************************************************
		Main Interaction
*******************************************************/

PrefPagActionGroup = GenInteraction {
	children = PrefPagInstalledList,
		PrefPagReplyBar;
	hints = {
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	}
}

PrefPagInstalledList = PrefPagDynamicList {
	moniker = "Currently installed pagers:";
	queryMsg = MSG_PREF_PAG_GET_INSTALLED_LIST_MONIKER;
	destination = PrefPagInstalledList;
	applyMsg = MSG_PREF_PAG_INSTALLED_DEVICE_SELECTED;
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_PLACE_MONIKER_ABOVE,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 40>
			SpecHeight <SST_LINES_OF_TEXT, 5>
			word	5
		},
	}
}

/************************************************
		Reply Bar
************************************************/
		
PrefPagReplyBar = GenInteraction {
	children = PrefPagRemoveTrigger,
		PrefPagChangeInteraction,	
		PrefPagAddInteraction,
		PrefPagNotificationInteraction,
		PrefPagCloseTrigger;
	hints = {
		HINT_MAKE_REPLY_BAR,
		HINT_WRAP_AFTER_CHILD_COUNT {
			word	3
		},
	}
}

PrefPagRemoveTrigger = GenTrigger {
	moniker = 'R', "Remove";
	action = MSG_PREF_PAG_REMOVE_PAGER, PrefPagRoot;
	hints = {
		HINT_SEEK_REPLY_BAR,
	}		
}

PrefPagChangeInteraction = PrefDialog {
	moniker = "Configure";
	children = PrefPagConfigContainer;
	genStates = default -enabled;	
	visibility = dialog;
	attributes = modal;
	hints = {
		HINT_SEEK_REPLY_BAR,
		HINT_DRAW_IN_BOX,
		HINT_PLACE_MONIKER_ABOVE,
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_INTERACTION_COMPLEX_PROPERTIES,
		ATTR_PREF_REBOOT_STRING {
			optr	PrefPagRebootString
		},
	}
}

PrefPagConfigContainer = PrefContainer {
	templateMoniker = "\\1";
	genStates = default +usable;
	hints = {
		HINT_PLACE_MONIKER_ABOVE,
		HINT_ORIENT_CHILDREN_VERTICALLY,
	}
}

/**********************************************
	Adding A Pager
**********************************************/
		
PrefPagAddInteraction = PrefInteraction {
	moniker = 'A', "Add";
	children = PrefPagDeviceList,
		PrefPagAppList,
		PrefPagPortList,
		PrefPagResponseBar;
	visibility = dialog;
	attributes = default +modal;
	prefInteractionAttrs = default +loadOptionsOnInitiate;
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_SEEK_REPLY_BAR,
	}
}			

PrefPagDeviceList = PrefTocList {
	tokenChars = "'PGDR'";
	tocFlags = extendedDeviceDrivers;
	moniker = "Select a pager:";
	hints = {
		ATTR_GEN_PATH_DATA {
			GenFilePath <SP_PAGER_DRIVERS, 0>
		},
		INIT_FILE_KEY(device),
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_PLACE_MONIKER_ABOVE,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 40>
			SpecHeight <SST_LINES_OF_TEXT, 5>
			word	5
		},
	}
}
		
PrefPagAppList = PrefPagDynamicList {
	moniker = "Select a pager application:";
	queryMsg = MSG_PREF_PAG_GET_APP_LIST_MONIKER;
	destination = PrefPagAppList;
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_PLACE_MONIKER_ABOVE,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 40>
			SpecHeight <SST_LINES_OF_TEXT, 5>
			word	5
		},
	}
}

PrefPagPortList = PrefItemGroup {
	moniker = "Select the port the pager is in:";
	children = Com1Item,
		Com2Item,
		Com3Item,
		Com4Item,
		Lpt1Item,
		Lpt2Item,
		Lpt3Item;
	behaviorType = exclusiveNone;
	numSelections = 0;
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_PLACE_MONIKER_ABOVE,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 40>
			SpecHeight <SST_LINES_OF_TEXT, 5>
			word	5
		},
	}
}

Com1Item = GenItem {
	moniker = "COM1";
	identifier = "offset Com1Item";
}

Com2Item = GenItem {
	moniker = "COM2";
	identifier = "offset Com2Item";
}

Com3Item = GenItem {
	moniker = "COM3";
	identifier = "offset Com3Item";
}

Com4Item = GenItem {
	moniker = "COM4";
	identifier = "offset Com4Item";
}

Lpt1Item = GenItem {
	moniker = "LPT1";
	identifier = "offset Lpt1Item";
}

Lpt2Item = GenItem {
	moniker = "LPT2";
	identifier = "offset Lpt2Item";
}

Lpt3Item = GenItem {
	moniker = "LPT3";
	identifier = "offset Lpt3Item";
}
		
PrefPagResponseBar = GenInteraction {
	children = PrefPagAddTrigger,
		PrefPagCancelTrigger;
	hints = {
		HINT_MAKE_REPLY_BAR,
	}
}

PrefPagAddTrigger = GenTrigger {
	moniker = 'A',"Add";
	action = MSG_PREF_PAG_ADD_PAGER, PrefPagRoot;
	genAttributes = signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR,
	}
}

PrefPagCancelTrigger = GenTrigger {
	moniker = 'C',"Cancel";
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word	IC_DISMISS
		},
	}
}
/********************************************
	Notification Options
********************************************/

PrefPagNotificationInteraction = PrefDialog {
	attributes = default -notUserInitiatable;
	prefInteractionAttrs = default +loadOptionsOnInitiate;
	moniker = 'O', "Options";
	type = properties;
	visibility = dialog;
	children = NotificationHelp,
		NotificationGroup;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_INTERACTION_COMPLEX_PROPERTIES,
		HINT_SEEK_REPLY_BAR,
	}
}

NotificationHelp = GenText {
	genAttributes = readOnly;
	text = "Select the way you want to be notified when messages are received.";
	hints = {
		HINT_FIXED_SIZE {
			SpecWidth <SST_PIXELS, 260>
			SpecHeight <>
		}
	}	
}

NotificationGroup = PrefInteraction {
	visibility = subGroup;
	prefInteractionAttrs = default +loadOptionsOnInitiate;
	children = BeepGroup,
		AppLaunchGroup,
		PollingGroup;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_DRAW_IN_BOX
	}
}

BeepGroup = PrefItemGroup {
	moniker = 'B', "Beep:";
	children = BeepEnable,
		BeepDisable;
	selection = FALSE;
	numSelections = 1;
	hints = {
		INIT_FILE_CATEGORY(pager),
		INIT_FILE_KEY(beep),
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		ATTR_GEN_ITEM_GROUP_INIT_FILE_BOOLEAN
	}
}

BeepEnable = GenItem {
	identifier = TRUE;
	moniker = "On";
}

BeepDisable = GenItem {
	identifier = FALSE;
	moniker = "Off";
}

AppLaunchGroup = PrefItemGroup {
	moniker = 'A', "Automatic Application Startup:";
	children = StartEnable,
		StartDisable;
	selection = FALSE;
	numSelections = 1;
	hints = {
		INIT_FILE_CATEGORY(pager),
		INIT_FILE_KEY(launchApp),
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		ATTR_GEN_ITEM_GROUP_INIT_FILE_BOOLEAN
	}
}

StartEnable = GenItem {
	identifier = TRUE;
	moniker = "On";
}

StartDisable = GenItem {
	identifier = FALSE;
	moniker = "Off";
}

PollingGroup = PrefItemGroup {
	moniker = "Set the polling interval.";
	children = ThirtySeconds,		
		OneMinute,
		FiveMinutes,
		TenMinutes;
	selection = THIRTY_SECONDS;
	numSelections = 1;
	hints = {
		INIT_FILE_CATEGORY(pager),
		INIT_FILE_KEY(pollInterval),
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_WRAP_AFTER_CHILD_COUNT {
			word	2
		},
		HINT_DRAW_IN_BOX,
	}
}

ThirtySeconds = GenItem {
	moniker = "Thirty Seconds";
	identifier = THIRTY_SECONDS;
}

OneMinute = GenItem {
	moniker = "One Minute";
	identifier = ONE_MINUTE;
}

FiveMinutes = GenItem {
	moniker = "Five Minutes";
	identifier = FIVE_MINUTES;
}

TenMinutes = GenItem {
	moniker = "Ten Minutes";
	identifier = TEN_MINUTES;
}

/***************************************************************
	Close Trigger for Pager Preferences Main Dialog Box
***************************************************************/

PrefPagCloseTrigger = GenTrigger {
	moniker = 'C', "Close";
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word	IC_DISMISS
		},
	}
}
		
/***************************************************************
	Dialog Boxes
***************************************************************/
PrefPagRemoveErrorBox = GenInteraction {
	moniker = "WARNING";
	type = notification;
	visibility = dialog;
	attributes = default +notUserInitiatable, +modal;
	children = PrefPagRemoveErrorText;
	hints = {
		HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}

PrefPagRemoveErrorText = GenText {
	genAttributes = default +readOnly;
	text = "You must select a device first.";
}

PrefPagErrorBox = GenInteraction {
	moniker = "WARNING";
	type = notification;
	visibility = dialog;
	attributes = default +notUserInitiatable,
		+modal;
	children = PrefPagErrorText;
	hints = {
		HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}	
	
PrefPagErrorText = GenText {
	genAttributes = default +readOnly;
	text = "Cannot install this pager.  Check your selections to be sure that you have specified a pager, an application, and a port.";
	hints = {
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 40>
			SpecHeight <>
		},
	}
}

end	PrefPagUI;

start	Strings, data;
	
chunk PrefPagRebootString = "pager device";

visMoniker PrefPagTextMoniker = "Pager";

visMoniker PrefPagMonikerList = list {
	PrefPagTextMoniker,
	PrefPagLCMoniker,
	PrefPagLMMoniker,
	PrefPagLCGAMoniker
}

end	Strings;





