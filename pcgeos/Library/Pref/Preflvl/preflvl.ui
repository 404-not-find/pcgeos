/***********************************************************************
 *
 *	Copyright (c) MyTurn.com 2001.  All rights reserved.
 *	MYTURN.COM CONFIDENTIAL
 *
 * PROJECT:	  GlobalPC
 * MODULE:	  User level pref module
 * FILE:	  preflvl.ui
 *
 * AUTHOR:  	  David Hunter, Jan 08, 2001
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	dhunter 1/08/01   	Initial version
 *
 * DESCRIPTION:
 *
 *	UI for user level module of Preferences
 *
 * 	$Id$
 *
 ***********************************************************************/

#include "generic.uih"
#include "config.uih"

class PreflvlDialog = PrefDialog {
}

/**************************************************************************
 *	Monikers
 **************************************************************************/ 

start	PreflvlMonikers, data;

#include "Art/mkrPreflvl.ui"

visMoniker PreflvlTextMoniker = "Skill Level";

visMoniker PreflvlMonikerList = list {
		PreflvlTextMoniker,
		PreflvlMoniker
}

end	PreflvlMonikers;

/**************************************************************************
 *	Main UI
 **************************************************************************/ 

start	PreflvlUI;

PreflvlTitle = TitledGlyph {
	moniker = list {
		PreflvlTextMoniker,
		PreflvlMoniker
	}
}

PreflvlTitleGroup = GenInteraction {
	children =
		PreflvlTitle,
		ChangeUILevelGlyph;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

PreflvlRoot = PreflvlDialog {
	moniker = "Change Skill Level";
	children = 
		PreflvlTitleGroup,
		AppLaunchLevelGroup,
		ChangeUILevelReplaceGroup,
		ChangeUILevelChange,
	        ChangeUILevelDontChange;
	attributes = default +modal, +notUserInitiatable;
	type = properties;
	visibility = dialog;
	genStates = default -usable;
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
		HINT_INTERACTION_SIMPLE_PROPERTIES
	}
}


ChangeUILevelChange = GenTrigger {
	moniker = 'C', "Change";
	genAttributes = signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word IC_APPLY
		}
	}
}

ChangeUILevelDontChange = GenTrigger {
	moniker = 'D', "Don't Change";
	genAttributes = signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word IC_DISMISS
		}
	}
}

ChangeUILevelGlyph = GenText {
	genAttributes = default +readOnly;
	text = "Select one of the options below to change your skill level setting:";
}

AppLaunchLevelGroup = PrefInteraction {
	children = AppLaunchLevel, AppLaunchLevelGlyphs;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

AppLaunchLevel = PrefItemGroup {
	selection = UIIL_INTRODUCTORY;
	children =
		AULIntro,
		AULBeginning,
		AULIntermediate,
		AULAdvanced;
	hints = {
		HINT_ITEM_GROUP_TOOLBOX_STYLE,
		HINT_ORIENT_CHILDREN_VERTICALLY,
	        ATTR_GEN_INIT_FILE_CATEGORY { char "uiFeatures - advanced", 0 },
		ATTR_GEN_INIT_FILE_KEY { char "launchLevel", 0 },
		ATTR_GEN_USES_HIERARCHICAL_INIT_FILE_CATEGORY
	}
}

AULIntro = GenItem {
	identifier = UIIL_INTRODUCTORY;
	hints = {
		ATTR_GEN_DEFAULT_MONIKER { word GDMT_LEVEL_0 }
	}
}

AULBeginning = GenItem {
	identifier = UIIL_BEGINNING;
	hints = {
		ATTR_GEN_DEFAULT_MONIKER { word GDMT_LEVEL_1 }
	}
}

AULIntermediate = GenItem {
	identifier = UIIL_INTERMEDIATE;
	hints = {
		ATTR_GEN_DEFAULT_MONIKER { word GDMT_LEVEL_2 }
	}
}

AULAdvanced = GenItem {
	identifier = UIIL_ADVANCED;
	hints = {
		ATTR_GEN_DEFAULT_MONIKER { word GDMT_LEVEL_3 }
	}
}

AppLaunchLevelGlyphs = GenInteraction {
	children =
		AULIntroGlyph,
		AULBeginningGlyph,
		AULIntermediateGlyph,
		AULAdvancedGlyph;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}

AULIntroGlyph = GenText vardataReloc {
	genAttributes = default +readOnly;
	text = "Click here if you are a Beginner user (default skill level).";
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ALIGN_TOP_EDGE_WITH_OBJECT { I_OPTR AULIntro },
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 40>
			SpecHeight <>
		}
	}
}

AULBeginningGlyph = GenText vardataReloc {
	genAttributes = default +readOnly;
	text = "Click here if you are an Intermediate user.";
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ALIGN_TOP_EDGE_WITH_OBJECT { I_OPTR AULBeginning },
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 40>
			SpecHeight <>
		}
	}
}
AULIntermediateGlyph = GenText vardataReloc {
	genAttributes = default +readOnly;
	text = "Click here if you are an Advanced user.";
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ALIGN_TOP_EDGE_WITH_OBJECT { I_OPTR AULIntermediate },
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 40>
			SpecHeight <>
		}
	}
}
AULAdvancedGlyph = GenText vardataReloc {
	genAttributes = default +readOnly;
	text = "Click here if you are an Expert user.";
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ALIGN_TOP_EDGE_WITH_OBJECT { I_OPTR AULAdvanced },
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 40>
			SpecHeight <>
		}
	}
}

ChangeUILevelReplaceGroup = GenBooleanGroup {
	children =
		ChangeUILevelReplaceItem;
	hints = {
		HINT_BOOLEAN_GROUP_CHECKBOX_STYLE
	}
}

ChangeUILevelReplaceItem = GenBoolean {
	moniker = "Replace previously selected skill settings";
	identifier = 1;
}

end	PreflvlUI;
