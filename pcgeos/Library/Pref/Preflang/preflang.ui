/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  preflang
 * FILE:	  preflang.ui
 *
 * AUTHOR:  	  Paul Canavese: November 8, 1994
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	11/8/94	  canavese  Initial version
 *
 * DESCRIPTION:
 *	UI for Language module of Preferences
 *
 * 	$Id: preflang.ui,v 1.1 97/04/05 01:43:45 newdeal Exp $
 *
 ***********************************************************************/

#include "preflang.uih"

/*#ifdef GPC_VERSION */
#include "Art/mkrPrefLang.ui"
/*#else
#include "../PrefIntl/Art/mkrPrefIntl.ui"
#endif */


/**************************************************************************
 *	UI FOR LANGUAGE
 **************************************************************************/ 
start	PrefLangUI;


PrefLangTitle = TitledGlyph {
	moniker = list {
		PrefLangTextMoniker,
/*#ifdef GPC_VERSION  */
		PrefLangLCMoniker
/*#else
		PrefIntlLCMoniker,
		PrefIntlLMMoniker,
		PrefIntlLCGAMoniker
#endif   */
	}
}


PrefLangRoot = PrefLangDialog vardataReloc {
	genStates = default -usable;
	children =
#ifdef GPC_VERSION
		PrefLangOKTrigger,
		PrefLangCloseTrigger,
#endif
		PrefLangTitleGroup,
		PrefLangSubgroup;
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
#ifndef GPC_VERSION
		HINT_INTERACTION_COMPLEX_PROPERTIES,
#endif
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		ATTR_GEN_HELP_CONTEXT {
			char "dbLanguage",0
		}, 
		ATTR_PREF_REBOOT_STRING {
			UN_OPTR prefSystemLanguageRebootString
		},
		ATTR_PREF_DIALOG_SYS_SHUTDOWN_TYPE {
			word SST_RESTART
		}
	}
}


PrefLangTitleGroup = GenInteraction {
	children = PrefLangTitle, PrefLangHelp;
	hints = { HINT_ORIENT_CHILDREN_HORIZONTALLY }
}

PrefLangHelp = GenText {
	genAttributes = readOnly;
#ifdef GPC_VERSION
	text = "Use these options to change the language used by your GlobalPC.";
#else
	text = "The language of application and system text can be changed.";
#endif
}

PrefLangSubgroup = PrefInteraction {
	children = ThisPrefLangIniDynamicList;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}

ThisPrefLangIniDynamicList = PrefLangIniDynamicList {
	moniker = "Select a language:";
	prefAttrs = default +rebootIfChanged, +saveIfUsable;
	destination = PrefLangRoot; 
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_PLACE_MONIKER_ABOVE,
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 30>
			SpecHeight <>
		},
		ATTR_GEN_INIT_FILE_CATEGORY {
			char "system", 0
		},
		ATTR_GEN_INIT_FILE_KEY {
			char "systemLanguage",0
		},
		ATTR_PREF_INI_INIT_FILE_LIST_CATEGORY {
			char "system", 0
		},
		ATTR_PREF_INI_INIT_FILE_LIST_KEY {
			char "availableSystemLanguages",0
		},
		ATTR_PREF_REQUIRED_FEATURES {
			word mask PMF_OTHER
		}
	}
}

#ifdef GPC_VERSION
PrefLangOKTrigger = GenTrigger {
	moniker = 'C', "Change";
	genStates = default -enabled;
	genAttributes = signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {word IC_APPLY}
	}
}

PrefLangCloseTrigger = GenTrigger {
	moniker = 'D', "Don't Change";
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {word IC_DISMISS}
	}
}
#endif

end	PrefLangUI;



start	Strings, data;

visMoniker PrefLangTextMoniker = "Language";

visMoniker PrefLangMonikerList = list {
		PrefLangTextMoniker,
/*#ifdef GPC_VERSION */
		PrefLangLCMoniker
/*#else
		PrefIntlLCMoniker,
		PrefIntlLMMoniker,
		PrefIntlLCGAMoniker
#endif */
}

/* This string is used by entirely different mechanisms between the
 * GPC & non-GPC versions of this module, so *be careful* changing it */

#ifdef GPC_VERSION
chunk prefSystemLanguageRebootString = "Are you sure you want to change the language?";
#else
chunk prefSystemLanguageRebootString = data {
	byte	'system language', 0
}
#endif

end	Strings;
