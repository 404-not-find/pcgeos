/*
 * Project:	PC GEOS
 * Module:	Text
 * File:	UI/uiDefaultTabsControl.ui
 *
 * Author:	Tony Requist
 *
 *	This file contains the user interface description for a style control
 *
 *	$Id: uiDefaultTabs.ui,v 1.1 97/04/07 11:17:11 newdeal Exp $
 *
 */

/*
 * DefaultTabs control
 */

#if !NO_CONTROLLERS	/*+++++++++++++++++++++++++++++++++++++++++++++++*/

start DefaultTabsControlUI, notDetachable;

DefaultTabsList = GenItemGroup {
	genStates = default -usable;
	children = DefaultTabsNoneEntry, DefaultTabsCentimeterEntry,
#ifdef DO_PIZZA
		   DefaultTabs2CentimeterEntry, DefaultTabs3CentimeterEntry;
#else
		   DefaultTabsHalfInchEntry, DefaultTabsInchEntry;
#endif
	applyMsg = MSG_DTC_SET_DEFAULT_TABS_VIA_LIST;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = { 
		HINT_IF_SYSTEM_ATTRS { SystemAttrs mask SA_HORIZONTALLY_TINY },
			HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_ELSE,
			HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_ENDIF
	}
}

DefaultTabsNoneEntry = GenItem {
	moniker = 'N', "None";
	identifier = "0";
}

#ifdef DO_PIZZA
DefaultTabsCentimeterEntry = GenItem {
	moniker = '1', "1 Centimeter";
	identifier = "227";
}

DefaultTabs2CentimeterEntry = GenItem {
	moniker = '2', "2 Centimeters";
	identifier = "227*2";
}

DefaultTabs3CentimeterEntry = GenItem {
	moniker = '3', "3 Centimeters";
	identifier = "227*3";
}
#else
DefaultTabsHalfInchEntry = GenItem {
	moniker = 'H', "Half Inch";
	identifier = "PIXELS_PER_INCH*8/2";
}

DefaultTabsInchEntry = GenItem {
	moniker = 'O', "One Inch";
	identifier = "PIXELS_PER_INCH*8";
}

DefaultTabsCentimeterEntry = GenItem {
	moniker = 'C', "Centimeter";
	identifier = "227";
}
#endif

DefaultTabsDistance = GenValue {
	genStates = default -usable;
	moniker = 'u', "Custom:";
	displayFormat = points;
	value = 0;
	increment = 1;
	minimum = 0;
	maximum = 250;
	applyMsg = MSG_DTC_SET_DEFAULT_TABS;
	destination = "TO_OBJ_BLOCK_OUTPUT";
}

end DefaultTabsControlUI;

/*---*/

/*
 * Strings for controllers
 */

start ControlStrings, data;

chunk DTCName = "Default tabs";

chunk DefaultTabCustomName = "Default tabs custom size";
chunk DefaultTabListName = "Default tabs list";

end ControlStrings;

#endif			/* !NO_CONTROLLERS ++++++++++++++++++++++++++++++*/

