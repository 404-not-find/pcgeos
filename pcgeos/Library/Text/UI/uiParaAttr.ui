/*
 * Project:	PC GEOS
 * Module:	Text
 * File:	UI/uiParaAttrControl.ui
 *
 * Author:	Tony Requist
 *
 *	This file contains the user interface description for a style control
 *
 *	$Id: uiParaAttr.ui,v 1.1 97/04/07 11:16:57 newdeal Exp $
 *
 */

/*
 * ParaAttr control
 */

#if !NO_CONTROLLERS	/*+++++++++++++++++++++++++++++++++++++++++++++++*/

start ParaAttrControlUI, notDetachable;

PAOverallGroup = GenInteraction {
	genStates = default -usable;
	children = PASimpleGroup, PAKeepGroup;
	hints = { HINT_ORIENT_CHILDREN_HORIZONTALLY }
}

PASimpleGroup = GenInteraction {
	moniker = "Attributes";
	children = PASimpleList;
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

PASimpleList = GenBooleanGroup {
	children = WordWrapEntry, ColumnBreakBeforeEntry,
		   KeepParaWithNextEntry, KeepParaTogetherEntry;
	applyMsg = MSG_PAC_SET_PARA_ATTR;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_CENTER_CHILDREN_VERTICALLY
	}
}

WordWrapEntry = GenBoolean {
	moniker = 'W', "Disable Word Wrap";
	identifier = "mask VTPAA_DISABLE_WORD_WRAP";
}

ColumnBreakBeforeEntry = GenBoolean {
	moniker = 'C', "Page/Column Break Before";
	identifier = "mask VTPAA_COLUMN_BREAK_BEFORE";
}

KeepParaWithNextEntry = GenBoolean {
	moniker = 'K', "Keep With Next Paragraph";
	identifier = "mask VTPAA_KEEP_PARA_WITH_NEXT";
}

KeepParaTogetherEntry = GenBoolean {
	moniker = 'P', "Keep Paragraph Together";
	identifier = "mask VTPAA_KEEP_PARA_TOGETHER";
}

/***/

PAKeepGroup = GenInteraction {
	moniker = "Widow and Orphan Control";
	children = PAKeepList, PACustomKeepGroup;
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

PAKeepList = GenItemGroup {
	children = PAOffEntry, PAOnEntry;
	applyMsg = MSG_PAC_SET_KEEP;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			dw MSG_PAC_USER_CHANGED_KEEP
		}
	}   
}

PAOffEntry = GenItem {
	moniker = 'f', "Off";
	identifier = "0";
}

PAOnEntry = GenItem {
	moniker = 'n', "On";
	identifier = "mask VTPAA_KEEP_LINES";
}

PACustomKeepGroup = GenInteraction {
	moniker = "Minimum Lines to Keep Together:";
	children = KeepFirstRange, KeepLastRange;
	hints = {
		HINT_PLACE_MONIKER_ABOVE,
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_CENTER_CHILDREN_ON_MONIKERS
	}
}

KeepFirstRange = GenValue {
	moniker = 'T', "At Paragraph Top:";
	value = 1;
	increment = 1;
	minimum = 2;
	maximum = 16;
	applyMsg = MSG_PAC_SET_KEEP_FIRST;
	destination = "TO_OBJ_BLOCK_OUTPUT";
}

KeepLastRange = GenValue {
	moniker = 'B', "At Paragraph Bottom:";
	value = 1;
	increment = 1;
	minimum = 2;
	maximum = 16;
	applyMsg = MSG_PAC_SET_KEEP_LAST;
	destination = "TO_OBJ_BLOCK_OUTPUT";
}

end ParaAttrControlUI;

/*---*/

/*
 * Strings for controllers
 */

start ControlStrings, data;

chunk PACName = "Paragraph Attributes";

chunk KeepLinesName = "Widow and Orphan Control";
chunk KeepParaWithNextName = "Keep Paragraph With Next";
chunk KeepParaTogetherName = "Keep Paragraph Together";
chunk HiddenName = "Hidden Text";
chunk WordWrapName = "Word wrap Text";

end ControlStrings;

#endif			/* !NO_CONTROLLERS ++++++++++++++++++++++++++++++*/

