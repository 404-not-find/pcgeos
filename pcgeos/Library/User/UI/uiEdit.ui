/*
 * Project:	PC GEOS
 * Module:	Text
 * File:	Gen/genEdit.ui
 *
 * Author:	Tony Requist
 *
 *	This file contains the user interface description for a style control
 *
 *	$Id: uiEdit.ui,v 1.1 97/04/07 11:47:12 newdeal Exp $
 *
 */

/*
 * Edit control
 */

#if !NO_CONTROLLERS	/*+++++++++++++++++++++++++++++++++++++++++++++++*/

#include "Art/mkrCut.ui"
#include "Art/mkrCopy.ui"
#include "Art/mkrPaste.ui"
#include "Art/mkrDelete.ui"
#include "Art/mkrUndo.ui"
#include "Art/mkrSelectAll.ui"


start GenEditControlUI, notDetachable;

FirstEditGroup = GenInteraction {
	genStates = default -usable;
	children = UndoTrigger, CutCopyPasteGroup, DeleteTrigger;
}

UndoTrigger = GenTrigger {
	genStates = default -enabled;
	genAttributes = default +initiatesInputHoldUp, +initiatesBusyState;
	kbdAccelerator = specificUI alt BACKSPACE;
	action = MSG_META_UNDO, "TO_OBJ_BLOCK_OUTPUT";
	moniker = StartMoniker;
}
visMoniker StartMoniker = ignoreDirty 'U', "Nothing to Undo";

CutCopyPasteGroup = GenInteraction {
	children = CutTrigger, CopyTrigger, PasteTrigger;
}

CutTrigger = GenTrigger {
	genStates = default -enabled;
	CUT_TRIGGER_VALUES
	action = MSG_META_CLIPBOARD_CUT, "TO_OBJ_BLOCK_OUTPUT";
}

CopyTrigger = GenTrigger {
	genStates = default -enabled;
	COPY_TRIGGER_VALUES
	action = MSG_META_CLIPBOARD_COPY, "TO_OBJ_BLOCK_OUTPUT";
}

PasteTrigger = GenTrigger {
	genStates = default -enabled;
	PASTE_TRIGGER_VALUES
	action = MSG_META_CLIPBOARD_PASTE, "TO_OBJ_BLOCK_OUTPUT";
}

DeleteTrigger = GenTrigger {
	genStates = default -enabled;
	DELETE_TRIGGER_VALUES
	action = MSG_META_DELETE, "TO_OBJ_BLOCK_OUTPUT";
}

SelectAllGroup = GenInteraction {
	genStates = default -usable;
	children = SelectAllTrigger;
}

SelectAllTrigger = GenTrigger {
	genStates = default -enabled;
	SELECT_ALL_TRIGGER_VALUES
	action = MSG_META_SELECT_ALL, "TO_OBJ_BLOCK_OUTPUT";
}

ClipArtGroup = GenInteraction {
	genStates = default -usable;
	children = ClipArtTrigger;
}

CharMapGroup = GenInteraction {
	genStates = default -usable;
	children = CharMapTrigger;
}

ClipArtTrigger = GenTrigger {
	moniker = "Insert Clip Art...";
	action = MSG_GEC_LAUNCH_CLIP_ART, "TO_OBJ_BLOCK_OUTPUT";
}

CharMapTrigger = GenTrigger {
	moniker = "Special Characters...";
	action = MSG_GEC_LAUNCH_CHAR_MAP, "TO_OBJ_BLOCK_OUTPUT";
}

end GenEditControlUI;

/*---*/

start GenEditControlToolboxUI, notDetachable;

UndoToolTrigger = GenTrigger {
	moniker = list { UndoTCMoniker }
	genAttributes = default +initiatesInputHoldUp, +initiatesBusyState;
	genStates = default -usable, -enabled;
	action = MSG_META_UNDO, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_FOCUS_HELP { optr UndoHelp },
		ATTR_GEN_FOCUS_DISABLED_HELP { optr UndoDisHelp }
	}
}

CutToolTrigger = GenTrigger {
	moniker = list { CutTCMoniker }
	genStates = default -usable, -enabled;
	action = MSG_META_CLIPBOARD_CUT, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_FOCUS_HELP { optr CutHelp },
		ATTR_GEN_FOCUS_DISABLED_HELP { optr CutDisHelp }
	}
}

CopyToolTrigger = GenTrigger {
	moniker = list { CopyTCMoniker }
	genStates = default -usable, -enabled;
	action = MSG_META_CLIPBOARD_COPY, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_FOCUS_HELP { optr CopyHelp },
		ATTR_GEN_FOCUS_DISABLED_HELP { optr CopyDisHelp }
	}
}

PasteToolTrigger = GenTrigger {
	moniker = list { PasteTCMoniker }
	genStates = default -usable, -enabled;
	action = MSG_META_CLIPBOARD_PASTE, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_FOCUS_HELP { optr PasteHelp },
		ATTR_GEN_FOCUS_DISABLED_HELP { optr PasteDisHelp }
	}
}

SelectAllToolTrigger = GenTrigger {
	moniker = list { SelectAllTCMoniker }
	genStates = default -usable;
	action = MSG_META_SELECT_ALL, "TO_OBJ_BLOCK_OUTPUT";
	hints = { ATTR_GEN_FOCUS_HELP { optr SelectAllName } }
}

DeleteToolTrigger = GenTrigger {
	moniker = list { DeleteTCMoniker }
	genStates = default -usable, -enabled;
	action = MSG_META_DELETE, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_FOCUS_HELP { optr DeleteHelp },
		ATTR_GEN_FOCUS_DISABLED_HELP { optr DeleteDisHelp }
	}
}

end GenEditControlToolboxUI;

/*
 * Strings for controllers
 */

start ControlStrings, data;

visMoniker	NoUndoMoniker = 'U', "Nothing to Undo";
visMoniker	CannotUndoMoniker = 'U', "Cannot Undo Last Action";

chunk GECName = "Edit / Clipboard";

chunk UndoName = "Undo";
chunk CutName = "Cut";
chunk CopyName = "Copy";
chunk PasteName = "Paste";
chunk SelectAllName = "Select All";
chunk DeleteName = "Delete";

chunk UndoTriggerMonikerPrefix = "Undo ";
chunk UndoTriggerMonikerSuffix = "";

chunk RedoTriggerMonikerPrefix = "Redo ";
chunk RedoTriggerMonikerSuffix = "";

chunk ClipArtName = "Insert Clip Art";
chunk CharMapName = "Insert Character";

/* Quick-Help strings */

chunk UndoHelp = "Undo the last action";
chunk UndoDisHelp = "Nothing to undo";

chunk CutHelp = "Cut selection and move to clipboard";
chunk CutDisHelp = "Nothing selected to cut";

chunk CopyHelp = "Copy selection to clipboard";
chunk CopyDisHelp = "Nothing selected to copy";

chunk PasteHelp = "Paste clipboard contents";
chunk PasteDisHelp = "Nothing to paste because the clipboard is empty";

chunk DeleteHelp = "Delete selection";
chunk DeleteDisHelp = "Nothing selected to delete";

end ControlStrings;

#endif			/* !NO_CONTROLLERS ++++++++++++++++++++++++++++++*/

