/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1994 -- All Rights Reserved
 *
 * PROJECT:	  
 * MODULE:	  UI Specification
 * FILE:	  uiOutPanel.ui
 *
 * AUTHOR:  	  Adam de Boor: May 25, 1994
 *
 * REVISION HISTORY:
 *	Name	  Date	    Description
 *	----	  ----	    -----------
 *	ardeb	  5/25/94	    Initial version
 *
 * DESCRIPTION:
 *	
 * 	$Id: uiInPanel.ui,v 1.1 97/04/05 01:19:11 newdeal Exp $
 *
 ***********************************************************************/

#if	_CONTROL_PANELS

start InboxPanelUI, notDetachable;

InboxPanelRoot = InboxControlPanel {
	genStates = default -usable;
	attributes = default +notUserInitiatable;
	moniker = "Inbox";
	specificMoniker = "\\1 Messages";
	children = InboxPanelAppGroup, InboxPanelHelpText,
		InboxPanelMessages,
		InboxPanelReplyBar,
		InboxPanelDetails;
	msgList = InboxPanelMessages;
	detailsBox = InboxPanelDetails;
	specificRoot = InboxPanelHelpText;
	nonSpecificRoot = InboxPanelAppGroup;
	deliveryTrigger = InboxPanelDeliverAll;
	deliveryMoniker = 'A', "\\1 All";
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_WINDOW_CUSTOM_PARENT {
			hptr	0	; use screen as parent
		},
		ATTR_GEN_WINDOW_CUSTOM_LAYER_PRIORITY {
			; keep on top. dont set win prio, though, as we want
			; it to not interfere with control panels, etc.
			LayerPriority	LAYER_PRIO_ON_TOP
		}
	}
}

	/* goofy interaction needed b/c MINIMIZE_SIZE + 
	 * DISPLAY_CURRENT_SELECTION is interpreted as "don't display a moniker"
	 */
InboxPanelAppGroup = GenInteraction {
	children = InboxPanelAppGlyph, InboxPanelApps;
	hints = { HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_VERTICALLY
	}
}

InboxPanelAppGlyph = GenGlyph {
	moniker = "Show:";
}

InboxPanelApps = InboxApplicationList {
	destination = InboxPanelRoot;
	applyMsg = MSG_ICP_SET_APPLICATION;
	hints = {
		HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,
		HINT_ITEM_GROUP_MINIMIZE_SIZE
	}
}

InboxPanelHelpText = GenGlyph {
	moniker = "The following can all now be \\1:";
	genStates = default -usable;
}

InboxPanelMessages = InboxMessageList {
	attrs = sourceQueue inbox;
	applyMsg = MSG_MCP_MESSAGE_SELECTED;
	destination = InboxPanelRoot;
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
		ATTR_GEN_ITEM_GROUP_SET_MODIFIED_ON_REDUNDANT_SELECTION,
		HINT_MAXIMUM_SIZE {
			SpecWidth <>
			SpecHeight <SST_PCT_OF_FIELD_HEIGHT, PCT_45>
			word 1
		}
	}
}

/* Need this to get the Get New menu into the reply bar... */
InboxPanelReplyBar = GenInteraction {
	children = InboxPanelGetNewTransports, InboxPanelDeliverAll,
		InboxPanelDone;
	hints = { HINT_MAKE_REPLY_BAR }
}

InboxPanelGetNewTransports = OutboxTransportMenu {
	moniker = 'G', "Get New";
	destination = InboxPanelRoot;
	actionMsg = MSG_ICP_RETRIEVE_MESSAGES;
	visibility = popup;
	monikerSource = InboxPanelGetNewMonikerSource;
	genStates = default -usable;
	hints = {
		HINT_ITEM_GROUP_MINIMIZE_SIZE,
		HINT_AVOID_MENU_BAR,
		HINT_SEEK_REPLY_BAR
	}
}

InboxPanelGetNewMonikerSource = OutboxTransportMonikerSource {
	type = filtered;
	filter = "mask MBTC_MESSAGE_RETRIEVE";
}

InboxPanelDeliverAll = GenTrigger {
	/* moniker set by MCP */
	action = MSG_ML_DELIVER_ALL, InboxPanelMessages;
	genStates = default -usable;
	hints = { HINT_SEEK_REPLY_BAR }
}

InboxPanelDone = GenTrigger {
	moniker = 'D', "Done";
	action = MSG_MA_DESTROY_DIALOG, MailboxApp;
	hints = { HINT_SEEK_REPLY_BAR }
}

InboxPanelDetails = InboxDetails {
	titleMoniker = "\\1 Message";
	deliveryMoniker = "\\1";
	boundMoniker = "\\1 By:";
	subjectText = InboxPanelDetailsSubject;
	dateText = InboxPanelDetailsDate;
	actionTrigger = InboxPanelDetailsSend;
	boundText = InboxPanelDetailsBounds;

	attributes = default +notUserInitiatable;

	children = InboxPanelDetailsSubject,
		InboxPanelDetailsDate,
		InboxPanelDetailsSize,
		InboxPanelDetailsPriority,
		InboxPanelDetailsBounds,
		InboxPanelDetailsSend,
		InboxPanelDetailsDelete,
		InboxPanelDetailsClose;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}

InboxPanelDetailsSubject = GenText {
	genAttributes = default +readOnly;
	moniker = "Subject:";
	text = "";
	hints = {
		HINT_MAXIMUM_SIZE {
			SpecWidth <>
			SpecHeight <SST_LINES_OF_TEXT, 3>
		},
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

InboxPanelDetailsDate = GenText {
	genAttributes = default +readOnly;
	moniker = "Date:";
	text = "";
	textDisplayAttributes = singleLineText, noWordWrapping;
	hints = { HINT_EXPAND_WIDTH_TO_FIT_PARENT }
}

InboxPanelDetailsSize = GenText {
	genAttributes = default +readOnly;
	moniker = "Size:";
	text = "";
	textDisplayAttributes = singleLineText, noWordWrapping;
	hints = { HINT_EXPAND_WIDTH_TO_FIT_PARENT }
}

InboxPanelDetailsPriority = GenText {
	genAttributes = default +readOnly;
	moniker = "Priority:";
	text = "";
	textDisplayAttributes = singleLineText, noWordWrapping;
	hints = { HINT_EXPAND_WIDTH_TO_FIT_PARENT }
}

InboxPanelDetailsBounds = GenText {
	/* moniker set by box */
	genAttributes = default +readOnly;
	text = "";
	textDisplayAttributes = singleLineText, noWordWrapping;
	hints = { HINT_EXPAND_WIDTH_TO_FIT_PARENT }
}

InboxPanelDetailsSend = GenTrigger {
	/* moniker set by box */
	action = MSG_ID_SEND_MESSAGE, InboxPanelDetails;
	hints = {
		HINT_DEFAULT_DEFAULT_ACTION,
		HINT_SEEK_REPLY_BAR
	}
}

InboxPanelDetailsDelete = GenTrigger {
	moniker = 'D', "Delete";
	action = MSG_ID_DELETE_MESSAGE, InboxPanelDetails;
	hints = {
		HINT_TRIGGER_DESTRUCTIVE_ACTION,
		HINT_SEEK_REPLY_BAR
	}
}

InboxPanelDetailsClose = GenTrigger {
	moniker = 'C', "Close";
	hints = {
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word IC_DISMISS
		},
		HINT_SEEK_REPLY_BAR
	}
}

end InboxPanelUI;

#endif	/* _CONTROL_PANELS */

#if	_SIMPLE_MESSAGE_NOTIFY

start SimpleMessageNotifyUI, notDetachable;

SimpleMessageNotifyRoot = GenInteraction {
	genStates = default -usable;
	type = notification;
	visibility = dialog;
	attributes = notUserInitiatable, sysModal;
	children = SimpleMessageNotifyGlyph, SimpleMessageNotifyOK;
}

SimpleMessageNotifyOK = GenTrigger {
	genAttributes = signalInteractionComplete;
	action = MSG_GEN_DESTROY_AND_FREE_BLOCK, SimpleMessageNotifyRoot;
		/* should use MSG_MA_DESTROY_DIALOG if we have more cleanup
		   work to do before freeing the dialog block */
	hints = {
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word	IC_OK
		},
		HINT_SEEK_REPLY_BAR
	}
}

SimpleMessageNotifyGlyph = GenGlyph {
	moniker = "Received \\1 message.";
	localize "The string used in the simple message notify dialog box.  The \\1 marker is replaced with the name of the application.";
}

end SimpleMessageNotifyUI;

#endif	/* _SIMPLE_MESSAGE_NOTIFY */
