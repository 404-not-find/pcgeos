/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1994 -- All Rights Reserved
 *
 * PROJECT:	  Mailbox
 * MODULE:	  UI
 * FILE:	  uiSendControl.ui
 *
 * AUTHOR:  	  Don Reeves, Aug  3, 1994
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	8/ 3/94	  Don	    Initial version
 *
 * DESCRIPTION:
 *	Contains the UI descriptions for the MailboxSendControlClass
 *
 * 	$Id: uiSendControl.ui,v 1.1 97/04/05 01:19:14 newdeal Exp $
 *
 ***********************************************************************/

/***********************************************************************
 * 		Menu UI
 ***********************************************************************/

start	SendControlUI, notDetachable;


MSCTransportMenu = OutboxTransportMenu {
	genStates = default -usable;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	actionMsg = MSG_MAILBOX_SEND_CONTROL_TRANSPORT_SELECTED;
	monikerSource = MSCTransportMonikerSource;
	attrs = default +bringsUpWindow;
	hints = {


		HINT_SAME_ORIENTATION_AS_PARENT,
		HINT_ALLOW_CHILDREN_TO_WRAP
	}
}

MSCTransportMonikerSource = OutboxTransportMonikerSource {
	type = menuUI;
}

#if	_POOF_MESSAGE_CREATION

MSCPoofMenu = OutboxPoofMenu {
	genStates = default -usable;
	attrs = default +bringsUpWindow;
	hints = {
		HINT_SAME_ORIENTATION_AS_PARENT
	}
}

#endif	/* _POOF_MESSAGE_CREATION */

end	SendControlUI;



/***********************************************************************
 * 		Toolbox UI
 ***********************************************************************/

start	SendControlToolboxUI, notDetachable;

MSCToolMenu = OutboxTransportMenu {
	genStates = default -usable;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	actionMsg = MSG_MAILBOX_SEND_CONTROL_TRANSPORT_SELECTED;
	monikerSource = MSCToolMonikerSource;
	hints = {
		HINT_SAME_ORIENTATION_AS_PARENT
	}
}

MSCToolMonikerSource = OutboxTransportMonikerSource {
	type = toolUI;
}

end	SendControlToolboxUI;



/***********************************************************************
 * 		Control & Toolbox strings
 ***********************************************************************/

start	ROStrings, data;

chunk	MSCName			= "Message Creation";

#if	_POOF_MESSAGE_CREATION

chunk	MSCPoofMenuName		= "Poof Menu";

#endif	/* _POOF_MESSAGE_CREATION */

chunk	MSCTransportMenuName = "Transport Menu";

chunk	MSCToolTriggerName	= "Transport Menu";

visMoniker uiCurrentPageMonikers = list {
	"Current Page";
}

visMoniker uiPageRangeMonikers = list {
	"Page Range";
}

visMoniker uiSelectionMonikers = list {
	"Selection";
}

visMoniker uiDocumentMonikers = list {
	"Document";
}

visMoniker uiClipboard = list {
	"Clipboard";
}

visMoniker uiFile = list {
	"File";
}

visMoniker uiQuickMessage = list {
	"Quick Message";
}

visMoniker uiDefaultTransmitMoniker = 'S', "Send";

visMoniker uiSpoolAddressTransmitMoniker = 'P', "Print";

end	ROStrings;

/**********************************************************************
 *		Send Dialog Box
 **********************************************************************/

start	SendDialogUI, notDetachable;

MSCSendDialog = MailboxSendDialog {
	genStates = default -usable;
	titleMoniker = "\\1 \\2";
	localize "Moniker used in the title of the dialog when you create a message. @1 is replaced by the transport string, like Fax or AOL or Print, while @2 gets the moniker from the list of possible things to be sent, like Current Page or Selection.";
	children =
#if 	_CAN_SELECT_CONTENTS
		MSCContentGroup,
#endif 	/* _CAN_SELECT_CONTENTS */
		MSCDataObjectUI, MSCSendTrigger, MSCCancelTrigger;
	sendTrigger = MSCSendTrigger;
	hints = { 
		HINT_ORIENT_CHILDREN_VERTICALLY,
	}
}

#if _CAN_SELECT_CONTENTS
MSCContentGroup = GenInteraction {
	moniker = 'C', "Contents:";
	children = MSCContentList;
	hints = { HINT_ORIENT_CHILDREN_HORIZONTALLY }
}

MSCContentList = GenDynamicList {
	destination = "TO_OBJ_BLOCK_OUTPUT";
	queryMsg = MSG_MAILBOX_SEND_CONTROL_GET_CONTENTS_MONIKER;
	applyMsg = MSG_MAILBOX_SEND_CONTROL_SET_CONTENTS;
	hints = {
		HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,
		HINT_ITEM_GROUP_MINIMIZE_SIZE
	}
}
#endif /* _CAN_SELECT_CONTENTS */

MSCDataObjectUI = GenInteraction {
#if 	!_CAN_SELECT_CONTENTS
	genStates = default -usable;
#endif	/* !_CAN_SELECT_CONTENTS */
}

MSCSendTrigger = GenTrigger {
	action = MSG_MAILBOX_SEND_CONTROL_GET_ADDRESSES, "TO_OBJ_BLOCK_OUTPUT";
	genStates = default -enabled;
	hints = { HINT_SEEK_REPLY_BAR,
		  HINT_SEEK_SLOT { word 0 },
		  HINT_DEFAULT_DEFAULT_ACTION }
}

MSCCancelTrigger = GenTrigger {
	action = MSG_MSD_CANCEL, MSCSendDialog;
	moniker = "VMO_CANCEL", "Cancel";
	hints = {
		HINT_SEEK_REPLY_BAR,
		/* This is just to make sure we get only one Cancel trigger */
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word	IC_DISMISS
		}
	}
}

/*-----------------------------------------------------------------------------

			  MOT_PAGE_RANGE UI

------------------------------------------------------------------------------*/
MSCPageRangeGroup = GenInteraction {
	genStates = default -usable;
	moniker = "Pages:";
	children = MSCPageAllFromList, MSCPageRangeFrom, MSCPageRangeTo;
	hints = { HINT_ORIENT_CHILDREN_HORIZONTALLY }
}

MSCPageAllFromList = GenItemGroup {
	children = MSCPageAllItem, MSCPageFromItem;
	destination = MSCSendDialog;
	applyMsg = MSG_MSD_ADJUST_ALL_OR_FROM;
	selection = "MSDRT_ALL_PAGES";
	numSelections = 1;
	hints = { HINT_ORIENT_CHILDREN_HORIZONTALLY }
}
MSCPageAllItem = GenItem {
	moniker = 'A', "All";
	identifier = MSDRT_ALL_PAGES;
}

MSCPageFromItem = GenItem {
	moniker = 'F', "From";
	identifier = MSDRT_PAGE_RANGE;
}

MSCPageRangeFrom = GenValue {
	value = 1;
	minimum = 1;
	maximum = 1;	/* Adjusted per notification */
	increment = 1;
	destination = MSCSendDialog;
	applyMsg = MSG_MSD_PAGE_RANGE_BOUNDS_ADJUSTED;
}

MSCPageRangeTo = GenValue {
	moniker = "to";
	value = 1;
	minimum = 1;
	maximum = 1;	/* Adjusted per notification */
	increment = 1;
	destination = MSCSendDialog;
	applyMsg = MSG_MSD_PAGE_RANGE_BOUNDS_ADJUSTED;
}

end SendDialogUI;

start	MSACKiddieRepository, notDetachable;

end	MSACKiddieRepository;
