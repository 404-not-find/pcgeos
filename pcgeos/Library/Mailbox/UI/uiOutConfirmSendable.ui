/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1994 -- All Rights Reserved
 *
 * PROJECT:	  Clavin
 * MODULE:	  UI Specification
 * FILE:	  uiOutConfirmSendable.ui
 *
 * AUTHOR:  	  Adam de Boor: May 24, 1994
 *
 * REVISION HISTORY:
 *	Name	  Date	    Description
 *	----	  ----	    -----------
 *	ardeb	  5/24/94    Initial version
 *
 * DESCRIPTION:
 *	Template resource for a box that confirms a message is
 *	in the outbox and offers a chance to send it.
 *
 *
 * 	$Id: uiOutConfirmSendable.ui,v 1.1 97/04/05 01:19:01 newdeal Exp $
 *
 ***********************************************************************/

#if	! _OUTBOX_SEND_WITHOUT_QUERY

start	OutConfirmSendableUI, notDetachable;

OutConfirmSendableRoot = OutboxSendableConfirmation {
	moniker = "Outbox Confirmation";
	genStates = default -usable;
	children = OutConfirmSendableMessage, OutConfirmSendableText,
		OutConfirmSendNow, OutConfirmSendLater;
	visibility = dialog;
	type = multipleResponse;
	attributes = notUserInitiatable;
	msgGlyph = OutConfirmSendableMessage;
	notifyText = OutConfirmSendableText;
	textHasTwoMarkers = BB_TRUE;
	nowTrigger = OutConfirmSendNow;
	laterTrigger = OutConfirmSendLater;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_WINDOW_CUSTOM_PARENT {
			hptr	0	; use screen as parent
		},
		ATTR_GEN_WINDOW_CUSTOM_LAYER_PRIORITY {
			; keep on top. dont set win prio, though, as we want
			; it to not interfere with control panels, etc.
			LayerPriority	LAYER_PRIO_ON_TOP
		}
	}
}

OutConfirmSendableMessage = MessageGlyph {
	/* moniker set before box put up */
	hints = {
		HINT_CAN_CLIP_MONIKER_WIDTH
	}
}

OutConfirmSendableText = GenText {
	genAttributes = default +readOnly;
	text = "is ready to \\1. Would you like to \\1 it now or later?";
		localize "the two \1 characters are replaced by the appropriate verb for the transport medium (e.g. 'fax' or 'mail')";
}

OutConfirmSendNow = GenTrigger {
	moniker = 'N', "\\1 Now";
		localize "the \1 character is replaced by the appropriate verb for the transport medium (e.g. 'fax' or 'mail')";
	action = MSG_MSND_SEND_MESSAGE_NOW, OutConfirmSendableRoot;
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_DEFAULT_DEFAULT_ACTION,
		HINT_SEEK_REPLY_BAR
	}
}

OutConfirmSendLater = GenTrigger {
	moniker = 'L', "\\1 Later";
		localize "the \1 character is replaced by the appropriate verb for the transport medium (e.g. 'fax' or 'mail')";
	action = MSG_MSND_SEND_MESSAGE_LATER, OutConfirmSendableRoot;
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

end	OutConfirmSendableUI;

#endif	/* ! _OUTBOX_SEND_WITHOUT_QUERY */
