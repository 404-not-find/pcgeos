/***********************************************************************
 *
 *	Copyright (c) Geoworks 1994 -- All Rights Reserved
 *
 * PROJECT:	GEOS
 * MODULE:	uiHyperlink.ui
 * FILE:	uiHyperlink.ui
 *
 * AUTHOR:  	Gene Anderson: Apr 23, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	4/23/92	  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI for setting attributes -- Hyperlink controller
 *
 * 	$Id: hyperlink.ui,v 1.1 97/04/04 18:09:23 newdeal Exp $
 *
 ***********************************************************************/

#include <generic.uih>
#include <hyprlnk.uih>

#include  "pageUI.ui"

start HyperlinkControlUI, notDetachable;

/*****************************************************************************
 *****************************************************************************
 *
 * Set Hyperlink Destination
 *
 *****************************************************************************
 *****************************************************************************/

HyperlinkSetDestBox = GenInteraction {
	moniker = 'S', "Set Destination";
	genStates = default -usable;
	children =
		SetDestFileGroup,
		SetDestPageGroup,
		SetDestSetHyperlinkTrigger,
		SetDestClearHyperlinkTrigger;
	type = command;
	visibility = dialog;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_DIVIDE_WIDTH_EQUALLY,
		HINT_INTERACTION_MAKE_RESIZABLE,
		ATTR_GEN_HELP_CONTEXT { char "dbSetDest", 0 },
		ATTR_GEN_HELP_FILE { char "bindery ", 0 }
	}
}

/*****************************************************************************
 *	Files
 *****************************************************************************/

SetDestFileGroup = GenInteraction {
	moniker = "Files";
	type = organizational;
	children =
		SetDestFileList,
		SetDestAddFileText;
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

SetDestFileList = GenDynamicList {
	queryMsg = MSG_HYPERLINK_CONTROL_FILE_LIST_GET_MONIKER;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	behaviorType = exclusiveNone;
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, SETDEST_LIST_WIDTH>
			SpecHeight <SST_LINES_OF_TEXT, SETDEST_LIST_NUM_ENTRIES>
			word	SETDEST_LIST_NUM_ENTRIES
		},
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word	MSG_HYPERLINK_CONTROL_SETDEST_FILE_CHANGED
		}
	}
}

SetDestAddFileText = GenText {
	moniker = "New File:";
	textDisplayAttributes = default +singleLineText;
	maxLength = "FILE_LONGNAME_LENGTH";
	applyMsg = MSG_HYPERLINK_CONTROL_SETDEST_ADD_FILE_NAME;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, SETDEST_LIST_WIDTH>
			SpecHeight <>
		}
	}	
}

/*****************************************************************************
 *	Pages
 *****************************************************************************/

SetDestPageGroup = GenInteraction {
	moniker = "Pages";
	type = organizational;
	children =
		SetDestPageList,
		SetDestAddPageText;
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

SetDestPageList = GenDynamicList {
	queryMsg = MSG_HYPERLINK_CONTROL_SETDEST_PAGE_LIST_GET_MONIKER;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	behaviorType = exclusiveNone;
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, SETDEST_LIST_WIDTH>
			SpecHeight <SST_LINES_OF_TEXT, \
				    SETDEST_LIST_NUM_ENTRIES>
			word	SETDEST_LIST_NUM_ENTRIES
		},
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word	MSG_HYPERLINK_CONTROL_SETDEST_PAGE_CHANGED
		}
	}
}

SetDestAddPageText = GenText {
	moniker = "New Page:";
	textDisplayAttributes = default +singleLineText;
	maxLength = "MAX_CONTEXT_NAME_SIZE";
	applyMsg = MSG_HYPERLINK_CONTROL_SETDEST_ADD_PAGE_NAME;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, SETDEST_LIST_WIDTH>
			SpecHeight <>
		}
	}	
}

/****************************************************************************
	Set/Clear Selected Hyperlink
*****************************************************************************/

SetDestSetHyperlinkTrigger = GenTrigger {
	moniker = "Set Destination";
	action = MSG_HYPERLINK_CONTROL_SET_HYPERLINK, \
		 "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

SetDestClearHyperlinkTrigger = GenTrigger {
	moniker = "Clear Destination";
	action = MSG_HYPERLINK_CONTROL_CLEAR_HYPERLINK, \
		 "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

/*****************************************************************************
 *****************************************************************************
 *
 * Manage Hyperlink Destinations 
 *
 *****************************************************************************
 *****************************************************************************/

HyperlinkManageDestBox = GenInteraction {
	moniker = 'M', "Manage Destinations";
	genStates = default -usable;
	children =
		ManageDestFileGroup,
		ManageDestPageGroup;
	type = command;
	visibility = dialog;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_INTERACTION_MAKE_RESIZABLE,
		ATTR_GEN_HELP_CONTEXT { char "dbManageDest", 0 },
		ATTR_GEN_HELP_FILE { char "bindery ", 0 }
	}
}

/*****************************************************************************
 *	Files
 *****************************************************************************/

ManageDestFileGroup = GenInteraction {
	type = organizational;
	children =
		ManageDestCurrentFileList,
		ManageDestManageFilesBox;
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_ALLOW_CHILDREN_TO_WRAP
	}
}

ManageDestCurrentFileList = GenDynamicList {
	behaviorType = exclusive;
	selection = 0;
	numSelections = 1;
	queryMsg = MSG_HYPERLINK_CONTROL_FILE_LIST_GET_MONIKER;
	applyMsg = MSG_HYPERLINK_CONTROL_MANAGEDEST_FILE_CHANGED;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_ITEM_GROUP_MINIMIZE_SIZE,
		HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,
		HINT_ALLOW_CHILDREN_TO_WRAP,
		HINT_WRAP_AFTER_CHILD_COUNT {
			word	25
		},
		HINT_PLACE_MONIKER_ABOVE,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, MANAGEDEST_LIST_WIDTH>
			SpecHeight <SST_LINES_OF_TEXT, 1>
		},
	}
}

ManageDestManageFilesBox = GenInteraction {
	moniker = "Manage Files";
	children = ManageFilesFileList, ManageFilesAddFileBox,
		   ManageFilesDeleteFileTrigger, ManageFilesRenameFileBox;
	visibility = dialog;
	type = command;
	hints = {
		HINT_INTERACTION_MAKE_RESIZABLE,
		ATTR_GEN_HELP_CONTEXT { char "dbManageFiles", 0 },
		ATTR_GEN_HELP_FILE { char "bindery ", 0 }
	}
}

/*****************************************************************************/

ManageFilesFileList = GenDynamicList {
	queryMsg = MSG_HYPERLINK_CONTROL_FILE_LIST_GET_MONIKER;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	behaviorType = exclusiveNone;
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, MANAGEDEST_LIST_WIDTH>
			SpecHeight <SST_LINES_OF_TEXT, \
				    MANAGEDEST_LIST_NUM_ENTRIES>
			word	MANAGEDEST_LIST_NUM_ENTRIES
		},
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word	MSG_HYPERLINK_CONTROL_MANAGEFILES_FILE_CHANGED
		}
	}
}

/*****************************************************************************/

ManageFilesAddFileBox = GenInteraction {
	moniker = "Add";
	children = ManageFilesAddFileText, ManageFilesAddFileTrigger;
	visibility = dialog;
	type = command;
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

ManageFilesAddFileText = GenText vardataReloc {
	moniker = "New File:";
	textDisplayAttributes = default +singleLineText;
	maxLength = "FILE_LONGNAME_LENGTH";
	hints = {
	    	ATTR_GEN_TEXT_LEGAL_FILENAMES,
		ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS {
			I_OPTR ManageFilesAddFileTrigger
		},
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, FILE_LONGNAME_LENGTH>
			SpecHeight <>
		}
	}	
}

ManageFilesAddFileTrigger = GenTrigger {
	moniker = "Add";
	genStates = default -enabled;
	action = MSG_HYPERLINK_CONTROL_MANAGEFILES_ADD_FILE_NAME, \
		 "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_DEFAULT_DEFAULT_ACTION,
		HINT_SEEK_REPLY_BAR
	}
}

/*****************************************************************************/

ManageFilesDeleteFileTrigger = GenTrigger {
	moniker = "Delete";
	action = MSG_HYPERLINK_CONTROL_DELETE_FILE_NAME, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

/*****************************************************************************/

ManageFilesRenameFileBox = GenInteraction {
	moniker = "Rename";
	children = ManageFilesRenameFileText,
		   ManageFilesRenameFileTrigger;
	visibility = dialog;
	type = command;
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
		HINT_SEEK_REPLY_BAR
	}
}

ManageFilesRenameFileText = GenText vardataReloc {
	moniker = "Rename File:";
	textDisplayAttributes = default +singleLineText;
	maxLength = "FILE_LONGNAME_LENGTH";
	hints = {
		HINT_DEFAULT_FOCUS,
		ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS {
			I_OPTR ManageFilesRenameFileTrigger
		},
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, FILE_LONGNAME_LENGTH>
			SpecHeight <>
		}
	}	
}

ManageFilesRenameFileTrigger = GenTrigger {
	moniker = "Rename";
	action = MSG_HYPERLINK_CONTROL_CHANGE_FILE_NAME, "TO_OBJ_BLOCK_OUTPUT";
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_DEFAULT_DEFAULT_ACTION,
		HINT_SEEK_REPLY_BAR
	}
}


/*****************************************************************************
 *	Pages
 *****************************************************************************/

ManageDestPageGroup = GenInteraction {
	moniker = "Pages";
	type = organizational;
	children =
		ManageDestPageList, ManageDestAddPageBox,
		ManageDestDeletePageTrigger, ManageDestRenamePageBox;
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

ManageDestPageList = GenDynamicList {
	queryMsg = MSG_HYPERLINK_CONTROL_MANAGEDEST_PAGE_LIST_GET_MONIKER;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	behaviorType = exclusiveNone;
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, MANAGEDEST_LIST_WIDTH>
			SpecHeight <SST_LINES_OF_TEXT, \
				    MANAGEDEST_LIST_NUM_ENTRIES>
			word	MANAGEDEST_LIST_NUM_ENTRIES
		},
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word	MSG_HYPERLINK_CONTROL_MANAGEDEST_PAGE_CHANGED
		}
	}
}


/*****************************************************************************/

ManageDestAddPageBox = GenInteraction {
	moniker = "Add";
	children = ManageDestAddPageText, ManageDestAddPageTrigger;
	visibility = dialog;
	type = command;
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

ManageDestAddPageText = GenText vardataReloc {
	moniker = "New Page:";
	textDisplayAttributes = default +singleLineText;
	maxLength = "MAX_CONTEXT_NAME_SIZE";
	hints = {
		ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS {
			I_OPTR ManageDestAddPageTrigger
		},
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, MAX_CONTEXT_NAME_SIZE>
			SpecHeight <>
		}
	}	
}

ManageDestAddPageTrigger = GenTrigger {
	moniker = "Add";
	genStates = default -enabled;
	action = MSG_HYPERLINK_CONTROL_MANAGEDEST_ADD_PAGE_NAME, \
		 "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_DEFAULT_DEFAULT_ACTION,
		HINT_SEEK_REPLY_BAR
	}
}

/*****************************************************************************/

ManageDestDeletePageTrigger = GenTrigger {
	moniker = "Delete";
	action = MSG_HYPERLINK_CONTROL_DELETE_PAGE_NAME, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

/*****************************************************************************/

ManageDestRenamePageBox = GenInteraction {
	moniker = "Rename";
	children = ManageDestRenamePageText, ManageDestRenamePageTrigger;
	visibility = dialog;
	type = command;
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
		HINT_SEEK_REPLY_BAR
	}
}

ManageDestRenamePageText = GenText vardataReloc {
	moniker = "Rename Page:";
	textDisplayAttributes = default +singleLineText;
	maxLength = "MAX_CONTEXT_NAME_SIZE";
	hints = {
		HINT_DEFAULT_FOCUS,
		ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS {
			I_OPTR ManageDestRenamePageTrigger
		},
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, MAX_CONTEXT_NAME_SIZE>
			SpecHeight <>
		}
	}	
}

ManageDestRenamePageTrigger = GenTrigger {
	moniker = "Rename";
	action = MSG_HYPERLINK_CONTROL_CHANGE_PAGE_NAME, "TO_OBJ_BLOCK_OUTPUT";
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_DEFAULT_DEFAULT_ACTION,
		HINT_SEEK_REPLY_BAR
	}
}

	
/****************************************************************************
	Delete All Hyperlinks
*****************************************************************************/

HyperlinkClearAllHyperlinksTrigger = GenTrigger {
	genStates = default -usable;
	moniker = "Clear All Hyperlinks";
	action = MSG_HYPERLINK_CONTROL_CLEAR_ALL_HYPERLINKS, "TO_OBJ_BLOCK_OUTPUT";
}

/*****************************************************************************
 *	Follow Hyperlink
 *****************************************************************************/

HyperlinkFollowHyperlinkTrigger = GenTrigger {
	genStates = default -usable;
	moniker = "Follow Hyperlink";
	action = MSG_HYPERLINK_CONTROL_FOLLOW_HYPERLINK, "TO_OBJ_BLOCK_OUTPUT";
}

/*****************************************************************************
 *	Show All Hyperlinks
 *****************************************************************************/


HyperlinkOptionsList = GenBooleanGroup {
	genStates = default -usable;
	selectedBooleans = "mask HCO_SHOW_HYPERLINKS";
	children = HyperlinkShowAllHyperlinksBoolean;
	applyMsg = MSG_HYPERLINK_CONTROL_OPTIONS_CHANGED;
	destination = "TO_OBJ_BLOCK_OUTPUT";
} 

HyperlinkShowAllHyperlinksBoolean = GenBoolean {
	moniker = "Show All Hyperlinks";
	identifier = "mask HCO_SHOW_HYPERLINKS";
} 

end HyperlinkControlUI;

/*****************************************************************************
 * 	Strings for controller
 *****************************************************************************/

start	HyperlinkControlStrings, data;

chunk HCName = "Hyperlink";
chunk SetDestinationName = "Set Destination";
chunk ManageDestinationsName = "Manage Destinations";
chunk ClearHyperlinksName = "Clear all Hyperlinks";
chunk FollowHyperlinkName = "Follow Hyperlink";
chunk ShowHyperlinksName = "Show all Hyperlinks";

end	HyperlinkControlStrings;

/*****************************************************************************
 * 	HyperlinkWarning strings
 *****************************************************************************/

start	Strings;

chunk	HyperlinkWarningTable = data {
	/* HW_WILL_DELETE_ALL_HYPERLINKS_IN_DOCUMENT */
	nptr	HyperlinkWarning00
	word	HYPERLINK_WARNING
	/* HW_WILL_DELETE_ALL_HYPERLINKS_TO_FILE */
	nptr	HyperlinkWarning01
	word	HYPERLINK_QUESTION
	/* HW_WILL_DELETE_ALL_HYPERLINKS_TO_PAGE */
	nptr	HyperlinkWarning02
	word	HYPERLINK_QUESTION
	/* HW_WILL_DELETE_ALL_PAGE_NAMES */
	nptr	HyperlinkWarning03
	word	HYPERLINK_WARNING
	/* HW_WILL_DELETE_THIS_PAGES_NAME */
	nptr	HyperlinkWarning04
	word	HYPERLINK_WARNING
}

chunk	HyperlinkWarning00 = "All hyperlinks in the current file will"
			      " be deleted. Do you wish to continue?";

chunk	HyperlinkWarning01 = "All hyperlinks to this file will be"
			      " deleted when you delete the file name."
			      " Do you wish to delete the name?";

chunk	HyperlinkWarning02 = "All hyperlinks to this page will be"
			      " deleted when you delete the page name."
			      " Do you wish to delete the name?";

chunk	HyperlinkWarning03 = "All page names in the current file will"
			     " be cleared from their pages. Do you wish"
			     " to continue?";

chunk	HyperlinkWarning04 = "The name of the page on which the cursor"
			     " is resting will be cleared from that page."
			     "  Do you wish to continue?";

end	Strings;



