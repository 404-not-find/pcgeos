/***********************************************************************
 *
 *	Copyright (c) Geoworks 1994 -- All Rights Reserved
 *
 * PROJECT:	  Condo viewer
 * MODULE:	  send controller
 * FILE:	  sendcontrolControl.ui
 *
 * AUTHOR:  	  Tom Lester, Aug 23, 1994
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	lester	8/23/94   	Initial version
 *
 * DESCRIPTION:
 *	UI for Send controller of content library.
 *
 *
 * 	$Id: sendcontrolControl.ui,v 1.1 97/04/04 17:50:10 newdeal Exp $
 *
 ***********************************************************************/
#define	_NIKE	0

/**********************************************************************
 *			Features UI				      *
 **********************************************************************/

start ContentSendUI, notDetachable;

ContentSendControlMenu = GenInteraction {
	genStates = default -usable;
	moniker = "Send";
	children = ContentSendPrintTrigger,
		   ContentSendCopyTrigger;
}

ContentSendPrintTrigger = GenTrigger {
	moniker = 'P', "To Printer";
	kbdAccelerator = control 'p';
	action = MSG_CSC_PRINT_TEXT, "TO_OBJ_BLOCK_OUTPUT";
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
	hints = {
		HINT_TRIGGER_BRINGS_UP_WINDOW
	}
}

ContentSendCopyTrigger = GenTrigger {
	moniker = 'C', "To Clipboard";
	action = MSG_CSC_COPY_TEXT, "TO_OBJ_BLOCK_OUTPUT";
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
}

end ContentSendUI;


/**********************************************************************
 *			Tool UI					      *
 **********************************************************************/
start ContentSendToolUI, notDetachable;

ContentSendToolSendTrigger = GenTrigger {
	genStates = default -usable;
	moniker = list {
/*#if	!_NO_COLOR_MONIKERS */
		SendCMoniker/*,
/*#endif
		SendMMoniker  */
	}
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
	action = MSG_CSC_INIT_SEND_DIALOG, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_FOCUS_HELP {
			optr	SendFocusHelpString
		}
	}
}

end ContentSendToolUI;



/**********************************************************************
 *	Send Dialog Template 
 **********************************************************************/

start ContentSendTemplate, notDetachable;

/* 
 * 	The Send Dialog
 */
ContentSendDialog = GenInteraction {
	moniker = 'S', "Send";
	genStates = default -usable;
	visibility = dialog;
	type = command;
	attributes = default +modal, +notUserInitiatable; 
	children = ContentSendDialogMessage,
		   ContentSendDialogOptionList,
		   ContentSendDialogApplyTrigger;
	hints = {
		ATTR_GEN_HELP_FILE { char "reader  ", 0 },
		ATTR_GEN_HELP_CONTEXT { char "dbSend", 0 },
		HINT_ORIENT_CHILDREN_VERTICALLY,
	}		
}

ContentSendDialogMessage = GenGlyph {
	moniker = "Send a copy of the current page to:";
}

ContentSendDialogOptionList = GenItemGroup {
	behaviorType = exclusive;
	selection = "CSOIT_CLIPBOARD";
	children = ContentSendDialogOptionItemClipboard,
		   ContentSendDialogOptionItemPrinter;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_CAN_CLIP_MONIKER_WIDTH,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT
	}
}

ContentSendDialogOptionItemClipboard = GenItem {
	moniker = "Clipboard";
	identifier = "CSOIT_CLIPBOARD";
}

ContentSendDialogOptionItemPrinter = GenItem {
	moniker = "Printer";
	identifier = "CSOIT_PRINTER";
}

ContentSendDialogApplyTrigger = GenTrigger {
	moniker = "Send";
	genAttributes = default +signalInteractionComplete, +initiatesBusyState;
	action = MSG_CSC_SEND_TEXT, "TO_OBJ_BLOCK_OUTPUT";
	/* 
	 * The destination is actually set in 
	 * CSCCreateSendDialogAndPrintControl to 
	 * the ContentSendControl object. 
	 */
	hints = {
		HINT_SEEK_REPLY_BAR,
		HINT_DEFAULT_DEFAULT_ACTION,
	}
}

end ContentSendTemplate;


/**********************************************************************
 *	PrintControl Template to duplicate	      *
 **********************************************************************/
start ContentPrintTemplate, notDetachable;

/*
 * Print Control object
 */
ContentSendPrintControl = PrintControl {
	genStates = default -usable;
	attributes = default +notUserInitiatable;
	visibility = dialog;
	printOutput = "TO_OBJ_BLOCK_OUTPUT";
	docNameOutput = "TO_OBJ_BLOCK_OUTPUT";
	/*
	 * In CSCCreateSendDialogAndPrintControl, the 
	 * obj_block_output is set to the ContentSendControl's 
	 * output, which should be set to the ContentGenView object.
	 */
	printAttributes = default -pageControls
		;
}

end ContentPrintTemplate;


/*
 *  Strings for ToolController and 
 *  for the Send Dialog description.
 */
start ContentSendStrings, data;

chunk ContentSendName = "Content Send";
chunk SendMenuName = "Send Menu";

chunk SendToolTriggerName = "Send tool trigger";

chunk SendPageString = "Send a copy of the current page to: ";
chunk SendSelectionString = "Send a copy of the selected text to: ";

chunk SendFocusHelpString = "Send a copy of this page to the clipboard or printer";

end ContentSendStrings;

