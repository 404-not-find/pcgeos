/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1990 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  Configuration Library 
 * FILE:	  config.uih
 *
 * AUTHOR:  	  Chris Boyke
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *
 * DESCRIPTION:
 *	Class definitions for the Config library
 *
 * 	$Id: config.uih,v 1.2 98/04/24 01:29:52 gene Exp $
 *
 ***********************************************************************/
#include "Objects/colorC.uih"

class	Pref = Gen, master, variant {
	prefAttrs = bitFieldComp byte {
		rebootIfChanged,
		loadIfUsable,
		saveIfUsable,
		saveIfEnabled,
		saveIfChanged
	} : loadIfUsable, saveIfUsable;
}

class	PrefInteraction = Pref {
	default Pref = GenInteraction;
	prefInteractionAttrs = bitFieldComp byte {
		loadOptionsOnInitiate, 
		saveOptionsOnApply,
		completeInteractionOnApply 
	} : {};
}

class	PrefContainer = PrefInteraction {
	static /* handle */ wordComp : 0;
	static /* fileName */ typeComp "FileLongName" : "";
	templateMoniker = visMonikerComp;
	static /* dupRoot */ optrComp : null;

	default genStates = default -usable;
}

class	PrefDialog = PrefInteraction {
	default Pref = GenInteraction;	/* So we can set attributes */
	default visibility = dialog;
	default type = properties;
	default attributes = default +notUserInitiatable, +modal;
	default prefInteractionAttrs = default +saveOptionsOnApply;
}


class	TitledGlyph = GenGlyph {
}

class	PrefItemGroup = Pref {
	default Pref = GenItemGroup;

	initFileFlags = bitFieldComp byte {
		useItemStrings, 
		useItemMonikers, 
		appendToKey,
		absentKeyOverridesDefaults,
		suspendOnLoadOptions
#ifdef DO_DBCS
		, numericMonikers
#endif
	} : {};
	originalSelection = wordComp : 0;
	originalNumSelections = wordComp : 0;
	suspendCount = byteComp : 0;
}

class	PrefBooleanGroup = Pref {
	default Pref = GenBooleanGroup;

	initFileFlags = bitFieldComp byte {
		useItemStrings, useItemMonikers, dontCreateCategory
	} : {};
	originalState = wordComp : 0;
}	

class	PrefBoolean = Pref {
	default Pref = GenBoolean;
}

class	PrefStringItem = GenItem {
	initFileString = nptrComp : null;
}

class	PrefValue = Pref {
	default Pref = GenValue;
	originalValue = wordComp : 0;
}

#if DO_DOVE
class	PrefValueZeroPad = PrefValue {
	default Pref = GenValue;
	isHour	= wordComp : BW_FALSE;	/* Hour field has special form */
}
#endif

class	PrefDynamicList = PrefItemGroup {
	default Pref = GenDynamicList;
}	

class	PrefSortedList = PrefDynamicList {
	array = nptrComp : null;
}

class	PrefFileList = PrefSortedList {
}

class	PrefIniDynamicList = PrefDynamicList {
	nameArray = hptrComp : null;
}

class	PrefTocList = PrefDynamicList {
	tocFlags = bitFieldComp word {
			extendedDeviceDrivers
	} : {};
	tokenChars = typeComp "TokenChars" : "<>";
	static dwordComp : 0;		 /* TCS_files */
	static dwordComp : 0;		 /* TCS_devices */
}

class	PrefText = Pref	{
	default Pref = GenText;
	originalText = nptrComp : null;
}

class	PrefTrigger = GenTrigger {
}

class PrefColorSelector = Pref {
	default Pref = ColorSelector;
}

class	PrefControl = PrefInteraction {
	default Pref = GenControl;
	default	genStates = default +enabled;
}

class PrefTimeDateControl = PrefControl {
	timer	= dwordComp : 0;	/* timer handle & ID */
	maxDays	= byteComp : 0;		/* maximum number of days */
	month	= byteComp : 0;		/* current month */
	year	= wordComp : 0;		/* current year */
}

class PrefPortItem = Pref {
	default Pref = PrefStringItem;
	portMask = wordComp : 0;
        type = byteComp : 0;
	static byteComp : 0;
}

#define INIT_FILE_KEY(name)	ATTR_GEN_INIT_FILE_KEY { char #name,0}
#define INIT_FILE_CATEGORY(name) ATTR_GEN_INIT_FILE_CATEGORY { char #name,0}
