/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  skeletonClock.ui
 * FILE:	  skeletonClock.ui
 *
 * AUTHOR:  	  Adam de Boor: Mar  7, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	3/ 7/92	  ardeb	    Initial version
 *
 * DESCRIPTION:
 *	Definitions for skeletal digital clock.
 *
 *
 * 	$Id: skeletonClock.ui,v 1.1 97/04/04 14:51:03 newdeal Exp $
 *
 ***********************************************************************/

class	VisSkeletonClock = VisDigitalClock {
	default selectedPart = "VDCC_TEXT";
	default numParts = "1";	/* Only text color can be set... */
	default colorsPtr = "offset VDCI_colors";
}

start	SkeletonUIResource;

SkeletonClock = VisSkeletonClock {
	optionTree = SkeletonOptions;
#ifdef DO_DBCS
	category = SkeletonCat;
#else
	category = "skeleton clock";
#endif
	firstPartString = SkeletonTextString;
}

#ifdef DO_DBCS
chunk SkeletonCat = data {
	char	'skeleton clock',0
}
#endif


SkeletonOptions = GenInteraction {
	genStates = default -usable;
	children =
		SkeletonClockSizeSpin,
		SkeletonFormatsList;
	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY {
			char "skeleton clock", 0
		},
		HINT_CENTER_CHILDREN_ON_MONIKERS,
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN
	}
}

SkeletonClockSizeSpin = GenValue {
	moniker = "Size:";
	value = "DIGITAL_DEFAULT_SIZE";
	minimum = "DIGITAL_MIN_SIZE";
	maximum = "DIGITAL_MAX_SIZE";
	increment = 1;
	applyMsg = MSG_VDC_SET_SIZE;
	destination = SkeletonClock;
	displayFormat = points;
	hints = {
		ATTR_GEN_INIT_FILE_KEY {
			char "size", 0
		}
	}
}

SkeletonFormatsList = GenItemGroup {
	moniker = "Format:";
	applyMsg = MSG_VDC_SET_FORMAT;
	destination = SkeletonClock;
	children =
		SkeletonFormat12Hour,
		SkeletonFormat24Hour;
	hints = {
		ATTR_GEN_INIT_FILE_KEY {
			char "format", 0
		},
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
	selection = DTF_HM;
}

SkeletonFormat12Hour = GenItem {
	moniker = "12-hour";
	identifier = "DTF_HM";
}

SkeletonFormat24Hour = GenItem {
	moniker = "24-hour";
	identifier = "DTF_HM_24HOUR";
}

chunk SkeletonTextString = "Text";

end	SkeletonUIResource;
