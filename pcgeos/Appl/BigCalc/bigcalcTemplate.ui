/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  bigcalcTemplate.ui
 * FILE:	  bigcalcTemplate.ui
 *
 * AUTHOR:  	  Christian Puscasiu, Apr 29, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	4/29/92	  CP	    Initial version
 *
 * DESCRIPTION:
 *	holds the template blocks that are being duplicated for teh new PCF's
 *	
 *
 * 	$Id: bigcalcTemplate.ui,v 1.2 97/04/18 12:06:21 newdeal Exp $
 *
 ***********************************************************************/


start	FixedArgsPCFTemplateResource;

GenericFixedArgsPCF	= FixedArgsPCF {

	genAttributes	= targetable;
	type		= command;
	visibility	= dialog;
	attributes	= notUserInitiatable;
	genStates	= default -usable;
	formula		= GenericFAPCFFormula;
	children	= GenericFAPCFNotes,
			GenericFAPCFResultGroup,
			GenericFAPCFButtonClose;
	hints		= {
			HINT_ORIENT_CHILDREN_VERTICALLY,
			HINT_CENTER_WINDOW,
			ATTR_GEN_HELP_CONTEXT {
				char "dbFixedArgs", 0
			}
			}
}

GenericFAPCFNotes	= GenText {
	genAttributes   = readOnly;
}

GenericFAPCFResultGroup	= GenInteraction {

	children	= GenericFAPCFResultText,
			GenericFAPCFResultNumberBox,
			GenericFAPCFButtonCalculate;
	hints		= {
			HINT_ORIENT_CHILDREN_HORIZONTALLY,
			HINT_CENTER_CHILDREN_VERTICALLY
			}
}

GenericFAPCFResultText		= GenText {
	genAttributes           = readOnly;

	hints = {
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 30>
			SpecHeight <0, 0>
			word	0
		}
	}
}


GenericFAPCFResultNumberBox	= GenInteraction {

	children	= GenericFAPCFResultNumber;
	hints		= {
			HINT_DRAW_IN_BOX
			}
}

GenericFAPCFResultNumber		= PCFResultDisplay {

	text		= "?";
	genAttributes	= default +targetable;
	maxLength	= NUMBER_DISPLAY_WIDTH;
	textDisplayAttributes = default +singleLineText;
	hints		= {
			ATTR_GEN_TEXT_DEFAULT_PARA_ATTR {
				word DEF_PARA_ATTR_RIGHT
			},
			HINT_DEFAULT_TARGET,
			HINT_FIXED_SIZE {
				SpecWidth <SST_AVG_CHAR_WIDTHS,
					NUMBER_DISPLAY_WIDTH+3>
					SpecHeight <>
			},
			ATTR_GEN_TEXT_EXTENDED_FILTER {
				byte	VTEFT_BEFORE_AFTER
				}
			}
}

GenericFAPCFButtonCalculate	= GenTrigger vardataReloc {

	moniker		= 'C', "Calculate";
	action		= MSG_PCF_CALCULATE, "TO_GEN_PARENT";
	hints		= {
			HINT_DEFAULT_DEFAULT_ACTION,
			ATTR_GEN_DESTINATION_CLASS {
				UN_DD	PreCannedFunctionClass
				}
			}
}

GenericFAPCFButtonClose		= GenTrigger vardataReloc {

	action		= MSG_PCF_CLOSE, "TO_GEN_PARENT";
	hints		= {
			HINT_SEEK_REPLY_BAR,
			ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
				word	IC_DISMISS
				},
			ATTR_GEN_DESTINATION_CLASS {
				UN_DD	PreCannedFunctionClass
				}
			}
} 

/*	Room for copy of formula plus 10 paranoid wchars/chars	*/
chunk	GenericFAPCFFormula 	= data {
#if DO_DBCS
	wchar	MAX_LENGTH_FORMULA_STRING+10	dup(0)
#else
	char	MAX_LENGTH_FORMULA_STRING+10	dup(0)
#endif
}

end	FixedArgsPCFTemplateResource;

start	PCFLineResource;

GenericLine		= GenInteraction {

	genStates	= default -usable;
	children	= GenericLineText,
			GenericLineNumber,
			GenericLineUnit;
	hints		= {
			HINT_ORIENT_CHILDREN_HORIZONTALLY,
			HINT_CENTER_CHILDREN_VERTICALLY,
			HINT_NO_WIDER_THAN_CHILDREN_REQUIRE
			}
}

GenericLineText		= GenText {
	genAttributes   = readOnly;

	hints		= {
			HINT_FIXED_SIZE {
				SpecWidth <SST_AVG_CHAR_WIDTHS, 30>
				SpecHeight <0, 0>
				word	0
				}
			}
}

GenericLineNumber	= FAPCFInputField {

	genAttributes	= default +targetable;
	maxLength	= NUMBER_DISPLAY_WIDTH;
	textDisplayAttributes = default +singleLineText;
	hints           = {
			ATTR_GEN_TEXT_MAKE_UPPERCASE,
			ATTR_GEN_TEXT_DEFAULT_CHAR_ATTR {
				word DEF_CHAR_ATTR_FONT_SIZE(VTDF_BISON, VTDS_12)
			},
			ATTR_GEN_TEXT_DEFAULT_PARA_ATTR {
				word DEF_PARA_ATTR_RIGHT
			},
			HINT_PLACE_MONIKER_TO_RIGHT,
			HINT_TEXT_SELECT_TEXT,
			HINT_FIXED_SIZE {
				SpecWidth <SST_AVG_CHAR_WIDTHS,
					NUMBER_DISPLAY_WIDTH+4>
					SpecHeight <>
				},
			HINT_TEXT_WASH_COLOR {
				ColorQuad <
					C_WHITE,
					CF_INDEX,
					0, 0
						>
					},
			ATTR_GEN_TEXT_FLOAT_DECIMAL,
			ATTR_GEN_TEXT_EXTENDED_FILTER {
				byte	VTEFT_BEFORE_AFTER
				},
			ATTR_GEN_TEXT_DONT_BEEP_ON_INSERTION_ERROR
			}
}

GenericLineUnit			= GenText {
	genAttributes   = readOnly;

	hints		= {
			HINT_FIXED_SIZE {
				SpecWidth <SST_AVG_CHAR_WIDTHS,12>
				SpecHeight <SST_LINES_OF_TEXT, 1>
					word 1
				}
			}
}

end	PCFLineResource;

start	VariableArgsPCFTemplateResource;

GenericVariableArgsPCF	= VariableArgsPCF {

	type		= command;
	visibility	= dialog;
	attributes	= notUserInitiatable;
	genStates	= default -usable;
	formula		= GenericVAPCFFormula;
	children	= GenericVAPCFNotes,
			GenericVAPCFNumbers;
	hints		= {
			HINT_ORIENT_CHILDREN_VERTICALLY,
			HINT_CENTER_WINDOW,
			ATTR_GEN_HELP_CONTEXT {
				char "dbVarArgs", 0
			}
	}
}

GenericVAPCFNotes	= GenText {
	genAttributes = readOnly;
}

GenericVAPCFNumbers	= GenInteraction {

	children	= GenericVAPCFInputGroup,
			GenericVAPCFListGroup,
			GenericVAPCFCommon;
	hints		= {
			HINT_CENTER_CHILDREN_ON_MONIKERS,
			HINT_ORIENT_CHILDREN_VERTICALLY
			}
}

GenericVAPCFInputGroup = GenInteraction {
	moniker		= "New value:";
	children	= GenericVAPCFInputNumber,
			GenericVAPCFAddButton;
	hints		= {
			HINT_EXPAND_WIDTH_TO_FIT_PARENT,
			HINT_ORIENT_CHILDREN_HORIZONTALLY,
			HINT_CENTER_CHILDREN_VERTICALLY,
			HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY

			}
}

GenericVAPCFInputNumber		= VAPCFInputField {
	genAttributes	= default +targetable;
	maxLength	= NUMBER_DISPLAY_WIDTH;
	textDisplayAttributes = default +singleLineText;
	hints           = {
			ATTR_GEN_TEXT_MAKE_UPPERCASE,
			ATTR_GEN_TEXT_DEFAULT_CHAR_ATTR {
				word DEF_CHAR_ATTR_FONT_SIZE(VTDF_BISON, VTDS_12)			},
			ATTR_GEN_TEXT_DEFAULT_PARA_ATTR {
				word DEF_PARA_ATTR_RIGHT
			},
			HINT_TEXT_SELECT_TEXT,
			HINT_DEFAULT_FOCUS,
			HINT_FIXED_SIZE {
				SpecWidth <SST_AVG_CHAR_WIDTHS,
					NUMBER_DISPLAY_WIDTH+4>
					SpecHeight <>
				},
			HINT_TEXT_WASH_COLOR {
				ColorQuad <
					C_WHITE,
					CF_INDEX,
					0, 0
						>
					},
			ATTR_GEN_TEXT_FLOAT_DECIMAL,
			ATTR_GEN_TEXT_EXTENDED_FILTER {
				byte	VTEFT_BEFORE_AFTER
				},
			ATTR_GEN_TEXT_DONT_BEEP_ON_INSERTION_ERROR
			}
}

GenericVAPCFListGroup	= GenInteraction {
	moniker		= "Current values:";
	children	= GenericVAPCFItemGroup,
			GenericVAPCFListButtons;
	hints		= {
			HINT_ORIENT_CHILDREN_HORIZONTALLY,
			HINT_CENTER_CHILDREN_VERTICALLY
			}
}

GenericVAPCFItemGroup	= VAItemGroup 	vardataReloc {
	behaviorType	= exclusiveNone;
	destination	= "TO_GEN_PARENT";
	applyMsg	= MSG_VARIABLE_ARGS_PCF_ITEM_SELECTED;
	hints		= {
			HINT_PLACE_MONIKER_TO_LEFT,
			ATTR_GEN_DESTINATION_CLASS {
				UN_DD	PreCannedFunctionClass
				},
			HINT_FIXED_SIZE {
				SpecWidth <SST_AVG_CHAR_WIDTHS,
						NUMBER_DISPLAY_WIDTH+4>
				SpecHeight <SST_LINES_OF_TEXT, 4>
				word 4
				},
			HINT_ITEM_GROUP_SCROLLABLE
			}
}

GenericVAPCFListButtons	= GenInteraction {

	children	= GenericVAPCFDeleteButton,
			GenericVAPCFResetButton;
	hints		= {
			HINT_ORIENT_CHILDREN_VERTICALLY,
			HINT_CENTER_CHILDREN_HORIZONTALLY
			}
}

GenericVAPCFAddButton		= GenTrigger vardataReloc {

	moniker		= "Add to list";
	genStates	= default -enabled; 
	action		= MSG_VARIABLE_ARGS_PCF_ADD_TO_LIST, "TO_GEN_PARENT";
	hints		= {
			HINT_CENTER_MONIKER,
			HINT_DEFAULT_DEFAULT_ACTION,
			ATTR_GEN_DESTINATION_CLASS {
				UN_DD	PreCannedFunctionClass
				},
			HINT_FIXED_SIZE {
				SpecWidth <SST_AVG_CHAR_WIDTHS, 15>
				SpecHeight <>
				}
			}
}

GenericVAPCFResetButton		= GenTrigger {

	moniker		= "Reset list";
	action		= MSG_VA_ITEM_GROUP_RESET_LIST, null;
	hints		= {
			HINT_CENTER_MONIKER,
			HINT_FIXED_SIZE {
				SpecWidth <SST_AVG_CHAR_WIDTHS, 15>
				SpecHeight <>
				}
			}
}

GenericVAPCFDeleteButton	= GenTrigger vardataReloc {

	moniker		= "Delete Number";
	genStates	= default -enabled;
	action		= MSG_VARIABLE_ARGS_PCF_DELETE_ITEM, "TO_GEN_PARENT";
	hints		= {
			HINT_CENTER_MONIKER,
			ATTR_GEN_DESTINATION_CLASS {
				UN_DD	PreCannedFunctionClass
				},
			HINT_FIXED_SIZE {
				SpecWidth <SST_AVG_CHAR_WIDTHS, 15>
				SpecHeight <>
				}
			}
}

GenericVAPCFCommon		= GenInteraction {

	children	= GenericVAPCFResultNumberBox,
			GenericVAPCFButtonCalculate;
	hints		= {
			HINT_EXPAND_WIDTH_TO_FIT_PARENT,
			HINT_ORIENT_CHILDREN_HORIZONTALLY,
			HINT_CENTER_CHILDREN_VERTICALLY,
			HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY
			}
}

GenericVAPCFResultNumberBox	= GenInteraction {

	children	= GenericVAPCFResultNumber;
	hints	= {
		HINT_DRAW_IN_BOX
		}
}

GenericVAPCFResultNumber		= PCFResultDisplay {

	text		= "?";
	genAttributes	= default +targetable;
	maxLength	= NUMBER_DISPLAY_WIDTH;
	textDisplayAttributes = default +singleLineText;
	hints		= {
			ATTR_GEN_TEXT_DEFAULT_PARA_ATTR {
				word DEF_PARA_ATTR_RIGHT
			},
			HINT_DEFAULT_TARGET,
			HINT_FIXED_SIZE {
				SpecWidth <SST_AVG_CHAR_WIDTHS,
					NUMBER_DISPLAY_WIDTH+3>
					SpecHeight <>
				},
			ATTR_GEN_TEXT_EXTENDED_FILTER {
				byte	VTEFT_BEFORE_AFTER
				}
			}
}

GenericVAPCFButtonCalculate	= GenTrigger vardataReloc {

	moniker		= 'C', "Calculate";
	genStates	= default -enabled;
	action		= MSG_PCF_CALCULATE, "TO_GEN_PARENT";
	hints		= {
			HINT_CENTER_MONIKER,
			ATTR_GEN_DESTINATION_CLASS {
				UN_DD	PreCannedFunctionClass
			},
			HINT_FIXED_SIZE {
				SpecWidth <SST_AVG_CHAR_WIDTHS, 15>
				SpecHeight <>
			}
	}
}


/*	Room for copy of formula plus 10 paranoid wchars/chars	*/
chunk	GenericVAPCFFormula = data {
#if DO_DBCS
	wchar	MAX_LENGTH_FORMULA_STRING+10	dup(0)
#else
	char	MAX_LENGTH_FORMULA_STRING+10	dup(0)
#endif
}

end	VariableArgsPCFTemplateResource;

