/***********************************************************************
 *
 *
 * PROJECT:	Breadbox HomeBase
 * FILE:	hbase.goc 
 * AUTHOR:	John F. Howard,   11/96
 *
 * DESCRIPTION:
 *	This file source code for the HomeBase application.
 *
 * WHATS NEW:   Release Version 1.0
 *              V1.1 new doc token to work w/ all 3
 *              
 *	        
 *		
 *		  
 *		
 *		
 *		
 *              
 *      
 ***********************************************************************/

/*
 ***************************************************************************
 *		Include files
 ***************************************************************************
 */

@include <stdapp.goh>
@include "hbase.goh"
@include "hbicons.goh"
@include "bblogo.goh"
@include "btns.goh"


/*
 ***************************************************************************
 *		Class & Method Declarations
 ***************************************************************************
 */
/* The Content & VLTextClass have to be defined here to avoid a
   glue error.  And all the objects and methods of these two
   classes have to be in the same source file!!*/

/* This does the initialization work for the text object */
@class HBaseContentClass, GenContentClass;
@endc
@classdecl	HBaseContentClass;

/* This is needed to intercept and initialize for LargeText model
 * and for printing   */
@class HBaseVLTextClass, VisLargeTextClass;
@endc
@classdecl	HBaseVLTextClass;

@classdecl	HBaseProcessClass, neverSaved;
@classdecl	HBaseApplicationClass;
@classdecl	HBaseTriggerClass;

/* methods in the doc methods .goc file */
@extern method HBaseProcessClass, MSG_META_DOC_OUTPUT_INITIALIZE_DOCUMENT_FILE;
@extern method HBaseProcessClass, MSG_META_DOC_OUTPUT_ATTACH_UI_TO_DOCUMENT;
@extern method HBaseProcessClass, MSG_META_DOC_OUTPUT_DETACH_UI_FROM_DOCUMENT;
@extern method HBaseProcessClass, MSG_META_DOC_OUTPUT_SAVE_AS_COMPLETED;
@extern method HBaseProcessClass, MSG_GEN_PROCESS_INSTALL_TOKEN;
@extern method HBaseProcessClass, MSG_GEN_PROCESS_OPEN_APPLICATION;

/* methods in the field methods .goc file*/
@extern method HBaseProcessClass, MSG_OPEN_CHANGE_NAME;
@extern method HBaseProcessClass, MSG_OPEN_CHANGE_NUMBER;
@extern method HBaseProcessClass, MSG_CHANGE_NAME;
@extern method HBaseProcessClass, MSG_CHANGE_NUMBER;
@extern method HBaseProcessClass, MSG_DISPLAY_NAMES;
@extern method HBaseProcessClass, MSG_DISPLAY_FIELDS;

/* methods in the data methods .goc file*/
@extern method HBaseProcessClass, MSG_ENTER_DATA;
@extern method HBaseProcessClass, MSG_MODIFY_DATA;
@extern method HBaseProcessClass, MSG_DELETE_DATA;
@extern method HBaseProcessClass, MSG_NEW_RECORD;

/* methods in the navigation methods .goc file*/
@extern method HBaseProcessClass, MSG_NAV_RECORDS;
@extern method HBaseProcessClass, MSG_INDEX_DLIST_MONIKER_QUERY;
@extern method HBaseProcessClass, MSG_INDEX_DLIST_APPLY;
@extern method HBaseTriggerClass, MSG_META_START_SELECT;
@extern method HBaseTriggerClass, MSG_HB_TRIGGER_CLICK;
@extern method HBaseTriggerClass, MSG_META_LOST_MOUSE_EXCL;

/* methods in the import methods .goc file*/
@extern method HBaseProcessClass, MSG_OPEN_IMPORT;
@extern method HBaseProcessClass, MSG_IMPORT_CSV;
@extern method HBaseProcessClass, MSG_IMPORT_SELECTOR_ACTION;
@extern method HBaseApplicationClass, MSG_CANCEL_IMPORT;

/* methods in the report methods .goc file*/
@extern method HBaseProcessClass, MSG_CREATE_REPORT;
@extern method HBaseProcessClass, MSG_ALL_RECORDS_APPLY;
@extern method HBaseProcessClass, MSG_FROM_TO_APPLY;
@extern method HBaseProcessClass, MSG_FROM_VAL_STATUS;
@extern method HBaseProcessClass, MSG_TO_VAL_STATUS;
@extern method HBaseProcessClass, MSG_SUM_SELECT_APPLY;
@extern method HBaseProcessClass, MSG_FIRST_ONLY_APPLY;

/* methods in the export methods .goc file*/
@extern method HBaseProcessClass, MSG_OPEN_EXPORT;
@extern method HBaseProcessClass, MSG_EXPORT_CSV;

/* methods in the search methods .goc file*/
@extern method HBaseProcessClass, MSG_SEARCH_FIELDS;

/*
 ***************************************************************************
 *		UI Objects
 ***************************************************************************
 */

@start  AppResource;
@object HBaseApplicationClass HBaseApp = {
    GI_visMoniker = list {
	 @HBLCAppMoniker,
	 @HBaseTextMoniker
	 }
    GI_comp = @HBasePrimary;
    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) = @HBasePrimary;
    gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST) = @HBaseDocumentControl;
    gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_APP_STARTUP) = @HBaseDocumentControl;
    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_SELF_LOAD_OPTIONS) =
			 @HBaseDocumentControl, @HBasePrintControl,
			 @HBaseEditControl;
    ATTR_GEN_APPLICATION_PRINT_CONTROL = @HBasePrintControl;
    ATTR_GEN_HELP_FILE = "HBHelp";
    HINT_APP_IS_PRODUCTIVITY_ORIENTED;
}

@visMoniker HBaseTextMoniker = "HomeBase";
    @localize "app moniker on title bar";
@end AppResource

@start	Interface;
@object GenPrimaryClass HBasePrimary = {
    GI_comp = @HBFileMenu, @FieldMenu, @ReportDBox,
	      @MainGroup, @HBDialogs, @SearchDBox;
    ATTR_GEN_HELP_CONTEXT = "TOC";    /* the help file Entry Point */
    HINT_SIZE_WINDOW_AS_DESIRED;
    HINT_NO_WIDER_THAN_CHILDREN_REQUIRE;
    HINT_NO_TALLER_THAN_CHILDREN_REQUIRE;
}
@end Interface;


@start Menu;
@object GenInteractionClass HBFileMenu = {
    GI_comp = @HBaseDocumentControl, @ImpExSubGroup;
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = (GIGT_FILE_MENU);
}

@object GenDocumentControlClass HBaseDocumentControl = {
    GDCI_features = @default & ~GDCF_SUPPORTS_TEMPLATES;
    GDCI_documentToken = {"HBDA", 16431};
    GDCI_noNameText = "No File";
    @localize "Shown on title bar if no file is acitve";
    GDCI_documentGroup = HBaseDocumentGroup;
    GDCI_enableDisableList = HBEnableList;
}

@chunk optr HBEnableList[] = {
    @MainGroup, @ImpExSubGroup, @FieldMenu,
    @ReportDBox, @SearchDBox
};

@object GenInteractionClass ImpExSubGroup = {
    GI_comp = @ImportSMenu, @ExportSMenu;
    GII_visibility = GIV_SUB_GROUP;
}

@object GenTriggerClass ImportSMenu = {
    GI_visMoniker = 'I', "Import";
    @localize "Import item in File menu";
    GTI_destination = process;
    GTI_actionMsg = MSG_OPEN_IMPORT;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
}

@object GenTriggerClass ExportSMenu = {
    GI_visMoniker = 'E', "Export";
    @localize "Export item in File menu";
    GTI_destination = process;
    GTI_actionMsg = MSG_OPEN_EXPORT;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
}

@object GenInteractionClass FieldMenu = {
    GI_visMoniker = 'd', "Fields";
    @localize "Fields main menu item";
    GI_comp = @FieldNameSM, @NumFieldsSM;
    GI_states = @default & ~GS_ENABLED; /* on doc enable list */
    GII_visibility = GIV_POPUP;
}

@object GenTriggerClass FieldNameSM = {
    GI_visMoniker = 'F', "Field Names";
    @localize "item in Fields menu";
    GTI_destination = process;
    GTI_actionMsg = MSG_OPEN_CHANGE_NAME;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
}

@object GenTriggerClass NumFieldsSM = {
    GI_visMoniker = 'N', "Number of Fields";
    @localize "item in Fields menu";
    GTI_destination = process;
    GTI_actionMsg = MSG_OPEN_CHANGE_NUMBER;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
}

@end Menu;

@start DocumentUI;
@object GenDocumentGroupClass HBaseDocumentGroup ={
    GDGI_untitledName = "HBase untitled";
    @localize "untitled HBase document";
    GDGI_protocolMajor = 1;
    GDGI_protocolMinor = 0;
    GDGI_output = process;
    GDGI_documentControl = HBaseDocumentControl;
}
@end DocumentUI;

/* ----------------------------------------------------------- */

@start Interface;
@object GenInteractionClass MainGroup = {
    GI_comp = @Info_Buttons, @FieldsGroup, @Controls; 
    GI_states = @default & ~GS_ENABLED; /* on doc enable list */
    GI_attrs = @default | GA_TARGETABLE;
    HINT_NO_TALLER_THAN_CHILDREN_REQUIRE;
    HINT_NO_WIDER_THAN_CHILDREN_REQUIRE;
}

@object GenInteractionClass Info_Buttons = {
    GI_comp = @BBLogo, @Buttons, @NumberBox;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY;
    HINT_CENTER_CHILDREN_VERTICALLY;
}

@object GenInteractionClass BBLogo = {
    GI_visMoniker = list {@BreadboxmonikerColor};
}

@object GenInteractionClass NumberBox = {
    GI_comp = @TitleGlyph, @RecordsSect;
    HINT_DRAW_IN_BOX;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
}

@object GenGlyphClass TitleGlyph = {
    GI_visMoniker = "Records";
    @localize "title of Records count info area";
    HINT_CENTER_MONIKER;
}

@object GenInteractionClass RecordsSect = {
    GI_comp = @CurrentRec, @TotalRecs;
    HINT_NO_WIDER_THAN_CHILDREN_REQUIRE;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY;
    HINT_INCLUDE_ENDS_IN_CHILD_SPACING;
}

@object GenInteractionClass CurrentRec = {
    GI_comp = @CRTitle, @CRNumber;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
}

@object GenGlyphClass CRTitle = {
    GI_visMoniker = "Current";
    @localize "lable for record # showing on the screen";
}

@object GenGlyphClass CRNumber = {
    GI_visMoniker = "-";
    @localize "indicates no record showing";
}

@object GenInteractionClass TotalRecs = {
    GI_comp = @TRTitle, @TRNumber;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
}

@object GenGlyphClass TRTitle = {
    GI_visMoniker = "Total";
    @localize "lable for total # of records in the database";
}

@object GenGlyphClass TRNumber = {
    GI_visMoniker = "0";
    @localize "default for no records in database";
}

@object GenInteractionClass Buttons = {
    GI_comp = @FirstButton, @PrevButton,
	      @NextButton, @LastButton;
    HINT_DRAW_IN_BOX;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY;
    HINT_INCLUDE_ENDS_IN_CHILD_SPACING;
}

@object GenInteractionClass FirstButton = {
    GI_comp = @FirstIcon, @FirstTextGlyph;
    GI_states = @default & ~GS_ENABLED;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_MINIMIZE_CHILD_SPACING;
}

@object GenTriggerClass FirstIcon = {
    GI_visMoniker = list {@FirstButtonIcon};
    GTI_destination = process;
    GTI_actionMsg = MSG_NAV_RECORDS;
    ATTR_GEN_TRIGGER_ACTION_DATA = FIRST;
}

@object GenGlyphClass FirstTextGlyph = {
    GI_visMoniker = list{@FirstTextMoniker};
    HINT_CENTER_MONIKER;
}

@visMoniker FirstTextMoniker = {
    gstring {
	GSSaveTransform(),
	GSSetFont(FID_DTC_URW_SANS, 12),
	GSDrawText(2,2),
	"First",
	GSRestoreTransform(),
	GSEndString()
    }
}

@object GenInteractionClass PrevButton = {
    GI_comp = @PrevIcon, @PrevTextGlyph;
    GI_states = @default & ~GS_ENABLED;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_MINIMIZE_CHILD_SPACING;
}

@object HBaseTriggerClass PrevIcon = {
    GI_visMoniker = list {@PrevButtonIcon};
    GTI_destination = process;
    GTI_actionMsg = MSG_NAV_RECORDS;
    ATTR_GEN_TRIGGER_ACTION_DATA = PREV;
}

@object GenGlyphClass PrevTextGlyph = {
    GI_visMoniker = list{@PrevTextMoniker};
    HINT_CENTER_MONIKER;
}

@visMoniker PrevTextMoniker = {
    gstring {
	GSSaveTransform(),
	GSSetFont(FID_DTC_URW_SANS, 12),
	GSDrawText(2,2),
	"Prev",
	GSRestoreTransform(),
	GSEndString()
    }
}

@object GenInteractionClass NextButton = {
    GI_comp = @NextIcon, @NextTextGlyph;
    GI_states = @default & ~GS_ENABLED;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_MINIMIZE_CHILD_SPACING;
}

@object HBaseTriggerClass NextIcon = {
    GI_visMoniker = list {@NextButtonIcon};
    GTI_destination = process;
    GTI_actionMsg = MSG_NAV_RECORDS;
    ATTR_GEN_TRIGGER_ACTION_DATA = NEXT;
}

@object GenGlyphClass NextTextGlyph = {
    GI_visMoniker = list{@NextTextMoniker};
    HINT_CENTER_MONIKER;
}

@visMoniker NextTextMoniker = {
    gstring {
	GSSaveTransform(),
	GSSetFont(FID_DTC_URW_SANS, 12),
	GSDrawText(2,2),
	"Next",
	GSRestoreTransform(),
	GSEndString()
    }
}

@object GenInteractionClass LastButton = {
    GI_comp = @LastIcon, @LastTextGlyph;
    GI_states = @default & ~GS_ENABLED;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_MINIMIZE_CHILD_SPACING;
}

@object GenTriggerClass LastIcon = {
    GI_visMoniker = list {@LastButtonIcon};
    GTI_destination = process;
    GTI_actionMsg = MSG_NAV_RECORDS;
    ATTR_GEN_TRIGGER_ACTION_DATA = LAST;
}

@object GenGlyphClass LastTextGlyph = {
    GI_visMoniker = list{@LastTextMoniker};
    HINT_CENTER_MONIKER;
}

@visMoniker LastTextMoniker = {
    gstring {
	GSSaveTransform(),
	GSSetFont(FID_DTC_URW_SANS, 12),
	GSDrawText(2,2),
	"Last",
	GSRestoreTransform(),
	GSEndString()
    }
}

@object GenInteractionClass FieldsGroup = {
    GI_comp = @Fields, @ListGroup;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_NO_TALLER_THAN_CHILDREN_REQUIRE;
    HINT_NO_WIDER_THAN_CHILDREN_REQUIRE;
}

@object GenInteractionClass Fields = {
    GI_comp = @Field1, @Field2, @Field3, @Field4,
	      @Field5, @Field6, @Field7, @Field8,
	      @Field9, @Field10;
    HINT_CENTER_CHILDREN_ON_MONIKERS;
    HINT_NO_WIDER_THAN_CHILDREN_REQUIRE;
    HINT_NO_TALLER_THAN_CHILDREN_REQUIRE;
}

@object GenTextClass Field1 = {
    GI_visMoniker = "Field 1";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (DATA_LEN - 1);
    ATTR_GEN_TEXT_CUSTOM_FILTER = (ChunkHandle) @FieldFilter;
    HINT_TEXT_WHITE_WASH_COLOR;
    HINT_DEFAULT_FOCUS;
}

@object GenTextClass Field2 = {
    GI_visMoniker = "Field 2";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (DATA_LEN - 1);
    ATTR_GEN_TEXT_CUSTOM_FILTER = (ChunkHandle) @FieldFilter;
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass Field3 = {
    GI_visMoniker = "Field 3";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (DATA_LEN - 1);
    ATTR_GEN_TEXT_CUSTOM_FILTER = (ChunkHandle) @FieldFilter;
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass Field4 = {
    GI_visMoniker = "Field 4";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (DATA_LEN - 1);
    ATTR_GEN_TEXT_CUSTOM_FILTER = (ChunkHandle) @FieldFilter;
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass Field5 = {
    GI_visMoniker = "Field 5";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (DATA_LEN - 1);
    ATTR_GEN_TEXT_CUSTOM_FILTER = (ChunkHandle) @FieldFilter;
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass Field6 = {
    GI_visMoniker = "Field 6";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (DATA_LEN - 1);
    ATTR_GEN_TEXT_CUSTOM_FILTER = (ChunkHandle) @FieldFilter;
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass Field7 = {
    GI_visMoniker = "Field 7";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (DATA_LEN - 1);
    ATTR_GEN_TEXT_CUSTOM_FILTER = (ChunkHandle) @FieldFilter;
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass Field8 = {
    GI_visMoniker = "Field 8";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (DATA_LEN - 1);
    ATTR_GEN_TEXT_CUSTOM_FILTER = (ChunkHandle) @FieldFilter;
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass Field9 = {
    GI_visMoniker = "Field 9";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (DATA_LEN - 1);
    ATTR_GEN_TEXT_CUSTOM_FILTER = (ChunkHandle) @FieldFilter;
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass Field10 = {
    GI_visMoniker = "Field 10";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (DATA_LEN - 1);
    ATTR_GEN_TEXT_CUSTOM_FILTER = (ChunkHandle) @FieldFilter;
    HINT_TEXT_WHITE_WASH_COLOR;
}

/* allows all but tilde / */
@chunkArray VisTextCustomFilterData FieldFilter = {
	C_ASCII_TILDE, C_ASCII_TILDE
	};

@object GenInteractionClass ListGroup = {
    GI_comp = @IndexDList;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    HINT_ALIGN_TOP_EDGE_WITH_OBJECT = @FieldsGroup;
    HINT_ALIGN_BOTTOM_EDGE_WITH_OBJECT = @FieldsGroup;
}

@object GenDynamicListClass IndexDList = {
    GI_visMoniker = "Records List";
    @localize "title over records scrolling list window";
    GDLI_numItems = 0;
    GIGI_selection = 0;
    GIGI_applyMsg = MSG_INDEX_DLIST_APPLY;
    GIGI_destination = process;
    GDLI_queryMsg = MSG_INDEX_DLIST_MONIKER_QUERY;
    HINT_INITIAL_SIZE = {
		SST_AVG_CHAR_WIDTHS | 35,
		0,
		0};
    HINT_ITEM_GROUP_SCROLLABLE;
    HINT_ALIGN_TOP_EDGE_WITH_OBJECT = @FieldsGroup;
    HINT_ALIGN_BOTTOM_EDGE_WITH_OBJECT = @FieldsGroup;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    HINT_CENTER_MONIKER;
    HINT_PLACE_MONIKER_ABOVE;
}

@object GenInteractionClass Controls = {
    GI_comp = @EnterButton, @ModButton, @DeleteButton,
              @NewButton;
    HINT_MAKE_REPLY_BAR;
}

@object GenTriggerClass EnterButton = {
    GI_visMoniker = "Add";
    @localize "button to add record to database";
    GTI_destination = process ;
    GTI_actionMsg = MSG_ENTER_DATA;
}

@object GenTriggerClass ModButton = {
    GI_visMoniker = "Modify";
    @localize "button to store modified record to database";
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = process ;
    GTI_actionMsg = MSG_MODIFY_DATA;
}

@object GenTriggerClass DeleteButton = {
    GI_visMoniker = "Delete";
    @localize "button to delete record from database";
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = process ;
    GTI_actionMsg = MSG_DELETE_DATA;
}

@object GenTriggerClass NewButton = {
    GI_visMoniker = "New";
    @localize "button to display empty record form";
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = process ;
    GTI_actionMsg = MSG_NEW_RECORD;
}

/* -------------------------------------------------------------- */
@object GenInteractionClass ReportDBox = {
    GI_visMoniker = 'R', "Report";
    @localize "title of report creation dialog box";
    GI_comp = @RDBOptions, @RepCreate, @RepCanx;
    GII_visibility = GIV_DIALOG;
    HINT_SEEK_MENU_BAR;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    ATTR_GEN_HELP_CONTEXT = "RptDB";    /* the help file Entry Point */
}

@object GenInteractionClass RDBOptions = {
    GI_visMoniker = "Options";
    @localize "Title of the Options section in the Create a Report dialog box";
    GI_comp = @RDBTitleOpts, @RDBRecordOpts, @RDBFieldOpts;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_PLACE_MONIKER_ABOVE;
    HINT_CENTER_MONIKER;
    HINT_DRAW_IN_BOX;
}

@object GenInteractionClass RDBTitleOpts = {
    GI_visMoniker = "Title";
    @localize "Title of the Title section in the Create a Report dialog box";
    GI_comp = @RDBFileSel, @RDBDateSel;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_PLACE_MONIKER_ABOVE;
    HINT_CENTER_MONIKER;
    HINT_DRAW_IN_BOX;
}

@object GenItemGroupClass RDBFileSel = {
    GI_comp = @RDBFile;
    GIGI_behaviorType = GIGBT_EXCLUSIVE_NONE;
    GIGI_selection = R_TITLE;
}

@object GenItemClass RDBFile = {
    GI_visMoniker = "File Name as Title";
    @localize "Moniker of the by date selector option in the Create a Report dialog box";
    GII_identifier = R_TITLE;
}

@object GenItemGroupClass RDBDateSel = {
    GI_comp = @RDBDate;
    GIGI_behaviorType = GIGBT_EXCLUSIVE_NONE;
    GIGI_selection = R_DATE;
}

@object GenItemClass RDBDate = {
    GI_visMoniker = "Today's Date in Title";
    @localize "Moniker of the by date selector option in the Create a Report dialog box";
    GII_identifier = R_DATE;
}

@object GenInteractionClass RDBRecordOpts = {
    GI_visMoniker = "Records";
    @localize "Title of the Records section in the Create a Report dialog box";
    GI_comp = @RDBRAllSel, @RDBRFromToSect;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_PLACE_MONIKER_ABOVE;
    HINT_CENTER_MONIKER;
    HINT_DRAW_IN_BOX;
}

@object GenItemGroupClass RDBRAllSel = {
    GI_comp = @RDBRAll;
    GIGI_behaviorType = GIGBT_EXCLUSIVE_NONE;
    GIGI_destination = process;
    GIGI_applyMsg = MSG_ALL_RECORDS_APPLY;
    GIGI_selection = R_ALL;
}

@object GenItemClass RDBRAll = {
    GI_visMoniker = "All";
    @localize "option to report all the records in the database";
    GII_identifier = R_ALL;
}

@object GenInteractionClass RDBRFromToSect = {
    GI_comp = @RDBRFromSel, @RDBRFromVal,
              @RDBRToGlyph, @RDBRToVal;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
}

@object GenItemGroupClass RDBRFromSel = {
    GI_comp = @RDBRFrom;
    GIGI_behaviorType = GIGBT_EXCLUSIVE_NONE;
    GIGI_destination = process;
    GIGI_applyMsg = MSG_FROM_TO_APPLY;
}

@object GenItemClass RDBRFrom = {
    GI_visMoniker = "From ";
    @localize "Moniker of the from record number selector";
    GII_identifier = R_FROM;
}

@object GenValueClass RDBRFromVal = {
    GI_states = @default & ~GS_ENABLED;
    GVLI_minimum = MakeWWFixed(1.0);
    GVLI_maximum = MakeWWFixed(10.0);
    GVLI_value = MakeWWFixed(1.0);
    GVLI_destination = process;
    ATTR_GEN_VALUE_STATUS_MSG = MSG_FROM_VAL_STATUS;
}

@object GenGlyphClass RDBRToGlyph = {
    GI_visMoniker = "to ";
    @localize "Moniker of the to record number selector";
}

@object GenValueClass RDBRToVal = {
    GI_states = @default & ~GS_ENABLED;
    GVLI_minimum = MakeWWFixed(1.0);
    GVLI_maximum = MakeWWFixed(10.0);
    GVLI_value = MakeWWFixed(1.0);
    GVLI_destination = process;
    ATTR_GEN_VALUE_STATUS_MSG = MSG_TO_VAL_STATUS;
}

@object GenInteractionClass RDBFieldOpts = {
    GI_visMoniker = "Fields";
    @localize "Title of the Fields section in the Create a Report dialog box";
    GI_comp = @RDBFNameSel, @RDBFirstOnlySel, @RDBSumSect;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_PLACE_MONIKER_ABOVE;
    HINT_CENTER_MONIKER;
    HINT_DRAW_IN_BOX;
}

@object GenItemGroupClass RDBFNameSel = {
    GI_comp = @RDBFName;
    GIGI_behaviorType = GIGBT_EXCLUSIVE_NONE;
    GIGI_selection = R_FNAME;
}

@object GenItemClass RDBFName = {
    GI_visMoniker = "Show Field Names";
    @localize "option to show field names in the report";
    GII_identifier = R_FNAME;
}

@object GenItemGroupClass RDBFirstOnlySel = {
    GI_comp = @RDBFirst;
    GIGI_behaviorType = GIGBT_EXCLUSIVE_NONE;
    GIGI_destination = process;
    GIGI_applyMsg = MSG_FIRST_ONLY_APPLY;
}

@object GenItemClass RDBFirst = {
    GI_visMoniker = "Show 1st field only";
    @localize "option to list only the first field info in the report ";
    GII_identifier = R_FIRST;
}

@object GenInteractionClass RDBSumSect = {
    GI_comp = @RDBSumSel, @RDBWhichSumSel;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
}

@object GenItemGroupClass RDBSumSel = {
    GI_comp = @RDBSum;
    GIGI_behaviorType = GIGBT_EXCLUSIVE_NONE;
    GIGI_destination = process;
    GIGI_applyMsg = MSG_SUM_SELECT_APPLY;
}

@object GenItemClass RDBSum = {
    GI_visMoniker = "Total for field:";
    @localize "option to show the sum of the select field in the report";
    GII_identifier = R_SUM;
}

@object GenValueClass RDBWhichSumSel = {
    GI_states = @default & ~GS_ENABLED;
    GVLI_minimum = MakeWWFixed(1.0);
    GVLI_maximum = MakeWWFixed(10.0);
    GVLI_value = MakeWWFixed(1.0);
}

@object GenTriggerClass RepCreate = {
    GI_visMoniker = "Create";
    @localize "button to create the report";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = process;
    GTI_actionMsg = MSG_CREATE_REPORT;
    HINT_SEEK_REPLY_BAR;
    HINT_DEFAULT_DEFAULT_ACTION;
}

@object GenTriggerClass RepCanx = {
    GI_visMoniker = "Cancel";
    @localize "button to cancel the report dialog box";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    HINT_SEEK_REPLY_BAR;
}

/* -------------------------------------------------------------- */
@object GenInteractionClass SearchDBox = {
    GI_visMoniker = 'S', "Search";
    @localize "title of the search dialog box";
    GI_comp = @SDBSearchText, @SDBOptions, @SDBSearch,
	      @SDBSearchNext, @SDBCanx;
    GII_visibility = GIV_DIALOG;
    HINT_SEEK_MENU_BAR;
    ATTR_GEN_HELP_CONTEXT = "SrchDB";    /* the help file Entry Point */
/*    HINT_INTERACTION_NO_DISTURB;  /* see what happens */
}

@object GenTextClass SDBSearchText = {
    GI_visMoniker = "Search for:";
    @localize "label of the search for text entry window";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (DATA_LEN - 1);
    HINT_DEFAULT_FOCUS;
}

@object GenInteractionClass SDBOptions = {
    GI_comp = @SDBFieldOpts, @SDBCaseOpts;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY;
    HINT_INCLUDE_ENDS_IN_CHILD_SPACING;
}

@object GenItemGroupClass SDBFieldOpts = {
    GI_comp = @SDBField;
    GIGI_behaviorType = GIGBT_EXCLUSIVE_NONE;
}

@object GenItemClass SDBField = {
    GI_visMoniker = "First Field Only";
    @localize "option to search in the first field only";
    GII_identifier = S_FIELD;
}

@object GenItemGroupClass SDBCaseOpts = {
    GI_comp = @SDBCase;
    GIGI_behaviorType = GIGBT_EXCLUSIVE_NONE;
}

@object GenItemClass SDBCase = {
    GI_visMoniker = "Ignore Case";
    @localize "option to ignore the case of the search text";
    GII_identifier = S_CASE;
}

@object GenTriggerClass SDBSearch = {
    GI_visMoniker = "Search";
    @localize "button to start searching";
    GTI_destination = process;
    GTI_actionMsg = MSG_SEARCH_FIELDS;
    ATTR_GEN_TRIGGER_ACTION_DATA = FIRST;
    HINT_SEEK_REPLY_BAR;
    HINT_DEFAULT_DEFAULT_ACTION;
}

@object GenTriggerClass SDBSearchNext = {
    GI_visMoniker = "Search next";
    @localize "button to continue searching from this point in the database";
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = process;
    GTI_actionMsg = MSG_SEARCH_FIELDS;
    ATTR_GEN_TRIGGER_ACTION_DATA = NEXT;
    HINT_SEEK_REPLY_BAR;
}

@object GenTriggerClass SDBCanx = {
    GI_visMoniker = "Cancel";
    @localize "button to cancel the search dialog box";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    HINT_SEEK_REPLY_BAR;
}

@end Interface;

/* ------------------------------------------------------------- */

@start Dialogs;
@object GenInteractionClass HBDialogs = {
    GI_comp = @ChangeNameDB, @ChangeNumberDB, @ImportDBox,
              @ImpStatusDBox, @ReportGroup, @ExportDBox;
    GII_type = GIT_ORGANIZATIONAL;
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default | GIA_NOT_USER_INITIATABLE;
}

@object GenInteractionClass ChangeNameDB = {
    GI_visMoniker = "Change Field Names";
    @localize "title of the change field names dialog box";
    GI_comp = @CName1, @CName2, @CName3, @CName4,
	      @CName5, @CName6, @CName7, @CName8,
	      @CName9, @CName10, @ChangeButton;
    GII_visibility = GIV_DIALOG;
    HINT_CENTER_CHILDREN_ON_MONIKERS;
}

@object GenTextClass CName1 = {
    GI_visMoniker = "Field 1";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (FIELD_NAME_LEN - 1);
    HINT_TEXT_WHITE_WASH_COLOR;
    HINT_DEFAULT_FOCUS;
}

@object GenTextClass CName2 = {
    GI_visMoniker = "Field 2";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (FIELD_NAME_LEN - 1);
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass CName3 = {
    GI_visMoniker = "Field 3";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (FIELD_NAME_LEN - 1);
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass CName4 = {
    GI_visMoniker = "Field 4";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (FIELD_NAME_LEN - 1);
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass CName5 = {
    GI_visMoniker = "Field 5";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (FIELD_NAME_LEN - 1);
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass CName6 = {
    GI_visMoniker = "Field 6";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (FIELD_NAME_LEN - 1);
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass CName7 = {
    GI_visMoniker = "Field 7";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (FIELD_NAME_LEN - 1);
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass CName8 = {
    GI_visMoniker = "Field 8";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (FIELD_NAME_LEN - 1);
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass CName9 = {
    GI_visMoniker = "Field 9";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (FIELD_NAME_LEN - 1);
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTextClass CName10 = {
    GI_visMoniker = "Field 10";
    @localize "default field name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (FIELD_NAME_LEN - 1);
    HINT_TEXT_WHITE_WASH_COLOR;
}

@object GenTriggerClass ChangeButton = {
    GI_visMoniker = "Change";
    @localize "button to enter the changed field name(s)";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = process;
    GTI_actionMsg = MSG_CHANGE_NAME;
    HINT_DEFAULT_DEFAULT_ACTION;
    HINT_SEEK_REPLY_BAR;
}

@object GenInteractionClass ChangeNumberDB = {
    GI_visMoniker = "Number of Fields";
    @localize "title of the number of fields dialog box";
    GI_comp = @Question, @NumberToShow, @NumEnter;
    GII_visibility = GIV_DIALOG;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
}

@object GenGlyphClass Question = {
    GI_visMoniker = "How many fields?";
    @localize "label for the number of fields selector";
}

@object GenValueClass NumberToShow = {
    GVLI_minimum = MakeWWFixed(1.0);
    GVLI_maximum = MakeWWFixed(10.0);
    GVLI_value = MakeWWFixed(10.0);
    HINT_DEFAULT_FOCUS;
}

@object GenTriggerClass NumEnter = {
    GI_visMoniker = "Enter";
    @localize "button to use the selected # of fields";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = process;
    GTI_actionMsg = MSG_CHANGE_NUMBER;
    HINT_DEFAULT_DEFAULT_ACTION;
    HINT_SEEK_REPLY_BAR;
}

/* -------------------------------------------------------------- */
@object GenInteractionClass ImportDBox = {
    GI_visMoniker = "Import from CSV";
    @localize "Title of Import from CSV file dialog box";
    GI_comp = @ImpFileSel, @ImpReply;
    GII_visibility = GIV_DIALOG;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    ATTR_GEN_HELP_CONTEXT = "ImpDB"; 
}

@object GenFileSelectorClass ImpFileSel = {
    GFSI_destination = process;
    GFSI_notificationMsg = MSG_IMPORT_SELECTOR_ACTION;
    ATTR_GEN_FILE_SELECTOR_NAME_MASK = {"*.CSV"};
    HINT_FILE_SELECTOR_NUMBER_OF_FILES_TO_SHOW = 8;
    HINT_FILE_SELECTOR_FILE_LIST_WIDTH = 40;
}

@object GenInteractionClass ImpReply = {
    GI_comp = @Import, @ImpCanx;
    HINT_MAKE_REPLY_BAR;
}

@object GenTriggerClass Import = {
    GI_visMoniker = "Import";
    @localize "Moniker of Import button in CSV file dialog box";
    GTI_destination = process;
    GTI_actionMsg = MSG_IMPORT_CSV;
    HINT_SEEK_REPLY_BAR;
    HINT_DEFAULT_DEFAULT_ACTION;
}

@object GenTriggerClass ImpCanx = {
    GI_visMoniker = "Cancel";
    @localize "Moniker of Cancel button in CSV file dialog box";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    HINT_SEEK_REPLY_BAR;
}

/* -------------------------------------------------------------- */

@object GenInteractionClass ImpStatusDBox = {
    GI_visMoniker = "Import Status";
    @localize "title of the import status dialog box";
    GI_comp = @ImpStatGlyph, @ImpCanxTrigger;
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default | GIA_MODAL;
    GII_type = GIT_PROGRESS;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
}

@object GenGlyphClass ImpStatGlyph = {
    GI_visMoniker = "Importing...";
    @localize "tells the user the import is in proress";
}

@object GenTriggerClass ImpCanxTrigger = {
    GI_visMoniker = "Stop Importing";
    @localize "button for the user to stop the import";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = @HBaseApp;
    GTI_actionMsg = MSG_CANCEL_IMPORT;
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = {IC_STOP};
    HINT_SEEK_REPLY_BAR;
    HINT_TRIGGER_DESTRUCTIVE_ACTION;
}

/* -------------------------------------------------------------- */
@object GenInteractionClass ExportDBox = {
    GI_visMoniker = "Export to CSV";
    @localize "title of the export dialog box";
    GI_comp = @ExpFileSel, @ExpNameStuff, @ExpReply;
    GII_visibility = GIV_DIALOG;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    ATTR_GEN_HELP_CONTEXT = "ExpDB";    /* the help file Entry Point */
}

@object GenFileSelectorClass ExpFileSel = {
    GI_visMoniker = "Select Directory";
    @localize "label for the file selector in the export dialog box";
    /*GFSI_fileCriteria = FSFC_DIRS;*/
    GFSI_attrs	= @default | FSA_SHOW_FILES_DISABLED;
    ATTR_GEN_FILE_SELECTOR_NAME_MASK = {"*.CSV"};
    HINT_FILE_SELECTOR_NUMBER_OF_FILES_TO_SHOW = 8;
    HINT_FILE_SELECTOR_FILE_LIST_WIDTH = 40;
    HINT_PLACE_MONIKER_ABOVE;
    HINT_CENTER_MONIKER;
}

@object GenInteractionClass ExpNameStuff = {
    GI_comp = @ExpFileName, @ExpNameWarn, @ExpFieldSel;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
}

@object GenTextClass ExpFileName = {
    GI_visMoniker = "File Name:";
    @localize "label for the export file name entry window";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (12);
    ATTR_GEN_TEXT_LEGAL_DOS_VOLUME_NAMES;
    ATTR_GEN_TEXT_MAKE_UPPERCASE;
    HINT_FIXED_SIZE = {
	   SST_AVG_CHAR_WIDTHS | 16,
	   0,    
           0};
    HINT_TEXT_WHITE_WASH_COLOR;
    HINT_DEFAULT_FOCUS;
}

@object GenGlyphClass ExpNameWarn = {
    GI_visMoniker = "(DOS file name)";
    @localize "reminder that export file name must be a DOS name";
}

@object GenItemGroupClass ExpFieldSel = {
    GI_comp = @ExpField;
    GIGI_behaviorType = GIGBT_EXCLUSIVE_NONE;
}

@object GenItemClass ExpField = {
    GI_visMoniker = "Field names as 1st record";
    @localize "option to add field names as first record of export";
    GII_identifier = E_FIELD;
}

@object GenInteractionClass ExpReply = {
    GI_comp = @Export, @ExpCanx;
    HINT_MAKE_REPLY_BAR;
}

@object GenTriggerClass Export = {
    GI_visMoniker = "Export";
    @localize "button to start exporting database";
    GTI_destination = process;
    GTI_actionMsg = MSG_EXPORT_CSV;
    HINT_SEEK_REPLY_BAR;
    HINT_DEFAULT_DEFAULT_ACTION;
}

@object GenTriggerClass ExpCanx = {
    GI_visMoniker = "Cancel";
    @localize "button to cancel exporting";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    HINT_SEEK_REPLY_BAR;
}

/* ------------------------------------------------------------------ */
@object GenInteractionClass ReportGroup = {
    GI_visMoniker = "Reports Window";
    @localize "Title at the top of the Reports window";
    GI_comp = @ReportWindowMenu, @TextView;
    GI_attrs = @default | GA_TARGETABLE;
    GII_visibility = GIV_DIALOG;
    HINT_INTERACTION_MAKE_RESIZABLE;
    HINT_POSITION_WINDOW_AT_RATIO_OF_PARENT = {
	SWSS_RATIO | PCT_5, SWSS_RATIO | PCT_5};
    HINT_SIZE_WINDOW_AS_RATIO_OF_FIELD = {
      SWSS_RATIO | PCT_75, SWSS_RATIO | PCT_50};
}

@object GenInteractionClass ReportWindowMenu = {
    GI_comp = @HBasePrintControl, @HBaseEditControl;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
}

@object PrintControlClass HBasePrintControl = {
    GI_states = (@default | GS_ENABLED);
    PCI_attrs = (@default & ~ PCA_PAGE_CONTROLS
			  | PCA_MARK_APP_BUSY);
    PCI_output = @TextWindow;
    PCI_docNameOutput = @HBaseDocumentGroup;
    PCI_docSizeInfo   = {(7.5*72),   /* width less margins */
	       		 (10*72),    /* height less margins */
	    		 PT_PAPER,    /* layout */
	    		 {(.5*72), (.5*72), (.5*72), (.5*72)}}; /* L,T,R,B margins */
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_PRINT_GROUP;
    HINT_AVOID_MENU_BAR;
}

@object GenEditControlClass HBaseEditControl = {              
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_EDIT_MENU;
    ATTR_GEN_CONTROL_PROHIBIT_UI = GECF_UNDO;           
    HINT_AVOID_MENU_BAR;
}

@object GenViewClass TextView = {
    GVI_vertAttrs  = @default | GVDA_SCROLLABLE;
    GVI_attrs = @default | GVA_DRAG_SCROLLING
	    	    	 | GVA_WINDOW_COORDINATE_MOUSE_EVENTS
			 | GVA_DONT_SEND_KBD_RELEASES
			 | GVA_FOCUSABLE;
    GVI_content = @TextContent;
    HINT_DEFAULT_FOCUS;
    ATTR_GEN_VIEW_DOES_NOT_ACCEPT_TEXT_INPUT;
}

@object HBaseContentClass TextContent = {
    GCI_genView = @TextView;
    GCI_attrs = @default | VCNA_LARGE_DOCUMENT_MODEL
	    	    	 | VCNA_WINDOW_COORDINATE_MOUSE_EVENTS
			 | VCNA_SAME_WIDTH_AS_VIEW;
    HINT_DEFAULT_FOCUS;
}

@object HBaseVLTextClass TextWindow = {
    VTI_state = @default | VTS_SELECTABLE
			 | VTS_EDITABLE
			 | VTS_TARGETABLE;
    VTI_output = process;
    VLTI_displayMode = VLTDM_DRAFT_WITH_STYLES;
    VLTI_attrs = VLTA_EXACT_HEIGHT;
    VLTI_regionArray = @HBVLTextRegionArray;
    VTI_charAttrRuns = ((VTDS_12 << VTDCA_SIZE_OFFSET) || VTDF_URW_MONO);
}

@chunk ChunkArrayHeader HBVLTextRegionArray = {
    0,
    sizeof(VisLargeTextRegionArrayElement),
    0,
    sizeof(ChunkArrayHeader)
};

@end Dialogs;

/***********************************************************************
 *
 * MESSAGE:	MSG_VIS_TEXT_FREE_ALL_STORAGE for HBaseVLTextClass
 *
 * DESCRIPTION: We must intercept FREE_ALL_STORAGE to do a little more work so that the
 *		text object is left in the correct state
 *              Most of this from vtext sample file
 *              Has to stay in this source file!!!!!
 *
 ***********************************************************************/
@method HBaseVLTextClass, MSG_VIS_TEXT_FREE_ALL_STORAGE
{
    ChunkArrayHeader *cahp;
    VisLargeTextInstance *vself;

    @callsuper();
    LMemReAlloc(@HBVLTextRegionArray, sizeof(ChunkArrayHeader));
    cahp = LMemDeref(@HBVLTextRegionArray);
    cahp->CAH_count = 0;
    cahp->CAH_elementSize = sizeof(VisLargeTextRegionArrayElement);
    cahp->CAH_curOffset = 0;
    cahp->CAH_offset = sizeof(ChunkArrayHeader);

    vself = ObjDerefVis(oself);
    vself->VTI_storageFlags &= ~VTSF_LARGE;
    vself->VI_optFlags |= VOF_GEOMETRY_INVALID;
}

/***********************************************************************
 *
 * MESSAGE:	MSG_META_CONTENT_VIEW_OPENING for HBaseContentClass
 *
 * DESCRIPTION:	good place to initialize stuff
 *              Most of this from vtext sample file
 *              Has to stay in this source file!!!!!
 *
 ***********************************************************************/
@method HBaseContentClass, MSG_META_CONTENT_VIEW_OPENING
{
    VisContentInstance *vself;
    VMFileHandle fileHan;

    @callsuper();

    /*
     * Set up the geometry for the content so that the geometry manager
     * is not used.
     */

    vself = ObjDerefVis(oself);
    vself->VI_attrs &= ~VA_MANAGED;
    vself->VI_optFlags &= ~(VOF_GEOMETRY_INVALID | VOF_GEO_UPDATE_PATH);
    vself->VI_geoAttrs |= VCGA_CUSTOM_MANAGE_CHILDREN;

    /*
     * Add the text object as a child of the content.  This must be done
     * dynamically because the text object is a visual child, not a generic
     * one.
     */

    @call TextContent::MSG_VIS_ADD_CHILD(@TextWindow, CCO_FIRST);

    /*
     * Set up the text object to be large
     */

    fileHan = ClipboardGetClipboardFile();
    @call TextWindow::MSG_VIS_TEXT_SET_VM_FILE(fileHan);
    @call TextWindow::MSG_VIS_LARGE_TEXT_CREATE_DATA_STRUCTURES();
    @call TextWindow::MSG_VIS_NOTIFY_GEOMETRY_VALID();

}

/* ********************************************************************
 *
 * MESSAGE:	MSG_META_CONTENT_VIEW_CLOSING for HBaseContentClass
 *
 * DESCRIPTION:	good place to initialize stuff
 *              Most of this from vtext sample file
 *              Has to stay in this source file!!!!!
 *
 ***********************************************************************/
@method HBaseContentClass, MSG_META_CONTENT_VIEW_CLOSING
{
    @callsuper();
    /*
     * Free the large text data structures and make the text object small
     * again.
     */

    @call TextWindow::MSG_VIS_TEXT_FREE_ALL_STORAGE(TRUE);

    @call TextWindow::MSG_VIS_TEXT_SET_VM_FILE(NullHandle);

    @call TextContent::MSG_VIS_REMOVE_CHILD(@TextWindow, CCO_FIRST);
}

/***********************************************************************
 *
 * MESSAGE:	MSG_PRINT_START_PRINTING for HBaseVLTextClass
 *
 * DESCRIPTION:	Does the display printing
 *              Has to stay in this source file!!!!!
 *
 ***********************************************************************/
@method HBaseVLTextClass, MSG_PRINT_START_PRINTING
{
    word		curPage, numPages;
    XYValueAsDWord	textSize;     /* Image size of text. */
   /* WWFixed		savedSize;    /* Previous point size. */
   /* WWFixed		newPtSize;    /* Used to set text to printing */
    VisLargeTextGetRegionPosReturnStruct    regPos;

    /* save region size */
    textSize = @call self::MSG_VIS_LARGE_TEXT_GET_DRAFT_REGION_SIZE(0);

    /* set point size, font and region size for printing */
  /*  newPtSize.WWF_int  = 12;
    newPtSize.WWF_frac = 0;
    @send self::MSG_VIS_TEXT_SET_POINT_SIZE(
                newPtSize, TEXT_ADDRESS_PAST_END, 0);
    @send self::MSG_VIS_TEXT_SET_FONT_ID(
                FID_DTC_URW_MONO, TEXT_ADDRESS_PAST_END, 0); */
    @send self::MSG_VIS_LARGE_TEXT_SET_DRAFT_REGION_SIZE(
		(7.5*72), (10*72));

    /* set number of pages to print */
    numPages = @call self::MSG_VIS_LARGE_TEXT_GET_REGION_COUNT();

    /* now do the printing */
    for (curPage=0; curPage < numPages; curPage++)
    {
       GrSaveState(gstate);
       @call self::MSG_VIS_LARGE_TEXT_GET_REGION_POS( &regPos, curPage );

       /* Clip it to match translation and margins */
       GrSetClipRect(gstate, PCT_REPLACE,
                             0,                     /* left */
                             0,      /* top */
                             (7.5 * 72),                 /* right */
			   /*  (10 * 72));   /* bottom */
			     ((10*72) - 3));   /* bottom */
			 /* the -3 is a quarter line offset to prevent
                            an extra line from printing at the bottom
                            of a page */

       /* Translate the gstate to selected page */
       GrApplyTranslationDWord(gstate, 0,
		   -(regPos.VLTGRPRS_yPosition));
       GrInitDefaultTransform(gstate);
	 /* Draw current page */
       @call self::MSG_VIS_DRAW(DF_PRINT, gstate);

       GrRestoreState(gstate);
       GrNewPage(gstate, PEC_FORM_FEED);
    }

   @send HBasePrintControl::MSG_PRINT_CONTROL_PRINTING_COMPLETED();

   /* reset things back to where the user had them */
 /*  @send self::MSG_VIS_TEXT_SET_FONT_ID(
	       FID_BERKELEY, TEXT_ADDRESS_PAST_END, 0);
   savedSize.WWF_int  = 10;
   savedSize.WWF_frac = 0;
   @send self::MSG_VIS_TEXT_SET_POINT_SIZE(
	       savedSize, TEXT_ADDRESS_PAST_END, 0); */
   @send self::MSG_VIS_LARGE_TEXT_SET_DRAFT_REGION_SIZE(
                  DWORD_X(textSize), DWORD_Y(textSize));


}



