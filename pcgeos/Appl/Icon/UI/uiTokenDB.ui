/*
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * Project:	PC GEOS
 * Module:	
 * File:	uiImport.ui
 *
 * Author:	Steve Yegge, Feb 18, 1993
 *
 * Revision History:
 *	Name	Date		Description
 *	----	----		-----------
 *	stevey	2/18/93	Initial revision
 *
 * Description:
 *	
 *	This file contains user-interface definitions for importing
 *	tokens from the token database.
 *
 *	$Id: uiTokenDB.ui,v 1.1 97/04/04 16:06:18 newdeal Exp $
 *
 */


start	TokenViewerUI;

/*-----------------------------------------------------------------------------
			ImportTokenInteraction
-----------------------------------------------------------------------------*/

ImportTokenInteraction = GenInteraction vardataReloc {
	visibility = dialog;
	genAttributes = default +notifyVisibility;
	moniker = 'I', "Import From Token Database";
	children = TokenDBViewerThingsGroup,
		   TokenDBViewer,
		   ImportSVGAMonikersGroup,
		   ImportTokenReplyBar;
	hints = {
		ATTR_GEN_VISIBILITY_MESSAGE {
			word MSG_ICON_APPLICATION_UPDATE_TOKEN_VIEWER
		},
	}
}

ImportSVGAMonikersGroup = GenInteraction {
	type = organizational;
	children = ImportSVGAMonikersBGroup;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING,
	}
}

ImportSVGAMonikersBGroup = GenBooleanGroup {
	children = ImportSVGAMonikersBoolean;
	selectedBooleans = "mask ITO_IMPORT_SVGA";
	applyMsg = MSG_META_DUMMY;
	destination = process;
}

ImportSVGAMonikersBoolean = GenBoolean {
	moniker = "Import SuperVGA Monikers";
	identifier = "mask ITO_IMPORT_SVGA";
}

/*-----------------------------------------------------------------------------
			TokenDBViewerGroup
-----------------------------------------------------------------------------*/

TokenDBViewer = GenDynamicList {
	behaviorType = nonExclusive;
	applyMsg = MSG_ICON_SELECT_TOKEN_FROM_LIST;
	destination = process;
	queryMsg = MSG_ICON_TOKEN_LIST_GET_ITEM_MONIKER;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_FIXED_SIZE {
			SpecWidth <SST_PIXELS, 400>
			SpecHeight <SST_PIXELS, 56>
			word 5
		}			
	}
}

/*-----------------------------------------------------------------------------
			TokenDBViewerThingsGroup
-----------------------------------------------------------------------------*/

TokenDBViewerThingsGroup = GenInteraction {
	type = organizational;
	children = TokenDBNumTokens,
		   TokenDBNumSelected;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING,
	}
}

TokenDBNumTokens = GenValue {
	genAttributes = default +readOnly;
	moniker = "Total:";
}

TokenDBNumSelected = GenValue {
	genAttributes = default +readOnly;
	moniker = "Selected:";
}

/*-----------------------------------------------------------------------------
			ImportTokenReplyBar
-----------------------------------------------------------------------------*/

ImportTokenReplyBar = GenInteraction {
	type = organizational;
	children = ImportTokenImportTrigger,
		   TokenDBUpdateTrigger,
		   TokenDBSelectAllTrigger,
		   ImportTokenCloseTrigger;
	hints = {
		HINT_MAKE_REPLY_BAR,
	}
}

ImportTokenImportTrigger = GenTrigger {
	moniker = 'I', "Import";
	action = MSG_DB_VIEWER_IMPORT_TOKEN_DATABASE, "TO_APP_MODEL";
}

TokenDBSelectAllTrigger = GenTrigger {
	moniker = 'S', "Select All";
	action = MSG_ICON_SELECT_ALL_TOKENS, process;
}

TokenDBUpdateTrigger = GenTrigger {
	moniker = 'R', "Rescan";
	action = MSG_ICON_APPLICATION_UPDATE_TOKEN_VIEWER, IconApp;
}

ImportTokenCloseTrigger = GenTrigger {
	moniker = "VMO_CANCEL", "Close";
	genAttributes = default +signalInteractionComplete;
}

/*-----------------------------------------------------------------------------
			ImportProgressDialog
-----------------------------------------------------------------------------*/

ImportProgressDialog = GenInteraction {
	visibility = dialog;
	attributes = default +notUserInitiatable, +modal;
	moniker = "Import Progress";
	children = ImportProgressValueGroup,
		   ImportProgressIconGroup,
		   ImportProgressReplyBar;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
	}
}

ImportProgressValueGroup = GenInteraction {
	type = organizational;
	children = ImportProgressValue;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING,
	}
}

ImportProgressValue = ImportValue {
	minimum = 0;
	maximum = 100;
	value = 0;
	destination = "TO_APP_MODEL";
	applyMsg = MSG_META_DUMMY;
	genAttributes = default +readOnly;
	hints = {
		HINT_PLACE_MONIKER_ABOVE,
		HINT_CENTER_MONIKER,
		HINT_VALUE_ANALOG_DISPLAY,
	}
}

ImportProgressIconGroup = GenInteraction {
	type = organizational;
	children = ImportProgressTitle,
		   ImportProgressGlyph;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING,
		HINT_CENTER_CHILDREN_VERTICALLY,
	}
}

ImportProgressTitle = GenGlyph {
	moniker = "Current Token:";
}

ImportProgressGlyph = GenGlyph {
	moniker = {
	    cachedSize = 48, 30; \
	    gstring { \
		GSBeginString | \
		GSEndString \
	    } \
	}
}

ImportProgressReplyBar = GenInteraction {
	type = organizational;
	children = ImportProgressStopTrigger;
	hints = {
		HINT_MAKE_REPLY_BAR,
	}
}

ImportProgressStopTrigger = StopImportTrigger {
	moniker = 'S', "Stop";
	genAttributes = default +signalInteractionComplete;
}

end TokenViewerUI;

/*-----------------------------------------------------------------------------
	UI for changing an app's token moniker in token database
-----------------------------------------------------------------------------*/

start	ChangeIconUI;

ExportTokenDialog = GenInteraction {
	visibility = dialog;
	type = command;
	moniker = 'E', "Export to Token Database";
	children = LeftGroup,
		   RightSide,
		   ChangeIconChangeTrigger,
		   ChangeIconResetTrigger;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
	}
}

LeftGroup = GenInteraction {
	type = organizational;
	children = FileSelectorGroup,
		   TokenCharsGroup;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
	}
}

FileSelectorGroup = GenInteraction {
	type = organizational;
	children = ChangeIconFileSelector;
}

ChangeIconFileSelector = GenFileSelector {
	fileCriteria = dirs, geosExecutables, geosNonExecutables;
	action = MSG_ICON_GET_APP_TOKEN, process;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	}
}

/*-----------------------------------------------------------------------------
			TokenCharsGroup
-----------------------------------------------------------------------------*/

TokenCharsGroup = GenInteraction {
	type = organizational;
	children = TokenCharsText,
		   ManufIDValue;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
/*		HINT_CENTER_CHILDREN_ON_MONIKERS,	*/
	}
}

TokenCharsText = GenText {
	moniker = "Token Characters:";
	textDisplayAttributes = singleLineText, useTabForNavigation;
	maxLength = 4;
	applyMsg = MSG_META_DUMMY;
	destination = process;
	hints = {
		HINT_PLACE_MONIKER_TO_LEFT,
		HINT_FIXED_SIZE {
			SpecWidth <SST_WIDE_CHAR_WIDTHS, 4>
			SpecHeight <>
		}
	}
}

ManufIDValue = TokenValue {
	moniker = "Manufacturer ID:";
	minimum = 0;
	hints = {
		HINT_PLACE_MONIKER_TO_LEFT,
	}
}

RightSide = GenInteraction {
	type = organizational;
	children = CurrentTokenGroup,
		   ChangeTokenGroup,
		   SourceSelectGroup;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_FULL_JUSTIFY_CHILDREN_VERTICALLY,
	}
}

CurrentTokenGroup = GenInteraction {
	type = organizational;
	moniker = "Current Icon:";
	children = CurrentTokenGlyph;
	hints = {
		HINT_PLACE_MONIKER_ABOVE,
		HINT_CENTER_MONIKER,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING,
	}
}

CurrentTokenGlyph = GenGlyph {
	moniker = {
	    cachedSize = STANDARD_COLOR_FILE_MONIKER_WIDTH,
			 STANDARD_COLOR_FILE_MONIKER_HEIGHT;
	    gstring {
		GSBeginString |
		GSEndString
	    }
	}
}

ChangeTokenGroup = GenInteraction {
	type = organizational;
	moniker = "Change To:";
	children = ChangeTokenGlyph;
	hints = {
		HINT_PLACE_MONIKER_ABOVE,
		HINT_CENTER_MONIKER,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING,
	}
}

ChangeTokenGlyph = GenGlyph {
	moniker = {
	    cachedSize = STANDARD_COLOR_FILE_MONIKER_WIDTH,
			 STANDARD_COLOR_FILE_MONIKER_HEIGHT;
	    gstring {
		GSBeginString |
		GSEndString
	    }
	}
}

SourceSelectGroup = GenInteraction {
	type = organizational;
	children = ChangeIconSourceSelector;
}

ChangeIconSourceSelector = GenItemGroup {
	behaviorType = exclusive;
	children = FileSelectorItem,
		   TokenCharsItem;
	selection = "CIST_FILE_SELECTOR";
	numSelections = 1;
	applyMsg = MSG_META_DUMMY;
	destination = process;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
	}
}

FileSelectorItem = GenItem {
	moniker = "Use File Selector";
	identifier = "CIST_FILE_SELECTOR";
}

TokenCharsItem = GenItem {
	moniker = "Use Token Chars";
	identifier = "CIST_TOKEN_CHARS";
}

/*-----------------------------------------------------------------------------
			Reply-bar triggers
-----------------------------------------------------------------------------*/

ChangeIconChangeTrigger = GenTrigger {
	moniker = 'C', "Change";
	action = MSG_DB_VIEWER_CHANGE_APP_TOKEN, "TO_APP_MODEL";
	hints = {
		HINT_SEEK_REPLY_BAR,
		HINT_DEFAULT_DEFAULT_ACTION,
	}
}

ChangeIconResetTrigger = GenTrigger {
	genStates = default +enabled;
	moniker = 'R', "Remove";
	action = MSG_ICON_RESET_APP_TOKEN, process;
	hints = {
		HINT_SEEK_REPLY_BAR,
	}
}

end	ChangeIconUI;
