/*
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * Project:	PC GEOS
 * Module:	Icon Editor
 * File:	uiIconMenu.ui
 *
 * Author:	Steve Yegge, Feb 23, 1993
 *
 * Revision History:
 *	Name	Date		Description
 *	----	----		-----------
 *	stevey	2/23/93		Initial revision
 *
 * Description:
 *	
 *
 *	$Id: uiIconMenu.ui,v 1.1 97/04/04 16:06:31 newdeal Exp $
 *
 */

start	DisplayTempUI;

/*-----------------------------------------------------------------------------
		Icon Menu
-----------------------------------------------------------------------------*/

IconMenu = GenInteraction {
	genStates = default +enabled;
	visibility = popup;
	children = AddIconDialog,
		   EditIconTrigger,
		   RenameIconDialog,
		   SaveIconTrigger,
		   WriteSourceDialog,
		   PreviewLaunchTrigger,
		   WriteToFileDialog,
		   GetInfoDialog;
	moniker = 'I', "Icon";
}

PreviewLaunchTrigger = GenTrigger {
	moniker = 'P', "Preview Icon";
	action = MSG_GEN_INTERACTION_INITIATE, IconPreviewDialog;
	hints = {
		HINT_TRIGGER_BRINGS_UP_WINDOW,
	}
}

/*-----------------------------------------------------------------------------
		RenameIconDialog
-----------------------------------------------------------------------------*/

RenameIconDialog = GenInteraction vardataReloc {
	genStates = default - enabled;
	genAttributes = default +notifyVisibility;
	attributes = modal;
	visibility = dialog;
	type = command;
	moniker = 'R', "Rename Icon";
	children = RenameTextField,
		   RenameTrigger;
	hints = {
		ATTR_GEN_VISIBILITY_DESTINATION {
			I_OPTR	RenameTextField
		},
		ATTR_GEN_VISIBILITY_MESSAGE {
			word	MSG_VIS_TEXT_SELECT_ALL
		},
	}
}

RenameTextField = GenText {
	moniker = "New Name:";
	textDisplayAttributes = singleLineText,useTabForNavigation;
	maxLength = FILE_LONGNAME_LENGTH;
	hints = {
		  HINT_PLACE_MONIKER_ABOVE,
		  HINT_TEXT_FRAME,
		  HINT_TEXT_WHITE_WASH_COLOR,
		  HINT_DEFAULT_FOCUS,
	}
}

RenameTrigger = GenTrigger {
	moniker = 'R', "Rename";
	genAttributes = default +signalInteractionComplete;
	action = MSG_DB_VIEWER_RENAME_ICON, "TO_APP_MODEL";
	hints = {
		HINT_SEEK_REPLY_BAR,
		HINT_DEFAULT_DEFAULT_ACTION,
	}
}

/*-----------------------------------------------------------------------------
		WriteToFileDialog
-----------------------------------------------------------------------------*/

WriteToFileDialog = GenInteraction {
	visibility = dialog;
	type = command;
	moniker = 'F', "Write to File";
	children = WriteToFileText,
		   WTFOptionsGroup,
		   WTFOK;
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
	}
}

WriteToFileText = GenText {
	moniker = 'F', "Filename:";
	textDisplayAttributes = singleLineText, useTabForNavigation;
	maxLength = MAX_LENGTH;
	hints = {
		HINT_DEFAULT_FOCUS,
		HINT_PLACE_MONIKER_TO_LEFT,
		ATTR_GEN_TEXT_LEGAL_DOS_FILENAMES,
		HINT_TEXT_FRAME,
		ATTR_GEN_TEXT_MAKE_UPPERCASE,
		ATTR_GEN_TEXT_NO_SPACES,
		HINT_TEXT_WHITE_WASH_COLOR,	
		HINT_FIXED_SIZE {
			SpecWidth <SST_WIDE_CHAR_WIDTHS, MAX_LENGTH>
			SpecHeight <>
		}
	}
}

WTFOptionsGroup = GenBooleanGroup {
	children = WTFWriteMask, WTFCompactBitmap;
	selectedBooleans = "mask WTFO_COMPACT_BITMAP";
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
	}
}

WTFWriteMask = GenBoolean {
	moniker = 'W', "Write Mask";
	identifier = "mask WTFO_WRITE_MASK";
}

WTFCompactBitmap = GenBoolean {
	moniker = 'C', "Compact Bitmap";
	identifier = "mask WTFO_COMPACT_BITMAP";
}

WTFOK = GenTrigger {
	moniker = "OK";
	action = MSG_DB_VIEWER_WRITE_TO_FILE, "TO_APP_MODEL";
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word	IC_APPLY
		}
	}
}

/*-----------------------------------------------------------------------------
		GetInfoDialog -- not yet implemented
-----------------------------------------------------------------------------*/

GetInfoDialog = GenInteraction {
	visibility = dialog;
	moniker = 'G', "Get Info";
	genStates = default -usable;
}

SaveIconTrigger = GenTrigger {
	moniker = 'S', "Save Icon";
	action = MSG_DB_VIEWER_SAVE_CURRENT_ICON, "TO_APP_MODEL";
}

EditIconTrigger = GenTrigger {
	moniker = 'E', "Edit Icon";
	action = MSG_DB_VIEWER_EDIT_ICON, "TO_APP_MODEL";
}

end	DisplayTempUI;
