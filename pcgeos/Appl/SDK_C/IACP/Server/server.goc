/********************************************************************
 *
 *	Copyright (c) Geoworks 1994 -- All Rights Reserved.
 *
 * PROJECT:	GEOS Sample Applications
 * MODULE:	IACP/Server
 * FILE:	server.goc
 *
 * AUTHOR:	Ed Ballot: Feb 23, 1994
 *
 * REVISION HISTORY:
 *      Name    Date            Description
 *      ----    ----            -----------
 *      EB      2/23/94         Initial version
 *      EB      8/9/94          Added MSG_SERVER_PROCESS_PRINT_RECEIVED
 *                              for client2.goc.
 *
 * DESCRIPTION:
 *	This is the server part of a sample application suite that
 *      demonstrates the use of IACP.  This application simply does
 *      nothing useful in itself. Its purpose in this world is to be
 *      controlled by the client applications (iacp\client1 and
 *      iacp\client2).
 *
 * RCS STAMP:
 *	$Id: server.goc,v 1.1 97/04/04 16:40:01 newdeal Exp $
 *
 *******************************************************************/

/********************************************************************
 *		Include files
 *******************************************************************/
    @include <stdapp.goh>
    @include <iacp.goh>

/********************************************************************
 *		Class Definitions
 *******************************************************************/
    @class ServerProcessClass, GenProcessClass;
    @message void MSG_SERVER_PROCESS_PRINT_RECEIVED();
    @endc;

/********************************************************************
 *		Class Declarations
 *******************************************************************/
    @classdecl ServerProcessClass, neverSaved;

/********************************************************************
 *		UI Object Resources
 *******************************************************************/

/********************************************************************
 *		AppResource Resource
 *******************************************************************/
@start AppResource;

@object GenApplicationClass ServerApp = {
    GI_visMoniker = list { @ServerTitle };
    GI_comp = @ServerPrimary;
    gcnList( MANUFACTURER_ID_GEOWORKS, GAGCNLT_WINDOWS ) =
        @ServerPrimary;
}

@visMoniker ServerTitle = "IACP Server";

@end AppResource;

/********************************************************************
 *		Interface Resource
 *******************************************************************/
@start Interface;

@object GenPrimaryClass ServerPrimary  = {
    GI_comp = @ServerStatusDisplayText;
    HINT_SIZE_WINDOW_AS_DESIRED;
}

@object GenTextClass ServerStatusDisplayText = {
    GI_visMoniker = "This app does nothing but wait for IACP messages";
    HINT_PLACE_MONIKER_ABOVE;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

@end Interface;

/********************************************************************
 *		Strings Resource
 * Localizable strings should be placed in their own resource.
 * When strings are stored in chunks they can be localized using
 * the ResEdit translation tool.
 *******************************************************************/
@start Strings, data;

@chunk char msgReceived[] = "IACP message received\r";
@localize { "Message displayed when server receives client message." };

@end Strings;

/********************************************************************
 *              Code for ServerProcessClass
 *******************************************************************/

/********************************************************************
 *              MSG_SERVER_PROCESS_PRINT_RECEIVED
 ********************************************************************
 * SYNOPSIS:     Display a message letting the user know that Client2
 *               has sent this message via IACP.
 * CONTEXT:      Client2 sends this message to Server via IACP.
 * PARAMETERS:   void
 * RETURNS:      void
 * STRATEGY:     See the description of MSG_CLIENT2_PROCESS_SEND_MESSAGE
 *               in IACP\Client2\client2.goc
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	EB	8/9/94	   	Initial Revision
 *******************************************************************/
@method ServerProcessClass, MSG_SERVER_PROCESS_PRINT_RECEIVED
{

    @call ServerStatusDisplayText::MSG_VIS_TEXT_APPEND_OPTR(
        @msgReceived, 0 );
} /* MSG_SERVER_PROCESS_PRINT_RECEIVED */

