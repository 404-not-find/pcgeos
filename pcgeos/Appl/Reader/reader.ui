/***********************************************************************
 *
 *	Copyright (c) Geoworks 1994 -- All Rights Reserved
 *
 * PROJECT:	  Condo viewer
 * MODULE:	  Book Reader
 * FILE:	  reader.ui
 *
 * AUTHOR:  	  Jonathan Magasin, Apr  8, 1994
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	JM	4/ 8/94   	Initial version
 * jfh	7/21/02	Added view menu item
 * DESCRIPTION:
 *	UI for Book Reader
 *
 *
 * 	$Id: reader.ui,v 1.3 98/04/28 19:54:17 joon Exp $
 *
 ***********************************************************************/

/*----------------------------------------------------------------------------
	Include files
-----------------------------------------------------------------------------*/
#include "generic.uih"
#include "conview.uih"

#define _NIKE	0

#define	_JEDI	0

#ifdef	PRODUCT_FULL_SCREEN
#define	_FULL_SCREEN	-1
#else
#define	_FULL_SCREEN	0
#endif


#if	_FULL_SCREEN
/* subclassed for special handling of Begin and Back triggers */
class FullScreenContentView = ContentGenView {
}
#endif


#include "Art/mkrCReader.ui"
#include "Art/mkrMReader.ui"
#include "Art/mkrCBookDoc.ui"
#include "Art/mkrMBookDoc.ui"
#include "Art/mkrReaderTiny.ui"
#include "Art/mkrBookDocTiny.ui"

start	AppSCMonikerResource, data;

visMoniker DatafileMonikerList = list {
	CBookDocSCMoniker,
	MBookDocSMMoniker,
	CBookDocYCMoniker,
	MBookDocYMMoniker,
	MBookDocSCGAMoniker,
	BookDocTinyTCMoniker,
	BookDocTinyTMMoniker,
	BookDocTinyTCGAMoniker
}

end	AppSCMonikerResource;




start	AppResource;

ContentViewApp = GenApplication {
	children = 

/*
 * ContentNav must be in the app, even though not visible, else
 * special hyperlinks (ie, !B, !N, !P) won't work. 
 */
#if _FULL_SCREEN	
		ContentNav,	
#endif
	        ContentViewPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) =
		ContentViewPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST) =
#if !_FULL_SCREEN
		ContextBar,
		ContentFind,
		ContentSend, 
		ReaderViewControl,
#endif
		ContentNav,
		ContentView;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_SELF_LOAD_OPTIONS) =
#if !_FULL_SCREEN
		ContextBar,
		ContentFind,
		ContentSend, 
		ReaderViewControl,
#endif
		ContentNav;
	moniker = AppMonikerList;

}

visMoniker AppMonikerList = list {
	AppTextMoniker,
	CReaderSCMoniker,
	CReaderYCMoniker,
	MReaderSMMoniker,
	MReaderYMMoniker,
	MReaderSCGAMoniker,
	ReaderTinyTCMoniker,
	ReaderTinyTMMoniker,
	ReaderTinyTCGAMoniker
}

visMoniker AppTextMoniker = "Book Reader";

end	AppResource;


start	ContentViewInterface;

ContentViewPrimary = GenPrimary {
#if _FULL_SCREEN
	children =
		ContentView;
#else
	children =
		ContentFileMenu,
		ContentSend,
		ReaderViewControl,
		ContentNavigationMenu,
		ToolAreaAboveView,
		ContextBar,	
		ContentView,
		ToolAreaBelowView;
#endif					
	hints = {
		/*
		 * Other useful (but not required) hints to make the viewer
		 * app come up looking nice.
		 */
#if !_FULL_SCREEN
		HINT_SIZE_WINDOW_AS_RATIO_OF_FIELD {
			SpecWinSizePair < mask SWSS_RATIO or PCT_70, \
					  mask SWSS_RATIO or PCT_70 >
		},
		HINT_CENTER_WINDOW,
		HINT_PRIMARY_NO_FILE_MENU,
#endif
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		ATTR_GEN_HELP_CONTEXT {
			char "TOC",0
		},
#if _FULL_SCREEN
	    	ATTR_GEN_WINDOW_CUSTOM_WINDOW,
		HINT_PRIMARY_FULL_SCREEN,
		HINT_PRIMARY_NO_EXPRESS_MENU,
		HINT_PRIMARY_NO_HELP_BUTTON,
		HINT_PRIMARY_NO_FILE_MENU,
		HINT_DISPLAY_MAXIMIZED_ON_STARTUP,
		ATTR_GEN_DISPLAY_NOT_RESTORABLE
#endif
	}
}

#if	_FULL_SCREEN

ContentView = FullScreenContentView {
	horizAttributes = default -scrollable;
	vertAttributes = default -scrollable;
	viewAttributes = default +controlled, +dragScrolling;
	docLeft	= 0;
	docTop = 0;
	docRight = "CONTENT_GEN_VIEW_INITIAL_WIDTH";
	docBottom = "CONTENT_GEN_VIEW_INITIAL_HEIGHT";
	hints = {
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		ATTR_GEN_VIEW_DOES_NOT_ACCEPT_TEXT_INPUT
	}
}

#else

ContentView = ContentGenView {
	viewAttributes = default +controlled, +dragScrolling;
	docLeft	= 0;
	docTop = 0;
	docRight = "CONTENT_GEN_VIEW_INITIAL_WIDTH";
	docBottom = "CONTENT_GEN_VIEW_INITIAL_HEIGHT";
	hints = {
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		ATTR_GEN_VIEW_DOES_NOT_ACCEPT_TEXT_INPUT
	}
}

#endif

/**************************************************************************
 *                  	Controllers
 **************************************************************************/

ContentNav = ContentNavControl {
	output = ContentView;	/* Controller sends messages to ContentView. */
}


#if !_FULL_SCREEN 		/*** No other UI is used in full screen mode */
/*=========================================================================*/

/* The Find controller. */
ContentFind = ContentFindControl {
	output = ContentView;	/* Controller sends messages to ContentView. */
}


/* The Send Controller */
ContentSend = ContentSendControl {
	moniker = 'S',"Send"; 
	output = ContentView;	/* Controller sends messages to ContentView. */
	visibility = popup;
}


/**************************************************************************
 *                  	Menus
 **************************************************************************/
ContentFileMenu = GenInteraction {
	moniker = 'B', "Book";
	children = ContentOpenTrigger;
	visibility = popup;
	hints = {
		ATTR_GEN_INTERACTION_GROUP_TYPE {
			GenInteractionGroupType GIGT_FILE_MENU
		},
	}
}

ContentOpenTrigger = GenTrigger {
	moniker = 'O', "Open...";
	action = MSG_CGV_INIT_FILE_SELECTOR, ContentView;  
	hints = {
	}
}

ReaderViewControl = GenViewControl {
	visibility = popup;
	hints = {
		ATTR_GEN_INTERACTION_GROUP_TYPE {
			GenInteractionGroupType GIGT_VIEW_MENU
		},
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word mask GVCF_BIG_ENLARGE or \
			mask GVCF_SCALE_TO_FIT or \
			mask GVCF_ADJUST_ASPECT_RATIO or \
			mask GVCF_APPLY_TO_ALL or \
			mask GVCF_SHOW_HORIZONTAL or \
			mask GVCF_SHOW_VERTICAL or \
			mask GVCF_CUSTOM_SCALE or \
			mask GVCF_REDRAW
		}
	}
}


/*
 *  The Navigation Menu 
 *  This menu contains the navigation controller and the find controller.
 */
ContentNavigationMenu = GenInteraction {
	moniker = 'N',"Navigate"; 
	children = ContentNav,
		   ContentFind;
	visibility = popup;
	hints = {
	}
}



/**************************************************************************
 *			TOOLS
 **************************************************************************/


ToolAreaAboveView = GenInteraction {
	children = ContextBarToolGroup;
}

/* The Context controller. */
ContextBar = ContextControl {
}

ContextBarToolGroup = GenToolGroup {
	controller = ContextBar;
}


ToolAreaBelowView = GenInteraction {
	children = ContentNavToolGroup,
		   ContentSendToolGroup,
		   ContentFindToolGroup;
	hints = {
		HINT_TOOLBOX,
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_IF_SYSTEM_ATTRS {SystemAttrs mask SA_HORIZONTALLY_TINY},
			HINT_CUSTOM_CHILD_SPACING {
				SpecSizeSpec <SST_PIXELS, 1>
			},
			HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_ENDIF
	}
}

		   
ContentNavToolGroup = GenToolGroup {
	controller = ContentNav;
	hints = {
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_IF_SYSTEM_ATTRS {SystemAttrs mask SA_HORIZONTALLY_TINY},
			HINT_CUSTOM_CHILD_SPACING {
				SpecSizeSpec <SST_PIXELS, 1>
			},
			HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_ENDIF,
	}
}

ContentSendToolGroup = GenToolGroup {
	controller = ContentSend;
}

ContentFindToolGroup = GenToolGroup {
	controller = ContentFind;
}



/*=========================================================================*/
#endif				/* if !_FULL_SCREEN */

end	ContentViewInterface;
