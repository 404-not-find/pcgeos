/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1994 -- All Rights Reserved
 *
 * PROJECT:	  GEOS
 * MODULE:	  Appl/Tools/PrntScrn
 * FILE:	  prntscrn.ui
 *
 * AUTHOR:  	  Don Reeves, Aug 11, 1994
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	8/11/94	  Don	    Initial version
 *
 * DESCRIPTION:
 *	Creates the UI description for the "Print to Screen" application
 *
 * 	$Id: prntscrn.ui,v 1.1 97/04/04 17:15:31 newdeal Exp $
 *
 ***********************************************************************/

#include "generic.uih"
#include "spool.uih"

start	AppResource;

PrntScrnApp = GenApplication {
	moniker = list {
		"Print to Screen";
	}
	children =
		PrntScrnPrimary;
	windows_list =
		PrntScrnPrimary;
	active_list =
		ViewControl;
	startup_load_options_list =
		PrintingStatus;
	self_load_options_list =
		ViewControl;
}

end	AppResource;



start	Interface;

PrntScrnPrimary = GenPrimary {
	children = 
		FileMenu,
		ViewMenu,
		DrawMenu,
		PrntScrnTitle,
		PrntScrnView;
}

FileMenu = GenInteraction {
	children =
		OpenSpoolFile,
		CloseSpoolFile,
		JobInfoDialog;
/*		OpenPrintFile;		*/
	visibility = popup;
	hints = {
		ATTR_GEN_INTERACTION_GROUP_TYPE {
			GenInteractionGroupType GIGT_FILE_MENU
		}
	}
}

ViewMenu = GenInteraction {
	children =
		ViewControl;
	visibility = popup;
	hints = {
		ATTR_GEN_INTERACTION_GROUP_TYPE {
			GenInteractionGroupType GIGT_VIEW_MENU
		}
	}
}

DrawMenu = GenInteraction {
	moniker = 'D', "Draw";
	children =
		DrawOptionsBoolean;
	visibility = popup;
}


/**************************************
 * Open Spool File
 **************************************/

OpenSpoolFile = GenInteraction {
	moniker = 'S', "Open Spool File";
	type = command;
	visibility = dialog;
	attributes = modal;
	children =
		OpenSpoolFileGlyph,
		OpenSpoolFileSelector,
		OpenSpoolFileTrigger;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}

OpenSpoolFileGlyph = GenGlyph {
	moniker = "Choose Spool File";
}

OpenSpoolFileSelector = GenFileSelector {
	fileCriteria = dirs, geosNonExecutables;
	action = MSG_PRNT_SCRN_SELECT_SPOOL_FILE, process;
	hints = {
		ATTR_GEN_PATH_DATA {
			GenFilePath <SP_SPOOL, "">
		}
	}
}

OpenSpoolFileTrigger = GenTrigger {
	moniker = 'O', "Open Spool File";
	action = MSG_PRNT_SCRN_OPEN_SPOOL_FILE, process;
	genAttributes = signalInteractionComplete;
	hints = {
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word	IC_OK
		},
		HINT_SEEK_REPLY_BAR
	}
}


/**************************************
 * Close Spool File
 **************************************/

CloseSpoolFile = GenTrigger {
	moniker = 'C', "Close Spool File";
	action = MSG_PRNT_SCRN_CLOSE_SPOOL_FILE, process;
	genStates = default -enabled;
}


/**************************************
 * Open Print File
 **************************************/

OpenPrintFile = GenInteraction {
	moniker = 'P', "Open Print File";
	type = command;
	visibility = dialog;
	attributes = modal;
	children =
		OpenPrintFileGlyph,
		OpenPrintFileSelector;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}

OpenPrintFileGlyph = GenGlyph {
	moniker = "Choose Print File";
}

OpenPrintFileSelector = GenFileSelector {
	fileCriteria = dirs, geosNonExecutables;
	action = MSG_PRNT_SCRN_OPEN_PRINT_FILE, process;
	hints = {
		ATTR_GEN_PATH_DATA {
			GenFilePath <SP_DOCUMENT, "Print">
		}
	}
}


/**************************************
 * ViewControl
 **************************************/

ViewControl = GenViewControl {
	maxZoom = 400;
	viewControlAttrs = default -applyToAll;
	visibility = subGroup;
	hints = {
		HINT_SAME_CATEGORY_AS_PARENT,
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word	mask GVCF_ZOOM_IN or mask GVCF_ZOOM_OUT or mask GVCF_APPLY_TO_ALL
		}
	}
}


/**************************************
 * Draw Options
 **************************************/

DrawOptionsBoolean = GenBooleanGroup {
	children =
		DrawRect,
		DrawMargins;
	selectedBooleans = "mask DrawOptions";
	destination = process;
	hints = {
		ATTR_GEN_BOOLEAN_GROUP_STATUS_MSG {
			word	MSG_PRNT_SCRN_INVALIDATE_VIEW
		}
	}	
}

DrawRect = GenBoolean {
	moniker = 'P', "Draw Page Bounds";
	identifier = "mask DO_PAGE_BOUNDS";
}

DrawMargins = GenBoolean {
	moniker = 'M', "Draw Margin Bounds";
	identifier = "mask DO_MARGIN_BOUNDS";
}


/**************************************
 * PrntScrnTitle
 **************************************/

PrntScrnTitle = GenInteraction {
	children =
		PrntScrnTitleLeft,
		PrintingStatus;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ALLOW_CHILDREN_TO_WRAP
	}
}

PrntScrnTitleLeft = GenInteraction {
	children =
		FileName,
		PageCurrent,
		PageTotal,
		PageTotalReported,
		PageTotalReportedGlyph;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_VERTICALLY
	}
}

FileName = GenText {
	moniker = "File:";
	maxLength = 13;
	textDisplayAttributes = default +singleLineText;
	genAttributes = default +readOnly;
	hints = {
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 13>
			SpecHeight <>
		}
	}
}
	
PageCurrent = GenValue {
	moniker = "Page:";
	minimum = 1;
	maximum = 99;
	value = 1;
	destination = process;
	applyMsg = MSG_PRNT_SCRN_INVALIDATE_VIEW;
}

PageTotal = GenValue {
	moniker = "of";
	minimum = 1;
	maximum = 99;
	value = 1;
	genAttributes = default +readOnly;
}	

PageTotalReported = GenValue {
	moniker = "  (";
	minimum = 1;
	maximum = 99;
	value = 1;
	genAttributes = default +readOnly;
}	

PageTotalReportedGlyph = GenGlyph {
	moniker = "reported )";
}	

PrintingStatus = GenItemGroup {
	moniker = "Printing is:";
	children =
		PrintingOn,
		PrintingOff;
	selection = "FALSE";
	numSelections = 1;
	destination = PrintingStatus;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		ATTR_GEN_INIT_FILE_CATEGORY {char "spool", 0},
		ATTR_GEN_INIT_FILE_KEY {char "noPrinting", 0},
		ATTR_GEN_USES_HIERARCHICAL_INIT_FILE_CATEGORY,
		ATTR_GEN_ITEM_GROUP_INIT_FILE_BOOLEAN,
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word	MSG_META_SAVE_OPTIONS
		}
	}
}

PrintingOn = GenItem {
	moniker = "On";
	identifier = "FALSE";
}

PrintingOff = GenItem {
	moniker = "Off";
	identifier = "TRUE";
}


/**************************************
 * PrntScrnView
 **************************************/

PrntScrnView = GenView {
	output = process;
	docRight = "17 * 36";			/* 8.5" */
	docBottom = "11 * 72";			/* 11"  */
	horizAttributes =
		scrollable;
	vertAttributes =
		scrollable;
	viewAttributes =
		default +controlled;
	hints = {
		HINT_INITIAL_SIZE {
			SpecWidth <SST_PIXELS, 550>
			SpecHeight <SST_PIXELS, 340>
		},
		HINT_DEFAULT_TARGET,
		HINT_DEFAULT_FOCUS
	}
}


/**************************************
 * JobInfoDialog
 **************************************/

JobInfoDialog = GenInteraction {
	moniker = 'J', "Job Info";
	children =
		SpoolFileTopGroup,
		SpoolFileDocSize;
	type = notification;
	visibility = dialog;
	genStates = default -enabled;
}

SpoolFileTopGroup = GenInteraction {
	children =
		SpoolFileCreatorName,
		SpoolFileDocumentName,
		SpoolFileNumPages;
	hints = {
		HINT_CENTER_CHILDREN_ON_MONIKERS
	}
}

SpoolFileCreatorName = GenText {
	moniker = "Creator:";
	maxLength = 33;
	textDisplayAttributes = default +singleLineText;
	genAttributes = default +readOnly;
	hints = {
		HINT_MINIMUM_SIZE {
		    SpecWidth <SST_AVG_CHAR_WIDTHS, 32>
		    SpecHeight <>
		}
	}
}

SpoolFileDocumentName = GenText {
	moniker = "Document:";
	maxLength = 33;
	textDisplayAttributes = default +singleLineText;
	genAttributes = default +readOnly;
	hints = {
		HINT_MINIMUM_SIZE {
		    SpecWidth <SST_AVG_CHAR_WIDTHS, 32>
		    SpecHeight <>
		}
	}
}

SpoolFileNumPages = GenValue {
	moniker = "Page Count:";
	minimum = 1;
	maximum = 99;
	value = 1;
	genAttributes = default +readOnly;
}	

SpoolFileDocSize = PageSizeControl {
	pageSizeAttrs = "mask PZCA_ACT_LIKE_GADGET";
	type = organizational;
	visibility = subGroup;
	genAttributes = default +readOnly;
	genStates = default +enabled;
	hints = {
		HINT_CENTER_CHILDREN_ON_MONIKERS,
		ATTR_GEN_CONTROL_REQUIRE_UI {
			word	mask PageSizeControlFeatures
		}
	}
}

end	Interface;

start	Strings, data;

chunk NotFileErrorMsg	= "The selection is not a file. Operation aborted.";

chunk BadPathErrorMsg	= "Could not change to indicated path. Operation aborted.";

chunk FileOpenErrorMsg	= "Could not open selected file. Operation aborted.";

chunk BadFileErrorMsg	= "The selected file does not contain a valid graphics string.";

end	Strings;
