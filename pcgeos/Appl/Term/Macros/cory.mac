;
; U.C. Berkeley Cory logon  file
;
; $Id: cory.mac,v 1.5.40.1 97/03/29 09:45:40 canavese Exp $
;
PORT	1
:comm
COMM	1200-8-N-1-FULL
TERM	VT100
; clear the script display
CLEAR
:retry PAUSE
PRINT	"DIALING" , CR
DIAL	"642-0071"
MATCH	"BUSY"		GOTO	retry
MATCH	"NO"		GOTO	retry
MATCH	"CONNECT"	GOTO	connect
PROMPT	1800
PRINT	"TIMEOUT ON DIAL",CR
GOTO	done
:connect
PAUSE	
SEND	CR
MATCH  "Request:"	GOTO    request
PROMPT	300
PRINT	"TIMEOUT ON REQUEST",CR
GOTO	done
:request
PAUSE 
SEND	"cory",CR
:getLogin
MATCH  "login:"		GOTO	login
MATCH  "Trying"		GOTO	getLogin
PROMPT  900
PRINT	"TIMEOUT ON login",CR
GOTO	done
:login
PAUSE
SEND    "MY_ACCT_NAME",CR
MATCH  "Password:"	GOTO	pword
PROMPT	300
PRINT	"TIMEOUT ON password",CR
GOTO	done
:pword
PAUSE
SEND    "MY_PASSWORD",CR
MATCH  	"TERM"          GOTO	term
PROMPT  1200
PRINT	"TIMEOUT ON term",CR
GOTO	done
:term
PAUSE	120
SEND    "vt100",CR
GOTO    done
:done
BELL
PRINT	"DONE",CR
END

:ABORT
PRINT	"HANGUP PHONE?",CR
SEND	"+++"
MATCH	"OK"		GOTO 	dohup
PROMPT	300
PRINT	"Couldn't disconnect the modem",CR
GOTO	end
; Hangup the connection
:dohup
SEND	"ATH0",CR
MATCH	"OK"	GOTO	hangup
PROMPT	300
PRINT	"TIMEOUT ON hangup",CR
GOTO	end
:hangup
PRINT	"HUNG UP",CR
:end
BELL
END
