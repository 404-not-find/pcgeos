/*
 * Project:	Studio
 * File:	UI/uiLayout.ui
 *
 * Author:	Jenny Greenwood   7 March 1994
 *
 *	This file contains the user interface description for the page
 * layout dialog box.
 *
 *	$Id: uiLayout.ui,v 1.1 97/04/04 14:40:09 newdeal Exp $
 *
 */

#define GEOSTUDIO_SECTION_LIST(mon, addhints) GenDynamicList vardataReloc {\
	moniker = mon;\
	queryMsg = MSG_STUDIO_DOCUMENT_QUERY_SECTION_LIST;\
	destination = "TO_APP_MODEL";\
	genAttributes = default +notifyVisibility;\
	hints = {\
		addhints,\
		HINT_PLACE_MONIKER_ABOVE,\
		ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass },\
		HINT_ITEM_GROUP_SCROLLABLE,\
		HINT_FIXED_SIZE {\
			SpecWidth <SST_AVG_CHAR_WIDTHS, 40> | \
			SpecHeight <SST_LINES_OF_TEXT, 5> | \
			word    5\
		},\
		ATTR_GEN_ITEM_GROUP_CUSTOM_DOUBLE_PRESS { word 0 },\
		ATTR_GEN_VISIBILITY_MESSAGE {\
			word MSG_STUDIO_APPLICATION_INIT_SECTION_LIST\
		}\
	}\
}


/*-----------------------------------------------------------------------------
	Page menu
-----------------------------------------------------------------------------*/

/*
 * Subclass PageNameControlClass so as to be able to disable our page
 * name controller when the text object doesn't have the target.
 */
class	StudioLocalPageNameControl = PageNameControl {
	static wordComp : 0;	/* SLPNCI_allowEnable */
}

start PrimaryUI;

StudioPageNameControl = StudioLocalPageNameControl vardataReloc {
	moniker = 'P', "Page";
	visibility = popup;
	output = "TO_CUR_TEXT";
	hints = {
		ATTR_GEN_CONTROL_APP_UI {
			UN_OPTR PageMenuGroup
		},
		ATTR_GEN_OUTPUT_TRAVEL_START {
			UN_OPTR StudioDocGroup
		}
	}
}

end PrimaryUI;


start LayoutMenuUI;

PageMenuGroup = GenInteraction {
	genStates = default -usable;
	children = InsertPageBreakTrigger;
}


InsertPageBreakTrigger = GenTrigger {
	moniker = 'B', "Insert Page Break";
	action = MSG_STUDIO_PROCESS_INSERT_COLUMN_BREAK, process;
}

#if 0
/* comment these 2 tiggers out for Studio to get rid of compiler warnings */
InsertPageTrigger = GenTrigger vardataReloc {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	moniker = 'I', "Insert (Before This One)";
	action = MSG_STUDIO_DOCUMENT_INSERT_PAGE, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass } }
}

AppendPageTrigger = GenTrigger vardataReloc {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	moniker = 'A', "Append (After This One)";
	action = MSG_STUDIO_DOCUMENT_APPEND_PAGE, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass } }
}
#endif

DeletePageTrigger = GenTrigger vardataReloc {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	moniker = 'D', "Delete";
	action = MSG_STUDIO_DOCUMENT_DELETE_PAGE, "TO_APP_MODEL";
	genAttributes = default +notifyVisibility;
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass },
		ATTR_GEN_VISIBILITY_DATA { word 0, mask GV_DELETE_PAGE }
	}
}

end LayoutMenuUI;


start LayoutMenuUI;

HeaderFooterSubGroup = GenInteraction {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	moniker = 'H', "Header/Footer";
	children = EditHeaderTrigger, EditFooterTrigger;
	visibility = popup;
}

EditHeaderTrigger = GenTrigger vardataReloc {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	moniker = 'H', "Edit Header";
	action = MSG_STUDIO_DOCUMENT_EDIT_HEADER, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass } }
}

EditFooterTrigger = GenTrigger vardataReloc {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	moniker = 'F', "Edit Footer";
	action = MSG_STUDIO_DOCUMENT_EDIT_FOOTER, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass } }
}

/***/

TitlePageSubGroup = GenInteraction {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	moniker = 'T', "Title Page";
	genAttributes = default +notifyVisibility;
	children = CreateTitlePageTrigger, GotoTitlePageTrigger,
		   DeleteTitlePageTrigger;
	visibility = popup;
	hints = {
		ATTR_GEN_VISIBILITY_DATA { word 0, mask GV_TITLE_PAGE_MENU }
	}
}

CreateTitlePageTrigger = GenTrigger vardataReloc {
	moniker = 'C', "Create Title Page";
	action = MSG_STUDIO_DOCUMENT_CREATE_TITLE_PAGE, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass } }
}

GotoTitlePageTrigger = GenTrigger vardataReloc {
	moniker = 'G', "Go to Title Page";
	action = MSG_STUDIO_DOCUMENT_GOTO_TITLE_PAGE, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass } }
}

DeleteTitlePageTrigger = GenTrigger vardataReloc {
	moniker = 'D', "Delete Title Page";
	action = MSG_STUDIO_DOCUMENT_DELETE_TITLE_PAGE, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass } }
}

/***/

CreateFrameSubGroup = GenInteraction {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	children = CreateFrameTrigger;
}

CreateFrameTrigger = GenTrigger {
	moniker = 'r', "Create Graphic Frame";
	action = MSG_STUDIO_APPLICATION_CREATE_GRAPHICS_FRAME, StudioApp;
}

/***/

RulerSubGroup = GenInteraction {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	children = StudioGridSpacingControl, StudioRulerGuideControl;
}

StudioGridSpacingControl = RulerGridControl {
	moniker = 'G', "Grids";
	type = properties;
	visibility = dialog;
}

StudioRulerGuideControl = RulerGuideControl {
	moniker = 'u', "Guides";
	type = properties;
	visibility = dialog;
}

/***/

EditMasterPageTrigger = GenTrigger vardataReloc {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	moniker = 'M', "Edit Master Page";
	action = MSG_STUDIO_DOCUMENT_EDIT_MASTER_PAGE, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass } }
}

end LayoutMenuUI;


/***/

start LayoutMenuUI;

SectionSubMenu = GenInteraction {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	moniker = 'S', "Section";
	children = InsertSectionDialog, AppendSectionDialog,
		   DeleteSectionDialog, SectionOpSubGroup,
		   ReapplyMasterPageDialog;
	visibility = popup;
	genAttributes = default +notifyVisibility;
	hints = { ATTR_GEN_VISIBILITY_DATA { word 0, mask GV_SECTION_MENU } }
}

end LayoutMenuUI;

start LayoutDialogUI;

InsertSectionDialog = GenInteraction {
	moniker = 'I', "Insert Section";
	children = InsertSectionList, InsertSectionText, DoInsertSectionTrigger;
	visibility = dialog;
	type = command;
	attributes = modal;
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbInsSec", 0 }
	}
}

InsertSectionList =
	GEOSTUDIO_SECTION_LIST("Section to Insert Before:",)

InsertSectionText = GenText {
	moniker = "New Section Name:";
	maxLength = "MAX_SECTION_NAME_SIZE";
	textDisplayAttributes = default +singleLineText;
	genAttributes = default +notifyVisibility;
	hints = {
		ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS {
			word	offset DoInsertSectionTrigger, 0
		},
		ATTR_GEN_VISIBILITY_MESSAGE {
			word MSG_STUDIO_APPLICATION_INIT_TEXT_OBJECT
		},
		HINT_DEFAULT_FOCUS
	}
}

DoInsertSectionTrigger = GenTrigger vardataReloc {
	genStates = default -enabled;
	moniker = 'I', "Insert Section";
	genAttributes = default +signalInteractionComplete;
	action = MSG_STUDIO_DOCUMENT_INSERT_SECTION, "TO_APP_MODEL";
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass },
		HINT_SEEK_REPLY_BAR, HINT_DEFAULT_DEFAULT_ACTION
	}
}

AppendSectionDialog = GenInteraction {
	moniker = 'A', "Append Section";
	children = AppendSectionList, AppendSectionText, DoAppendSectionTrigger;
	visibility = dialog;
	type = command;
	attributes = modal;
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbAppSec", 0 }
	}
}

AppendSectionList =
	GEOSTUDIO_SECTION_LIST("Section to Append After:",)

AppendSectionText = GenText {
	moniker = "New Section Name:";
	maxLength = "MAX_SECTION_NAME_SIZE";
	textDisplayAttributes = default +singleLineText;
	genAttributes = default +notifyVisibility;
	hints = {
		ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS {
			word	offset DoAppendSectionTrigger, 0
		},
		ATTR_GEN_VISIBILITY_MESSAGE {
			word MSG_STUDIO_APPLICATION_INIT_TEXT_OBJECT
		},
		HINT_DEFAULT_FOCUS
	}
}

DoAppendSectionTrigger = GenTrigger vardataReloc {
	genStates = default -enabled;
	moniker = 'A', "Append Section";
	action = MSG_STUDIO_DOCUMENT_APPEND_SECTION, "TO_APP_MODEL";
	genAttributes = default +signalInteractionComplete;
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass },
		HINT_SEEK_REPLY_BAR, HINT_DEFAULT_DEFAULT_ACTION
	}
}

DeleteSectionDialog = GenInteraction {
	moniker = 'D', "Delete Section";
	children = DeleteSectionList, DoDeleteSectionTrigger;
	visibility = dialog;
	type = command;
	attributes = modal;
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbDelSec", 0 }
	}
}

DeleteSectionList = GEOSTUDIO_SECTION_LIST("Section to Delete",)

DoDeleteSectionTrigger = GenTrigger vardataReloc {
	moniker = 'D', "Delete";
	action = MSG_STUDIO_DOCUMENT_DELETE_SECTION, "TO_APP_MODEL";
	genAttributes = default +signalInteractionComplete;
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass },
		HINT_SEEK_REPLY_BAR, HINT_DEFAULT_DEFAULT_ACTION
	}
}

SectionOpSubGroup = GenInteraction {
	children = RenameSectionDialog, SetFirstSectionDialog;
}

RenameSectionDialog = GenInteraction {
	moniker = 'R', "Rename Section";
	children = RenameSectionList, RenameSectionText,
		   DoRenameSectionTrigger;
	visibility = dialog;
	type = command;
	attributes = modal;
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbRenSec", 0 }
	}
}

RenameSectionList =
	GEOSTUDIO_SECTION_LIST("Section to Rename",ATTR_GEN_ITEM_GROUP_STATUS_MSG {word MSG_STUDIO_DOCUMENT_UPDATE_RENAME_SECTION })

RenameSectionText = GenText {
	moniker = "New Name:";
	maxLength = "MAX_SECTION_NAME_SIZE";
	textDisplayAttributes = default +singleLineText;
	hints = {
		ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS {
			word	offset DoRenameSectionTrigger, 0
		},
		HINT_DEFAULT_FOCUS
	}
}

DoRenameSectionTrigger = GenTrigger vardataReloc {
	genStates = default -enabled;
	moniker = 'R', "Rename";
	action = MSG_STUDIO_DOCUMENT_RENAME_SECTION, "TO_APP_MODEL";
	genAttributes = default +signalInteractionComplete;
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass },
		HINT_SEEK_REPLY_BAR, HINT_DEFAULT_DEFAULT_ACTION
	}
}

SetFirstSectionDialog = GenInteraction {
	moniker = 'S', "Set Starting Section #";
	children = SetFirstSectionValue;
	type = properties;
	visibility = dialog;
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbSetFSec", 0 }
	}
}

SetFirstSectionValue = GenValue vardataReloc {
	moniker = "Starting Section Number:";
	destination = "TO_APP_MODEL";
	applyMsg = MSG_STUDIO_DOCUMENT_SET_STARTING_SECTION;
	genAttributes = default +notifyVisibility;
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass },
		ATTR_GEN_VISIBILITY_DATA { word 0, mask GV_SET_FIRST_SECTION }
	}
}

ReapplyMasterPageDialog = GenInteraction {
	moniker = 'M', "Reapply Master Page";
	children = ReapplyText, ResetReapplyTrigger, ReapplyOnlyTrigger;
	attributes = modal;
	type = command;
	visibility = dialog;
	/*
	 * No help context here (since this is like a confirmation box)
	 */
}

ReapplyText = GenText {
	genAttributes = default +readOnly;
	text = "Do you wish to reset the master page before reapplying it"
	       " to all the page layouts in this section, or do you wish"
	       " to reapply the existing master page without resetting it?";
}

ResetReapplyTrigger = GenTrigger vardataReloc {
	moniker = 'R', "Reset, Then Reapply";
	action = MSG_STUDIO_DOCUMENT_RESET_REAPPLY_MASTER_PAGE, "TO_APP_MODEL";
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass }
	}
}

ReapplyOnlyTrigger = GenTrigger vardataReloc {
	moniker = 'E', "Reapply Existing";
	action = MSG_STUDIO_DOCUMENT_REAPPLY_EXISTING_MASTER_PAGE,
								"TO_APP_MODEL";
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass }
	}
}

end LayoutDialogUI;

/***/

start LayoutMenuUI;

RecalcTrigger = GenTrigger vardataReloc {
	genStates = default -enabled;
	moniker = 'c', "Recalculate Layout";
	action = MSG_STUDIO_DOCUMENT_RECALC_LAYOUT, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass } }
}

end LayoutMenuUI;

/***/

#if 0

start LayoutMenuUI;

ArticlesDialog = GenInteraction {
	NOT_IMPLEMENTED
	moniker = 'A', "\\xa5Articles\\xa5";
	visibility = dialog;
	type = command;
}

end LayoutMenuUI;

#endif
