/*
 * Project:	Studio
 * File:	UI/uiPageSetup.ui
 *
 * Author:	Jenny Greenwood   7 March 1994
 *
 *	This file contains the user interface description for the page
 * setup dialog box.
 *
 *	$Id: uiPageSetup.ui,v 1.1 97/04/04 14:39:52 newdeal Exp $
 *
 */

/*
 * Page Size
 */

start FileMenuUI;

/*
 * Condo doesn't currently use this object
 */ 
#if 0
StudioPageSizeControl = PageSizeControl vardataReloc {
	moniker = 'z', "Page Size";
	pageSizeAttrs = "mask PZCA_ACT_LIKE_GADGET";
	genAttributes = default +notifyVisibility;
	output = "TO_APP_MODEL";
	type = properties;
	hints = {
		ATTR_GEN_VISIBILITY_DATA { word 0, mask GV_PAGE_SIZE },
		ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass },
		ATTR_GEN_CONTROL_REQUIRE_UI {
			word 	mask PSIZECF_CUSTOM_SIZE
		},
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word 	mask PSIZECF_LAYOUT or \
				mask PSIZECF_SIZE_LIST or \
				mask PSIZECF_PAGE_TYPE or \
				mask PSIZECF_MARGINS
		}

	}
}
#endif

/*
 * Page Setup
 */

StudioPageSetupDialog = GenInteraction {
	moniker = 'g', "Page Setup";
	visibility = dialog;
	type = properties;
	children = LayoutSectionNameText, LayoutFirstPageGroup,
		   LayoutColumnsGroup,
		   LayoutMasterPageList, LayoutMarginsGroup,
		   PageSetupApplyTrigger;
	genAttributes = default +notifyVisibility;
	hints = {
		ATTR_GEN_VISIBILITY_DATA { word 0, mask GV_PAGE_SETUP },
		ATTR_GEN_HELP_CONTEXT { char "dbPgSet", 0 }

	}
}

end FileMenuUI;

start PageSetupUI;

LayoutSectionNameText = GenText {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	genAttributes = readOnly;
	moniker = "Section to Change Page Setup for:";
	textDisplayAttributes = default +singleLineText;
	hints = { HINT_EXPAND_WIDTH_TO_FIT_PARENT }
}

LayoutFirstPageGroup = GenInteraction {
	children = LayoutFirstPageValue, LayoutFirstBooleanGroup;
	hints = { HINT_ORIENT_CHILDREN_HORIZONTALLY }
}

LayoutFirstPageValue = GenValue vardataReloc {
	moniker = "Starting Page Number:";
}

LayoutFirstBooleanGroup = GenBooleanGroup {
	children = FollowLastSectionBoolean;
	destination = StudioApp;
	hints = {
		ATTR_GEN_BOOLEAN_GROUP_STATUS_MSG {
			word MSG_STUDIO_APPLICATION_UPDATE_UI_FOR_FIRST_PAGE
		}
	}
}

FollowLastSectionBoolean = GenBoolean {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	moniker = "Follow Last Section";
	identifier = "mask SF_PAGE_NUMBER_FOLLOWS_LAST_SECTION";
}

LayoutColumnsGroup = GenInteraction {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	moniker = "Columns:";
	children = LayoutColumnsValue, LayoutColumnSpacingDistance,
		   LayoutColumnRuleWidthDistance;
	hints = {
	    HINT_ORIENT_CHILDREN_HORIZONTALLY,
	    HINT_ALLOW_CHILDREN_TO_WRAP,
	    HINT_MAXIMUM_SIZE {
		SpecWidth <SST_PCT_OF_FIELD_WIDTH, PCT_95>
		SpecHeight <>
		word	2
	    }
	}
}

LayoutColumnsValue = GenValue {
	minimum = 1;
	maximum = MAXIMUM_NUMBER_OF_COLUMNS;
	destination = StudioApp;
	hints = {
		ATTR_GEN_VALUE_STATUS_MSG {
			word MSG_STUDIO_APPLICATION_UPDATE_UI_FOR_COLUMNS
		}
	}
}


LayoutColumnSpacingDistance = GenValue ignoreDirty {
	genStates = default -enabled;
	moniker = "Spacing:";
	displayFormat = inchesOrCentimeters;
	minimum = "MINIMUM_COLUMN_SPACING/8";
	maximum = "MAXIMUM_COLUMN_SPACING/8";
	increment = 9;
}

LayoutColumnRuleWidthDistance = GenValue ignoreDirty {
	genStates = default -enabled;
	moniker = "Rule Width:";
	displayFormat = pointsOrMillimeters;
	value = "0";
	minimum = "0";
	maximum = "MAXIMUM_RULE_WIDTH/8";
	increment = 1;
}

/***/

LayoutMasterPageList = GenItemGroup {
	/*
	 * Make this unusable since Condo doesn't need it.
	 */
	genStates = default -usable;	
	moniker = "Master Pages:";
	children = LayoutMPOneEntry, LayoutMPTwoEntry;
	selection = 0;
	numSelections = 1;
	hints = { HINT_ORIENT_CHILDREN_HORIZONTALLY }
}

LayoutMPOneEntry = GenItem ignoreDirty {
	moniker = "One";
	identifier = 0;
}

LayoutMPTwoEntry = GenItem ignoreDirty {
	moniker = "Two (Left and Right)";
	identifier = 1;
}

LayoutMarginsGroup = GenInteraction {
    	/*
	 * Make this unusable since users shouldn't be dealing
	 * with margins.
	 */
    	genStates = default -usable;
	moniker = "Margins:";
	children = LayoutMarginLeftDistance, LayoutMarginTopDistance,
		   LayoutMarginRightDistance, LayoutMarginBottomDistance;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_ALLOW_CHILDREN_TO_WRAP,
		HINT_RIGHT_JUSTIFY_CHILDREN,
		HINT_WRAP_AFTER_CHILD_COUNT {
			word 	2
		}
	}
}

LayoutMarginLeftDistance = GenValue ignoreDirty {
	moniker = "Left";
	displayFormat = inchesOrCentimeters;
	minimum = "MINIMUM_MARGIN_SIZE/8";
	maximum = "MAXIMUM_MARGIN_SIZE/8";
	increment = 9;
}

LayoutMarginTopDistance = GenValue ignoreDirty {
	moniker = "Top";
	displayFormat = inchesOrCentimeters;
	minimum = "MINIMUM_MARGIN_SIZE/8";
	maximum = "MAXIMUM_MARGIN_SIZE/8";
	increment = 9;
}

LayoutMarginRightDistance = GenValue ignoreDirty {
	moniker = "Right";
	displayFormat = inchesOrCentimeters;
	minimum = "MINIMUM_MARGIN_SIZE/8";
	maximum = "MAXIMUM_MARGIN_SIZE/8";
	increment = 9;
}

LayoutMarginBottomDistance = GenValue ignoreDirty {
	moniker = "Bottom";
	displayFormat = inchesOrCentimeters;
	minimum = "MINIMUM_MARGIN_SIZE/8";
	maximum = "MAXIMUM_MARGIN_SIZE/8";
	increment = 9;
}

PageSetupApplyTrigger = GenTrigger vardataReloc {
	action = MSG_STUDIO_DOCUMENT_CHANGE_PAGE_SETUP, "TO_APP_MODEL";
	genAttributes = default +signalInteractionComplete;
	hints = {
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND { word IC_APPLY },
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass }	
	}
}

end PageSetupUI;
