/* -----------------------------------------------------------------
 *   converting the maker into a player
 *
 *   7/25/00	jfh    
 * -----------------------------------------------------------------*/

@include "wsplay.goh"
@include "bboxreg.goh"


/***************************************************************************
 * Application resource
 **************************************************************************/

@start AppResource;

@object GenApplicationClass WSPApp =
{
    GI_visMoniker = list { @WSPGraphicAppMoniker,
					  @WSPTinyAppMoniker,
					  @WSPTextMoniker };
    GI_comp = @WSPPrimary;
    gcnList(MANUFACTURER_ID_GEOWORKS, GAGCNLT_WINDOWS) = @WSPPrimary;
    gcnList(MANUFACTURER_ID_GEOWORKS, GAGCNLT_SELF_LOAD_OPTIONS) = 
	 @WSPDocumentControl, @WSPPrintControl;
    gcnList(MANUFACTURER_ID_GEOWORKS, MGCNLT_APP_STARTUP) = 
      @WSPDocumentControl;
    gcnList(MANUFACTURER_ID_GEOWORKS, MGCNLT_ACTIVE_LIST) = 
      @WSPDocumentControl;
    ATTR_GEN_APPLICATION_PRINT_CONTROL = @WSPPrintControl;
    ATTR_GEN_HELP_FILE = "WSP Help";
}

@visMoniker WSPTextMoniker = "Word Search Player";

@end AppResource;

/***************************************************************************
 * Moniker Resource
 **************************************************************************/

@start WSPMonikerResource, data;

@include "wspicon.goh"

@end WSPMonikerResource;

/***************************************************************************
 * Document Group Resource
 **************************************************************************/

@start DocGroupResource;

@object GenDocumentGroupClass WSPDocumentGroup =
{
    GDGI_untitledName = "Word Search Player Untitled";
    GDGI_output = process;
    GDGI_documentControl = WSPDocumentControl;
    GDGI_protocolMajor = WSP_DOCUMENT_PROTOCOL_MAJOR;
    GDGI_protocolMinor = WSP_DOCUMENT_PROTOCOL_MINOR;
}

@end    DocGroupResource;

/***************************************************************************
 * Interace resource
 **************************************************************************/

@start Interface;

@object GenPrimaryClass WSPPrimary =
{
	GI_visMoniker = "Word Search Player";
	GI_comp = @WSPFileGroup, @WSPDocumentGroups;
	ATTR_GEN_HELP_CONTEXT = "TOC";
}

@object GenInteractionClass WSPDocumentGroups =
{
    GI_comp = @WSPDocumentUIGroup, @WSPDocumentViewGroup;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

/***** File Group *****/

@object GenInteractionClass WSPFileGroup =
{
	 GI_comp = @WSPDocumentControl, @WSPInfo, @WSPSettingsGroup,
				  @WSPWSMTrig, @WSPPrintControl, @AboutSubGroup;
	 GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = (GIGT_FILE_MENU);
}

@object WSPDocumentControlClass WSPDocumentControl =
{
    GDCI_documentToken = {"WSMD", MANUFACTURER_ID_BREADBOX };
    GDCI_noNameText = "No Document";
    GDCI_documentGroup = @WSPDocumentGroup;
    GDCI_features = @default & ~(GDCF_SUPPORTS_TEMPLATES
       | GDCF_SUPPORTS_USER_SETTABLE_EMPTY_DOCUMENT
	  | GDCF_SUPPORTS_USER_SETTABLE_DEFAULT_DOCUMENT);
    GDCI_attrs = (@default & ~GDCA_MODE) | (GDCM_VIEWER << GDCA_MODE_OFFSET);
    GDCI_enableDisableList = WSPEnableList;
}

@chunk optr WSPEnableList[] = {
    @WSPDocumentUIGroup, @WSPSettingsGroup, @WSPInfo, @WSPPrintControl
};


@object GenInteractionClass WSPInfo =
{
    GI_visMoniker = 'I', "Puzzle Info";
	 GI_comp = @WSPInfoSize, @WSPInfoLevel, @WSPInfoCrossing;
    GI_states = @default & ~GS_ENABLED;
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_NOTIFICATION;
	 ATTR_GEN_HELP_CONTEXT = "Info";
	 HINT_CENTER_CHILDREN_ON_MONIKERS;
}

@object GenTextClass WSPInfoSize = {
    GI_visMoniker = "Size: ";
    GI_attrs = @default | GA_READ_ONLY;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
}

@object GenTextClass WSPInfoLevel = {
    GI_visMoniker = "Level: ";
    GI_attrs = @default | GA_READ_ONLY;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
}

@object GenTextClass WSPInfoCrossing = {
	 GI_visMoniker = "Crossing: ";
	 GI_attrs = @default | GA_READ_ONLY;
	 GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
}

/***** Settings Group *****/

@object GenInteractionClass WSPSettingsGroup =
{
    GI_visMoniker = 'A', "Answers";
    GI_comp = @WSPSettingsOther;
    GI_states = @default & ~GS_ENABLED;
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_PROPERTIES;
}

@object GenBooleanGroupClass WSPSettingsOther =
{
    GI_comp = @WSPSettingsShowAnswers;
    GBGI_indeterminateBooleans = WSPS_ANSWERS;
    GBGI_destination = process;
    GBGI_applyMsg = MSG_WSP_PROCESS_APPLY_SETTINGS_OTHER;
}

@object GenBooleanClass WSPSettingsShowAnswers =
{
    GI_visMoniker = "Show answers";
    GBI_identifier = WSPS_ANSWERS;
}

@object GenTriggerClass WSPWSMTrig =
{
	 GI_visMoniker = 'M', "Puzzle Maker";
	 GTI_destination = process ;
	 GTI_actionMsg   = MSG_WSP_OPEN_MAKER ;
}

@object PrintControlClass WSPPrintControl =
{
    GI_states = @default & ~GS_ENABLED;
    PCI_attrs = (@default & ~(PCA_PAGE_CONTROLS | PCA_VERIFY_PRINT)); 
    PCI_startUserPage = 1;
    PCI_endUserPage = 1;
    PCI_output = process;
    PCI_docNameOutput = @WSPDocumentGroup;
    PCI_docSizeInfo = { (8.5 * 72), (11 * 72), 0,
      {1 * 72, 1 * 72, 1 * 72, 1 * 72} };
}

@object GenInteractionClass AboutSubGroup = {
    GI_comp = @AboutDBox;
    GII_visibility = GIV_SUB_GROUP;
}

/* ---------------------------------------------------------------- */
@object GenInteractionClass AboutDBox = {
	 GI_visMoniker = 't', "About";
    GI_comp = @AppName, @Beta, @By, @BreadBoxLogo, @Visit,
		    @Web, @Copyright;
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_NOTIFICATION;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
}

@object GenTextClass AppName = {
    GI_attrs = @default | GA_READ_ONLY;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
	 GTXI_text = "Word Search Player";
	 ATTR_GEN_TEXT_CHAR_ATTR = (ChunkHandle) @AppNameCharAttrs;
}

@chunk VisTextCharAttr AppNameCharAttrs =
	CHAR_ATTR_FONT_SIZE_STYLE(
		FID_DTC_URW_SANS, 24, TS_BOLD);

@object GenGlyphClass Beta = {
  GI_visMoniker = "An Educational Product";
}

@object GenGlyphClass By = {
  GI_visMoniker = "from";
}

@object GenGlyphClass BreadBoxLogo = {
  GI_visMoniker = list { @BreadboxLogoMoniker}
}

@object GenGlyphClass Copyright = {
  GI_visMoniker = "Copyright 1994-2001 Breadbox Computer Company LLC";
}

@object GenGlyphClass Visit = {
  GI_visMoniker = "Visit us at...";
}

@object GenGlyphClass Web = {
  GI_visMoniker = "www.breadbox.com";
}


/***** Document UI Group *****/

@object GenInteractionClass WSPDocumentUIGroup = 
{
    GI_comp = @WSPWordListGroup;
    GI_states = @default & ~GS_ENABLED;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

/***** Word List Group *****/

@object GenInteractionClass WSPWordListGroup =
{
    GI_visMoniker = "Word List";
    GI_comp = @WSPWordList, @WSPWordsToFind, @WSPWordsFound;
    HINT_DRAW_IN_BOX;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenDynamicListClass WSPWordList =
{
    GI_attrs = @default | GA_READ_ONLY;
	 GDLI_queryMsg = MSG_WSP_PROCESS_QUERY_WORD_LIST;
    GIGI_destination = process;
//    GIGI_behaviorType = GIGBT_EXCLUSIVE;
    HINT_ITEM_GROUP_SCROLLABLE;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    HINT_MINIMUM_SIZE = {SST_AVG_CHAR_WIDTHS | 15, 0};
}

@object GenValueClass WSPWordsToFind = {
    GI_visMoniker = "Words to Find: ";
    GI_attrs = @default | GA_READ_ONLY;
    HINT_MAXIMUM_SIZE = {SST_AVG_CHAR_WIDTHS | 4, 0};
}

@object GenValueClass WSPWordsFound = {
    GI_visMoniker = "Words Found: ";
    GI_attrs = @default | GA_READ_ONLY;
    HINT_MAXIMUM_SIZE = {SST_AVG_CHAR_WIDTHS | 4, 0};
}


/***** Document View Group *****/

@object GenInteractionClass WSPDocumentViewGroup = 
{
    GI_comp = @WSPDocumentView;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
}

@object WSPGenViewClass WSPDocumentView =
{
    GVI_content = process;
    GVI_docBounds = { 0, 0, 0, 0 };
    GVI_attrs = @default & ~GVA_FOCUSABLE;
    GVI_horizAttrs = @default | GVDA_NO_LARGER_THAN_CONTENT;
    GVI_vertAttrs = @default | GVDA_NO_LARGER_THAN_CONTENT;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@end Interface;

@start StringsResource;

@chunk char EasyStr[] = "Easy";
@chunk char MedStr[] = "Medium";
@chunk char HardStr[] = "Hard";
@chunk char CongratsStr[] = "Congratulations! You have solved the puzzle!";
@chunk char YesStr[] = "Yes";
@chunk char NoStr[] = "No";
@chunk char WSFolderTxt[] = "Word Search Puzzles";

@end StringsResource;

