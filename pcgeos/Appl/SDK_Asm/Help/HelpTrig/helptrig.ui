/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  helptrig.ui
 * FILE:	  helptrig.ui
 *
 * AUTHOR:  	  Gene Anderson: Nov  6, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	11/ 6/92  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI for sample app with help
 *
 *
 * 	$Id: helptrig.ui,v 1.1 97/04/04 16:33:50 newdeal Exp $
 *
 ***********************************************************************/

/*----------------------------------------------------------------------------
	Include files
-----------------------------------------------------------------------------*/
#include "generic.uih"


start	AppResource;

HelpTrigApp = GenApplication {
	children =
		HelpTrigPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) =
		HelpTrigPrimary;
	moniker = list {
		HelpTrigTextMoniker
	}
	hints = {
		/*
		 * This attribute allows specifying the help file in geos.ini
		 * file.  Use:
		 *	[helptrig]
		 *	helpfile = <filename>
		 * If you wish to hardwire this application to a help file,
		 * it will be default use the permanent name of the app,
		 * or use ATTR_GEN_HELP_FILE instead.
		 */
		ATTR_GEN_HELP_FILE_FROM_INIT_FILE
	}
}

visMoniker HelpTrigTextMoniker = "Sample App with Help";

end	AppResource;



/* ----------------------------------------------------------------------------
	 Primary window.
-----------------------------------------------------------------------------*/
start	Interface;

HelpTrigPrimary = GenPrimary {
	children =
		Menu1,
		Menu2;
	hints = {
		HINT_PRIMARY_FULL_SCREEN,
		/*
		 * Motif will automatically add a help trigger and
		 * place it in the title bar if ATTR_GEN_HELP_CONTEXT is
		 * placed on the GenPrimary.
		 */
		ATTR_GEN_HELP_CONTEXT {
			char "TOC", 0
		}
	}
}

/* --------------------------------------------------------------------------*/

Menu1 = GenInteraction {
	moniker = '1', "Menu 1";
	children =
		DB1,
		DB2;
	visibility = popup;
}

DB1 = GenInteraction {
	moniker = '1', "Dialog #1";
	visibility = dialog;
	type = command;
	children =
		RandomObject1;
	hints = {
		ATTR_GEN_HELP_CONTEXT {
			char "dbDialog1", 0
		}
	}
}

RandomObject1 = GenText {
	text = "your objects go here";
}

DB2 = GenInteraction {
	moniker = '2', "Dialog #2";
	visibility = dialog;
	type = command;
	children =
		RandomObject2;
	hints = {
		ATTR_GEN_HELP_CONTEXT {
			char "dbDialog2", 0
		},
		HINT_INTERACTION_MODAL
	}
}

RandomObject2 = GenText {
	text = "more objects, but in a modal DB";
}

/* --------------------------------------------------------------------------*/

Menu2 = GenInteraction {
	moniker = '2', "Menu 2";
	children =
		DB3;
	visibility = popup;
}

DB3 = GenInteraction {
	moniker = '3', "Dialog #3";
	visibility = dialog;
	type = properties;
	children =
		RandomObject3;
	hints = {
		ATTR_GEN_HELP_CONTEXT {
			char "dbDialog3", 0
		}
	}
}

RandomObject3 = GenValue {
	moniker = "A GenValue";
}

end Interface;
