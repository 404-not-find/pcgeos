/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  helpview.ui
 * FILE:	  helpview.ui
 *
 * AUTHOR:  	  Gene Anderson: Nov  6, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	11/ 6/92  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI for sample help viewer app
 *
 *
 * 	$Id: helpview.ui,v 1.1 97/04/04 16:33:44 newdeal Exp $
 *
 ***********************************************************************/

/*----------------------------------------------------------------------------
	Include files
-----------------------------------------------------------------------------*/
#include "generic.uih"


start	AppResource;

HelpViewApp = GenApplication {
	children =
		HelpViewPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) =
		HelpViewPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST) =
		HelpViewHelpObject;
	moniker = list {
		HelpViewTextMoniker
	}
	hints = {
		/*
		 * This attribute allows specifying the help file in geos.ini
		 * file.  Use:
		 *	[helpview]
		 *	helpfile = <filename>
		 * If you wish to hardwire this application to a help file,
		 * it will be default use the permanent name of the app,
		 * or use ATTR_GEN_HELP_FILE instead.
		 */
		ATTR_GEN_HELP_FILE_FROM_INIT_FILE,
		/*
		 * The type of the help specified here should match the
		 * help type specified in the help controller below.
		 * If the help text has links, this should probably
		 * either be HT_NORMAL_HELP or HT_FIRST_AID.
		 */
		ATTR_GEN_HELP_TYPE {byte HT_NORMAL_HELP}
	}
}

visMoniker HelpViewTextMoniker = "Sample Help Viewer";

end	AppResource;



/* ----------------------------------------------------------------------------
	 Primary window.
-----------------------------------------------------------------------------*/
start	Interface;

HelpViewPrimary = GenPrimary {
	children =
		HelpViewHelpObject;
	hints = {
		/*
		 * Other useful (but not required) hints to make the viewer
		 * app come up looking nice.
		 */
		HINT_SIZE_WINDOW_AS_RATIO_OF_FIELD {
			SpecWinSizePair < mask SWSS_RATIO or PCT_70, \
					  mask SWSS_RATIO or PCT_70 >
		},
		HINT_CENTER_WINDOW
	}
}

HelpViewHelpObject = HelpControl {
	helpType = "HT_NORMAL_HELP";
	visibility = subGroup;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		/*
		 * This prevents the controller from having a close/exit
		 * trigger, which given that this is not a dialog box,
		 * doesn't make any sense or work anyway.
		 */
		ATTR_GEN_CONTROL_PROHIBIT_UI {word mask HPCF_CLOSE},
		/*
		 * Name of the initial help text to bring up.  This
		 * is usually most useful if it is "TOC", but there
		 * may be exceptions...
		 */
		ATTR_HELP_INITIAL_HELP {
			char "TOC", 0
		}
	}
}

end Interface;
