/*
 * Project:	PC/GEOS
 * Module:	DocUI (Sample PC/GEOS application)
 * File:	docui.ui
 *
 * Author:	Tony Requist
 *
 *	This file contains the user Interface description for this
 * application. This file is written in a language called ESPIRE, which is
 * compiled by the UIC compiler. See Spec/User/uic.doc for info.
 *
 * IMPORTANT NOTE:
 *	This sample application is primarily intended to demonstrate a
 *	model for handling documents.  Basic parts of a PC/GEOS application
 *	are not documented heavily here.  See the "Hello" sample application
 *	for more detailed documentation on the standard parts of a PC/GEOS
 *	application.
 *
 * RCS STAMP:
 *	$Id: docui.ui,v 1.1 97/04/04 16:33:06 newdeal Exp $
 *
 */

/* ----------------------------------------------------------------------------
	Include files
-----------------------------------------------------------------------------*/

#include "generic.uih"

/* ----------------------------------------------------------------------------
	Application Object
-----------------------------------------------------------------------------*/

start	AppResource;

DUIApp = GenApplication {
	children = DUIPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) = DUIPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_APP_STARTUP) =
		DUIDocumentControl;
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST) =
		DUIDocumentControl;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_SELF_LOAD_OPTIONS) =
		DUIDocumentControl;
	moniker = list {
		DUITextMoniker
	}
	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY {
		char	"docui", 0
		}
	}
}

visMoniker DUITextMoniker = "DocUI Sample Application";

end	AppResource;

/* ----------------------------------------------------------------------------
	 Primary window.
-----------------------------------------------------------------------------*/

/*
 * The GenDocumentControl must be a child of the primary, and must be
 * on the active list.
 */

start	Interface;		/* this resource is for misc UI objects */

DUIPrimary = GenPrimary {
	children = DUIFileMenu, DUIValueX, DUIValueY;
	hints = { ATTR_GEN_DISPLAY_NOT_MINIMIZABLE, HINT_SIZE_WINDOW_AS_DESIRED }
}

DUIFileMenu = GenInteraction {
	children = DUIDocumentControl;
	visibility = popup;
	hints = { ATTR_GEN_INTERACTION_GROUP_TYPE { GenInteractionGroupType GIGT_FILE_MENU } }
}

/* ----------------------------------------------------------------------------
	 UI Document Control
-----------------------------------------------------------------------------*/

/*
 * The GenDocumentControl should be put in the same resource as the
 * GenPrimary (for efficiency).  The GenDocumentControl will build the
 * file menu.
 */

DUIDocumentControl = GenDocumentControl {
	/*
	 * Document file's token
	 */
	documentTokenChars = "'S', 'A', 'M', 'P'";
	documentTokenID = "MANUFACTURER_ID_GEOWORKS";
	/*
	 * Creator's file's token
	 */
	/*
	 * Text to display in the header area of the primary when no
	 * document is open.
	 */
	noNameText = "No Document";
	/*
	 * Template directory
	 */
	templateDir = "SAMPLE";
	/*
	 * Pointer to the GenDocumentGroup
	 */
	appDC = DUIDocumentGroup;
	/*
	 * List of objects to enable/disable based on whether or not a
	 * document exists
	 */
	enableDisableList = DUIUIEnableList;
}

chunk DUIUIEnableList = data {
	UN_OPTR	DUIValueX
	UN_OPTR	DUIValueY
}

end Interface;

/* ----------------------------------------------------------------------------
	 Document Group
-----------------------------------------------------------------------------*/

/*
 * The GenDocumentGroup should be put in a resource of its own run by
 * the application thread.
 */

start DocumentUI;

DUIDocumentGroup = GenDocumentGroup {
	/*
	 * Name for untitled documents
	 */
	defaultName = "DUI Untitled";
	/*
	 * Class for document objects
	 */
	documentClass = "DUIDocumentClass";
	/*
	 * Document protocol
	 */
	protocolMajor = "DOCUMENT_PROTOCOL_MAJOR";
	protocolMinor = "DOCUMENT_PROTOCOL_MINOR";
	/*
	 * Pointer to the GenDocumentControl
	 */
	uiDC = DUIDocumentControl;
}

end DocumentUI;

/*-----------------------------------------------------------------------------
	UI objects to display the document
-----------------------------------------------------------------------------*/

start Interface;

DUIValueX = GenValue vardataReloc {
	genStates = default -enabled;
	minimum = 0;
	maximum = "DOCUMENT_WIDTH-1";
	applyMsg = MSG_DUI_DOCUMENT_X_CHANGE;
	destination = "TO_APP_MODEL";
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD GenDocumentClass }
	}
}

DUIValueY = GenValue vardataReloc {
	genStates = default -enabled;
	minimum = 0;
	maximum = "DOCUMENT_HEIGHT-1";
	applyMsg = MSG_DUI_DOCUMENT_Y_CHANGE;
	destination = "TO_APP_MODEL";
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD GenDocumentClass }
	}
}

end Interface;
