/*
 * Project:	PC/GEOS
 * Module:	Item (Sample PC/GEOS application)
 * File:	item.ui
 *
 * Author:	Eric E. Del Sesto
 *
 *	This file contains the user Interface description for this
 * application. This file is written in a language called ESPIRE, which is
 * compiled by the UIC compiler. See Spec/User/uic.doc for info.
 *
 * RCS STAMP:
 *	$Id: item.ui,v 1.1 97/04/04 16:34:30 newdeal Exp $
 *
 */

/* ----------------------------------------------------------------------------
	Include files
-----------------------------------------------------------------------------*/

/* first, read pre-defined class definitions. See the file
 * /staff/pcgeos/Include/generic.uih. */

#include "generic.uih"

/* ----------------------------------------------------------------------------
	Application Object
-----------------------------------------------------------------------------*/
/* The very top-level generic object of an application MUST be a GenApplication
 * object. The item.gp file contains "appobj" statement which indicates
 * that this "ItemApp" object is in fact the top-level UI object.
 * This object should be in its own resource so that GeoManager can load it
 * quickly to grab the icon for this application. GeoManager will store this
 * icon in its "Token Database", so that the icon image can be reused quickly.*/

start	AppResource;

ItemApp = GenApplication {
	children = ItemPrimary;		/* The GenApplication object only
					 * has one child: the main application
					 * window. */

	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) = ItemPrimary;
					/* The main window should appear
					 * on-screen when the application
					 * is launched. */

	moniker = list {		/* The application object has a list
					 * of visual monikers */
		ItemTextMoniker		/* Used in the express menu and in
					 * the header for the primary window */
	}
}

visMoniker ItemTextMoniker = "Item";

end	AppResource;


/* ----------------------------------------------------------------------------
	 Primary window.
-----------------------------------------------------------------------------*/
/* Every application has at least one GenPrimary object. This object serves
 * as the main window of the application. Inside this window, various UI
 * components, such as menus or lists, can be placed. */

start	Interface;		/* this resource is for misc UI objects */

ItemPrimary = GenPrimary {
	children = ItemOptionsMenu, ItemGenValue,
		   ItemGenDynamicList, ItemReplyBar;

	/* At present, we are not supplying the icon image for this application,
	 * (just the text moniker given above), so we want to prevent the
	 * user from minimizing this window. */

	hints = {
		ATTR_GEN_DISPLAY_NOT_MINIMIZABLE
	}
}

/*-----------------------------------------------------------------------------
			ItemOptionsMenu
-----------------------------------------------------------------------------*/

ItemOptionsMenu = GenInteraction {
	visibility = popup;
	moniker = "Options";
	children = ItemRescanListTrigger;
}

ItemRescanListTrigger = GenTrigger {

	/* GenClass Instance Data */

		genStates = default +enabled;

		moniker = "Rescan All Items";

	/* GenTriggerClass Instance Data */

		action = MSG_ITEM_RESCAN_LIST, process;
}

/*-----------------------------------------------------------------------------
			ItemGenValue
-----------------------------------------------------------------------------*/

ItemGenValue = GenValue {

	/* GenClass Instance Data */

		genStates = default -enabled;
		moniker = "Value:";

	/* GenValueClass Instance Data */

		minimum = 0;
		maximum = 99;
		applyMsg = MSG_ITEM_SET_ITEM_VALUE;
		destination = process;
}


/*-----------------------------------------------------------------------------
			ItemGenDynamicList
-----------------------------------------------------------------------------*/

ItemGenDynamicList = GenDynamicList {

	/* GenClass Instance Data */

		genStates = default +enabled;

		hints = {
			HINT_ITEM_GROUP_SCROLLABLE,
			HINT_ORIENT_CHILDREN_VERTICALLY
		}

	/* GenItemGroup Instance Data */

		behaviorType = exclusiveNone;

		numSelections = 1;

		destination = process;

		applyMsg = MSG_ITEM_LIST_ITEM_SELECTED;

	/* GenDynamicListClass Instance Data */

		numItems = 0;

		queryMsg = MSG_ITEM_LIST_REQUEST_ITEM_MONIKER;
}


/*-----------------------------------------------------------------------------
			ItemReplyBar
-----------------------------------------------------------------------------*/

ItemReplyBar = GenInteraction {
	children = ItemInsertTrigger, ItemDeleteTrigger;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

ItemInsertTrigger = GenTrigger {
	moniker = "Insert";
	action = MSG_ITEM_INSERT_ITEM, process;
}

ItemDeleteTrigger = GenTrigger {
	moniker = "Delete";
	action = MSG_ITEM_DELETE_ITEM, process;
}

end Interface;
