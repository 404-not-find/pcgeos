/***********************************************************************
 *
 *	Copyright (c) Geoworks 1994 -- All Rights Reserved
 *
 * PROJECT:	  Chart2
 * MODULE:	  
 * FILE:	  chart2.ui
 *
 * AUTHOR:  	  David Litwin, Jan 19, 1994
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	dlitwin	1/19/94   	Initial version
 *
 * DESCRIPTION:
 *	UI for the Chart2 application
 *
 *
 * 	$Id: chart2.ui,v 1.1 97/04/04 16:35:16 newdeal Exp $
 *
 ***********************************************************************/

/* ----------------------------------------------------------------------------
	Include files
-----------------------------------------------------------------------------*/
#include "generic.uih"


/* ----------------------------------------------------------------------------
	Class definitions
-----------------------------------------------------------------------------*/
class	Chart = Vis {
	chartDataBlock	= hptrComp : null;
	chartDataSize	= wordComp : "0";
}


/* ----------------------------------------------------------------------------
	Application Object
-----------------------------------------------------------------------------*/

start	AppResource;

ChartApp = GenApplication {

	moniker = ChartTextMoniker;

	children = ChartPrimary;

	windows_list = ChartPrimary;
}

visMoniker ChartTextMoniker = "Chart";

end	AppResource;


/* ----------------------------------------------------------------------------
	 Primary window.
-----------------------------------------------------------------------------*/

start	Interface;		/* this resource is for misc UI objects */
ChartPrimary = GenPrimary {
	children =	ChartMenu,
			ChartView;
}


ChartView = GenView {
	backColorR = C_LIGHT_GRAY;

	output = ChartContent;
}

ChartMenu = GenInteraction {
	moniker = "Chart";
	visibility = popup;
	children = SetNewChart;
}

SetNewChart = GenInteraction {
	moniker = "New Chart";
	visibility = dialog;
	children =	ChartHigh,
			ChartLow,
			ChartBars,
			ChartAscending,
			ChartDescending,
			ChartCancel;
}

ChartHigh = GenValue {
	moniker = "High range";
	value = 80;
	minimum = 0;
	maximum = "MAX_HEIGHT";
}

ChartLow = GenValue {
	moniker = "Low Range";
	value = 10;
	minimum = 0;
	maximum = "MAX_HEIGHT";
}

ChartBars = GenValue {
	moniker = "Number of bars";
	value = 10;
	minimum = 2;
	maximum = 100;
}

ChartAscending = GenTrigger {
	moniker = "Ascending";
	genAttributes = default +signalInteractionComplete;
	action = MSG_CHART_SET_NEW_CHART, ChartObj;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	NCT_ASCENDING
		}
	}
}

ChartDescending = GenTrigger {
	moniker = "Descending";
	genAttributes = default +signalInteractionComplete;
	action = MSG_CHART_SET_NEW_CHART, ChartObj;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	NCT_DESCENDING
		}
	}
}

ChartCancel = GenTrigger {
	moniker = "Cancel";
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

end	Interface;

start	AppObjects;
ChartContent = VisContent {
	view = ChartView;
	children = ChartObj;
	visCompGeoAttrs = default +customManageChildren;
}

ChartObj = Chart {
	right = 450;
	bottom = 300;
}

end	AppObjects;



