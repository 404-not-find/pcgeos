
COMMENT }%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	Copyright (c) Berkeley Softworks 1990 -- All Rights Reserved


PROJECT:	PC GEOS
MODULE:		Epson Escape P2 24-pin printer driver
FILE:		escp2Page.asm

AUTHOR:		Dave Durran

ROUTINES:
	Name		Description
	----		-----------
	PrintStartPage	initialize the page-related variables, called once/page
			by EXTERNAL at start of page.
	PrintEndPage	Tidy up the page-related variables, called once/page
			by EXTERNAL at end of page.

REVISION HISTORY:
	Name	Date	Description
	----	----	-----------
	Dave	3/90	initial version

DESCRIPTION:

	$Id: escp2Page.asm,v 1.1 97/04/18 11:54:21 newdeal Exp $

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%}


COMMENT }%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		PrintStartPage
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:	Initialize the page

CALLED BY:	GLOBAL

PASS:		bp	- PSTATE segment address.

RETURN:		

DESTROYED:

PSEUDO CODE/STRATEGY:

KNOWN BUGS/SIDE EFFECTS/IDEAS:
	nothing

REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	Dave	3/90		Initial version

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%}

PrintStartPage	proc	far
		uses	ds,dx
		.enter
		
		; see if the spooler is in the suppress formfeed mode.

		cmp	cl,C_FF
		jne	suppressformfeed

		; start cursor out at top,left position

		call	PrintHomeCursor	;start out from home position.
		jc	exit

suppressformfeed:
		; init font style.  

		clr	dx			; init styles to all clear
		call	PrintSetStyles		; set clear styles
exit:
		.leave
		ret
PrintStartPage	endp


COMMENT }%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		PrintEndPage
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:	Initialize the page

CALLED BY:	GLOBAL

PASS:		bp	- PSTATE segment address.

RETURN:		

DESTROYED:

PSEUDO CODE/STRATEGY:

KNOWN BUGS/SIDE EFFECTS/IDEAS:
	nothing

REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	Dave	3/90		Initial version

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%}

PrintEndPage	proc	far
		uses	ax,cx, es, dx
		.enter

		mov	es, bp

		;get rid of more than 1 integral form length.

		mov	dx,es:PS_customHeight	;get the paper size in Y.
		call	PrTo360s		;convert to 1/180" units
		mov	ax,es:PS_cursorPos.P_y	;get the current Y pos.
checkIfMoreThanPage:
		sub	ax,dx			;subtract a paper size
		js	nosub
		mov	es:PS_cursorPos.P_y,ax	;set the current Y pos.
		jmp	checkIfMoreThanPage	;do till less than page length.
nosub:
		; see if the spooler is in the suppress formfeed mode.

		cmp	cl,C_FF
		jne	suppressformfeed

		; send a form feed to the bugger

		test	es:PS_paperPath,mask PFO_TRACTOR
		jnz	newFormFeed
		mov	cl,C_FF		;a form-feed is pre loaded.
		call	PrintStreamWriteByte
		jmp	formFeedOut
;NEW FORM FEED ROUTINE TO TAKE CARE OF CUSTOM PAPER SIZE PROBLEMS.
newFormFeed:
		mov	dx,es:PS_customHeight	;get the paper size in Y.
		call	PrTo360s		;convert to 1/360" units
		sub	dx,es:PS_cursorPos.P_y	;subtract the current Y pos.
		call	PrLineFeed		;hand line-feed it to next TOF.
formFeedOut:
		jc	exit

		; make sure all the styles are reset at the printer for 
		; the next page.  Use version that doesn't care about
		; NLQ mode, since we want to biff it

		clr	dx			; init styles to all clear
		call	PrintSetStylesInt	; set clear styles @ printer
		jmp	exit
suppressformfeed:
                ; If in graphics mode, everything is OK....

                cmp     es:PS_mode,PM_FIRST_TEXT_MODE
                jge      toascii

		clc		;get rid of any error generated by cmp.
		jmp	exit

toascii:
                mov     dx,es:PS_curFont.FE_size ;load the current font size.
                add     dx,es:PS_cursorPos.P_y  ;add to current position.
                mov     cx,0		       ;return to left margin.
                call    PrintSetCursor  ;do line feed of the correct length.
exit:
		.leave
		ret
PrintEndPage	endp
