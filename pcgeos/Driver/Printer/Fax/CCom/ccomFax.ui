/****************************************************************************

	Copyright (c) GeoWorks 1989 -- All Rights Reserved

PROJECT:	PC GEOS
MODULE:		Printer/Fax/CCom
FILE:		ccomFax.ui

AUTHOR:		Don Reeves, 5-2-91

REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	Don	5/91		Initial revision

DESCRIPTION:
	Contains the UI definitions for the CCom fax driver
		
	$Id: ccomFax.ui,v 1.1 97/04/18 11:52:38 newdeal Exp $

*****************************************************************************/

#include	"generic.uih"

/*****************************************************************************
 *                       Facsimile Driver User Interface
 *****************************************************************************/

class	FaxInfo	= GenInteraction {
	numPages	= wordComp : 1;	/* default to 1 in case -pageRange */
}


start	FaxUI, notDetachable;

FaxDialogBox = FaxInfo {
	moniker = "Complete Communicator Options";
	children =
		PhoneGroup,
		OptionsGroup;
	genStates = default -usable;
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}


/****************************************
 * Phone Number Group
 ****************************************/

PhoneGroup = GenInteraction {
	children =
		PhoneNumber,
		DexTrigger;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

PhoneNumber = GenText {
	moniker = "Phone Number:";
	text = "";
	textDisplayAttributes =
		singleLineText,
		noWordWrapping,
		useTabForNavigation;
	maxLength = 26;
	hints = {
		HINT_DEFAULT_FOCUS,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 16>
			SpecHeight <>
			word	0
		}
	}
}

DexTrigger = GenTrigger {
	moniker = "Use GeoDex";
	action = MSG_FAX_INFO_WANTS_GEODEX, FaxDialogBox;
}


/****************************************/
/* Options Group			*/
/****************************************/

OptionsGroup = GenInteraction {
	children =
		CoverSheetList,
		OptionsSpacer,
		CoverSheetDialogBox;
	hints = { 
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_CENTER_CHILDREN_VERTICALLY
	}
}

CoverSheetList = GenItemGroup {
	moniker = "Cover Sheet:";
	children =
		CoverYes,
		CoverNo;
	behaviorType = exclusive;
	numSelections = 1;
	selection = FALSE;
	applyMsg = MSG_FAX_INFO_SET_COVER_SHEET;
	destination = FaxDialogBox;
}

CoverYes = GenItem {
	moniker = "Yes";
	identifier = TRUE;
}

CoverNo = GenItem {
	moniker = "No";
	identifier = FALSE;
}

OptionsSpacer = GenInteraction {
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

CoverSheetDialogBox = GenInteraction {
	moniker = "Show Cover Sheet";
	children =
		CoverSheetInfo,
		CoverSheetDoneTrigger;
	visibility = dialog;
	attributes = default +modal;
	genStates = default -enabled;
}

CoverSheetDoneTrigger = GenTrigger {
	moniker = "Done";
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR 
	}
}

/****************************************
 * Cover sheet information group
 ****************************************/

CoverSheetInfo = GenInteraction {
	moniker = "Cover Sheet Information";
	children =
		CoverSheetDateAndPages,
		CoverSheetTo,
		CoverSheetFrom,
		CoverSheetCC,
		CoverSheetSubject,
		CoverSheetMessage;
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_CENTER_CHILDREN_ON_MONIKERS,
		HINT_LEFT_JUSTIFY_MONIKERS
	}
}


CoverSheetDateAndPages = GenInteraction {
	moniker = "Date:";
	children =
		CoverSheetDate,
		CoverSheetPages;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_VERTICALLY,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT
	}
}

#define CI_EDIT_ATTRIBUTES	singleLineText, \
				noWordWrapping, \
				useTabForNavigation

CoverSheetDate = GenText {
	maxLength = 30;
	textDisplayAttributes = CI_EDIT_ATTRIBUTES;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 24>
			SpecHeight <>
		}
	}
}

CoverSheetPages = GenText {
	moniker = "# Pages (with cover):";
	text = "";
	textDisplayAttributes =
		singleLineText,
		noWordWrapping;
	hints = {
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 3>
			SpecHeight <>
		}
	}
}

CoverSheetTo = GenText {
	moniker = "To:";
	maxLength = 60;
	textDisplayAttributes = CI_EDIT_ATTRIBUTES;
	hints = {
		HINT_DEFAULT_FOCUS,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_PIXELS, HEADER_WIDTH>
			SpecHeight <>
		}
	}
}

CoverSheetFrom = GenText {
	moniker = "From:";
	maxLength = 60;
	textDisplayAttributes = CI_EDIT_ATTRIBUTES;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_PIXELS, HEADER_WIDTH>
			SpecHeight <>
		}
	}
}

CoverSheetCC = GenText {
	moniker = "CC:";
	maxLength = 60;
	textDisplayAttributes = CI_EDIT_ATTRIBUTES;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_PIXELS, HEADER_WIDTH>
			SpecHeight <>
		}
	}
}

CoverSheetSubject = GenText {
	moniker = "Subject:";
	maxLength = 60;
	textDisplayAttributes = CI_EDIT_ATTRIBUTES;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_PIXELS, HEADER_WIDTH>
			SpecHeight <>
		}
	}
}

CoverSheetMessage = GenText {
	maxLength = COVER_SHEET_MSG_LENGTH;
	moniker = "Message:";
	text = "";
	textDisplayAttributes =
		initScrolling;
	hints = {
		HINT_PLACE_MONIKER_ABOVE,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_FIXED_SIZE {
			SpecWidth <>
			SpecHeight <SST_LINES_OF_TEXT, 4>
		}
	}
}

#include	"ccomCoversheet.ui"

chunk	badPhoneNumberMessage	= "The phone number you gave is either missing or invalid. A phone number may contain only digits, parentheses, spaces, *, or # for the actual number, and commas or semi-colons to indicate pauses. It must have at least one digit in it.";

end FaxUI;
