/***********************************************************************
 *
 *	Copyright (c) Geoworks 1994 -- All Rights Reserved
 *
 * PROJECT:	  Serial/IR communication protocol
 * MODULE:	  IrLAP address controller
 * FILE:	  irlapAddressControl.ui
 *
 * AUTHOR:  	  Steve Jang, Nov 15, 1994
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	SJ	11/15/94   	Initial version
 *
 * DESCRIPTION:
 *	IrLAP address controller UI definitions
 *
 * 	$Id: irlapAddressControl.ui,v 1.1 97/04/18 11:57:02 newdeal Exp $
 *
 ***********************************************************************/

#include "generic.uih"

start	IrlapAddrCtrlUI, notDetachable;

/*
----------------------------
	Main UI
----------------------------
*/

IrlapAddrCtrlBox	= GenInteraction {
	genStates = default -usable;
	children = IrlapAddrCtrlPrompt,
		   IrlapAddrCtrlDiscoveryButton,
		   IrlapAddrCtrlDiscoveryDialog;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	}
}

IrlapAddrCtrlPrompt	= GenText {
	moniker = "Address";
	maxLength = IRLAP_ADDRESS_LEN;
	textDisplayAttributes = default +singleLineText;
	hints = {
		HINT_PLACE_MONIKER_TO_LEFT,
		HINT_TEXT_WHITE_WASH_COLOR
	}
}

IrlapAddrCtrlDiscoveryButton = GenTrigger {
	moniker = "Discover";
	action = MSG_IRLAP_ADDR_START_DISCOVERY, "TO_OBJ_BLOCK_OUTPUT";
}

/*
----------------------------------
	Discovery Dialog
----------------------------------
*/

IrlapAddrCtrlDiscoveryDialog = GenInteraction {
	moniker = "IrLAP Discovery";
	visibility = dialog;
	attributes = notUserInitiatable, modal;
	children = IrlapAddrCtrlAddressList,
		   IrlapAddrCtrlDiscoverySelect,
		   IrlapAddrCtrlDiscoveryRetry,
		   IrlapAddrCtrlDiscoveryDismiss;
}

IrlapAddrCtrlAddressList	= GenDynamicList {
	moniker = "Available Addresses";
	applyMsg = MSG_IRLAP_ADDR_SET_ADDR;
	queryMsg = MSG_IRLAP_ADDR_GET_ADDR_STR;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_PLACE_MONIKER_ABOVE,
		HINT_DRAW_IN_BOX,
		HINT_ITEM_GROUP_SCROLLABLE,
	}
}

IrlapAddrCtrlDiscoveryRetry 	= GenTrigger {
	moniker	= "Retry";
	action = MSG_IRLAP_ADDR_DO_DISCOVERY, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

IrlapAddrCtrlDiscoverySelect	= GenTrigger {
	moniker = "Select";
	genStates = default -enabled;
	action = MSG_IRLAP_ADDR_CONFIRM_ADDR, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

IrlapAddrCtrlDiscoveryDismiss	= GenTrigger {
	moniker = "Dismiss";
	action = MSG_IRLAP_ADDR_DISCOVERY_DISMISS, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

end	IrlapAddrCtrlUI;

