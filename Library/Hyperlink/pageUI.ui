/***********************************************************************
 *
 *	Copyright (c) Geoworks 1994 -- All Rights Reserved
 *
 * PROJECT:	GEOS
 * MODULE:	Hyperlink library
 * FILE:	pageUI.ui
 *
 * AUTHOR:  	Cassie Hartzog:	Jun 2, 1994
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	6/02/94	  cassie    Initial version
 *
 * DESCRIPTION:
 *	UI for PageNameController
 *
 * 	$Id: pageUI.ui,v 1.1 97/04/04 18:09:19 newdeal Exp $
 *
 ***********************************************************************/
/*
 * UI for PageNameControl tools
 */
start PageNameControlToolUI, notDetachable;

PageStatusBarInteraction = GenInteraction {
	genStates = default -usable;
	type = organizational;
	visibility = subGroup;
	children = PageStatusBarText;
	hints = {
		HINT_NO_WIDER_THAN_CHILDREN_REQUIRE
	}
}

PageStatusBarText = GenText {
	moniker = "Page Name:";
	textDisplayAttributes = default +singleLineText;
	maxLength = "MAX_PAGE_NAME_LENGTH";
	genAttributes = default +readOnly;
	hints = {
		HINT_MINIMUM_SIZE {
		    SpecWidth <SST_AVG_CHAR_WIDTHS, MAX_PAGE_NAME_LENGTH+10>
		    SpecHeight <>
		},
		HINT_PLACE_MONIKER_TO_LEFT,
	}
}

end PageNameControlToolUI;


/*
 * UI for PageNameControl features
 */
start PageNameControlUI, notDetachable;

PageClearTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'C', "Clear Page Name";
	action = MSG_PNC_CLEAR_PAGE_NAME, "TO_OBJ_BLOCK_OUTPUT";
}

PageClearAllTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'A', "Clear All Page Names";
	action = MSG_PNC_CLEAR_ALL_PAGES, "TO_OBJ_BLOCK_OUTPUT";
}

PageNameDialog = GenInteraction {
	moniker = 'N', "Name Current Page";
	genStates = default -usable;
	type = command;
	visibility = dialog;
	children =
		PageNameList,
		PageNameText,
		PageNameTrigger;
	hints = {
		HINT_INTERACTION_MAKE_RESIZABLE,
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_HELP_CONTEXT { char "dbPageName", 0 },
		ATTR_GEN_HELP_FILE { char "bindery ", 0 }
	}
}

PageNameList = GenDynamicList {
	queryMsg = MSG_PNC_PAGE_LIST_GET_MONIKER;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	behaviorType = exclusiveNone;
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, MAX_PAGE_NAME_LENGTH>
			SpecHeight <SST_LINES_OF_TEXT, SETDEST_LIST_NUM_ENTRIES>
			word	SETDEST_LIST_NUM_ENTRIES
		},
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word	MSG_PNC_PAGE_LIST_PAGE_CHANGED
		}
	}
}

PageNameText = GenText vardataReloc {
	moniker = "Name:";
	textDisplayAttributes = default +singleLineText;
	maxLength = "MAX_PAGE_NAME_LENGTH";
	/*
	 * We specify the destination so the PageNameControl can
	 * intercept MSG_META_TEXT_USER_MODIFIED.
	 */
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_DEFAULT_FOCUS,
		ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS {
			I_OPTR PageNameTrigger
		}
	}
}

PageNameTrigger = GenTrigger {
	moniker = "Name Current Page";
	action = MSG_PNC_APPLY_PAGE_NAME, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_DEFAULT_DEFAULT_ACTION,
		HINT_SEEK_REPLY_BAR,
	}
}

end PageNameControlUI;



/*****************************************************************************
 * Strings for controller
 *****************************************************************************/

start HyperlinkControlStrings, data;

chunk PCStatusBarName = "Page Name Status Bar";

chunk PCName = "Page Dialog";
chunk PageClearName = "Clear Page Name";
chunk PageClearAllName = "Clear All Page Names";

chunk RenamePageString = "The page you are renaming may have hyperlinks"
	" pointing to its current name.  If so, should those hyperlinks"
	" be updated to point to the new name for this page, or should"
	" they continue to point to the old name?";

visMoniker OldNameTriggerString = "Old Name";
visMoniker NewNameTriggerString = "New Name";
visMoniker CancelTriggerString = "Cancel";

end HyperlinkControlStrings;
