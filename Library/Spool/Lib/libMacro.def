COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	Copyright (c) GeoWorks 1989 -- All Rights Reserved

PROJECT:	PC GEOS
MODULE:		Print Spooler Library
FILE:		libMacro.def

AUTHOR:		Don Reeves, March 12, 1991

MACROS:
	Name			Description
	----			-----------

	
REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	Don	3/12/91		Initial revision


DESCRIPTION:
		
	$Id: libMacro.def,v 1.1 97/04/07 11:10:53 newdeal Exp $

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@


COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		AddPaperTable
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:	Define a driver escape table and table entries

PASS: 		numEntries	- number of entries in table
		widthLabel	- label for the width table
		heightLabel	- label for the height table

PSEUDO CODE/STRATEGY:
		Calculate some table sizes, and initalize a few variables 

REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	Don	3/91		Initial version (stolen from Jim)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@

AddPaperTable	macro	numEntries, widthLabel, heightLabel, layoutLabel

	SpoolPaper		segment	resource
	widthLabel	label	word
	_widthTable		= $		;; remmember start of widths
	org	$ + 2* numEntries 		;; leave room for table

	heightLabel	label	word
	_heightTable		= $		;; remember start of heights
	org	$ + 2* numEntries		;; leave room for table

	layoutLabel	label	PageLayout
	_layoutTable		= $		;; remember start of layouts
	org	$ + 2* numEntries		;; leave room for table

	_numPaperEntries	= numEntries
	NUM_PAPER_ENTRIES	= 0		;; initialize counter
	SpoolPaper		ends
endm


COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		AddPaperSize
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:	Define a new paper size & string

PASS: 		AddPaperSize:
		    nameString	- the string containing the name of the paper
		    width	- width of the paper size
		    height	- height of the paper size
		    layout	- default PageLayout for the paper size
		    entryNum	- the PaperSizes enum for the paper size
		    la		- labels across (if layout = "label")
		    ld		- labels down   (if layout = "label")

PSEUDO CODE/STRATEGY:
		Define the width & height of a paper size, and add a string
		for this size to the PageSizeData resource.

REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	Don	3/91		Initial version (stolen from Jim)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@

AddPaperSize	macro	width, height, layout, entryNum, nameString, la, ld
	_original = $

	SpoolPaper		segment	resource

	org	_widthTable+2*NUM_PAPER_ENTRIES  ;; set loc to next table entry
	word	width

	org	_heightTable+2*NUM_PAPER_ENTRIES ;; set loc to next table entry
	word	height

	org	_layoutTable+2*NUM_PAPER_ENTRIES ;; set loc to next table entry
	ifidn	<&layout>, <label>
		word	PageLayoutLabel<ld, la, PT_LABEL>
	else
		word	layout
	endif
	SpoolPaper		ends

	PageSizeData	segment	lmem LMEM_TYPE_GENERAL
	String_&entryNum chunk.TCHAR &nameString,0
	ForceRef String_&entryNum
	PageSizeData	ends

	if NUM_PAPER_ENTRIES ne entryNum
	.err
	PrintMessage <Error -- Paper size strings not ordered by PaperSizes>
	endif

	NUM_PAPER_ENTRIES	= NUM_PAPER_ENTRIES+1	;; bump counter

	if	NUM_PAPER_ENTRIES gt _numPaperEntries
	.err
	PrintMessage <Error -- Not enough slots allocated for AddPaperTable>
	endif

	org	_original			;; reset loc counter
endm


COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		EndPaperTable
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:	End the definition of a paper table

PASS:		numEntries - number of entries that should have been created

PSEUDO CODE/STRATEGY:
		Establish if the correct number of table entries have been
		made.

REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	Don	1/92		Initial version

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@

EndPaperTable	macro	numEntries

	if	NUM_PAPER_ENTRIES ne _numPaperEntries
	.err
	PrintMessage <Error -- A paper table has not been completely filled>
	endif
endm


COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		MakeDefaultOrderEntry
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:	Add another DefaultOrderEntry to a chunk

PASS:		value	- enumeration of either PaperSizes,
			  EnvelopeSizes, or LabelSizes.

PSEUDO CODE/STRATEGY:

REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	Don	1/92		Initial version (courtesy of Adam)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@

MakeDefaultOrderEntry	macro	value
	local	oog, len, str

oog	catstr <000>, <value>
len	sizestr oog
str	substr oog, len-2
oog	catstr <">, str, <,">
	TCHAR	oog
endm
