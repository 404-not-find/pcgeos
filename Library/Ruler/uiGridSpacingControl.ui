/*
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	PC GEOS 
 * MODULE:	
 * FILE:	uiGridSpacingControl.ui
 *
 * AUTHOR:	Jon Witort
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	jon	14 feb 1992	initial perversion
 *
 * DESCRIPTION:
 *	UI description for GridSpacingControl stuff
 *
 * 	$Id: uiGridSpacingControl.ui,v 1.1 97/04/07 10:43:23 newdeal Exp $
 *
 */

start	RulerGridControlUI, notDetachable;

GridSpacingGroup = GenInteraction {
	genStates = default -usable;
	moniker = "Grid Spacing";
	children = GridSpacingValue, GridSpacingUnitsList;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_DRAW_IN_BOX,
		HINT_LEFT_JUSTIFY_MONIKERS,
		HINT_PLACE_MONIKER_ABOVE
	}
}

GridSpacingValue = GenValue {
	displayFormat = inchesOrCentimeters;
	applyMsg = MSG_RGC_SET_GRID_SPACING;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	increment = 18;
	hints = {
		/* .5 cm when in Metric mode */
		ATTR_GEN_VALUE_METRIC_INCREMENT {WWFixed <11353, 14>}
	}
}

GridSpacingUnitsList = GenItemGroup {
	children = GridSpacingInchesEntry, GridSpacingCentimetersEntry,
			GridSpacingPointsEntry, GridSpacingPicasEntry;
	
	destination = GridSpacingValue;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word	MSG_GEN_VALUE_SET_DISPLAY_FORMAT
		}
	}
}

GridSpacingInchesEntry = GenItem {
	moniker = 'I', "Inches";
	identifier = "GVDF_INCHES";
}

GridSpacingCentimetersEntry = GenItem {
	moniker = 'C', "Centimeters";
	identifier = "GVDF_CENTIMETERS";
}

GridSpacingPointsEntry = GenItem {
	moniker = 'P', "Points";
	identifier = "GVDF_POINTS";
}

GridSpacingPicasEntry = GenItem {
	moniker = 'a', "Picas";
	identifier = "GVDF_PICAS";
}

GridOptionsList = GenBooleanGroup {
	genStates = default -usable;
	selectedBooleans = "mask GO_SHOW_GRID or mask GO_SNAP_TO_GRID";
	applyMsg = MSG_RGC_SET_GRID_OPTIONS;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	children = GridSnapToGridEntry, GridShowGridEntry;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}

GridSnapToGridEntry = GenBoolean {
	moniker = 'S', "Snap to Grid and Guides";
	identifier = "mask GO_SNAP_TO_GRID";
}
	
GridShowGridEntry = GenBoolean {
	moniker = 'w', "Show Grid";
	identifier = "mask GO_SHOW_GRID";
}

end	RulerGridControlUI;

start ControlStrings, data;

chunk RGCName = "Ruler Grid";
chunk GridSpacingName = "Grid Spacing";
chunk SnapToGridName = "Snap to Grid";
chunk ShowGridName = "Show Grid";

end ControlStrings;
