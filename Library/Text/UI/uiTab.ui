/*
 * Project:	PC GEOS
 * Module:	Text
 * File:	UI/uiTabControl.ui
 *
 * Author:	Tony Requist
 *
 *	This file contains the user interface description for a tab control
 *
 *	$Id: uiTab.ui,v 1.1 97/04/07 11:17:43 newdeal Exp $
 *
 */

/*
 * Tab control
 */

#if !NO_CONTROLLERS	/*+++++++++++++++++++++++++++++++++++++++++++++++*/

start TabControlUI, notDetachable;

TabList = GenDynamicList {
	genStates = default -usable;
	moniker = 'T', "Tabs:";
	queryMsg = MSG_TC_QUERY_TAB;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
		ATTR_GEN_ITEM_GROUP_CUSTOM_DOUBLE_PRESS { word 0 },
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 25>
			SpecHeight <SST_LINES_OF_TEXT, 5>
			word 5
		},
		HINT_IF_SYSTEM_ATTRS { SystemAttrs mask SA_HORIZONTALLY_TINY },
			HINT_PLACE_MONIKER_ABOVE,
		HINT_ELSE,
			HINT_PLACE_MONIKER_TO_LEFT,
		HINT_ENDIF,
		ATTR_GEN_ITEM_GROUP_STATUS_MSG { word MSG_TC_SELECT_TAB }
	}
}

/***/

TabPositionRange = GenValue {
	genStates = default -usable;
	moniker = 'P', "Tab Position:";
	displayFormat = inchesOrCentimeters;
	value = 0;
	increment = 18;
	minimum = 0;
	maximum = "VIS_TEXT_MAX_PARA_WIDTH";
}

/***/

TabTypeList = GenItemGroup {
	genStates = default -usable;
	moniker = 'y', "Tab Type:";
	children = TabTypeLeftEntry, TabTypeCenterEntry, TabTypeRightEntry,
		   TabTypeDecimalEntry;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = { 
	    	ATTR_GEN_ITEM_GROUP_STATUS_MSG { dw MSG_TC_SET_TAB_TYPE },
		HINT_IF_SYSTEM_ATTRS { SystemAttrs mask SA_HORIZONTALLY_TINY },
			HINT_PLACE_MONIKER_ABOVE,
		HINT_ELSE,
			HINT_PLACE_MONIKER_TO_LEFT,
		HINT_ENDIF
	}
}

TabTypeLeftEntry = GenItem {
	moniker = "Left";
	identifier = "TT_LEFT";
}

TabTypeCenterEntry = GenItem {
	moniker = "Center";
	identifier = "TT_CENTER";
}

TabTypeRightEntry = GenItem {
	moniker = "Right";
	identifier = "TT_RIGHT";
}

TabTypeDecimalEntry = GenItem {
	moniker = "Decimal";
	identifier = "TT_ANCHORED";
}

/***/

TabLeaderList = GenItemGroup {
	genStates = default -usable;
	moniker = 'L', "Tab Leader:";
	children = TabLeaderNoneEntry, TabLeaderDotEntry, TabLeaderLineEntry,
		   TabLeaderBulletEntry;
	hints = { 
		HINT_IF_SYSTEM_ATTRS { SystemAttrs mask SA_HORIZONTALLY_TINY },
			HINT_PLACE_MONIKER_ABOVE,
		HINT_ELSE,
			HINT_PLACE_MONIKER_TO_LEFT,
		HINT_ENDIF
	}
}

TabLeaderNoneEntry = GenItem {
	moniker = "None";
	identifier = "TL_NONE shl offset TA_LEADER";
}

TabLeaderDotEntry = GenItem {
	moniker = "Dot";
	identifier = "TL_DOT shl offset TA_LEADER";
}

TabLeaderLineEntry = GenItem {
	moniker = "Line";
	identifier = "TL_LINE shl offset TA_LEADER";
}

TabLeaderBulletEntry = GenItem {
	moniker = "Bullet";
	identifier = "TL_BULLET shl offset TA_LEADER";
}

/***/

TabLineGroup = GenInteraction {
	genStates = default -usable;
	moniker = "Vertical Line";
	children = TabLineGroup1, TabWidthSpacingGroup;
	hints = { HINT_DRAW_IN_BOX, HINT_EXPAND_WIDTH_TO_FIT_PARENT }
}

TabLineGroup1 = GenInteraction {
	children = TabLineList, TabGrayScreenRange;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

TabLineList = GenItemGroup {
	moniker = 'n', "Lines:";
	children = TabLineOffEntry, TabLineOnEntry;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_ITEM_GROUP_STATUS_MSG { word MSG_TC_SET_TAB_LINE },
		ATTR_GEN_FEATURE_LINK { word offset TabWidthSpacingGroup }
	}
}

TabLineOffEntry = GenItem {
	moniker = "Off";
	identifier = "0";
}

TabLineOnEntry = GenItem {
	moniker = "On";
	identifier = "1";
}

/***/


TabGrayScreenRange = GenValue {
	genStates = default -enabled;
	moniker = 'S', "% Shaded:";
	value = 0;
	minimum = 0;
	maximum = 100;
}


/***/

TabWidthSpacingGroup = GenInteraction {
	children = TabLineWidthRange, TabSpacingRange;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

TabLineWidthRange = GenValue {
	moniker = 'W', "Width:";
	displayFormat = points;
	value = 0;
	increment = 1;
	minimum = 0;
	maximum = "15";
}


TabSpacingRange = GenValue {
	moniker = 'O', "Offset:";
	displayFormat = points;
	value = 0;
	increment = 1;
	minimum = 0;
	maximum = 15;
}

/***/

ClearTabTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'D', "Delete Tab";
	hints = { HINT_SEEK_REPLY_BAR }
	action = MSG_TC_CLEAR_TAB, "TO_OBJ_BLOCK_OUTPUT";
}

ClearAllTabsTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'E', "Delete Every Tab";
	hints = { HINT_SEEK_REPLY_BAR }
	action = MSG_TC_CLEAR_ALL_TABS, "TO_OBJ_BLOCK_OUTPUT";
}

end TabControlUI;

/*---*/

/*
 * Strings for controllers
 */

start ControlStrings, data;

chunk TCName = "Tabs";

chunk TabListName = "Tab List";
chunk TabPositionName = "Tab Position";
chunk TabGrayScreenName = "Tab Gray Screen";
chunk TabTypeName = "Tab Type";
chunk TabLeaderName = "Tab Leader";
chunk TabLineName = "Tab Lines";
chunk ClearTabName = "Delete Tab";
chunk ClearAllTabsName = "Delete All Tabs";

chunk NewTabString = "<< Create New Tab >>";

end ControlStrings;

#endif			/* !NO_CONTROLLERS ++++++++++++++++++++++++++++++*/

