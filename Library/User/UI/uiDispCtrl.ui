/*
 * Project:	PC GEOS
 * Module:	Text
 * File:	Gen/genDisplay.ui
 *
 * Author:	Tony Requist
 *
 *	This file contains the user interface description for a style control
 *
 *	$Id: uiDispCtrl.ui,v 1.1 97/04/07 11:46:58 newdeal Exp $
 *
 */

/*
 * Display control
 */

#if !NO_CONTROLLERS	/*+++++++++++++++++++++++++++++++++++++++++++++++*/

#include "Art/mkrOverlapping.ui"
#include "Art/mkrFullSize.ui"
#include "Art/mkrTile.ui"

specificUI; /* Allow us to use specific UI shortcuts */

start GenDisplayControlUI, notDetachable;

OverlappingList = GenItemGroup {
	genStates = default -usable;
	children = OverlappingItem, FullSizeItem;
	applyMsg = MSG_GDC_SET_OVERLAPPING;
	destination = "TO_OBJ_BLOCK_OUTPUT";
}


OverlappingItem = GenItem {
	moniker = 'O', "Overlapping";

	kbdAccelerator = control F5;
	identifier = 1;
}


FullSizeItem = GenItem {
	moniker = 'F', "Full-Sized";
	kbdAccelerator = control F10;
	identifier = 0;
}


/***/

TileTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'T', "Tile";
	kbdAccelerator = shift F4;
	action = MSG_GDC_TILE, "TO_OBJ_BLOCK_OUTPUT";
}

/***/

/*
 * Put and extra interaction here to cause a seperator to be drawn
 */

DisplayListGroup = GenInteraction {
	genStates = default -usable;
	children = DisplayList;
}

DisplayList = GenDynamicList {
	applyMsg = MSG_GDC_LIST_APPLY;
	queryMsg = MSG_GDC_LIST_QUERY;
	destination = "TO_OBJ_BLOCK_OUTPUT";
}

end GenDisplayControlUI;

/*---*/

start GenDisplayControlToolboxUI, notDetachable;

OverlappingToolList = GenItemGroup {
	genStates = default -usable;
	children = OverlappingToolItem, FullSizeToolItem;
	applyMsg = MSG_GDC_SET_OVERLAPPING;
	destination = "TO_OBJ_BLOCK_OUTPUT";
}

OverlappingToolItem = GenItem {
	moniker = list { OverlappingTCMoniker }
	identifier = 1;
	hints = { ATTR_GEN_FOCUS_HELP { optr OverlappingToolHelpString } }
}

FullSizeToolItem = GenItem {
	moniker = list { FullSizeTCMoniker }
	identifier = 0;
	hints = { ATTR_GEN_FOCUS_HELP { optr FullSizeToolHelpString } }
}

/***/

TileToolTrigger = GenTrigger {
	genStates = default -usable;
	moniker = list { TileTCMoniker }
	action = MSG_GDC_TILE, "TO_OBJ_BLOCK_OUTPUT";
	hints = { ATTR_GEN_FOCUS_HELP { optr TileName } }
}

/***/

DisplayToolList = GenDynamicList {
	moniker = "No display selected";  /* Used when no selection */
	genStates = default -usable;
	applyMsg = MSG_GDC_LIST_APPLY;
	queryMsg = MSG_GDC_LIST_QUERY;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_ITEM_GROUP_MINIMIZE_SIZE, 
		HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 20>
			SpecHeight <>
		},
		ATTR_GEN_FOCUS_HELP { optr DisplayListName }
	}
}

end GenDisplayControlToolboxUI;

/*
 * Strings for controllers
 */

start ControlStrings, data;

chunk GDCName = "Display Control";
chunk OverlappingName = "Overlapping/Full-size";
chunk TileName = "Tile Displays";
chunk DisplayListName = "List of Displays";

chunk OverlappingToolHelpString = "Overlapping Displays";
chunk FullSizeToolHelpString = "Full-size Displays";

end ControlStrings;

#endif			/* !NO_CONTROLLERS ++++++++++++++++++++++++++++++*/

