/*
 * Project:	PC GEOS
 * Module:	Text
 * File:	Gen/genView.ui
 *
 * Author:	Tony Requist
 *
 *	This file contains the user interface description for a style control
 *
 *	$Id: uiView.ui,v 1.1 97/04/07 11:47:01 newdeal Exp $
 *
 */

/*
 * View control
 */

#if !NO_CONTROLLERS	/*+++++++++++++++++++++++++++++++++++++++++++++++*/

#include "Art/mkrAdjustAspect.ui"
#include "Art/mkrApplyToAll.ui"
#include "Art/mkrNextPage.ui"
#include "Art/mkrNormalScale.ui"
#include "Art/mkrPageDown.ui"
#include "Art/mkrPageLeft.ui"
#include "Art/mkrPageRight.ui"
#include "Art/mkrPageUp.ui"
#include "Art/mkrPreviousPage.ui"
#include "Art/mkrScaleToFit.ui"
#include "Art/mkrShowHorizontal.ui"
#include "Art/mkrShowVertical.ui"
#include "Art/mkrZoomIn.ui"
#include "Art/mkrZoomOut.ui"
#include "Art/mkrRedraw.ui"
#include "Art/mkrFirstPage.ui"
#include "Art/mkrLastPage.ui"

start GenViewControlUI, notDetachable;

MainScaleList = GenItemGroup {
	genStates = default -usable;
	children = Main100Entry, MainScaleToFitEntry;
	applyMsg = MSG_GVC_SET_SCALE_VIA_LIST;
	destination = "TO_OBJ_BLOCK_OUTPUT";
}

Main100Entry = GenItem {
	moniker = 'S', "Normal Size";
	identifier = 100;
}

MainScaleToFitEntry = GenItem {
	moniker = 'F', "Scale to Fit";
	identifier = "GVCSSF_TO_FIT";
}

ZoomInTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'I', "Zoom In";
	action = MSG_GVC_ZOOM_IN, "TO_OBJ_BLOCK_OUTPUT";
}

ZoomOutTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'O', "Zoom Out";
	action = MSG_GVC_ZOOM_OUT, "TO_OBJ_BLOCK_OUTPUT";
}

/***/
StandardScaleSubMenu = GenInteraction {
	genStates = default -usable;
	moniker = 'V', "View at %";
	children = StandardScaleList, CustomScaleBox;
	visibility = popup;
}

StandardScaleList = GenItemGroup {
	children = Zoom25Entry, Zoom50Entry, Zoom75Entry, Zoom100Entry,
		   Zoom125Entry, Zoom150Entry, Zoom175Entry, Zoom200Entry,
		   Zoom300Entry, Zoom400Entry, ScaleToFitEntry;
	applyMsg = MSG_GVC_SET_SCALE_VIA_LIST;
	destination = "TO_OBJ_BLOCK_OUTPUT";
}

Zoom25Entry = GenItem {
	moniker = '1', "1.  Reduced to 25 %";
	identifier = 25;
	hints = { ATTR_GEN_FEATURE_LINK { word offset Zoom50Entry }}
}

Zoom50Entry = GenItem {
	moniker = '2', "2.  Reduced to 50 %";
	identifier = 50;
	hints = { ATTR_GEN_FEATURE_LINK { word offset Zoom75Entry }}
}

Zoom75Entry = GenItem {
	moniker = '3', "3.  Reduced to 75 %";
	identifier = 75;
}

Zoom100Entry = GenItem {
	moniker = 'S', "Normal Size";
	identifier = 100;
}

Zoom125Entry = GenItem {
	moniker = '4', "4.  Enlarged to 125 %";
	identifier = 125;
	hints = { ATTR_GEN_FEATURE_LINK { word offset Zoom150Entry }}
}

Zoom150Entry = GenItem {
	moniker = '5', "5.  Enlarged to 150 %";
	identifier = 150;
	hints = { ATTR_GEN_FEATURE_LINK { word offset Zoom175Entry }}
}

Zoom175Entry = GenItem {
	moniker = '6', "6.  Enlarged to 175 %";
	identifier = 175;
	hints = { ATTR_GEN_FEATURE_LINK { word offset Zoom200Entry }}
}

Zoom200Entry = GenItem {
	moniker = '7', "7.  Enlarged to 200 %";
	identifier = 200;
}

Zoom300Entry = GenItem {
	moniker = '8', "8.  Enlarged to 300 %";
	identifier = 300;
	hints = { ATTR_GEN_FEATURE_LINK { word offset Zoom400Entry }}
}

Zoom400Entry = GenItem {
	moniker = '9', "9.  Enlarged to 400 %";
	identifier = 400;
}

ScaleToFitEntry = GenItem {
	moniker = 'F', "Scale to Fit";
	identifier = "GVCSSF_TO_FIT";
}

/***/

OptionsSubMenu = GenInteraction {
	genStates = default -usable;
	children = ViewAttrsList;
	moniker = 't', "View Options";
	visibility = popup;
}

ViewAttrsList = GenBooleanGroup {
	children = AdjustForAspectRatioEntry, ApplyToAllEntry,
		   ShowHorizontalEntry, ShowVerticalEntry;
	applyMsg = MSG_GVC_SET_ATTRS;
	destination = "TO_OBJ_BLOCK_OUTPUT";
}

AdjustForAspectRatioEntry = GenBoolean {
	moniker = 'C', "Correct for Aspect Ratio";
	identifier = "mask GVCA_ADJUST_ASPECT_RATIO";
}

ApplyToAllEntry = GenBoolean {
	moniker = 'A', "Change All Views Together";
	identifier = "mask GVCA_APPLY_TO_ALL";
}

ShowHorizontalEntry = GenBoolean {
	moniker = 'H', "Show Horizontal Scroll Bar";
	identifier = "mask GVCA_SHOW_HORIZONTAL";
}

ShowVerticalEntry = GenBoolean {
	moniker = 'V', "Show Vertical Scroll Bar";
	identifier = "mask GVCA_SHOW_VERTICAL";
}

/***/

CustomScaleBox = GenInteraction {
	moniker = 'C', "Custom %";
	children = CustomScaleSpin;
	type = properties;
	visibility = dialog;
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbCustScale", 0 }
	}
}

CustomScaleSpin = GenValue ignoreDirty {
	moniker = "Scale (%):";
	value = "100";
	minimum = "DEFAULT_ZOOM_MINIMUM";
	maximum = "DEFAULT_ZOOM_MAXIMUM";
	applyMsg = MSG_GVC_SET_SCALE;
	destination = "TO_OBJ_BLOCK_OUTPUT";
}

/***/

RedrawSubGroup = GenInteraction {
	genStates = default -usable;

	children = RedrawTrigger;
}


RedrawTrigger = GenTrigger {
	moniker = 'R', "Redraw";
	kbdAccelerator = F5;
	action = MSG_GVC_REDRAW, "TO_OBJ_BLOCK_OUTPUT";
}

end GenViewControlUI;

/*---*/

start GenViewControlToolboxUI, notDetachable;

ScaleToolList = GenItemGroup {
	genStates = default -usable;
	children = Zoom100ToolEntry, ScaleToFitToolEntry;
	applyMsg = MSG_GVC_SET_SCALE_VIA_LIST;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_SAME_ORIENTATION_AS_PARENT
	}
}

Zoom100ToolEntry = GenItem {
	moniker = list { NormalScaleTCMoniker }
	identifier = 100;
	hints = { ATTR_GEN_FOCUS_HELP {	optr Zoom100Help } }
}

ScaleToFitToolEntry = GenItem {
	moniker = list { ScaleToFitTCMoniker }
	identifier = "GVCSSF_TO_FIT";
	hints = { ATTR_GEN_FOCUS_HELP {	optr ScaleToFitHelp } }
}

/***/

ZoomInToolTrigger = GenTrigger {
	genStates = default -usable;
	moniker = list { ZoomInTCMoniker }
	action = MSG_GVC_ZOOM_IN, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_FOCUS_HELP { optr ZoomInHelp },
		ATTR_GEN_FOCUS_DISABLED_HELP { optr ZoomInDisHelp }
	}
}

ZoomOutToolTrigger = GenTrigger {
	genStates = default -usable;
	moniker = list { ZoomOutTCMoniker }
	action = MSG_GVC_ZOOM_OUT, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_FOCUS_HELP { optr ZoomOutHelp },
		ATTR_GEN_FOCUS_DISABLED_HELP { optr ZoomOutDisHelp }
	}
}

/***/

RedrawToolTrigger = GenTrigger {
	genStates = default -usable;
	moniker = list { RedrawTCMoniker }
	action = MSG_GVC_REDRAW, "TO_OBJ_BLOCK_OUTPUT";
	hints = { ATTR_GEN_FOCUS_HELP {	optr RedrawName } }
}

PageLeftToolTrigger = GenTrigger {
	genStates = default -usable;
	moniker = list { PageLeftTCMoniker }
	action = MSG_GVC_PAGE_LEFT, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_FOCUS_HELP { optr PageLeftHelp },
		ATTR_GEN_FOCUS_DISABLED_HELP { optr PageLeftDisHelp } 
	}
}

PageRightToolTrigger = GenTrigger {
	genStates = default -usable;
	moniker = list { PageRightTCMoniker }
	action = MSG_GVC_PAGE_RIGHT, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_FOCUS_HELP { optr PageRightHelp },
		ATTR_GEN_FOCUS_DISABLED_HELP { optr PageRightDisHelp } 
	}
}

PageUpToolTrigger = GenTrigger {
	genStates = default -usable;
	moniker = list { PageUpTCMoniker }
	action = MSG_GVC_PAGE_UP, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_FOCUS_HELP { optr PageUpHelp },
		ATTR_GEN_FOCUS_DISABLED_HELP { optr PageUpDisHelp } 
	}
}

PageDownToolTrigger = GenTrigger {
	genStates = default -usable;
	moniker = list { PageDownTCMoniker }
	action = MSG_GVC_PAGE_DOWN, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_FOCUS_HELP { optr PageDownHelp },
		ATTR_GEN_FOCUS_DISABLED_HELP { optr PageDownDisHelp } 
	}
}

/***/

ViewAttrsToolList = GenBooleanGroup {
	genStates = default -usable;
	children = AdjustForAspectRatioToolEntry, ApplyToAllToolEntry,
		   ShowHorizontalToolEntry, ShowVerticalToolEntry;
	applyMsg = MSG_GVC_SET_ATTRS;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_SAME_ORIENTATION_AS_PARENT
	}
}

AdjustForAspectRatioToolEntry = GenBoolean {
	moniker = list { AdjustAspectTCMoniker }
	identifier = "mask GVCA_ADJUST_ASPECT_RATIO";
	hints = { ATTR_GEN_FOCUS_HELP {	optr AdjustForAspectRatioName } }
}

ApplyToAllToolEntry = GenBoolean {
	moniker = list { ApplyToAllTCMoniker }
	identifier = "mask GVCA_APPLY_TO_ALL";
	hints = { ATTR_GEN_FOCUS_HELP {	optr ApplyToAllName } }
}

ShowHorizontalToolEntry = GenBoolean {
	moniker = list { ShowHorizontalTCMoniker }
	identifier = "mask GVCA_SHOW_HORIZONTAL";
	hints = { ATTR_GEN_FOCUS_HELP {	optr ShowHorizontalHelp } }
}

ShowVerticalToolEntry = GenBoolean {
	moniker = list { ShowVerticalTCMoniker }
	identifier = "mask GVCA_SHOW_VERTICAL";
	hints = { ATTR_GEN_FOCUS_HELP {	optr ShowVerticalHelp } }
}

end GenViewControlToolboxUI;

/*
 * Strings for controllers
 */

start ControlStrings, data;

chunk GVCName = "View Options";

chunk CustomScaleName = "Custom Scale";
chunk ShowVerticalName = "Show Vertical Scrollbar";
chunk ShowHorizontalName = "Show Horizontal Scrollbar";
chunk ApplyToAllName = "Change All Views Together";
chunk AdjustForAspectRatioName = "Adjust For Aspect Ratio";
chunk ScaleToFitName = "Scale to Fit";
chunk BigEnlargeName = "Greatly Enlarge View";
chunk EnlargeName = "Enlarge View";
chunk Zoom100Name = "Normal Size";
chunk ReduceName = "Reduce View";

chunk PageDownName = "Page Down";
chunk PageUpName = "Page Up";
chunk PageRightName = "Page Right";
chunk PageLeftName = "Page Left";
chunk ZoomOutName = "Zoom Out";
chunk ZoomInName = "Zoom In";
chunk RedrawName = "Redraw Display";

/* Quick-Help strings */

chunk ZoomInHelp = "Zoom in to make document larger";
chunk ZoomInDisHelp = "Cannot zoom in any further";

chunk Zoom100Help = "Display document at normal size";

chunk ZoomOutHelp = "Zoom out to see more of the document at once";
chunk ZoomOutDisHelp = "Cannot zoom out any further";

chunk ScaleToFitHelp = "Scale to fit entire document on screen";

chunk PageLeftHelp = "Scroll document to the left";
chunk PageLeftDisHelp = "Cannot scroll to the left any further";

chunk PageRightHelp = "Scroll document to the right";
chunk PageRightDisHelp = "Cannot scroll to the right any further";

chunk PageDownHelp = "Scroll document down";
chunk PageDownDisHelp = "Cannot scroll down any further";

chunk PageUpHelp = "Scroll document up";
chunk PageUpDisHelp = "Cannot scroll up any further";

chunk ShowVerticalHelp = "Show/hide vertical scrollbar";
chunk ShowHorizontalHelp = "Show/hide horizontal scrollbar";

end ControlStrings;

#endif			/* !NO_CONTROLLERS ++++++++++++++++++++++++++++++*/

