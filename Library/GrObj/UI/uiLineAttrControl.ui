/*
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	PC GEOS 
 * MODULE:	
 * FILE:	uiLineAttrControl.ui
 *
 * AUTHOR:	Jon Witort
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	jon	24 feb 1992	initial perversion
 *
 * DESCRIPTION:
 *	UI description for GrObjLineAttrControl stuff
 *
 * 	$Id: uiLineAttrControl.ui,v 1.1 97/04/04 18:06:55 newdeal Exp $
 *
 */

#include "Art/mkrLineAttr.ui"

#define THICK_WIDTH 28
#define THICK_HEIGHT 12

#define THICK_ENTRY(name, thickness) \
name = GenItem { \
	moniker = { \
	    cachedSize = THICK_WIDTH, THICK_HEIGHT; \
	    gstring { \
		GSBeginString | \
		GSSetLineColorIndex	C_BLACK | \
		GSSetLineEnd LE_BUTTCAP | \
		GSSetLineWidth thickness | \
		GSDrawLine 2,<THICK_HEIGHT/2>, <THICK_WIDTH-3>, <THICK_HEIGHT/2> | \
		GSEndString \
	    } \
	} \
	identifier = #thickness; \
}

#define STYLE_WIDTH 28
#define STYLE_HEIGHT 12

#define STYLE_ENTRY(name, style) \
name = GenItem { \
	moniker = { \
	    cachedSize = STYLE_WIDTH, STYLE_HEIGHT; \
	    gstring { \
		GSBeginString | \
		GSSetLineColorIndex	C_BLACK | \
		GSSetLineEnd LE_BUTTCAP | \
		GSSetLineStyle style | \
		GSDrawLine 2,<STYLE_HEIGHT/2>, <STYLE_WIDTH-3>, <STYLE_HEIGHT/2> | \
		GSEndString \
	    } \
	} \
	identifier = #style; \
}

/***/

start	GrObjLineAttrControlUI, notDetachable;

GrObjLineWidthItemGroup = GenItemGroup {
	moniker = 'W', "Width:";
	genStates = default -usable;
	behaviorType = exclusive;
	children = Width1Entry, Width2Entry, Width3Entry,
		   Width4Entry, Width5Entry, Width6Entry, Width7Entry;
	applyMsg = MSG_GOLAC_SET_INTEGER_LINE_WIDTH;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_IF_SYSTEM_ATTRS { SystemAttrs mask SA_HORIZONTALLY_TINY },
			HINT_WRAP_AFTER_CHILD_COUNT {
				word	4
			},
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_ITEM_GROUP_TOOLBOX_STYLE,
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word MSG_GOLAC_SET_LINE_VALUE_FROM_INDEX
		}
	}
}

THICK_ENTRY(Width1Entry,1)
THICK_ENTRY(Width2Entry,2)
THICK_ENTRY(Width3Entry,3)
THICK_ENTRY(Width4Entry,4)
THICK_ENTRY(Width5Entry,5)
THICK_ENTRY(Width6Entry,6)
THICK_ENTRY(Width7Entry,7)

GrObjLineWidthValue = GenValue {
	genStates = default -usable;
	moniker = 'T', "Fine Tune Width:";
	applyMsg = MSG_GOLAC_SET_LINE_WIDTH;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word MSG_GOLAC_SET_LINE_INDEX_FROM_VALUE
		}
	}
	displayFormat = points;
	maximum = 100;
}

GrObjLineStyleItemGroup = GenItemGroup {
	moniker = 'y', "Style:";
	genStates = default -usable;
	behaviorType = exclusive;
	children = StyleSolidEntry, StyleDashedEntry, StyleDottedEntry,
			StyleDashDotEntry, StyleDashDDotEntry;
	applyMsg = MSG_GOLAC_SET_LINE_STYLE;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_ITEM_GROUP_TOOLBOX_STYLE
	}
}

STYLE_ENTRY(StyleSolidEntry, LS_SOLID)
STYLE_ENTRY(StyleDashedEntry, LS_DASHED)
STYLE_ENTRY(StyleDottedEntry, LS_DOTTED)
STYLE_ENTRY(StyleDashDotEntry, LS_DASHDOT)
STYLE_ENTRY(StyleDashDDotEntry, LS_DASHDDOT)

GrObjArrowheadTypeList = GenItemGroup {
	moniker = 'h', "Arrowhead:";
	genStates = default -usable;
	behaviorType = exclusive;
	applyMsg = MSG_GOLAC_SET_ARROWHEAD_TYPE;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	children = 	Arrowhead30UnfilledItem,
			Arrowhead30FilledItem,
			Arrowhead30FilledAreaItem,
			Arrowhead45UnfilledItem,
			Arrowhead45FilledItem,
			Arrowhead45FilledAreaItem,
			Arrowhead90Item;
	hints = {
			HINT_ORIENT_CHILDREN_HORIZONTALLY,
			HINT_ITEM_GROUP_TOOLBOX_STYLE,
			HINT_ALLOW_CHILDREN_TO_WRAP
	}
}

Arrowhead30UnfilledItem = GenItem {
	moniker = list {
		Arrow30UnfilledTCMoniker,
		Arrow30UnfilledTMMoniker,
		Arrow30UnfilledTCGAMoniker
	}
	identifier = "(9 shl offset SAT_LENGTH) or (30 shl offset SAT_ANGLE)";
}

Arrowhead45UnfilledItem = GenItem {
	moniker = list {
		Arrow45UnfilledTCMoniker,
		Arrow45UnfilledTMMoniker,
		Arrow45UnfilledTCGAMoniker
	}
	identifier = "(9 shl offset SAT_LENGTH) or (45 shl offset SAT_ANGLE)";
}

Arrowhead90Item = GenItem {
	moniker = list {
		Arrow90TCMoniker,
		Arrow90TMMoniker,
		Arrow90TCGAMoniker
	}
	identifier = "(9 shl offset SAT_LENGTH) or (90 shl offset SAT_ANGLE)";
}

Arrowhead30FilledItem = GenItem {
	moniker = list {
		Arrow30FilledTCMoniker,
		Arrow30FilledTMMoniker,
		Arrow30FilledTCGAMoniker
	}
	identifier = "(9 shl offset SAT_LENGTH) or (mask SAT_FILLED) or (30 shl offset SAT_ANGLE)";
}

Arrowhead45FilledItem = GenItem {
	moniker = list {
		Arrow45FilledTCMoniker,
		Arrow45FilledTMMoniker,
		Arrow45FilledTCGAMoniker
	}
	identifier = "(9 shl offset SAT_LENGTH) or (mask SAT_FILLED) or (45 shl offset SAT_ANGLE)";
}

Arrowhead30FilledAreaItem = GenItem {
	moniker = list {
		Arrow30FilledAreaTCMoniker,
		Arrow30FilledAreaTMMoniker,
		Arrow30FilledAreaTCGAMoniker
	}
	identifier = "(9 shl offset SAT_LENGTH) or (mask SAT_FILLED) or (mask SAT_FILL_WITH_AREA_ATTRIBUTES) or (30 shl offset SAT_ANGLE)";
}

Arrowhead45FilledAreaItem = GenItem {
	moniker = list {
		Arrow45FilledAreaTCMoniker,
		Arrow45FilledAreaTMMoniker,
		Arrow45FilledAreaTCGAMoniker
	}
	identifier = "(9 shl offset SAT_LENGTH) or (mask SAT_FILLED) or (mask SAT_FILL_WITH_AREA_ATTRIBUTES) or (45 shl offset SAT_ANGLE)";
}

GrObjArrowheadWhichEndList = GenBooleanGroup {
	genStates = default -usable;
	applyMsg = MSG_GOLAC_SET_ARROWHEAD_WHICH_END;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	children = ArrowheadOnStartBoolean, ArrowheadOnEndBoolean;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

ArrowheadOnStartBoolean = GenBoolean {
	moniker = "Arrowhead at Start";
	identifier = "mask GOLAIR_ARROWHEAD_ON_START";
}

ArrowheadOnEndBoolean = GenBoolean {
	moniker = "Arrowhead at End";
	identifier = "mask GOLAIR_ARROWHEAD_ON_END";
}

end	GrObjLineAttrControlUI;


#define TOOL_THICK_WIDTH 28
#define TOOL_THICK_HEIGHT 14

#define TOOL_THICK_ENTRY(name, thickness) \
name = GenItem { \
	moniker = { \
	    cachedSize = TOOL_THICK_WIDTH, TOOL_THICK_HEIGHT; \
	    gstring { \
		GSBeginString | \
		GSSetLineColorIndex	C_BLACK | \
		GSSetLineEnd LE_BUTTCAP | \
		GSSetLineWidth thickness | \
		GSDrawHLine 0, <(TOOL_THICK_HEIGHT - 6 + thickness)/2>, TOOL_THICK_WIDTH | \
		GSEndString \
	    } \
	} \
	identifier = #thickness; \
}

#define TOOL_STYLE_WIDTH 28
#define TOOL_STYLE_HEIGHT 14

#define TOOL_STYLE_ENTRY(name, style) \
name = GenItem { \
	moniker = { \
	    cachedSize = TOOL_STYLE_WIDTH, TOOL_STYLE_HEIGHT; \
	    gstring { \
		GSBeginString | \
		GSSetLineColorIndex	C_BLACK | \
		GSSetLineEnd LE_BUTTCAP | \
		GSSetLineStyle style | \
		GSDrawHLine 2, <TOOL_STYLE_HEIGHT/2>, <TOOL_STYLE_WIDTH - 3> | \
		GSEndString \
	    } \
	} \
	identifier = #style; \
}

/***/




start	GrObjLineAttrControlToolboxUI, notDetachable;

GrObjLineWidthToolboxItemGroup = GenItemGroup {
	genStates = default -usable;

	moniker = list {
		LineWidthMoniker
	}

	behaviorType = exclusive;
	children = Width1Tool, Width2Tool, Width3Tool,
		   Width4Tool, Width5Tool, Width6Tool, Width7Tool;
	applyMsg = MSG_GOLAC_SET_INTEGER_LINE_WIDTH;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_ITEM_GROUP_TOOLBOX_STYLE,
		HINT_IF_SYSTEM_ATTRS { SystemAttrs mask SA_HORIZONTALLY_TINY },
			HINT_WRAP_AFTER_CHILD_COUNT {
				word	4
			},
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_ITEM_GROUP_MINIMIZE_SIZE,
		ATTR_GEN_FOCUS_HELP { optr IndexedLineWidthHelp }
	}
}

TOOL_THICK_ENTRY(Width1Tool,1)
TOOL_THICK_ENTRY(Width2Tool,2)
TOOL_THICK_ENTRY(Width3Tool,3)
TOOL_THICK_ENTRY(Width4Tool,4)
TOOL_THICK_ENTRY(Width5Tool,5)
TOOL_THICK_ENTRY(Width6Tool,6)
TOOL_THICK_ENTRY(Width7Tool,7)

GrObjLineStyleToolboxItemGroup = GenItemGroup {
	genStates = default -usable;

	moniker = list {
		LineStyleMoniker
	}

	behaviorType = exclusive;
	children = StyleSolidTool, StyleDashedTool, StyleDottedTool,
			StyleDashDotTool, StyleDashDDotTool;
	applyMsg = MSG_GOLAC_SET_LINE_STYLE;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_ITEM_GROUP_TOOLBOX_STYLE,
		HINT_ITEM_GROUP_MINIMIZE_SIZE,
		ATTR_GEN_FOCUS_HELP { optr LineStylesHelp }
	}
}

TOOL_STYLE_ENTRY(StyleSolidTool, LS_SOLID)
TOOL_STYLE_ENTRY(StyleDashedTool, LS_DASHED)
TOOL_STYLE_ENTRY(StyleDottedTool, LS_DOTTED)
TOOL_STYLE_ENTRY(StyleDashDotTool, LS_DASHDOT)
TOOL_STYLE_ENTRY(StyleDashDDotTool, LS_DASHDDOT)

end	GrObjLineAttrControlToolboxUI;


start	GrObjControlUIStrings;

chunk	GOLACName = "Line Width/Style";

chunk	IndexedLineWidthName = "Indexed Line Width";
chunk	FineTuneLineWidthName = "Fine Tune Line Width";
chunk	LineStylesName = "Line Styles";
chunk	ArrowheadTypeName = "Arrowhead Type";
chunk	ArrowheadWhichEndName = "Arrowhead End Selector";

chunk	IndexedLineWidthHelp = "Line width";
chunk	LineStylesHelp = "Line styles";

end	GrObjControlUIStrings;
