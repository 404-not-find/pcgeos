/*
 *	Copyright (c) GeoWorks 1991 -- All Rights Reserved
 *
 * PROJECT:	PC GEOS 
 * MODULE:	Chart Library
 * FILE:	uiGroup.ui
 *
 * AUTHOR:	Chris Boyke
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	CDB	1/ 2/92   	Initial version.
 *
 * DESCRIPTION:
 *	User interface for controlling titles in charts
 *
 * 	$Id: uiGroup.ui,v 1.1 97/04/04 17:47:29 newdeal Exp $
 *
 */

start GroupControlUI, notDetachable;

TitleInteraction = GenInteraction {
	genStates = default -usable;
	children = TitleGroup, TitleTextGroup;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}


TitleGroup = GenBooleanGroup {
	children = ChartTitleBoolean,
			XAxisTitleBoolean,
			YAxisTitleBoolean;
	applyMsg = MSG_CGC_SET_GROUP_FLAGS;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_DIVIDE_HEIGHT_EQUALLY,
		ATTR_GEN_BOOLEAN_GROUP_STATUS_MSG {
			word MSG_CGC_TITLE_STATUS
		}		
	}
}


ChartTitleBoolean = GenBoolean {
#ifdef GPC
	moniker = "Chart Title:";
#else
	moniker = "Chart Title";
#endif
	identifier = "mask CGF_CHART_TITLE";
	hints = { HINT_EXPAND_HEIGHT_TO_FIT_PARENT }
}

XAxisTitleBoolean = GenBoolean {
#ifdef GPC
	moniker = "X-Axis Title:";
#else
	moniker = "X-Axis Title";
#endif
	identifier = "mask CGF_X_AXIS_TITLE";
	hints = { HINT_EXPAND_HEIGHT_TO_FIT_PARENT }
}

YAxisTitleBoolean = GenBoolean {
	genStates = default -enabled;
#ifdef GPC
	moniker = "Y-Axis Title:";
#else
	moniker = "Y-Axis Title";
#endif
	identifier = "mask CGF_Y_AXIS_TITLE";
	hints = { HINT_EXPAND_HEIGHT_TO_FIT_PARENT }
}

TitleTextGroup = GenInteraction {
	children = ChartTitleText, XAxisTitleText, YAxisTitleText;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}

ChartTitleText = GenText {
	textDisplayAttributes = default +singleLineText;
	applyMsg = MSG_CGC_SET_TITLE_TEXT;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		  HINT_FIXED_SIZE {
#ifdef GPC
			SpecWidth <SST_AVG_CHAR_WIDTHS, 25>
#else
			SpecWidth <SST_AVG_CHAR_WIDTHS, 20>
#endif
		  }
	}
}

XAxisTitleText = GenText {
	textDisplayAttributes = default +singleLineText;
	applyMsg = MSG_CGC_SET_X_AXIS_TEXT;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		  HINT_FIXED_SIZE {
#ifdef GPC
			SpecWidth <SST_AVG_CHAR_WIDTHS, 25>
#else
			SpecWidth <SST_AVG_CHAR_WIDTHS, 20>
#endif
		  }
	}
}

YAxisTitleText = GenText {
	textDisplayAttributes = default +singleLineText;
	applyMsg = MSG_CGC_SET_Y_AXIS_TEXT;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		  HINT_FIXED_SIZE {
#ifdef GPC
			SpecWidth <SST_AVG_CHAR_WIDTHS, 25>
#else
			SpecWidth <SST_AVG_CHAR_WIDTHS, 20>
#endif
		  }
	}
}

/**********************************************************************
	LEGEND
**********************************************************************/

LegendInteraction = GenInteraction {
	genStates = default -usable;
	children = LegendOnOffGroup,
			LegendTypeGroup;
	hints = { HINT_ORIENT_CHILDREN_VERTICALLY,
#ifndef GPC
		  HINT_EXPAND_WIDTH_TO_FIT_PARENT,
#endif
		  HINT_DRAW_IN_BOX
		}

}

LegendOnOffGroup = GenBooleanGroup {
	children = LegendOnOffBoolean;
	applyMsg = MSG_CGC_SET_GROUP_FLAGS;
	destination  = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_BOOLEAN_GROUP_STATUS_MSG {
			word MSG_CGC_LEGEND_STATUS
		}
	}
}

LegendOnOffBoolean = GenBoolean {
	moniker = "Legend";
	identifier = "mask CGF_LEGEND";
}

LegendTypeGroup = GenItemGroup {
	numSelections  = 1;
	selection = "mask CGF_LEGEND or mask CGF_LEGEND_VERTICAL";
	children = LegendHorizontalItem, LegendVerticalItem;
	applyMsg = MSG_CGC_SET_LEGEND_TYPE;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = { HINT_ORIENT_CHILDREN_VERTICALLY }
}

LegendHorizontalItem = GenItem {
	moniker = "Horizontal (below chart)";
	identifier = "mask CGF_LEGEND";
}

LegendVerticalItem = GenItem {
	moniker = "Vertical (at right of chart)";
	identifier = "mask CGF_LEGEND or mask CGF_LEGEND_VERTICAL";
}

end GroupControlUI;

start StringUI, data;

chunk CGCName = "Group";

end StringUI;
