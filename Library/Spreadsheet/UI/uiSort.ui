/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	PCGEOS
 * MODULE:	uiSort.ui
 * FILE:	uiSort.ui
 *
 * AUTHOR:  	Gene Anderson: May 22, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	5/22/92	  gene	    Initial version
 *
 * DESCRIPTION:
 *	
 *
 * 	$Id: uiSort.ui,v 1.1 97/04/07 11:12:20 newdeal Exp $
 *
 ***********************************************************************/

#include "Art/mkrSortAscend.ui"
#include "Art/mkrSortDescend.ui"

start SSSortControlUI, notDetachable;

/****************************************************************************
 *	"Sort DB"
 ***************************************************************************/

SortDB = GenInteraction {
	genStates = default -usable;
	children =
		SortByList,
		SortOrderList,
		SortOptionsList,
		SortTrigger;
	hints = {
		HINT_IF_SYSTEM_ATTRS { SystemAttrs mask SA_HORIZONTALLY_TINY },
			HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_ELSE,
			HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_ENDIF,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

/****************************************************************************
 *	"Sort By" list
 ***************************************************************************/

SortByList = GenItemGroup {
	moniker = "Sort By";
	children =
		SortRowsEntry,
		SortColumnsEntry;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_DRAW_IN_BOX
	}
}

SortRowsEntry = GenItem {
	moniker = 'R', "Rows";
	identifier = "mask RSF_SORT_ROWS";
}

SortColumnsEntry = GenItem {
	moniker = 'C', "Columns";
	identifier = "0";
}

/****************************************************************************
 *	"Sort Order" list
 ***************************************************************************/

SortOrderList = GenItemGroup {
	moniker = "Sort Order";
	children =
		SortAscendingEntry,
		SortDescendingEntry;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_DRAW_IN_BOX
	}
	selection = "mask RSF_SORT_ASCENDING";
}

SortAscendingEntry = GenItem {
	moniker = 'A', "Ascending";
	identifier = "mask RSF_SORT_ASCENDING";
}

SortDescendingEntry = GenItem {
	moniker = 'D', "Descending";
	identifier = "0";
}


/****************************************************************************
 *	"Sort Options" list
 ***************************************************************************/

SortOptionsList = GenBooleanGroup {
	moniker = "Sort Options";
	children =
		SortCaseEntry,
		SortSpaceEntry;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_DRAW_IN_BOX
	}
}

SortCaseEntry = GenBoolean {
#if DO_PIZZA
	moniker = 'I', "Ignore Case/Width";
#else
	moniker = 'I', "Ignore Case/Accents";
#endif
	identifier = "mask RSF_IGNORE_CASE";
}

SortSpaceEntry = GenBoolean {
	moniker = 'S', "Ignore Spaces/Punctuation";
	identifier = "mask RSF_IGNORE_SPACES";
}

/****************************************************************************
 *	"Sort" trigger
 ***************************************************************************/

SortTrigger = GenTrigger {
	moniker = "Sort";
	action = MSG_SSSC_SORT, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_SEEK_REPLY_BAR,
		HINT_DEFAULT_DEFAULT_ACTION
	}
}

end SSSortControlUI;

/****************************************************************************
 *	Tool UI
 ***************************************************************************/

start SSSortControlToolboxUI, notDetachable;

SortAscendingToolTrigger = GenTrigger {
	genStates = default -usable;
	TOOL_MONIKER_LIST(SortAscend)
	action = MSG_SSSC_SORT_TRIGGER, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA { byte mask RSF_SORT_ROWS or \
						mask RSF_SORT_ASCENDING },
		ATTR_GEN_FOCUS_HELP { optr SortAscendingToolName }
	}
}

SortDescendingToolTrigger = GenTrigger {
	genStates = default -usable;
	TOOL_MONIKER_LIST(SortDescend)
	action = MSG_SSSC_SORT_TRIGGER, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA { byte mask RSF_SORT_ROWS },
		ATTR_GEN_FOCUS_HELP { optr SortDescendingToolName }
	}
}

end SSSortControlToolboxUI;

/****************************************************************************
 *	Strings for controllers
 ***************************************************************************/

start ControlStrings, data;

chunk SSSCName = "Sort";
chunk SortOptionsName = "Sort Options";
chunk SortOrderName = "Sort Order";
chunk SortByName = "Sort By";

chunk SortAscendingToolName = "Sort Rows Ascending";
chunk SortDescendingToolName = "Sort Rows Descending";

end ControlStrings;
