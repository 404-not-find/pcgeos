/*
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	PC GEOS 
 * MODULE:	Chart library
 * FILE:	uiMarker.ui
 *
 * AUTHOR:	Chris Boyke
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	CDB	1/20/92   	Initial version.
 *
 * DESCRIPTION:
 *
 * 	$Id: uiMarker.ui,v 1.1 97/04/07 11:09:46 newdeal Exp $
 *
 */

start	MarkerControlUI, notDetachable;

MarkerList = GenItemGroup {
	children =
		NoneItem,
		SquareItem,
		Cross1Item,
		DiamondItem,
		Cross2Item,
		TriangleItem,
		Bar1Item,
		Bar2Item,
		CircleItem;

	applyMsg = MSG_SMC_SET_MARKER_SHAPE;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	genStates = default -usable;
	hints = {
			HINT_ITEM_GROUP_TOOLBOX_STYLE
		}
}
#ifdef GPC
#define MARKER_ENTRY_VALUES	\
	hints = {\
		HINT_CENTER_MONIKER,\
		HINT_FIXED_SIZE { \
			SpecWidth <SST_PIXELS, 20> | \
			SpecHeight <SST_PIXELS, 20> \
		}\
	}
#define MARKER_TEXT_ENTRY_VALUES	\
	hints = {\
		HINT_FIXED_SIZE { \
			SpecWidth <> | \
			SpecHeight <SST_PIXELS, 20> \
		}\
	}
#else
#define MARKER_ENTRY_VALUES
#define MARKER_TEXT_ENTRY_VALUES
#endif

NoneItem = GenItem {
	moniker = "None";
	identifier = "MS_NONE";
	MARKER_TEXT_ENTRY_VALUES
}	

SquareItem = GenItem {
	moniker = list {
#ifdef GPC_ONLY
			ListSquareMoniker
#else
			SquareMoniker
#endif
	}
	identifier = "MS_SQUARE";
	MARKER_ENTRY_VALUES
}				   

Cross1Item = GenItem {
	moniker = list {
#ifdef GPC_ONLY
			ListCross1Moniker
#else
			Cross1Moniker
#endif
	}
	identifier = "MS_CROSS1";
	MARKER_ENTRY_VALUES
}

DiamondItem	= GenItem {
	moniker = list {
#ifdef GPC_ONLY
			ListDiamondMoniker
#else
			DiamondMoniker
#endif
	}
	identifier = "MS_DIAMOND";
	MARKER_ENTRY_VALUES
}

Cross2Item	= GenItem {
	moniker = list {
#ifdef GPC_ONLY
			ListCross2Moniker
#else
			Cross2Moniker
#endif
	}
	identifier = "MS_CROSS2";
	MARKER_ENTRY_VALUES
}

TriangleItem = GenItem {
	moniker = list {
#ifdef GPC_ONLY
			ListTriangleMoniker
#else
			TriangleMoniker
#endif
	}
	identifier = "MS_TRIANGLE";
	MARKER_ENTRY_VALUES
}

Bar1Item = GenItem {
	moniker = list {
#ifdef GPC_ONLY
			ListBar1Moniker
#else
			Bar1Moniker
#endif
	}
	identifier = "MS_BAR1";
	MARKER_ENTRY_VALUES
}

Bar2Item = GenItem {
	moniker = list {
#ifdef GPC_ONLY
			ListBar2Moniker
#else
			Bar2Moniker
#endif
	}
	identifier = "MS_BAR2";
	MARKER_ENTRY_VALUES
}

CircleItem = GenItem {
	moniker = list {
#ifdef GPC_ONLY
			ListCircleMoniker
#else
			CircleMoniker
#endif
	}
	identifier = "MS_CIRCLE";
	MARKER_ENTRY_VALUES
}

end 	MarkerControlUI;


start	MarkerGStringUI, data;
/**************************************************************************
 *	MONIKERS 
 **************************************************************************/ 

#ifdef GPC_ONLY

#include "../Art/mkrSquare.ui"
#include "../Art/mkrCross1.ui"
#include "../Art/mkrDiamond.ui"
#include "../Art/mkrCross2.ui"
#include "../Art/mkrTriangle.ui"
#include "../Art/mkrBar1.ui"
#include "../Art/mkrBar2.ui"
#include "../Art/mkrCircle.ui"

#endif

#define MONIKER_VALUES	\
	cachedSize = 12, 12;

#define M1	MARKER_STD_SIZE
#define M2	MARKER_STD_SIZE/2
#define M4	MARKER_STD_SIZE/4
#define	M34	MARKER_STD_SIZE*3/4

#define M3	MARKER_STD_SIZE/3
#define M23	MARKER_STD_SIZE*2/3

/**************************************************************************
 *	Make the square moniker a liiiitttle smaller than the rest
 **************************************************************************/ 


visMoniker SquareMoniker = {
	MONIKER_VALUES
	gstring {
		GSBeginString
		GSFillRect	2,2, <M1-2>, <M1-2>
		GSDrawRect	2,2, <M1-2>, <M1-2>
		GSEndString
	}
}

visMoniker DiamondMoniker = {
	MONIKER_VALUES
	gstring {
		GSBeginString
		GSFillPolygon	4, RFR_ODD_EVEN
		word	M1/2, 0,
			M1, M1/2,
			M1/2, M1,
			0, M1/2

		GSDrawPolygon 4
		word	M1/2, 0,
			M1, M1/2,
			M1/2, M1,
			0, M1/2

		GSEndString
	}

}

visMoniker Cross1Moniker = {
	MONIKER_VALUES
	gstring {
		GSBeginString
		GSFillPolygon	12, RFR_ODD_EVEN
		word	M4,0,
			M2, M4,
		 	M34,0,
			M1,M4,
			M34, M2,
			M1, M34,
			M34, M1,
			M2, M34,
			M4, M1,
			0, M34,
			M4, M2,
			0, M4

		GSDrawPolygon	12
		word	M4,0,
			M2, M4,
		 	M34,0,
			M1,M4,
			M34, M2,
			M1, M34,
			M34, M1,
			M2, M34,
			M4, M1,
			0, M34,
			M4, M2,
			0, M4

		GSEndString
	}
}

/**************************************************************************
 *	Cross2 is a "plus" sign
 **************************************************************************/ 
visMoniker Cross2Moniker = {
	MONIKER_VALUES
	gstring {
		GSBeginString
		GSFillPolygon	12, RFR_ODD_EVEN
		word	M3,0,
			M23,0,
			M23,M3,
			M1,M3,
			M1,M23,
			M23,M23,
			M23,M1,
			M3,M1,
			M3,M23,
			0,M23,
			0,M3,
			M3,M3

		GSDrawPolygon	12
		word	M3,0,
			M23,0,
			M23,M3,
			M1,M3,
			M1,M23,
			M23,M23,
			M23,M1,
			M3,M1,
			M3,M23,
			0,M23,
			0,M3,
			M3,M3

		GSEndString
	}
}

visMoniker TriangleMoniker = {
	MONIKER_VALUES
	gstring {
		GSBeginString
		GSFillPolygon	3, RFR_ODD_EVEN
		word	M2, 0,
			M1, M1,
			0, M1
		GSDrawPolygon	3
		word	M2, 0,
			M1, M1,
			0, M1
		GSEndString
	}
}

visMoniker Bar1Moniker = {
	MONIKER_VALUES
	gstring {
		GSBeginString
		GSFillRect	M3, 0, M23, M1
		GSDrawRect	M3, 0, M23, M1
		GSEndString
	}
}

visMoniker Bar2Moniker = {
	MONIKER_VALUES
	gstring {
		GSBeginString
		GSFillRect	0,M3,M1,M23
		GSDrawRect	0,M3,M1,M23
		GSEndString
	}
}

visMoniker CircleMoniker = {
	MONIKER_VALUES
	gstring {
		GSBeginString
		GSFillEllipse	0,0,M1,M1
		GSDrawEllipse	0,0,M1,M1
		GSEndString
	}
}

end	MarkerGStringUI;

start	StringsUI, data;

chunk	MarkerName = "Marker Shape";

end	StringsUI;



