/***********************************************************************
 *
 *	Copyright (c) Geoworks 1994 -- All Rights Reserved
 *
 * PROJECT:	  Condo viewer
 * MODULE:	  find controller
 * FILE:	  findcontrolControl.ui
 *
 * AUTHOR:  	  Tom Lester, Aug 23, 1994
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	lester	8/23/94   	Initial version
 *
 * DESCRIPTION:
 *	UI for Find controller of content library.
 *
 *
 * 	$Id: findcontrolControl.ui,v 1.1 97/04/04 17:50:05 newdeal Exp $
 *
 ***********************************************************************/


/**********************************************************************
 *			Features UI				      *
 **********************************************************************/

start ContentFindUI, notDetachable;

ContentFindToolFindTrigger = GenTrigger {
	genStates = default -usable;
	moniker = "Find";
	kbdAccelerator = control 'F';
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
	action = MSG_CFC_INITIATE_SEARCH_CONTROL, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_TRIGGER_BRINGS_UP_WINDOW
	}
}

end ContentFindUI;


/**********************************************************************
 *			Tool UI					      *
 **********************************************************************/
start ContentFindToolUI, notDetachable;

ContentFindFindTrigger = GenTrigger {
	genStates = default -usable;
	moniker = list {
/*#if	!_NO_COLOR_MONIKERS  */
		FindCMoniker/*,
/*#endif
		FindMMoniker */
	}
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
	action = MSG_CFC_INITIATE_SEARCH_CONTROL, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_FOCUS_HELP {
			optr	FindFocusHelpString
		}
	}
}

end ContentFindToolUI;


/**********************************************************************
 *		Search Control Template				      *
 **********************************************************************/
start ContentFindSearchControlTemplate, notDetachable;

/*
 *  The actual Search controller
 */
ContentFindSearchControl = SearchReplaceControl {
	genStates = default -usable;
	visibility = dialog;
	output = "TO_OBJ_BLOCK_OUTPUT";	

	/* So it does not put a trigger in the menu. */
	attributes = default +notUserInitiatable; 
	hints = {
		ATTR_GEN_CONTROL_REQUIRE_UI {
			word	mask SRCF_FIND_NEXT or \
				mask SRCF_CLOSE or \
				mask SRCF_FIND_PREV
		},
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word	mask SRCF_REPLACE_CURRENT or \
				mask SRCF_REPLACE_ALL_IN_SELECTION or \
				mask SRCF_REPLACE_ALL or \
				mask SRCF_PARTIAL_WORDS or \
				mask SRCF_IGNORE_CASE or \
				mask SRCF_WILDCARDS or \
				mask SRCF_SPECIAL_CHARS
		},
		HINT_NO_WIDER_THAN_CHILDREN_REQUIRE,    
		HINT_KEEP_ENTIRELY_ONSCREEN, 
		HINT_INITIAL_SIZE { 
			SpecWidth <SST_AVG_CHAR_WIDTHS, 30>
			SpecHeight <>
		},
	}
}

end ContentFindSearchControlTemplate;



/*********************************
 * Here come the control strings *
 *********************************/

start ContentFindStrings, data;

chunk ContentFindName = "Content Find";

chunk FindTriggerName = "Find trigger";

chunk FindToolTriggerName = "Find tool trigger";

chunk FindFocusHelpString = "Find a word or phrase in the book";

end ContentFindStrings;
