/***********************************************************************
 *
 *	Copyright (c) Geoworks 1995 -- All Rights Reserved
 *
 * PROJECT:	  Clavin
 * MODULE:	  UI Specification
 * FILE:	  uiOutErrorRetry.ui
 *
 * AUTHOR:  	  Allen Yuen, Mar 15, 1995
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	AY	3/15/95   	Initial version
 *
 * DESCRIPTION:
 *	UI for notification dialog when a permanent error is encountered
 *	during transmission.
 *
 *
 * 	$Id: uiOutErrorRetry.ui,v 1.1 97/04/05 01:19:25 newdeal Exp $
 *
 ***********************************************************************/

#if	_QUERY_DELETE_AFTER_PERMANENT_ERROR

start	OutErrorRetryUI, notDetachable;

OutErrorRetryRoot = OutboxErrorRetry {
	moniker = "Permanent Error";
	genStates = default -usable;
	/* need to fixup this error dialog for non-Responder */
	attributes = notUserInitiatable;
	children = OutErrorRetryReason, OutErrorRetryRetry, OutErrorRetryDelete;
	hints = {
		ATTR_GEN_WINDOW_CUSTOM_PARENT {
			hptr	0	; use screen as parent
		},
		ATTR_GEN_WINDOW_CUSTOM_LAYER_PRIORITY {
			; keep on top. dont set win prio, though, as we want
			; it to not interfere with control panels, etc.
			LayerPriority	LAYER_PRIO_ON_TOP
		}
	}
}


/*
 * Since ResEdit cannot deal with more than two arguments so far, we use '$'
 * and '&' to mark the 3rd and 4th arguments.
 */
chunk OutErrorRetryString = "Failed to & \\1 to \\2: $\\rDo you want to try again?";
		localize "String used in the permanent error box.  @1 is replaced by the subject of the message (usually the document name). @2 is the destination address. '$' is the reason for the failure. '&' is the transmission verb, downcased.";

OutErrorRetryReason = GenText {
	genAttributes = default +readOnly;
	text = OutErrorRetryString;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

OutErrorRetryRetry = GenTrigger {
	moniker = 'R', "Retry";
	genAttributes = default +signalInteractionComplete;
	action = MSG_OER_RETRY, OutErrorRetryRoot;
	hints = {
		HINT_SEEK_REPLY_BAR,
		HINT_SEEK_SLOT { word 0 }
	}
}

chunk OutErrorRetryDeleteString = "Cancel\\rsend";
		localize "Selecting this trigger causes the message that couldn't be sent to be deleted."; 

OutErrorRetryDelete = ComplexMoniker {
	ComplexMoniker = GenTrigger;
	topText = "offset OutErrorRetryDeleteString";
	genAttributes = default +signalInteractionComplete;
	action = MSG_OER_DELETE_MESSAGE, OutErrorRetryRoot;
	hints = {
		HINT_TRIGGER_DESTRUCTIVE_ACTION,
		HINT_SEEK_REPLY_BAR,
		HINT_SEEK_SLOT {word 3}
	}
}

end	OutErrorRetryUI;

#endif	/* _QUERY_DELETE_AFTER_PERMANENT_ERROR */
