/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1994 -- All Rights Reserved
 *
 * PROJECT:	  Clavin
 * MODULE:	  UI Specification
 * FILE:	  uiOutProgress.ui
 *
 * AUTHOR:  	  Adam de Boor: May 10, 1994
 *
 * REVISION HISTORY:
 *	Name	  Date	    Description
 *	----	  ----	    -----------
 *	ardeb	  5/10/94   Initial version
 *
 * DESCRIPTION:
 *	Template resource for Outbox progress box
 *
 *
 * 	$Id: uiOutProgress.ui,v 1.1 97/04/05 01:19:02 newdeal Exp $
 *
 ***********************************************************************/

#if	MAILBOX_PERSISTENT_PROGRESS_BOXES

start	OutProg, notDetachable;

OutProgRoot = OutboxProgress {
#if _TRANSMIT_THREADS_KEYED_BY_MEDIUM
	templateMoniker = "\\1 Progress";
#else
	moniker = "\\1 Progress";
#endif	/* _TRANSMIT_THREADS_KEYED_BY_MEDIUM */
		localize "\1 is replaced by the Send-menu entry corresponding to the transport+medium being used to send the messages this box is telling the user about";
	
	genStates = default -usable;
	attributes = default +notUserInitiatable;
	children = OutProgMessage, OutProgPercent, OutProgHide, OutProgStop;
	progressGauge = OutProgPercent;
	boxType = outbox;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		MPB_REQUIRED_HINTS
	}
}

OutProgMessage = MessageGlyph {
	/* Moniker is set by progress box when it's told the message & addr */
}

OutProgPercent = MailboxProgressGauge {
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

OutProgHide = GenTrigger {
	moniker = "Hide";
	action = MSG_MPB_HIDE_THYSELF, OutProgRoot;
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

/* hack trigger to avoid default IC_STOP behaviour, that of completing
 * the interaction */
OutProgStop = GenTrigger {
	hints = {
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {word IC_STOP},
		HINT_SEEK_REPLY_BAR,
		HINT_TRIGGER_DESTRUCTIVE_ACTION
	}
}

end OutProg;

#endif	/* MAILBOX_PERSISTENT_PROGRESS_BOXES */
