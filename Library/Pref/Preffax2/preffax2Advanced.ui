/***********************************************************************
 *
 *	Copyright (c) Geoworks 1995 -- All Rights Reserved
 *
 * PROJECT:	  Tiramisu
 * MODULE:	  Preferences
 * FILE:	  preffax2Advanced.ui
 *
 * AUTHOR:  	  Peter Trinh, Feb  3, 1995
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	PT	2/ 3/95   	Initial version
 *
 * DESCRIPTION:
 *	Contains UIs for Advanced DB of the Tiramisu Preference Module
 *
 *
 * 	$Id: preffax2Advanced.ui,v 1.2 98/03/24 21:29:22 gene Exp $
 *
 ***********************************************************************/

start PrefFaxUI;

PrefFaxAdvancedDB = PrefFaxDialog {
	moniker = 'A', "Advanced";
	children = PhoneLineQualitySelector,
		   FineModeSelector,
		   TimeOutInteraction,
		   ConnectSequenceText,
		   DisconnectSequenceText,
		   AdvancedDoneTrigger;

	attributes = default -notUserInitiatable;
	prefInteractionAttrs = default 	+loadOptionsOnInitiate,
					+completeInteractionOnApply;

	/*********************************************************/
	/* This "type" will allow customization of the reply bar */
	/*********************************************************/
	type = multipleResponse;

	hints	= {
		ATTR_GEN_INIT_FILE_CATEGORY{ 
			char FAX_INI_MAIN_CATEGORY,0 
		},
		HINT_SEEK_REPLY_BAR,
		HINT_INTERACTION_SINGLE_USAGE,
		ATTR_GEN_HELP_FILE {
			char	FAX_HELP_FILE, 0
		},
		ATTR_GEN_HELP_CONTEXT {
			char	FAX_HELP_CONTEXT_ADVANCED_DB, 0
		},
		HINT_RIGHT_JUSTIFY_CHILDREN,
	}
}


/************************************
	PhoneLineQualitySelector
************************************/

PhoneLineQualitySelector = PrefItemGroup {
	moniker = "Phone Line Quality:";
	children = NormalLine, UnevenLine, PoorLine;
	destination = PrefFaxRoot;
	selection = "FAX_NORMAL_LINE";
	numSelections = 1;
	initFileFlags = default +useItemMonikers;
	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY{ 
			char FAX_INI_MAIN_CATEGORY,0 
		},
		ATTR_GEN_INIT_FILE_KEY {
			char FAX_INI_MAIN_PHONE_LINE_QUALITY,0
		},
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	}
}

NormalLine = GenItem {
	moniker = 'o', "Normal";
	identifier = "FAX_NORMAL_LINE";
}

UnevenLine = GenItem {
	moniker = 'U', "Uneven";
	identifier = "FAX_UNEVEN_LINE";
}

PoorLine = GenItem {
	moniker = 'P', "Poor";
	identifier = "FAX_POOR_LINE";
}



/************************************
	FineModeSelector
************************************/
FineModeSelector = PrefItemGroup {
	moniker = "Accept Fine Mode Faxes:";
	children = FineModeYes, FineModeNo;
	destination = PrefFaxRoot;
	selection = "FAX_NOT_ACCEPT_FINE_MODE";
	numSelections = 1;
	initFileFlags = default +useItemStrings;
	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY{ 
			char FAX_INI_RECEIVE_CATEGORY,0 
		},
		ATTR_GEN_INIT_FILE_KEY {
			char FAX_INI_RECEIVE_FINE_MODE_KEY,0
		},
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	}
}

FineModeNo = PrefStringItem {
	moniker = 'N', "No";
	identifier = "FAX_NOT_ACCEPT_FINE_MODE";
	initFileString = "false";
}

FineModeYes = PrefStringItem {
	moniker = 'Y', "Yes";
	identifier = "FAX_ACCEPT_FINE_MODE";
	initFileString = "true";
}



/************************************
	TimeOutInteraction
************************************/
TimeOutInteraction = PrefInteraction {
	children = WaitGlyph, TimeOutValues, SecondsGlyph;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	}
}

WaitGlyph = GenGlyph {
	moniker = "Wait";
}

TimeOutValues = PrefValue {
	value = 30;
	minimum = "FAX_MIN_TIMEOUT_TIME";
	maximum = "FAX_MAX_TIMEOUT_TIME";
	increment = 5;
	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY{ 
			char FAX_INI_FAXOUT_CATEGORY,0 
		},
		ATTR_GEN_INIT_FILE_KEY {
			char FAX_INI_FAXOUT_TIMEOUT_KEY,0
		},
		HINT_VALUE_DO_NOT_MAKE_LARGER_ON_PEN_SYSTEMS,
	}
}

SecondsGlyph = GenGlyph {
	moniker = "seconds for a fax connection";
}


/************************************
	ConnectSequenceText
************************************/
ConnectSequenceText = PrefText {
	moniker = "Initialization:";
	textDisplayAttributes = default +singleLineText;
	maxLength = "FAX_MAX_USER_MODEM_STRING";
	prefAttrs = default +saveIfChanged;
	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY { 
			char FAX_INI_MAIN_CATEGORY,0
		},
		ATTR_GEN_INIT_FILE_KEY {
			char FAX_INI_MAIN_USER_PREPEND_STRING,0
		},
	}
}
	
/************************************
	DisconnectSequenceText
************************************/
DisconnectSequenceText = PrefText {
	moniker = "Post-modem use:";
	textDisplayAttributes = default +singleLineText;
	maxLength = "FAX_MAX_USER_MODEM_STRING";
	prefAttrs = default +saveIfChanged;
	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY { 
			char FAX_INI_MAIN_CATEGORY,0
		},
		ATTR_GEN_INIT_FILE_KEY {
			char FAX_INI_MAIN_USER_EXIT_STRING,0
		},
	}
}


AdvancedDoneTrigger = GenTrigger {
	moniker = "OK";
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {word IC_APPLY},
		HINT_DEFAULT_DEFAULT_ACTION,
	}
}


end	PrefFaxUI;



