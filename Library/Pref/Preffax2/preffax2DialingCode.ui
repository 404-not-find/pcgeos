/***********************************************************************
 *
 *	Copyright (c) Geoworks 1995 -- All Rights Reserved
 *
 * PROJECT:	  Tiramisu
 * MODULE:	  Preferences
 * FILE:	  preffax2DialingCode.ui
 *
 * AUTHOR:  	  Peter Trinh, Feb  1, 1995
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	PT	2/ 1/95   	Initial version
 *
 * DESCRIPTION:
 *	Contains UIs for the DialingCode DB of the Tiramisu Preference Module.
 *
 *
 * 	$Id: preffax2DialingCode.ui,v 1.2 98/02/17 04:06:44 gene Exp $
 *
 ***********************************************************************/

/***********************************************************************
 * 			PrefFaxDialingCodeDB
 ***********************************************************************/

start	PrefFaxUI;

PrefFaxDialingCodeDB = PrefFaxDialog {
#if LIMITED_FAX_SUPPORT
	genStates = default -usable;
#endif
	moniker = 'C', "Dialing Codes";
	children = DialingCodeListSelector,
		   DialingCodeTextObjs,
		   DialingCodeEditInteraction,
		   DialingCodeDoneTrigger;

	attributes = default -notUserInitiatable;
	prefInteractionAttrs = default 	+loadOptionsOnInitiate, 
					+completeInteractionOnApply;

	/*********************************************************/
	/* This "type" will allow customization of the reply bar */
	/*********************************************************/
	type = multipleResponse;

	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY{ 
			char FAX_INI_MAIN_CATEGORY,0 
		},
		ATTR_GEN_HELP_FILE {
			char	FAX_HELP_FILE, 0
		},
		ATTR_GEN_HELP_CONTEXT {
			char	FAX_HELP_CONTEXT_DIALING_CODE_DB, 0
		},
		HINT_SEEK_REPLY_BAR,
		HINT_INTERACTION_SINGLE_USAGE,
	}
}

DialingCodeListSelector = PrefInteraction {
	children = DialingCodeSelector,
		   DialingCodeAccessList,
		   DialingCodeLongDistList;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
	}
}

DialingCodeSelector = GenItemGroup {
	moniker = "Lists:";
	children = DialingCodeSelectorAccessItem,
		   DialingCodeSelectorLongDistItem;
	destination = PrefFaxDialingCodeDB;
	hints = {
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word	MSG_PREF_FAX_DIALOG_SELECT_DIALING_CODE_LIST
		}
	}
}

DialingCodeSelectorAccessItem = GenItem {
	moniker = 'A', "Access";
	identifier = "FAX_ACCESS_ITEM";
}

DialingCodeSelectorLongDistItem = GenItem {
	moniker = 'L', "Long Distance";
	identifier = "FAX_LONG_DISTANCE_ITEM";
}

DialingCodeAccessList = PrefDialingCodeList {
	behaviorType = exclusive;
	selection = 0;		/* first item should be displayed */
	applyMsg = MSG_PREF_DIALING_CODE_LIST_SELECT_ITEM;
	destination = DialingCodeAccessList;
	genStates = default -usable;
	prefAttrs = default -saveIfUsable, +saveIfChanged;

	category = faxChunk;
	notesKey = accessNotesChunk;
	codeOneKey = accessCodeChunk;
	notesTextObj = DialingCodeNotesText;
	codeOneTextObj = DialingCodeNumberText;

	hints = {
		HINT_ITEM_GROUP_MINIMIZE_SIZE,
		HINT_TOOLBOX,
		HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION
	}
}

DialingCodeLongDistList = PrefDialingCodeList {
	behaviorType = exclusive;
	selection = 0;		/* first item should be displayed */
	applyMsg = MSG_PREF_DIALING_CODE_LIST_SELECT_ITEM;
	destination = DialingCodeLongDistList;
	genStates = default -usable;
	prefAttrs = default -saveIfUsable, +saveIfChanged;

	category = faxChunk;
	notesKey = longDistNotesChunk;
	codeOneKey = longDistCodeChunk;
	codeTwoKey = billingCardCodeChunk;
	notesTextObj = DialingCodeNotesText;
	codeOneTextObj = DialingCodeNumberText;
	codeTwoTextObj = DialingCodeBillingNumText;

	hints = {
		HINT_ITEM_GROUP_MINIMIZE_SIZE,
		HINT_TOOLBOX,
		HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,
	}
}

DialingCodeTextObjs = GenInteraction {
	children = DialingCodeNumberText, 
		   DialingCodeNotesText, 
		   DialingCodeBillingNumText;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_RIGHT_JUSTIFY_CHILDREN,
	}
}

DialingCodeNumberText = GenText {
	moniker = "Number:";
	textDisplayAttributes = default +singleLineText;
	maxLength = "FAX_LONG_DISTANCE_NUMBER_SIZE - 1";
	hints = {
		ATTR_GEN_TEXT_CUSTOM_FILTER {
			word	offset FaxNumberTextFilter
		},
	}
}

DialingCodeNotesText = GenText {
	moniker = "Notes:";
	textDisplayAttributes = default +singleLineText;
	maxLength = "FAX_LONG_DISTANCE_NOTE_SIZE - 1";
}

DialingCodeBillingNumText = GenText {
	moniker = "Billing Card#";
	textDisplayAttributes = default +singleLineText;
	maxLength = "FAX_BILLING_CARD_SIZE - 1";
	hints = {
		ATTR_GEN_TEXT_CUSTOM_FILTER {
			word	offset FaxNumberTextFilter
		}
	}
}


DialingCodeEditInteraction = GenInteraction {
	children = DialingCodeEditAddB, DialingCodeEditModifyB, 
		   DialingCodeEditDeleteB;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING,
	}
}

DialingCodeEditAddB = GenTrigger {
	moniker = "Add";
	action = MSG_PREF_FAX_DIALOG_SEND_MSG_TO_SELECTED_LIST, PrefFaxDialingCodeDB;
	hints = {
		HINT_TRIGGER_DESTRUCTIVE_ACTION,
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	MSG_PREF_DIALING_CODE_LIST_INSERT_ITEM
			word	0
			word	0
			word	0
		}
	}
}

DialingCodeEditModifyB = GenTrigger {
	moniker = "Modify";
	action = MSG_PREF_FAX_DIALOG_SEND_MSG_TO_SELECTED_LIST, PrefFaxDialingCodeDB;
	genStates = default -enabled;
	hints = {
		HINT_TRIGGER_DESTRUCTIVE_ACTION,
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	MSG_PREF_DIALING_CODE_LIST_MODIFY_SELECTED_ITEM
			word	0
			word	0
			word	0
		}
	}
}

DialingCodeEditDeleteB = GenTrigger {
	moniker = "Delete";
	action = MSG_PREF_FAX_DIALOG_SEND_MSG_TO_SELECTED_LIST, PrefFaxDialingCodeDB;
	genStates = default -enabled;
	hints = {
		HINT_TRIGGER_DESTRUCTIVE_ACTION,
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	MSG_PREF_DIALING_CODE_LIST_DELETE_SELECTED_ITEM
			word	0
			word	0
			word	0
		}
	}
}


/* Used by the PrefDialingCodeList objects */

chunk faxChunk = data {
	char	FAX_INI_MAIN_CATEGORY, 0
}

chunk accessNotesChunk = data {
	char	FAX_INI_MAIN_ACCESS_NOTES_KEY, 0
}

chunk accessCodeChunk = data {
	char	FAX_INI_MAIN_ACCESS_CODE_KEY, 0
}

chunk longDistNotesChunk = data {
	char	FAX_INI_MAIN_LONG_DIST_NOTE_KEY, 0
}

chunk longDistCodeChunk = data {
	char	FAX_INI_MAIN_LONG_DIST_CODE_KEY, 0
}

chunk billingCardCodeChunk = data {
	char	FAX_INI_MAIN_BILLING_CARD_CODE_KEY, 0
}


/*  A filter set up to pass these characters:
 *
 *		0-9   W   *   #   !   @   ,   -
 */

chunk FaxNumberTextFilter =  data {
	VisTextCustomFilterData	<0, 31 >	/* null - US is gone	*/
						/* space is included	*/
						/* ! is included	*/
	VisTextCustomFilterData <34, 34 >	/* " is gone		*/
						/* include #		*/
	VisTextCustomFilterData <36, 39 >	/* $ - ' is gone	*/
						/* () is included	*/
	VisTextCustomFilterData	<43, 43 >	/* + is gone		*/
						/* , - - is included	*/
	VisTextCustomFilterData	<46, 47 >	/* . - / is gone	*/
						/* 0 - 9 is include	*/
	VisTextCustomFilterData	<58, 63 >	/* nuke more crap	*/
						/* include @		*/
	VisTextCustomFilterData	<65, 86 >	/* nuke more crap	*/
						/* include W		*/
	VisTextCustomFilterData	<88, 118 >	/* nuke more crap	*/
						/* include w		*/
	VisTextCustomFilterData <120, 255>	/* don't need it	*/
}


DialingCodeDoneTrigger = GenTrigger {
	moniker = "OK";
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {word IC_APPLY},
		HINT_DEFAULT_DEFAULT_ACTION,
	}
}


end	PrefFaxUI;

