/***********************************************************************
 *
 *	Copyright (c) New Deal 1998 -- All Rights Reserved
 *
 * PROJECT:	  Newdeal
 * MODULE:	  Tweak UI
 * FILE:	  tweakuiApp.ui
 *
 * AUTHOR:  	  Gene Anderson: 1998
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	4/18/98	  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI for UI module of Preferences
 *
 *
 * 	$Id: tweakuiApp.ui,v 1.1 98/04/24 01:44:35 gene Exp $
 *
 ***********************************************************************/

start TweakUIUI;


AppSettings = PrefInteraction vardataReloc {
	genStates = default -usable;
	prefInteractionAttrs = default 	+loadOptionsOnInitiate,
					+saveOptionsOnApply;
	children =
		AppOptionsLists,
		AppLaunchModel,
		AppLaunchLevel;
	hints = {
	        ATTR_GEN_INIT_FILE_CATEGORY {char "uiFeatures", 0},
		ATTR_PREF_REBOOT_STRING {
			UN_OPTR	TweakUIAppSettingsRebootString
		},
		HINT_CENTER_CHILDREN_ON_MONIKERS,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

AppOptionsLists = PrefInteraction {
	children =
		AppOptions1,
		AppOptions2;
	hints = {
		HINT_CENTER_CHILDREN_ON_MONIKERS,
		HINT_MINIMIZE_CHILD_SPACING
	}
}

AppOptions1 = PrefBooleanGroup {
	moniker = "Options:";
	children =
		ASBypassNew,
		ASAddOpen,
		ASNoPasswords;
	selectedBooleans = 0;
	prefAttrs = default +rebootIfChanged;
	hints = {
		ATTR_GEN_INIT_FILE_KEY { char "docControlOptions", 0 },
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}

ASBypassNew = GenBoolean {
	moniker = 'B', "Bypass New/Open File Dialog";
	identifier = "mask DCO_BYPASS_BIG_DIALOG";
}

ASAddOpen = GenBoolean {
	moniker = 'O', "Add Open to File Menu";
	identifier = "mask DCO_HAVE_FILE_OPEN";
}

ASNoPasswords = GenBoolean {
	moniker = 'N', "No Document Passwords";
	identifier = "mask DCO_NO_DOC_PASSWORD";
}

AppOptions2 = PrefBooleanGroup {
	children =
		ASDeskAccOnTop;
	selectedBooleans = "mask UILO_DESK_ACCESSORIES";
	prefAttrs = default +rebootIfChanged;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_INIT_FILE_KEY { char "launchOptions",0 }
	}
}

ASDeskAccOnTop = GenBoolean {
	moniker = 'D', "Desk Accessories Stay on Top";
	identifier = "mask UILO_DESK_ACCESSORIES";
}

AppLaunchModel = PrefItemGroup {
	moniker = "Launch Model:";
	prefAttrs = default +rebootIfChanged;
	selection = UILM_MULTIPLE_INSTANCES;
	children =
		ALMSingle,
		ALMMultiple,
		ALMGuru;
	hints = {
		ATTR_GEN_INIT_FILE_KEY { char "launchModel", 0 },
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}

ALMSingle = GenItem {
	moniker = 'S', "Single Instance";
	identifier = UILM_SINGLE_INSTANCE;
}

ALMMultiple = GenItem {
	moniker = 'P', "Prompt User";
	identifier = UILM_MULTIPLE_INSTANCES;
}

ALMGuru = GenItem {
	moniker = 'M', "Multiple Instances";
	identifier = UILM_GURU;
}

AppLaunchLevel = PrefItemGroup {
	moniker = "Launch Level:";
	prefAttrs = default +rebootIfChanged;
	selection = UIIL_ADVANCED;
	children =
		AULIntro,
		AULBeginning,
		AULIntermediate,
		AULAdvanced;
	hints = {
/*		HINT_TOOLBOX, */
		ATTR_GEN_INIT_FILE_KEY { char "launchLevel", 0 }
	}
}

AULIntro = GenItem {
	identifier = UIIL_INTRODUCTORY;
	kbdAccelerator = control '1';
	hints = {
		ATTR_GEN_DEFAULT_MONIKER { word GDMT_LEVEL_0 }
	}
}

AULBeginning = GenItem {
	identifier = UIIL_BEGINNING;
	kbdAccelerator = control '2';
	hints = {
		ATTR_GEN_DEFAULT_MONIKER { word GDMT_LEVEL_1 }
	}
}

AULIntermediate = GenItem {
	identifier = UIIL_INTERMEDIATE;
	kbdAccelerator = control '3';
	hints = {
		ATTR_GEN_DEFAULT_MONIKER { word GDMT_LEVEL_2 }
	}
}

AULAdvanced = GenItem {
	identifier = UIIL_ADVANCED;
	kbdAccelerator = control '4';
	hints = {
		ATTR_GEN_DEFAULT_MONIKER { word GDMT_LEVEL_3 }
	}
}

end TweakUIUI;
