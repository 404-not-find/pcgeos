/***********************************************************************
 *
 *	Copyright (c) New Deal 1998 -- All Rights Reserved
 *
 * PROJECT:	  New Deal
 * MODULE:	  Tweak UI
 * FILE:	  tweakui.ui
 *
 * AUTHOR:  	  Gene Anderson
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	4/20/98	  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI for Tweak UI module of Preferences
 *
 *
 * 	$Id: tweakui.ui,v 1.5 98/05/15 17:43:48 gene Exp $
 *
 ***********************************************************************/

#include "generic.uih"
#include "config.uih"
#include "Art/mkrTweakUI.ui"

class TweakUIDialog = PrefDialog {
}

class TweakUIStartupList = PrefDynamicList {
	static /* array chunk */ optrComp : null;
	static /* dirty */ byteComp : 0;
}

#if 0
class TweakUIHideDriveBoolean = GenBoolean {
	drive = byteComp : 0;
}
#endif

#include "tweakuiExpress.ui"
#include "tweakuiApp.ui"
#include "tweakuiInterface.ui"
#include "tweakuiAdvanced.ui"
#include "tweakuiDrives.ui"
#include "tweakuiAppearance.ui"

/**************************************************************************
 *	UI tweaking UI settings
 **************************************************************************/ 
start	TweakUIUI;


TweakUITitle = TitledGlyph {
	moniker = list {
		TweakUITextMoniker,
		TweakUILCMoniker,
		TweakUILMMoniker,
		TweakUILCGAMoniker
	}
}

TweakUIRoot = TweakUIDialog {
	moniker = "Configure UI";
	prefAttrs = default +rebootIfChanged;
	attributes = default +modal, +notUserInitiatable;
	visibility = dialog;
	type = properties;
	genStates = default -usable;
	children =
		TweakUITitleGroup,
		TweakUISubgroup;
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
		HINT_INTERACTION_COMPLEX_PROPERTIES,
		ATTR_GEN_HELP_CONTEXT {	char "dbTweakUI",0 } 
	}
}


TweakUITitleGroup = GenInteraction {
	children =
		TweakUITitle,
		TweakUIHelp;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

TweakUIHelp = GenText {
	genAttributes = readOnly;
	text = "Fine-tune the settings for the user interface.";
	hints = {
		  HINT_FIXED_SIZE {
			SpecWidth <SST_PIXELS, 430>
			SpecHeight <>
		  }
	}
}

TweakUISubgroup = PrefInteraction {
	children =
		SectionList, TweakUISubgroupHolder;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_MINIMIZE_CHILD_SPACING
	}
}

TweakUISubgroupHolder = PrefInteraction {
	children =
	        AppearanceSettings,
		AppSettings,
		ExpressSettings,
		InterfaceSettings,
		AdvancedSettings,
		DriveSettings;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_RAISED_FRAME { word mask GRFI_LEFT or \
				    mask GRFI_RIGHT or mask GRFI_BOTTOM }
	}
}

/*
SectionListGroup = GenInteraction {
        moniker = 'f', "Settings for:";
	children =
	    SectionList;
	hints = {
	    HINT_PLACE_MONIKER_TO_LEFT
	}
}
*/

SectionList = GenItemGroup {
	children =
	        SLAppearance,
		SLApp,
		SLExpress,
		SLInterface,
		SLAdvanced,
		SLFileMgr;
	destination = TweakUIRoot;
	selection = TUIS_APPEARANCE;
	hints = {
	        HINT_ITEM_GROUP_TAB_STYLE, /*MINIMIZE_SIZE,*/
		/*HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,*/
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word MSG_TUID_SECTION_CHANGED
		},
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

SLApp = GenItem {
	moniker = "Applications";
	identifier = TUIS_APPLICATIONS;
}

SLExpress = GenItem {
	moniker = "Express Menu";
	identifier = TUIS_EXPRESS_MENU;
}

SLInterface = GenItem {
	moniker = "Interface";
	identifier = TUIS_INTERFACE;
}

SLAdvanced = GenItem {
	moniker = "Advanced";
	identifier = TUIS_ADVANCED;
}

SLFileMgr = GenItem {
	moniker = "File Manager";
	identifier = TUIS_FILE_MGRS;
}

SLAppearance = GenItem {
    moniker = "UI Appearance";
    identifier = TUIS_APPEARANCE;
}

end	TweakUIUI;

start	Strings, data;

chunk TweakUIExpressRebootString = "express menu or start menu settings";
chunk TweakUIAppSettingsRebootString = "application settings";
chunk TweakUIInterfaceSettingsRebootString = "interface settings";
chunk TweakUIAdvancedSettingsRebootString = "advanced settings";
chunk TweakUIDriveSettingsRebootString = "file manager settings";
chunk PrefUIColorRebootString = "UI color";
chunk PrefUIRebootString = "UI and will reset the state files";
chunk PrefUIFontRebootString = "font size";

chunk cuiDesktopString = " Easy Desktop";
chunk cuiDefProdName = "NewDeal";

chunk NewUIName = "NewUI \\xa8 Specific UI";
localize "long filename of newui.geo";

visMoniker TweakUITextMoniker = "Configure UI";

visMoniker TweakUIMonikerList = list {
		TweakUITextMoniker,
		TweakUILCMoniker,
		TweakUILMMoniker,
		TweakUILCGAMoniker
}

end	Strings;
