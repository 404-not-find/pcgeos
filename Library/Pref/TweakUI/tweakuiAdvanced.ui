/***********************************************************************
 *
 *	Copyright (c) New Deal 1998 -- All Rights Reserved
 *
 * PROJECT:	  New Deal
 * MODULE:	  Tweak UI
 * FILE:	  tweakuiAdvanced.ui
 *
 * AUTHOR:  	  Gene Anderson
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	4/20/98	  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI for Tweak UI module of Preferences
 *
 *
 * 	$Id: tweakuiAdvanced.ui,v 1.2 98/05/02 22:10:21 gene Exp $
 *
 ***********************************************************************/

class	PrefMinuteValue = PrefValue {
}

start TweakUIUI;

AdvancedSettings = PrefInteraction vardataReloc {
	genStates = default -usable;
	prefInteractionAttrs = default 	+loadOptionsOnInitiate,
					+saveOptionsOnApply;
	children =
	        AdvancedTopGroup;
	    /*StartupGroup;*/
	hints = {
		ATTR_PREF_REBOOT_STRING {
			UN_OPTR	TweakUIAdvancedSettingsRebootString
		},
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

AdvancedTopGroup = PrefInteraction {
    children =
	AdvancedOptionsList,
	AutosaveGroup;
    hints = {
	HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	HINT_ORIENT_CHILDREN_HORIZONTALLY
    }
}

AdvancedOptionsList = PrefBooleanGroup {
	prefAttrs = default +rebootIfChanged;
	moniker = "Options:";
	selectedBooleans = "mask TUIA_CONFIRM_SHUTDOWN or mask TUIA_REBOOT_3";
	children =
		AOLAutoReset,
		AOLConfirmShutdown,
		AOLReboot3,
		AOLEnableHelpEditor;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_INIT_FILE_CATEGORY { char "ui", 0},
		ATTR_PREF_BOOLEAN_GROUP_SEPARATE_BOOLEAN_KEYS
	}
}

AOLAutoReset = PrefBoolean {
	moniker = 'R', "Reset State Files After Crash";
	identifier = "mask TUIA_AUTO_RESET";
	hints = {
		ATTR_GEN_INIT_FILE_KEY { char "doNotDisplayResetBox",0 }
	}
}

AOLConfirmShutdown = PrefBoolean {
	moniker = 'C', "Confirm Shutdown";
	identifier = "mask TUIA_CONFIRM_SHUTDOWN";
	hints = {
		ATTR_GEN_INIT_FILE_KEY { char "confirmShutDown",0 }
	}
}

AOLReboot3 = PrefBoolean {
	moniker = 'b', "Reboot on CTRL-ALT-DEL";
	identifier = "mask TUIA_REBOOT_3";
	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY { char "input",0 },
		ATTR_GEN_INIT_FILE_KEY { char "rebootOnReset",0 }
	}
}

AOLEnableHelpEditor = PrefBoolean {
	moniker = 'n', "Enable Help Editor";
	identifier = "mask TUIA_HELP_EDITOR";
	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY { char "configure",0 },
		ATTR_GEN_INIT_FILE_KEY { char "helpEditor",0 }
	}
}

/***********************************************************************/

#if 0
StartupGroup = PrefInteraction {
	moniker = "Execute on Startup";
	genStates = default -usable;
	prefInteractionAttrs = default 	+saveOptionsOnApply;
	children =
		AddRemoveGroup,
		StartupList;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_DRAW_IN_BOX,
		HINT_PLACE_MONIKER_ABOVE,
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

StartupList = TweakUIStartupList {
	destination = "TO_SELF";
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 50>
			SpecHeight <SST_LINES_OF_TEXT, 3>
			word	3
		},
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word MSG_TUISL_STARTUP_PROGRAM_SELECTED
		}
	}
}

AddRemoveGroup = PrefInteraction {
	children =
		AddAppDialog,
		RemoveAppTrigger;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}

AddAppDialog = PrefInteraction {
	moniker = 'd', "Add";
	visibility = dialog;
	attributes = default +modal;
	type = command;
	children =
		AddAppSelector,
		AddAppTrigger;
}

AddAppSelector = GenFileSelector {
	fileCriteria =
		dirs,
		geosExecutables;
	hints = {
		ATTR_GEN_PATH_DATA {
			GenFilePath <SP_APPLICATION, 0>
		}
	}
}

AddAppTrigger = GenTrigger {
	moniker = "Add";
	action = MSG_TUISL_ADD_PROGRAM, StartupList;
	hints = {
		HINT_SEEK_REPLY_BAR,
		HINT_DEFAULT_DEFAULT_ACTION
	}
}

RemoveAppTrigger = GenTrigger {
	genStates = default -enabled;
	moniker = 'm', "Remove";
	action = MSG_TUISL_DELETE_PROGRAM, StartupList;
}
#endif

/***********************************************************************/

AutosaveGroup = PrefInteraction vardataReloc {
	moniker = "Document Safeguarding";
	children = AutosaveEnableItemGroup, AutosaveSpin;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_PLACE_MONIKER_ABOVE,
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_CUSTOM_CHILD_SPACING {
			SpecSizeSpec <SST_PIXELS, 1>
		},
		ATTR_GEN_INIT_FILE_CATEGORY {
			char "ui",0
		}
	}
}

AutosaveEnableItemGroup = PrefItemGroup {
	numSelections  = 1;
	selection = TRUE;
	moniker = 'S',"Safeguarding:";
	children = AutosaveEnable, AutosaveDisable;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		ATTR_GEN_INIT_FILE_KEY {
			char "autosave",0
		},
		ATTR_PREF_ITEM_GROUP_ENABLE {
			PrefEnableData <TRUE,AutosaveSpin>
		},
		ATTR_GEN_ITEM_GROUP_INIT_FILE_BOOLEAN
	}
}

AutosaveEnable = GenItem {
	moniker = "On";
	identifier = TRUE;
}

AutosaveDisable = GenItem {
	moniker = "Off";
	identifier = FALSE;
}

AutosaveSpin = PrefMinuteValue {
	moniker = 'T',"Time (minutes):";
	value = 1;
	minimum = 1;
	maximum = 18;
	increment = 1;
	hints = {
		ATTR_GEN_INIT_FILE_KEY {
			char "autosaveTime",0
		}
	}
}

end TweakUIUI;
