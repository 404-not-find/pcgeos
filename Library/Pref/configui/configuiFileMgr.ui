/***********************************************************************
 *
 *	Copyright (c) Designs in Light 2002 -- All Rights Reserved
 *
 * FILE:	  configuiDrives.ui
 *
 ***********************************************************************/

start ConfigUIUI;

DriveSettings = PrefInteraction vardataReloc {
    genStates = default -usable;
    prefInteractionAttrs = default 	+loadOptionsOnInitiate,
					+saveOptionsOnApply;
    children =
	FileMgrOptionsGroup,
	FileAssocGroup;
    hints = {
	ATTR_PREF_REBOOT_STRING {
	    UN_OPTR	ConfigUIDriveSettingsRebootString
	},
	HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	ATTR_GEN_INIT_FILE_CATEGORY { char "fileManager", 0}
    }
}

/***********************************************************************/

FileMgrOptionsGroup = PrefInteraction {
    children =
	FileMgrLeftGroup,
	FileMgrRightGroup;
    hints = {
	HINT_ORIENT_CHILDREN_HORIZONTALLY
    }
}

FileMgrLeftGroup = PrefInteraction {
    moniker = "Options:";
    children =
	FileMgrOptions,
	FileMgrDOSLaunchers;
    hints = {
	HINT_MINIMIZE_CHILD_SPACING
    }
}

FileMgrRightGroup = PrefInteraction {
    children =
	FileMgrMaxOverlapFolders,
	FileMgrMaxFullSizeFolders;
    hints = {
	HINT_CENTER_CHILDREN_ON_MONIKERS
    }
}

FileMgrOptions = PrefBooleanGroup {
    prefAttrs = default +rebootIfChanged;
    children =
	FMOConnect,
	FMOBlueLinks,
	FMOWrapNames;
    selectedBooleans = "mask DF_CONNECT or \
                        mask DF_SHOW_LINKS or \
			mask DF_WRAP";
    hints = {
	HINT_ORIENT_CHILDREN_VERTICALLY,
	ATTR_GEN_INIT_FILE_KEY { char "features", 0}
    }
}

FMOConnect = GenBoolean {
    moniker = 'C', "Connect to Remote";
    identifier = "mask DF_CONNECT";
}

FMOBlueLinks = GenBoolean {
    moniker = 'L', "Show Links in Blue";
    identifier = "mask DF_SHOW_LINKS";
}

FMOWrapNames = GenBoolean {
    moniker = 'W', "Word Wrap File Names";
    identifier = "mask DF_WRAP";
}


FileMgrDOSLaunchers = PrefBooleanGroup {
    prefAttrs = default +rebootIfChanged;
    children =
	FMDLOnOff;
    selectedBooleans = 1;
    hints = {
	ATTR_PREF_BOOLEAN_GROUP_SEPARATE_BOOLEAN_KEYS
    }
}

FMDLOnOff = PrefBoolean {
    moniker = 'O', "DOS Launchers";
    identifier = 1;
    hints = {
	ATTR_GEN_INIT_FILE_KEY { char "dosLaunchers", 0 }
    }
}

/***********************************************************************/

FileMgrMaxOverlapFolders = PrefValue {
    prefAttrs = default +rebootIfChanged;
    moniker = 'x', "Maximum Overlapping Folders:";
    minimum = 1;
    maximum = 32;
    value = 32;
    hints = {
	ATTR_GEN_INIT_FILE_KEY { char "maxOverlappingFolders",0 }
    }
}

FileMgrMaxFullSizeFolders = PrefValue {
    prefAttrs = default +rebootIfChanged;
    moniker = 'd', "Maximum Full Sized Folders:";
    minimum = 1;
    maximum = 32;
    value = 3;
    hints = {
	ATTR_GEN_INIT_FILE_KEY { char "maxFullSizedFolders",0 }
    }
}

/***********************************************************************/

FileAssocGroup = PrefInteraction {
    moniker = "File Associations";
    prefInteractionAttrs = default 	+saveOptionsOnApply;
    children =
	FileAssocLeft,
	FileAssocRight;
    hints = {
	HINT_DRAW_IN_BOX,
	HINT_PLACE_MONIKER_ABOVE,
	HINT_ORIENT_CHILDREN_HORIZONTALLY
    }
}

FileAssocLeft = PrefInteraction {
    children =
	FileTypesList,
	AddRemoveFileTypeGroup;
    hints = {
	HINT_CENTER_CHILDREN_HORIZONTALLY
    }
}

FileTypesList = FileAssocList {
    destination = "TO_SELF";
    hints = {
	HINT_ITEM_GROUP_SCROLLABLE,
	HINT_FIXED_SIZE {
	    SpecWidth <SST_AVG_CHAR_WIDTHS, 10>
	    SpecHeight <SST_LINES_OF_TEXT, 3>
	    word	3
	},
	ATTR_GEN_ITEM_GROUP_STATUS_MSG {
	    word MSG_FAL_FILE_ASSOC_SELECTED
	}
    }
}

AddRemoveFileTypeGroup = PrefInteraction {
    children =
	AddFileTypeDialog,
	RemoveFileTypeTrigger;
    hints = {
	HINT_ORIENT_CHILDREN_HORIZONTALLY
    }
}

AddFileTypeDialog = PrefInteraction {
    moniker = 'd', "Add";
    visibility = dialog;
    attributes = default +modal;
    type = command;
    children =
	AddFileTypeEntry,
	AddFileTypeTrigger;
}

AddFileTypeEntry = GenText {
    moniker = "Mask:";
    maxLength = DOS_DOT_FILE_NAME_LENGTH;
    textDisplayAttributes = default +singleLineText;
    hints = {
	HINT_DEFAULT_FOCUS,
	ATTR_GEN_TEXT_MAKE_UPPERCASE
    }
}

AddFileTypeTrigger = GenTrigger {
    moniker = "Add";
    action = MSG_FAL_ADD_ASSOCIATION, FileTypesList;
    hints = {
	HINT_SEEK_REPLY_BAR,
	HINT_DEFAULT_DEFAULT_ACTION
    }
}

RemoveFileTypeTrigger = GenTrigger {
    moniker = 'm', "Remove";
    action = MSG_FAL_DELETE_ASSOCIATION, FileTypesList;
}

FileAssocRight = GenInteraction {
    children =
	FileAssocToken,
	FileAssocProg;
}

FileAssocToken = GenInteraction {
    moniker = "Icon:";
    children =
	TokenGlyph,
	ChangeTokenDB;
    hints = {
	HINT_ORIENT_CHILDREN_HORIZONTALLY,
	HINT_CENTER_CHILDREN_VERTICALLY
    }
}

TokenGlyph = GenGlyph {
    hints = {
	HINT_MINIMUM_SIZE {
	    SpecWidth <SST_PIXELS, 48>
	    SpecHeight <SST_PIXELS, 30>
	}
    }
}

ChangeTokenDB = GenInteraction {
    moniker = "Change";
    visibility = dialog;
    attributes = default +modal;
    type = command;
    children =
	ChangeTokenList,
	ChangeTokenTrigger;
}

ChangeTokenList = IconTokenList {
    queryMsg = MSG_ITL_GET_MONIKER;
    destination = ChangeTokenList;
    hints = {
	HINT_ITEM_GROUP_SCROLLABLE,
	HINT_ORIENT_CHILDREN_HORIZONTALLY,
	HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	HINT_FIXED_SIZE {
	    SpecWidth <SST_PIXELS, (48+2)*5>
	    SpecHeight <SST_PIXELS, 32>
	    word 5
	}
    }
}

ChangeTokenTrigger = GenTrigger {
    moniker = 'U', "Use Icon";
    action = MSG_FAL_CHANGE_ICON, FileTypesList;
    hints = {
	HINT_SEEK_REPLY_BAR
    }
}

FileAssocProg = GenInteraction {
    moniker = "Program to run:";
    children =
	FileAssocProgEntry,
	FileAssocProgChange;
    hints = {
	HINT_PLACE_MONIKER_ABOVE,
	HINT_ORIENT_CHILDREN_HORIZONTALLY
    }
}

FileAssocProgEntry = GenText {
    genAttributes = default +readOnly;
    textDisplayAttributes = default +singleLineText;
    maxLength = FILE_LONGNAME_LENGTH;
    hints = {
	HINT_MINIMUM_SIZE {
	    SpecWidth <SST_AVG_CHAR_WIDTHS, 16>
	    SpecHeight <>
	}
    }
}

FileAssocProgChange = GenInteraction {
    moniker = "Change";
    visibility = dialog;
    attributes = default +modal;
    type = command;
    children =
	ChangeProgTrigger,
	FileAssocProgSelector;
}

ChangeProgTrigger = GenTrigger {
    moniker = 'U', "Use Program";
    action = MSG_FAL_CHANGE_PROGRAM, FileTypesList;
    hints = {
	HINT_SEEK_REPLY_BAR
    }
}

FileAssocProgSelector = GenFileSelector {
    fileCriteria =
	dirs,
	geosExecutables;
    hints = {
	ATTR_GEN_PATH_DATA {
	    GenFilePath <SP_APPLICATION, 0>
	}
    }
}

visMoniker noneStr = "N/A";

chunk ExtExistsError = "The extension \\1 is already defined.";

/***********************************************************************/

end ConfigUIUI;
