/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  prefuic
 * FILE:	  prefuic.ui
 *
 * AUTHOR:  	  Gene Anderson: Aug 25, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	8/25/92	  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI for UI module of Preferences
 *
 *
 * 	$Id: prefuic.ui,v 1.4 98/06/17 21:48:23 gene Exp $
 *
 ***********************************************************************/

#include "generic.uih"
#include "config.uih"
#include "Objects/colorC.uih"
#include "Art/mkrPrefUIC.ui"

class PrefUICDialog = PrefDialog {
}

class PrefUICColorSelector = PrefColorSelector {
}

class PrefColorsSample = GenGadget {
}

/**************************************************************************
 *	UI for specific UI & color
 **************************************************************************/ 
start	PrefUICUI;


PrefUICTitle = TitledGlyph {
	moniker = list {
		PrefUICTextMoniker,
		PrefUICLCMoniker,
		PrefUICLMMoniker,
		PrefUICLCGAMoniker
	}
}


PrefUICRoot = PrefUICDialog {
	moniker = "Change UI";
	prefAttrs = default +rebootIfChanged;
	attributes = default +modal, +notUserInitiatable;
	visibility = dialog;
	type = properties;
	genStates = default -usable;
	children =
		PrefUICTitleGroup,
		PrefUICSubgroup;
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
		HINT_INTERACTION_COMPLEX_PROPERTIES,
		ATTR_GEN_HELP_CONTEXT {	char "dbPrefUI",0 } 
	}
}


PrefUICTitleGroup = GenInteraction {
	children =
		PrefUICTitle,
		PrefUICHelp;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

PrefUICHelp = GenText {
	genAttributes = readOnly;
	text = "Change the user interface you use or its colors.";
	hints = {
		  HINT_FIXED_SIZE {
			SpecWidth <SST_PIXELS, 425>
			SpecHeight <>
		  }
	}
}

PrefUICSubgroup = PrefInteraction {
	children =
		SpecificUIGroup,
		ColorGroup;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

/******************************************************************************
 *	Specfic UI stuff
 *****************************************************************************/

SpecificUIGroup = PrefInteraction {
    moniker = "User Interface";
    children =
	UIList;
    hints = {
	HINT_DRAW_IN_BOX,
	HINT_CENTER_CHILDREN_HORIZONTALLY
    }
}

UIList = PrefItemGroup vardataReloc {
    children =
	UILMotif,
	UILNewUI,
	UILNewUICUI;
    prefAttrs = default +rebootIfChanged;
    initFileFlags = default +useItemMonikers;
    hints = {
	ATTR_PREF_REBOOT_STRING {
	    UN_OPTR	PrefUIRebootString
	},
	HINT_ITEM_GROUP_SCROLLABLE,
	HINT_FIXED_SIZE {
	    SpecWidth <SST_AVG_CHAR_WIDTHS, 32>
	    SpecHeight <SST_LINES_OF_TEXT, 3>
	    word 3
	},
	ATTR_GEN_INIT_FILE_CATEGORY {char "ui", 0},
	ATTR_GEN_INIT_FILE_KEY {char "specname", 0}
    }
}

UILMotif = GenItem {
    moniker = "Motif + NewManager";
    identifier = PUIC_MOTIF;
}

UILNewUI = GenItem {
    moniker = "NewUI + NewDesk";
    identifier = PUIC_NEWUI;
}

UILNewUICUI = GenItem {
    moniker = "NewUI + NewDesk + Consumer UI";
    identifier = PUIC_NEWUI_CUI;
}

/******************************************************************************
 *
 *	Color stuff
 *
 ******************************************************************************/

ColorGroup = PrefInteraction {
	moniker = "UI Colors";
	prefAttrs = default +rebootIfChanged;
	children =
		AreaSampleGroup,
		ColorListDefaultGroup;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_DRAW_IN_BOX,
		HINT_PLACE_MONIKER_ABOVE
	}
}

ColorSample = PrefColorsSample {
}

chunk SampleText = "Sample";

AreaSampleGroup = PrefInteraction {
	children =
		AreaList,
		ColorSample;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING
	}
}

AreaList = PrefItemGroup vardataReloc {
	destination = PrefUICRoot;
	selection = PUIC_TITLE_BARS;
	children =
		TitleEntry,
		DarkEntry,
		LightEntry,
		FileMgrEntry;
	hints = {
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word MSG_PREF_UICD_AREA_CHANGED
		}
	}
}

TitleEntry = GenItem {
	moniker = "Title bars";
	identifier = PUIC_TITLE_BARS;
}

DarkEntry = GenItem {
	moniker = "Dark items";
	identifier = PUIC_DARK_ITEMS;
}

LightEntry = GenItem {
	moniker = "Light items";
	identifier = PUIC_LIGHT_ITEMS;
}

FileMgrEntry = GenItem {
	moniker = "File Folders";
	identifier = PUIC_FILE_MGR;
}

ColorListDefaultGroup = PrefInteraction {
	children =
		ColorList,
		DefaultGroup;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING
	}
}

DefaultGroup = PrefInteraction {
	moniker = "Scheme:";
	children =
		SchemesList;
}

SchemesList = PrefItemGroup {
	destination = PrefUICRoot;
	initFileFlags = default +useItemMonikers;
	hints =  {
		ATTR_GEN_ITEM_GROUP_SET_MODIFIED_ON_REDUNDANT_SELECTION,
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word MSG_PREF_UICD_SET_DEFAULT_COLORS
		},
		HINT_ITEM_GROUP_MINIMIZE_SIZE,
		HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,
		ATTR_GEN_INIT_FILE_CATEGORY {char "ui", 0},
		ATTR_GEN_INIT_FILE_KEY {char "scheme", 0}
	}
	children =
		SLDefaultMotif,
		SLDefaultNewUI,
		SLDefaultCyan,
		SLDefaultEarth;
}

SLDefaultMotif = GenItem {
	moniker = "Motif Default";
	identifier = PUIDCS_DEFAULT;
}

SLDefaultNewUI = GenItem {
	moniker = "NewUI Default";
	identifier = PUIDCS_NEWUI;
}

SLDefaultCyan = GenItem {
	moniker = "Cyan";
	identifier = PUIDCS_CYAN;
}

SLDefaultEarth = GenItem {
	moniker = "Earth Tones (256)";
	identifier = PUIDCS_EARTH;
}

ColorList = PrefUICColorSelector vardataReloc {
	output = PrefUICRoot;
	prefAttrs = default +rebootIfChanged;
	hints = {
		ATTR_GEN_CONTROL_PROHIBIT_UI { word \
			not mask CSF_INDEX
		},
		ATTR_PREF_REBOOT_STRING {
			UN_OPTR	PrefUIColorRebootString
		}
	}
}

end	PrefUICUI;

start	Strings, data;

chunk PrefUIColorRebootString = "UI color";
chunk PrefUIRebootString = "UI and will reset the state files";

visMoniker PrefUICTextMoniker = "Change UI";

visMoniker PrefUICMonikerList = list {
		PrefUICTextMoniker,
		PrefUICLCMoniker,
		PrefUICLMMoniker,
		PrefUICLCGAMoniker
}

end	Strings;
