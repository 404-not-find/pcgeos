/***********************************************************************
 *
 *	Copyright (c) New Deal 1998 -- All Rights Reserved
 *
 * PROJECT:	  Newdeal
 * MODULE:	  prefint
 * FILE:	  prefint.ui
 *
 * AUTHOR:  	  Gene Anderson: Mar 25, 1998
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	3/25/98	  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI for Internet module of Preferences
 *
 *
 * 	$Id: prefint.ui,v 1.2 98/04/24 19:42:37 gene Exp $
 *
 ***********************************************************************/

#include "generic.uih"
#include "config.uih"
#include "Art/mkrPrefInt.ui"

class PrefIntDialog = PrefDialog {
}

class PrefModemDialog = PrefDialog {
}

#include "prefintAccess.ui"
#include "prefintIP.ui"
#include "prefintAdvanced.ui"
#include "prefintModem.ui"

/**************************************************************************
 *	UI
 **************************************************************************/ 
start	PrefIntUI;


PrefIntTitle = TitledGlyph {
	moniker = list {
		PrefIntTextMoniker,
		PrefIntLCMoniker,
		PrefIntLMMoniker,
		PrefIntLCGAMoniker
	}
}


PrefIntRoot = PrefIntDialog {
	moniker = "Internet";
	prefAttrs = default +rebootIfChanged;
	attributes = default +modal, +notUserInitiatable;
	visibility = dialog;
	type = properties;
	genStates = default -usable;
	children =
		PrefIntTitleGroup,
		PrefIntSubgroup;
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
		HINT_INTERACTION_COMPLEX_PROPERTIES,
		ATTR_GEN_HELP_CONTEXT {
			char "dbInternet",0
		},
		ATTR_GEN_HELP_FILE {
			char "prefint",0
		}
	}
}


PrefIntTitleGroup = GenInteraction {
	children =
		PrefIntTitle,
		PrefIntHelp;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

PrefIntHelp = GenText {
	genAttributes = readOnly;
	text = "Change your Internet connection settings.";
	hints = {
		  HINT_FIXED_SIZE {
			SpecWidth <SST_PIXELS, 425>
			SpecHeight <>
		  }
	}
}

PrefIntSubgroup = PrefInteraction {
	children =
		AccessGatewayGroup,
		IPSettings;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

AccessGatewayGroup = PrefInteraction {
	children =
		AccessSettings,
		ModemSettings,
		AdvancedSettings;
}

end	PrefIntUI;

start	Strings, data;

visMoniker PrefIntTextMoniker = "Internet";

visMoniker PrefIntMonikerList = list {
		PrefIntTextMoniker,
		PrefIntLCMoniker,
		PrefIntLMMoniker,
		PrefIntLCGAMoniker
}

end	Strings;
