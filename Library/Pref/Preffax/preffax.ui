/***********************************************************************
 *
 *	Copyright (c) Geoworks 1994.  All rights reserved.
 *	GEOWORKS CONFIDENTIAL
 *
 * PROJECT:	  Pasta
 * MODULE:	  PrefFax
 * FILE:	  PrefFax.ui
 *
 * AUTHOR:  	  Don Reeves, Nov 30, 1992
 *
 * REVISION HISTORY:
 *	Date	  	Name	Description
 *	----	  	----	-----------
 *	12/22/93	CL	Initial version 
 *
 * DESCRIPTION:
 *	User interface descriptions for Fax Preferences
 *
 * 	$Id: preffax.ui,v 1.1 97/04/05 01:38:37 newdeal Exp $
 *
 ***********************************************************************/

#include "generic.uih"
#include "config.uih"
#include "spool.uih"
#include "Art/mkrPrefFax.ui"

class	PrefFaxDialog = PrefDialog {
}

class	PrefInteractionSpecial = PrefInteraction {
}

class	PrefItemGroupSpecial = PrefItemGroup {
}

class	PreffaxOKTrigger = GenTrigger {
}

#define	HARDWARE_OPTIONS	0		/* turn off hardware options */

/***********************************************************************
 * 		Fax Preferences UI
 ***********************************************************************/

start	PrefFaxUI;

PrefFaxRoot = PrefFaxDialog {
	moniker = "Fax";
	attributes = default +modal, +notUserInitiatable;
	visibility = dialog;
	type = multipleResponse;
	genStates = default -usable;
	children =
		PrefFaxTitleGroup,
		PrefFaxFaxGroup,
		PrefFaxSerialOptions,
		PrefFaxOKTrigger,
		PrefFaxCloseTrigger;
	hints = {
		ATTR_GEN_HELP_CONTEXT {
			char	"dbPrefFax", 0
		},
		ATTR_GEN_HELP_FILE {
			char	"pfax", 0
		},
		HINT_INTERACTION_COMPLEX_PROPERTIES,
		HINT_INTERACTION_SINGLE_USAGE
	}
}


/************************************
	Children of PrefFaxRoot
************************************/
PrefFaxTitleGroup = GenInteraction {
	children =
		PrefFaxTitle,
		PrefFaxHelp;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}


PrefFaxFaxGroup = PrefInteraction {
	children = 	PrefFaxDeviceList,
			PrefFaxPortList;
	hints = {HINT_ORIENT_CHILDREN_HORIZONTALLY}
}


PrefFaxSerialOptions = GenInteraction {
	children =
		PrefFaxSerialOpts;
	hints = {
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
	}
}

/****************************************
	Children of PrefFaxTitleGroup
****************************************/
PrefFaxTitle = TitledGlyph {
	moniker = list {
		PrefFaxLCMoniker,
		PrefFaxLMMoniker,
		PrefFaxTextMoniker
	}
}

PrefFaxHelp = GenText {
	genAttributes = readOnly;
	text = "Use these options to configure your fax modem and port settings. Select a fax modem and choose \\\"OK\\\" to start the fax software.";
	hints = {
			HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}
	

/************************************************
	Children of PrefFaxFaxGroup
************************************************/
PrefFaxDeviceList = PrefTocList vardataReloc {
	moniker = "Select a fax modem:";
	tocFlags = default +extendedDeviceDrivers;
	tokenChars = "'FODR'";
	destination = PrefFaxRoot;
	hints = {
		HINT_DEFAULT_FOCUS, 
		HINT_PLACE_MONIKER_ABOVE,
		HINT_ITEM_GROUP_SCROLLABLE,
		ATTR_GEN_PATH_DATA {
			GenFilePath <SP_SYSTEM, "fax">
		},
		INIT_FILE_CATEGORY(fax),
		INIT_FILE_KEY(device),
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 36>
			SpecHeight <SST_LINES_OF_TEXT, 3>
			word	3
		},
	}
}

PrefFaxPortList = PrefItemGroup {
	moniker = "Select a fax modem port:";
	children =
		Com1Item,
		Com2Item,
		Com3Item,
		Com4Item;
	destination = PrefFaxRoot;
	selection = "offset com1Info";
	numSelections = 1;
	initFileFlags = default +useItemMonikers;
	hints = {
		INIT_FILE_CATEGORY(fax),
		INIT_FILE_KEY(port),
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_PLACE_MONIKER_ABOVE,
		HINT_FIXED_SIZE {
			SpecWidth  <SST_AVG_CHAR_WIDTHS, 25>
			SpecHeight <SST_LINES_OF_TEXT, 3>
			word	3
		},
	}
}

Com1Item = PrefStringItem {
	moniker = "COM1";
	identifier = "offset com1Info";
}

Com2Item = PrefStringItem {
	moniker = "COM2";
	identifier = "offset com2Info";
}


Com3Item = PrefStringItem {
	moniker = "COM3";
	identifier = "offset com3Info";
}


Com4Item = PrefStringItem {
	moniker = "COM4";
	identifier = "offset com4Info";
}


/***********************************************************************
 * 		Children of PrefFaxSerialOptions
 ***********************************************************************/
PrefFaxSerialOpts = PrefInteraction {
	moniker = "Change Modem Options";
	children =
		PrefFaxSerialDialTypeParent,
		PrefFaxSerialHandshakeParent,
		PrefFaxSerialHardwareHandshake;
	type = properties;
	prefInteractionAttrs = default +loadOptionsOnInitiate,
				+saveOptionsOnApply;
	visibility = dialog;
	attributes = modal;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_HELP_CONTEXT {
			char	"dbModemOpts", 0
		},
		HINT_INTERACTION_COMPLEX_PROPERTIES,
		HINT_INTERACTION_SINGLE_USAGE
	}
}


/****************************************
 * 	Dial Type
 ****************************************/
PrefFaxSerialDialTypeParent = PrefInteraction {
	moniker = "Dial Type:";
	children =
		PrefFaxSerialDialType;
}


PrefFaxSerialDialType = PrefItemGroupSpecial {
	initFileFlags = default +useItemMonikers;
	numSelections  = 1;
	originalNumSelections  = 1;
	selection = 0;
	originalSelection = 0;
	children =
			DialToneEntry,
			DialPulseEntry;
	hints = {
		HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		ATTR_GEN_INIT_FILE_KEY {
			char	"dialtype",0
		},
		ATTR_GEN_INIT_FILE_CATEGORY {
			char "fax",0
		},
	}
}


DialToneEntry = PrefStringItem {
	moniker = "Tone";
	identifier = 0;
	initFileString = DialTtext;
}

DialPulseEntry = PrefStringItem {
	moniker = "Pulse";
	identifier = 1;
	initFileString = DialPtext;
}


/****************************************
 * 	Handshake
 ****************************************/
PrefFaxSerialHandshakeParent = PrefInteraction {
	moniker = "Handshaking:";
	children =
		PrefFaxSerialHandshake;
}


PrefFaxSerialHandshake = PrefItemGroupSpecial {
	initFileFlags = default +useItemStrings;
	behaviorType = nonExclusive;
	selection = handshkOptions;
	numSelections = 2;
	children =
		HandshakeNoneEntry,
		HandshakeHardwareEntry, 
		HandshakeSoftwareEntry; 
	hints = {

		HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		ATTR_GEN_INIT_FILE_KEY {
			char	"handshake",0
		},
		ATTR_GEN_INIT_FILE_CATEGORY {
			char "fax",0
		},
	
		ATTR_PREF_ITEM_GROUP_ENABLE {
			PrefEnableData <mask SFC_HARDWARE,
					PrefFaxSerialHardwareHandshake>
		},
		ATTR_PREF_ITEM_GROUP_OVERRIDE {
			word 0
		}
	}
}


HandshakeHardwareEntry = PrefStringItem {
	moniker = "Hardware";
	identifier = "mask SFC_HARDWARE";
	initFileString = HardText;
}


HandshakeSoftwareEntry = PrefStringItem {
	moniker = "Software";
	identifier = "mask SFC_SOFTWARE";
	initFileString = SoftText;
}

HandshakeNoneEntry = PrefStringItem {
	moniker = "None";
	identifier = 0;
	initFileString = NoneText;
}

/* This is the options setting for software and hardware handshake */
chunk	handshkOptions = data {
	word 1
	word 2
}


/****************************************
 * 	Hardware Handshake
 ****************************************/


PrefFaxSerialHardwareHandshake = PrefInteraction {
	moniker = "Hardware Handshaking Options:";
	genStates = default +enabled;
	children =
		StopRemoteList,
		StopLocalList;
	hints = {
		HINT_PLACE_MONIKER_ABOVE
	}
}

StopRemoteList = PrefItemGroupSpecial {
	initFileFlags = default +useItemStrings;
	behaviorType = nonExclusive;
	
	moniker = "Stop Remote:";
	selection = "mask SMC_RTS";
	numSelections = 1;
	children =
		DTREntry,
		RTSEntry;
	destination = PrefFaxRoot;
	hints = {
		ATTR_GEN_INIT_FILE_KEY {
			char	"stopRemote",0
		},
		ATTR_GEN_INIT_FILE_CATEGORY {
			char "fax",0
		},
	}
}

DTREntry = PrefStringItem {
	moniker = "DTR";
	identifier = "mask SMC_DTR";
	initFileString = DTRtext;
}

RTSEntry = PrefStringItem {
	moniker = "RTS";
	identifier = "mask SMC_RTS";
	initFileString = RTStext;
}

StopLocalList = PrefItemGroupSpecial {
	initFileFlags = default +useItemStrings;
	behaviorType = nonExclusive;
	selection = "mask SMS_CTS";
	numSelections = 1;
	moniker = "Stop Local:";
	children =
		CTSEntry,
		DCDEntry,
		DSREntry;
	hints = {
		ATTR_GEN_INIT_FILE_KEY {
			char	"stopLocal",0
		},
		ATTR_GEN_INIT_FILE_CATEGORY {
			char "fax",0
		},
	}
}

CTSEntry = PrefStringItem {
	moniker = "CTS";
	identifier = "mask SMS_CTS";
	initFileString = CTStext;
}

DCDEntry = PrefStringItem {
	moniker = "DCD";
	identifier = "mask SMS_DCD";
	initFileString = DCDtext;
}

DSREntry = PrefStringItem {
	moniker = "DSR";
	identifier = "mask SMS_DSR";
	initFileString = DSRtext;
}

PrefFaxOKTrigger = PreffaxOKTrigger {
	genStates = default +enabled;
	action = MSG_PREF_FAX_DIALOG_START_FAX_SOFTWARE, PrefFaxRoot;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {word IC_APPLY},
		HINT_DEFAULT_DEFAULT_ACTION,
	}
}

PrefFaxCloseTrigger = GenTrigger {
	action = MSG_PREF_FAX_DIALOG_CLOSE, PrefFaxRoot;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND
			{word	IC_DISMISS}
	}
}

chunk DialTtext = "tone";
chunk DialPtext = "pulse";
chunk NoneText = "none";
chunk HardText = "hardware";
chunk SoftText = "software";
chunk DTRtext = "dtr";
chunk RTStext = "rts";
chunk CTStext = "cts";
chunk DCDtext = "dcd";
chunk DSRtext = "dsr";

end	PrefFaxUI;



/***********************************************************************
 * 		 Preference Strings
 ***********************************************************************/
start	Strings, data;

visMoniker PrefFaxTextMoniker = "Fax";

visMoniker PrefFaxMonikerList = list {
		PrefFaxLCMoniker,
		PrefFaxLMMoniker,
		PrefFaxTextMoniker
}

chunk NoDriverString = {You must select a fax modem.}

chunk CannotStartString = {The system could not start the fax software.}

chunk FaxReadyString = {The system is now ready for sending faxes.}

chunk NotFaxReadyString = {The system is not ready for sending faxes.}

chunk ExitNoStartString = {The fax software will not be started unless you hit OK. Are you sure you want to exit without starting the fax software?}

end	Strings;
