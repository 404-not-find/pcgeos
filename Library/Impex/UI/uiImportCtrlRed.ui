/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  Impex/UI
 * FILE:	  uiImportCtrl.ui
 *
 * AUTHOR:  	  Don Reeves, May 24, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	5/24/92	  Don	    Initial version
 *
 * DESCRIPTION:
 *	Contains the UI definitions for the ImportControlClass
 *
 * 	$Id: uiImportCtrlRed.ui,v 1.1 97/04/04 22:49:09 newdeal Exp $
 *
 ***********************************************************************/

/***********************************************************************
 * 			Import Controller UI
 ***********************************************************************/

start	ImportControlUI, notDetachable;

ImportDummyGlyph = GenGlyph {
	genStates = default -usable;
}

/************************************
 *	Top Group
 ************************************/

ImportTop = GenInteraction {
	children = ImportFileSelectGroup,
		   ImportFormatGroup;
	genStates = default -usable;
}

/************************************
 *	FileSelect Group
 ************************************/

ImportFileSelectGroup = GenInteraction {
	children = ImportFileSelectGlyph,
		   ImportFileSelector;
}

ImportFileSelectGlyph = GenGlyph {
	moniker = "Choose File to Import:";
}

ImportFileSelector = GenFileSelector {
	action = MSG_IMPORT_EXPORT_SELECT_FILE, "TO_OBJ_BLOCK_OUTPUT";
	fileCriteria = default -geosExecutables, -geosNonExecutables;
	hints = {
		HINT_FILE_SELECTOR_SINGLE_ACTION,
		ATTR_GEN_PATH_DATA { GenFilePath <SP_DOCUMENT,> },
		HINT_FILE_SELECTOR_FILE_LIST_WIDTH { word 30 },
		HINT_FILE_SELECTOR_NUMBER_OF_FILES_TO_SHOW { word 12 }
	}
}

/************************************
 *	Format Group
 ************************************/

ImportFormatGroup = GenInteraction {
	children = ImportFormatGlyph,
		   ImportFormatListTrigger,
		   ImportFormatList;
	genStates = default -usable;
}

ImportFormatGlyph = GenGlyph {
	moniker = "Choose Import Format";
}

ImportFormatListTrigger = GenTrigger {
	moniker = 'N', "New Library Disk";
	action = MSG_IMPORT_CONTROL_NEW_LIBRARY_DISK, "TO_OBJ_BLOCK_OUTPUT";
}

ImportFormatList = FormatList {
/*	moniker = 'F', "Formats:";	*/
	formatListAttrs = "mask FLA_IMPORT";
	destination = ImportFormatList;
	queryMsg = MSG_FORMAT_LIST_REQUEST_FORMAT_MONIKER;
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
               	HINT_PLACE_MONIKER_ABOVE,
               	HINT_DEFAULT_FOCUS,
		HINT_IF_SYSTEM_ATTRS {word mask SA_TINY},
			HINT_FIXED_SIZE
			{
	                       	SpecWidth <SST_AVG_CHAR_WIDTHS, 30>
	                       	SpecHeight <SST_LINES_OF_TEXT, 3>
				word	3
	               	},
		HINT_ELSE,
			HINT_FIXED_SIZE 
			{
	                       	SpecWidth <SST_AVG_CHAR_WIDTHS, 30>
	                       	SpecHeight <SST_LINES_OF_TEXT, 12>
				word	12
	               	},
		HINT_ENDIF,
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word	MSG_FORMAT_LIST_SELECT_FORMAT
		}
	}
}


/************************************
 *	File Mask
 ************************************/

ImportFileMask = GenGlyph {
	genStates = default -usable;
}


/************************************
 *	Format & App UI parents
 ************************************/

ImportFormatUIParent = GenInteraction {
	genStates = default -usable;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	}
}

ImportAppUIParent = GenInteraction {
	genStates = default -usable;
	hints =  {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	}
}


/************************************
 *	Reply bar triggers
 ************************************/

ImportTrigger = GenTrigger {
	moniker = "Import";
	action = MSG_IMPORT_CONTROL_IMPORT, "TO_OBJ_BLOCK_OUTPUT";
	genStates = default -enabled, -usable;
	genAttributes = default	+initiatesInputIgnore;
	hints = {
		HINT_DEFAULT_DEFAULT_ACTION,
		HINT_SEEK_REPLY_BAR
	}
}

ImportCancelTrigger = GenTrigger {
	action = MSG_IMPORT_CONTROL_CANCEL, "TO_OBJ_BLOCK_OUTPUT";
	genAttributes = default	+signalInteractionComplete;
	genStates = default -usable;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {word IC_DISMISS}

	}
}

end	ImportControlUI;



/***********************************************************************
 * 		Import Toolbox UI
 ***********************************************************************/

start	ImportToolboxUI, notDetachable;

ImportToolTrigger = GenTrigger {
	moniker = "IMP";
	action = MSG_GEN_INTERACTION_INITIATE, "TO_OBJ_BLOCK_OUTPUT";
	genStates = default -usable;
	hints = {
		HINT_TRIGGER_BRINGS_UP_WINDOW
	}
}

end	ImportToolboxUI;



/***********************************************************************
 * 		Control & Toolbox strings
 ***********************************************************************/

start	ControllerStrings, data;

chunk	ImportControllerName	= "Import";


chunk	ImportFileMaskName	= "Import File Mask";

chunk	ImportFormatOptsName	= "Import Format Options";


chunk	ImportTriggerToolName	= "Import Dialog Box";

end	ControllerStrings;
