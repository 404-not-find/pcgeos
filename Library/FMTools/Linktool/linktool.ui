/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  
 * MODULE:	  UI Specification
 * FILE:	  convert.ui
 *
 * AUTHOR:  	  Adam de Boor: Aug 26, 1992
 *
 * REVISION HISTORY:
 *	Name	  Date	    Description
 *	----	  ----	    -----------
 *	ardeb	  8/26/92   Initial version
 *
 * DESCRIPTION:
 *	Monikers and UI for our tool aspect, primarily.
 *
 *
 * 	$Id: linktool.ui,v 1.1 97/04/04 18:01:23 newdeal Exp $
 *
 ***********************************************************************/

#include	<generic.uih>

class FileSelectorText = GenText {
}

start	LinktoolUI, notDetachable;


/*-----------------------------------------------------------------------------
	Linktool box
-----------------------------------------------------------------------------*/

LinktoolBox = GenInteraction {
	genStates = default -usable;
	visibility = dialog;
	moniker = "Create Link";
	children =	LinktoolGroup,
			LinktoolDestName,
			LinktoolOK,
			LinktoolCancel;
	attributes = modal, initiatedViaUserDoDialog;
	hints = {
		HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}

LinktoolGroup = GenInteraction {
	moniker = "Select destination for link";
	children = LinktoolFileSelector,
			LinktoolDestText;
	hints = {
		HINT_PLACE_MONIKER_ABOVE,
		HINT_CENTER_MONIKER,
		HINT_DRAW_IN_BOX
	}

}

LinktoolFileSelector = GenFileSelector {
	attributes = default +showFilesDisabled;
	action = MSG_FS_TEXT_FILE_SELECTOR_CHANGE, LinktoolDestText;
	hints = {
		HINT_FILE_SELECTOR_SINGLE_ACTION
	}
}

LinktoolDestText = FileSelectorText {
	moniker = "Place link in";
	genAttributes = default +readOnly;
	textDisplayAttributes = default +singleLineText;
	hints = {
		ATTR_GEN_TEXT_NEVER_MAKE_SCROLLABLE,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

LinktoolDestName = GenText {
	moniker = "Link Name:";
	text = "";
	maxLength = FILE_LONGNAME_LENGTH;
	textDisplayAttributes = singleLineText, noWordWrapping, 
		allowTextOffEnd, useTabForNavigation;
	hints = {
		ATTR_GEN_TEXT_NEVER_MAKE_SCROLLABLE,
		ATTR_GEN_TEXT_LEGAL_FILENAMES,
		HINT_DEFAULT_FOCUS,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, FILE_LONGNAME_LENGTH+2>
			SpecHeight <>
		}
	}
}

LinktoolOK = GenTrigger ignoreDirty {
	moniker = 'C', "Create";
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {word IC_OK},
		HINT_DEFAULT_DEFAULT_ACTION
	}
}

LinktoolCancel = GenTrigger ignoreDirty {
	moniker = "VMO_CANCEL", "Cancel";
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {word IC_DISMISS}
	}
}

end LinktoolUI;


start	LinktoolStrings, data;

visMoniker LinktoolToolMoniker = "Create Link";

chunk LinkError = "Unable to create link.";

end	LinktoolStrings;
