/*
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	PC GEOS 
 * MODULE:	
 * FILE:	prefSerial.ui
 *
 * AUTHOR:	Chris Boyke
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *       chrisb	12/ 2/92   	Initial version.
 *
 * DESCRIPTION:
 *
 *	UI for the serial options dialg
 *
 * 	$Id: prefSerial.ui,v 1.1 97/04/04 17:50:38 newdeal Exp $
 *
 */

start	SerialUI;


SerialInteraction = GenInteraction {
	type = organizational;
	children = ComPortText, ComPortOptions,
			HardwareHandshakeGroup;
}


/*
 *	ComPortText
 */

ComPortText = GenText {
	genAttributes = readOnly;
	moniker = "Port Options for:";
}

/*
 *	ComPortOptions
 */

ComPortOptions = PrefInteraction {
	children = BaudRateList,
			ParityList,
			WordLengthList, 
			StopBitsList, 
			HandshakeList;
	hints = {HINT_CENTER_CHILDREN_ON_MONIKERS}
}

/*
 *	BaudRateList
 */

BaudRateList = PrefItemGroup {
	initFileFlags = default +useItemMonikers, +numericMonikers;
	numSelections  = 1;
	selection = SB_300;
	moniker = "Baud Rate:";
	children =
		Baud300Entry,
		Baud600Entry,
		Baud1200Entry,
		Baud2400Entry,
		Baud4800Entry,
		Baud9600Entry,
		Baud19200Entry;
	hints = {
		ATTR_GEN_INIT_FILE_KEY {
			char	"baudRate",0
		}
	}

}


Baud300Entry = GenItem {
	moniker = "300";
	identifier = SB_300;
}

Baud600Entry = GenItem {
	moniker = "600";
	identifier = SB_600;
}

Baud1200Entry = GenItem {
	moniker = "1200";
	identifier = SB_1200;
}

Baud2400Entry = GenItem {
	moniker = "2400";
	identifier = SB_2400;
}

Baud4800Entry = GenItem {
	moniker = "4800";
	identifier = SB_4800;
}

Baud9600Entry = GenItem {
	moniker = "9600";
	identifier = SB_9600;
}

Baud19200Entry = GenItem {
	moniker = "19200";
	identifier = SB_19200;
}

/*
 *	ParityList
 */

ParityList = PrefItemGroup {
	initFileFlags = default +useItemStrings;
	numSelections  = 1;
	moniker = "Parity:";
	children = ParityEvenEntry, 
			ParityOddEntry, 
			ParitySpaceEntry, 
			ParityMarkEntry, 
			ParityNoneEntry;
	selection = "SP_NONE shl offset SF_PARITY";
	hints = {
		ATTR_GEN_INIT_FILE_KEY {
			char	"parity",0
		}
	}
}

ParityEvenEntry = PrefStringItem {
	moniker = "Even";
	identifier = "SP_EVEN shl offset SF_PARITY";
	initFileString = "even";
}

ParityOddEntry = PrefStringItem {
	moniker = "Odd";
	identifier = "SP_ODD shl offset SF_PARITY";
	initFileString = "odd";
}

ParitySpaceEntry = PrefStringItem {
	moniker = "Space";
	identifier = "SP_SPACE shl offset SF_PARITY";
	initFileString = "space";
}

ParityMarkEntry = PrefStringItem {
	moniker = "Mark";
	identifier = "SP_MARK shl offset SF_PARITY";
	initFileString = "mark";
}

ParityNoneEntry = PrefStringItem {
	moniker = "None";
	identifier = "SP_NONE shl offset SF_PARITY";
	initFileString = "none";
}

/*
 *	WordLengthList
 */

WordLengthList = PrefItemGroup {
	initFileFlags = default +useItemMonikers, +numericMonikers;
	numSelections  = 1;
	moniker = "Word Length:";
	children = WordLen5Entry, 
			WordLen6Entry, 
			WordLen7Entry, 
			WordLen8Entry;
	selection = "SL_8BITS";
	destination = process;
	hints = {
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word MSG_SERIAL_SET_WORD_LENGTH
		},
		ATTR_PREF_ITEM_GROUP_ENABLE {
			PrefEnableData <SL_5BITS,
					StopBits1_5Entry>
		},
		ATTR_PREF_ITEM_GROUP_ENABLE {
			PrefEnableData <SL_5BITS,
					StopBits2Entry,
					mask PEF_DISABLE_IF_SELECTED>
		},
		ATTR_GEN_INIT_FILE_KEY {
			char	"wordLength",0
		}

	}
}

WordLen5Entry = GenItem {
	moniker = "5";
	identifier = "SL_5BITS";
}

WordLen6Entry = GenItem {
	moniker = "6";
	identifier = "SL_6BITS";
}

WordLen7Entry = GenItem {
	moniker = "7";
	identifier = "SL_7BITS";
}

WordLen8Entry = GenItem {
	moniker = "8";
	identifier = "SL_8BITS";
}

/*
 *	StopBitsList
 */

StopBitsList = PrefItemGroup {
	initFileFlags = default +useItemMonikers;
	numSelections  = 1;
	moniker = "Stop Bits:";
	children = StopBits1Entry, StopBits1_5Entry, StopBits2Entry;
	selection = 0;
	hints = {
		ATTR_GEN_INIT_FILE_KEY {
			char	"stopBits",0
		}
	}

}

StopBits1Entry = GenItem {
	moniker = "1";
	identifier = SB_1;
}

StopBits1_5Entry = GenItem {
	moniker = "1.5";
	genStates = default -enabled;
	identifier = SB_1_5;
}

StopBits2Entry = GenItem {
	moniker = "2";
	identifier = SB_2;
}

/*
 *	HandshakeList
 */

HandshakeList = PrefItemGroup {
	initFileFlags = default +useItemStrings;
	moniker = "Handshake:";
	children = HandshakeNoneEntry,
		HandshakeHardwareEntry, 
		HandshakeSoftwareEntry; 
	hints = {
		ATTR_GEN_BOOLEAN_GROUP_STATUS_MSG {
			word MSG_SERIAL_SET_HANDSHAKE_MODE
		},
		ATTR_PREF_ITEM_GROUP_OVERRIDE {
			word 0
		},
		ATTR_GEN_INIT_FILE_KEY {
			char	"handshake",0
		},
		ATTR_PREF_ITEM_GROUP_ENABLE {
			PrefEnableData <mask SFC_HARDWARE,
					HardwareHandshakeGroup>
		}
	}
	destination = process;
	selection = 0;
	numSelections = 1;
	behaviorType = nonExclusive;	
}

HandshakeHardwareEntry = PrefStringItem {
	moniker = "Hardware";
	identifier = "mask SFC_HARDWARE";
	initFileString = "hardware";
}

HandshakeSoftwareEntry = PrefStringItem {
	moniker = "Software (XON/XOFF)";
	identifier = "mask SFC_SOFTWARE";
	initFileString = "software";
}

HandshakeNoneEntry = PrefStringItem {
	moniker = "None";
	identifier = 0;
	initFileString = "none";
}

/*
 *	HardwareHandshakeGroup
 */

HardwareHandshakeGroup = PrefInteraction {
	genStates = default -enabled;
	children = HardwareHandshakeTitle, StopRemoteList, StopLocalList;
	hints = {HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_ON_MONIKERS,
		HINT_DO_NOT_USE_MONIKER}
}

HardwareHandshakeTitle = GenText {
	genAttributes = readOnly;
	text = "Hardware Handshaking Options:";
}

StopRemoteList = PrefItemGroup {
	initFileFlags = default +useItemStrings;
	behaviorType = nonExclusive;
	moniker = "Stop Remote:";
	children = DTREntry, RTSEntry;
	destination = process;
	hints = {
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word MSG_SERIAL_SET_STOP_REMOTE
		},
		ATTR_GEN_INIT_FILE_KEY {
			char	"stopRemote",0
		}
	}
}

DTREntry = PrefStringItem {
	moniker = "DTR";
	identifier = "mask SMC_DTR";
	initFileString = "dtr";
}

RTSEntry = PrefStringItem {
	moniker = "RTS";
	identifier = "mask SMC_RTS";
	initFileString = "rts";
}

StopLocalList = PrefItemGroup {
	initFileFlags = default +useItemStrings;
	behaviorType = nonExclusive;
	moniker = "Stop Local:";
	children = CTSEntry, DCDEntry, DSREntry;
	hints = {
		ATTR_GEN_INIT_FILE_KEY {
			char	"stopLocal",0
		}
	}
}

CTSEntry = PrefStringItem {
	moniker = "CTS";
	identifier = "mask SMS_CTS";
	initFileString = "cts";
}

DCDEntry = PrefStringItem {
	moniker = "DCD";
	identifier = "mask SMS_DCD";
	initFileString = "dcd";
}

DSREntry = PrefStringItem {
	moniker = "DSR";
	identifier = "mask SMS_DSR";
	initFileString = "dsr";
}

end	SerialUI;
