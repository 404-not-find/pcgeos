;	$Id: h_padd.asm,v 1.1 97/04/07 12:04:03 newdeal Exp $
;[]-----------------------------------------------------------------[]
;|      H_PADD.ASM -- long pointer routines                          |
;[]-----------------------------------------------------------------[]

;
;       C/C++ Run Time Library - Version 5.0
; 
;       Copyright (c) 1987, 1992 by Borland International
;       All Rights Reserved.
; 

                INCLUDE rules.asi

; calls to these routines are generated by the compiler to perform
; arithmetic operations on huge pointers.

_TEXT           SEGMENT BYTE PUBLIC 'CODE'
                ASSUME  CS:_TEXT

                public  PADD@
                public  F_PADD@
                public  N_PADD@

N_PADD@		proc	near
		call	F_PADD@
		ret
N_PADD@		endp
PADD@		proc	far
		.fall_thru
PADD@		endp

F_PADD@		proc	far
                or      cx,cx
                jge     add1
                not     bx
                not     cx
                add     bx,1
                adc     cx,0
                jmp     sub1
add1 label near
                add     ax,bx
                jnc     addok
                add     dx,1000h
addok:
                mov     ch,cl
                mov     cl,4
                shl     ch,cl
                add     dh,ch
                mov     ch,al
                shr     ax,cl
                add     dx,ax
                mov     al,ch
                and     ax,0fh
                ret
F_PADD@		endp

                public  PSUB@
                public  F_PSUB@
                public  N_PSUB@

N_PSUB@		proc	near
		call	F_PSUB@
		ret
N_PSUB@		endp

PSUB@		proc	far
		.fall_thru
PSUB@		endp

F_PSUB@		proc	far
                or      cx,cx
                jge     sub1
                not     bx
                not     cx
                add     bx,1
                adc     cx,0
                jmp     add1
sub1 label near
                sub     ax,bx
                jnc     subok
                sub     dx,1000h
subok:
                mov     bh,cl
                mov     cl,4
                shl     bh,cl
                xor     bl,bl
                sub     dx,bx
                mov     ch,al
                shr     ax,cl
                add     dx,ax
                mov     al,ch
                and     ax,0fh
                ret
F_PSUB@		endp

_TEXT           ENDS


