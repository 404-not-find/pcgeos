/***********************************************************************
 *
 *	Copyright (c) Geoworks 1994 -- All Rights Reserved
 *
 * PROJECT:	  Interface Gadgets
 * MODULE:	  Date Input Gadget
 * FILE:	  uiDateInput.ui
 *
 * AUTHOR:  	  Skarpi Hedinsson, Jul  1, 1994
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	SH	7/ 1/94   	Initial version
 * jfh	8/30/04	port to GOC
 *
 * DESCRIPTION:
 *	
 *
 ***********************************************************************/
@include <Objects\cgadget.goh>

@start DateInputUI, notDetachable;

/*---------------------------------------------------------------------
		Art for DateInput - same as Time
---------------------------------------------------------------------*/
@include "Art\mkrDate.goh"

@object GenInteractionClass DateInputGroup = {
	GI_comp = @DIText, @DateInputTriggerGroup;
	GI_states = @default & ~GS_USABLE;
	HINT_CUSTOM_CHILD_SPACING = (SST_PIXELS | 1);
	HINT_ORIENT_CHILDREN_HORIZONTALLY;
}

@object DateInputTextClass DIText = {
   GTXI_text = "temp";
	GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT | GTA_NO_WORD_WRAPPING
					| GTA_DONT_SCROLL_TO_CHANGES;
	GTXI_maxLength = 10;
	GTXI_destination = (TO_OBJ_BLOCK_OUTPUT);
	GTXI_applyMsg = MSG_META_TEXT_LOST_FOCUS;
	HINT_DO_NOT_USE_MONIKER;
	HINT_INITIAL_SIZE = {SST_AVG_CHAR_WIDTHS | 9, SST_PIXELS | 20};
	HINT_MINIMUM_SIZE = {SST_AVG_CHAR_WIDTHS | 9, SST_PIXELS | 20};
}

@object GenInteractionClass DateInputTriggerGroup = {
	GI_comp = @DateInputIncTrigger, @DateInputDecTrigger;
	HINT_MINIMIZE_CHILD_SPACING;
	HINT_DIVIDE_HEIGHT_EQUALLY;
}

@object RepeatTriggerClass DateInputIncTrigger = {
	GI_visMoniker = list {@DatePlusMoniker};
	GTI_destination = (TO_OBJ_BLOCK_OUTPUT);
	GTI_actionMsg = MSG_DI_DATE_INC;
	ATTR_GEN_DESTINATION_CLASS = {(ClassStruct *)&DateInputClass};
	HINT_CENTER_MONIKER;
	HINT_REPEAT_TRIGGER_NOT_FOCUSABLE;
	HINT_FIXED_SIZE = {SST_PIXELS | 10, 0};
}

@object RepeatTriggerClass DateInputDecTrigger = {
	GI_visMoniker = list {@DateMinusMoniker};
	GTI_destination = (TO_OBJ_BLOCK_OUTPUT);
	GTI_actionMsg = MSG_DI_DATE_DEC;
	ATTR_GEN_DESTINATION_CLASS = {(ClassStruct *)&DateInputClass};
	HINT_CENTER_MONIKER;
	HINT_REPEAT_TRIGGER_NOT_FOCUSABLE;
	HINT_FIXED_SIZE = {SST_PIXELS | 10, 0};
}

@end	DateInputUI;


/*
 * Strings for controllers
 */

/*@start ControlStrings, data;
	@chunk char DateInputName[] = "Date Input";
@end ControlStrings;

*/

