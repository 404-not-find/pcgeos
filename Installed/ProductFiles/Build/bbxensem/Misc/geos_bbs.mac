; GeoComm Script
; for Geoworks BBS
;
CLEAR
PRINT  "  Logging on to", CR
PRINT  "  the Geoworks BBS.", CR
;
; If your modem is not on COM 2,
; change the PORT number below appropriately
;
PORT  2
;
; If your modem is capable of a faster baud rate,
; change the value below as appropriate
;
COMM  2400-8-N-1-FULL
TERM  ANSI
;
:retry
PRINT  "  Dialing.", CR
;
; If you live in the 510 area code, delete "1-510" below
;
DIAL  "1-510-814-4262"
MATCH  "BUSY"  GOTO  retry
MATCH  "NO"  GOTO  retry
MATCH  "CONNECT"  GOTO  connect
PROMPT  2400
PRINT  "Unable to dial, check your modem connection.", CR
GOTO  done
:connect
PRINT  "  Connected to the", CR
PRINT  "  Geoworks BBS", CR

MATCH  "first name?" GOTO  first
PROMPT  1800
PRINT  "  Time out on first name.", CR
GOTO  done
:first
PRINT  "  Sending first name.", CR
PAUSE
;
; Fill in your first name below.
;
SEND  "firstname",CR

MATCH  "last name?" GOTO  last
PROMPT  600
PRINT  "TIMEOUT ON LAST NAME",CR
GOTO  done
:last
PRINT  "  Sending last name.", CR
PAUSE
;
; Fill in your last name below.
;
SEND  "lastname", CR

MATCH  "Password?" GOTO pword
PROMPT  600
PRINT  "Time out on password.", CR
GOTO  done
:pword
PRINT  "  Sending password.", CR
PAUSE
;
; Fill in  your password below.
;
SEND  "mypassword",CR

:done
BELL
PRINT  "Finished running script.", CR
PRINT  "Please click on the Close button.", CR
END

:ABORT
PRINT  "Hanging up.", CR
SEND  "+++"
MATCH  "OK" GOTO shup
MATCH  "NO CARRIER" GOTO done
PROMPT  600
PRINT  "Couldn't disconnect the modem", CR
GOTO  done
:shup
SEND  "ATH0", CR
MATCH  "OK"  GOTO  hangup
PROMPT  600
PRINT  "Couldn't hang up.", CR
GOTO  done
:hangup
PRINT  "Hung up.", CR
GOTO  done
