#!/bin/sh -
##############################################################################
#
# 	Copyright (c) GeoWorks 1993 -- All Rights Reserved
#
# PROJECT:	Swat
# MODULE:	Fatal Error handling
# FILE: 	extract_explanations
# AUTHOR: 	Adam de Boor, Feb 10, 1993
#
# REVISION HISTORY:
#	Name	Date		Description
#	----	----		-----------
#	ardeb	2/10/93		Initial Revision
#
# DESCRIPTION:
#	Script to extract fatal-error explanations from any .def file in the
#	passed directory. Result is spewed to standard output to be placed in
#	a <geodename>.fei file someplace.
#
# USAGE:
#	extract_explanations <geodename> <dir>
#
#	$Id: extract_explanations,v 1.1 93/02/11 16:10:37 adam Exp $
#
###############################################################################

sed -n -e '
:top
/^[ 	]*[^; 	][^; 	]*[ 	][ 	]*enum[ 	][ 	]*FatalErrors/{
s/^[ 	]*\([^ 	][^ 	]*\)[ 	].*$/table enter $fatalerr_info_table '$1'::\1 {/
h
n
/^;/!btop
:loop
s/^;//
H
n
/^;/bloop
x
s/{\n/{/
s/$/}/p
x
btop
}' $2/*.def
