/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  digitalClock.ui
 * FILE:	  digitalClock.ui
 *
 * AUTHOR:  	  Adam de Boor: Feb  2, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	2/ 2/92	  ardeb	    Initial version
 *
 * DESCRIPTION:
 *	Definitions for standard digital clock.
 *
 *
 * 	$Id: digitalClock.ui,v 1.1 97/04/04 14:51:08 newdeal Exp $
 *
 ***********************************************************************/

class	VisDigitalClock = VisClock {
	size = wordComp : 18;		/* Pointsize for display */
	format = wordComp : DTF_HM;	/* Selected format */
	display = wordComp : DTF_HM;	/* Displayed format */
	static /* width */ wordComp : 0;
	static /* height */ wordComp : 0;
	colors = typeComp "ColorQuad" : "C_LIGHT_RED, CF_INDEX>, <C_BLACK, CF_INDEX";
	static /* buffer */ byteComp : "DATE_TIME_BUFFER_SIZE dup(0)";

	default selectedPart = "VDCC_TEXT";
	default numParts = "VisDigitalClockColor";
	default colorsPtr = "offset VDCI_colors";
}

start	DigitalUIResource;

DigitalClock = VisDigitalClock {
	region = data {
		word	PARAM_1-1,	EOREGREC
		; rounded top edge
		word	PARAM_1+0,	PARAM_0+4, PARAM_2-5, EOREGREC
		word	PARAM_1+1,	PARAM_0+2, PARAM_2-3, EOREGREC
		word	PARAM_1+3,	PARAM_0+1, PARAM_2-2, EOREGREC

		; central portion
		word	PARAM_3-5,	PARAM_0,   PARAM_2-1, EOREGREC

		; rounded bottom edge
		word	PARAM_3-3,	PARAM_0+1, PARAM_2-2, EOREGREC
		word	PARAM_3-2,	PARAM_0+2, PARAM_2-3, EOREGREC
		word	PARAM_3-1,	PARAM_0+4, PARAM_2-5, EOREGREC
		word	EOREGREC
	}
	optionTree = DigitalOptions;
#if DO_DBCS
	category = DigitalCat;
#else
	category = "digital clock";
#endif
	firstPartString = DigitalTextString;
}

#if DO_DBCS
chunk DigitalCat = data {
	char	'digital clock',0
}
#endif

DigitalOptions = GenInteraction {
	genStates = default -usable;
	children =
		DigitalClockSizeSpin,
		DigitalFormatsList;
	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY {
			char	"digital clock", 0
		},
		HINT_CENTER_CHILDREN_ON_MONIKERS,
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN
	}
}

DigitalClockSizeSpin = GenValue {
	moniker = "Size:";
	value = "DIGITAL_DEFAULT_SIZE";
	minimum = "DIGITAL_MIN_SIZE";
	maximum = "DIGITAL_MAX_SIZE";
	increment = 1;
	applyMsg = MSG_VDC_SET_SIZE;
	destination = DigitalClock;
	displayFormat = points;
	hints = {
		ATTR_GEN_INIT_FILE_KEY {
			char	"size", 0
		}
	}
}

DigitalFormatsList = GenItemGroup {
	moniker = "Format:";
	applyMsg = MSG_VDC_SET_FORMAT;
	destination = DigitalClock;
	children =
		Format12Hour,
		Format24Hour;
	hints = {
		ATTR_GEN_INIT_FILE_KEY {
			char	"format", 0
		},
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
	selection = DTF_HM;
}

Format12Hour = GenItem {
	moniker = "12-hour";
	identifier = "DTF_HM";
}

Format24Hour = GenItem {
	moniker = "24-hour";
	identifier = "DTF_HM_24HOUR";
}

chunk DigitalTextString = "Text";
chunk DigitalBackgroundString = "Background";
end	DigitalUIResource;
