/*
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	PC GEOS 
 * MODULE:	
 * FILE:	cuiNDPrinter.ui
 *
 * AUTHOR:	Chris Boyke
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	cdb	8/25/92   	Initial version.
 *
 * DESCRIPTION:
 *
 *	Instance of a printer object -- kept around all the time, so
 *	we can field printing requests.
 *
 * 	$Id: cuiNDPrinter.ui,v 1.2 98/06/03 13:16:48 joon Exp $
 *
 */

/**************************************************************************
 	The printer's "dummy" object goes in the same resource as the
	default dummy
 **************************************************************************/ 

start DummyResource;
NDPrinterDummy = NDPrinter {
	shellObjectAttrs = default -movable, -copyable, -deletable;
	ndObjType = "WOT_PRINTER";
	popUpType = WPUT_SELECTION;
	hints = {
		ATTR_SHELL_OBJECT_ACCEPT_TRANSFERS {
			word WOT_DOCUMENT
		},
		ATTR_FOLDER_HELP_CONTEXT { char "oPrnt", 0 }
	}
}



/**************************************************************************
	For no better place to put it, here lies the help context 
	of the print control panel. 
	(used by the Assistance Window in entry level) 
 **************************************************************************/ 

#if _NEWDESKBA
chunk PrintControlHelpContext 	= "aPrtQ";
#endif

end DummyResource;

start	NDPrinterOptionsUI, notDetachable;

NDPrinterOptionsDialog = NEWDESK_INTERACTION {
	moniker = 'P', "Printer Options";
	type = properties;
	visibility = dialog;
	attributes = default +modal, +notUserInitiatable;
	children = 
#if _NEWDESKBA
			NDPrinterList,
			NDPrinterBannerInteraction;
#else
			NDPrinterList;
#endif
	hints = {
#if _NEWDESKBA
		ATTR_EL_HELP_CONTEXT { char "aPrtQ", 0 },
#endif		
		HINT_FIXED_SIZE {
			SpecWidth < SST_PCT_OF_FIELD_WIDTH, PCT_40 >
			SpecHeight < SST_PCT_OF_FIELD_HEIGHT, PCT_40 >
		},
		ATTR_GEN_HELP_CONTEXT {
			char "dPrintOpt",0
		},
		HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}

NDPrinterList = GenDynamicList {
	moniker = "Select your default printer:";
	queryMsg = MSG_ND_PRINTER_REQUEST_ITEM_MONIKER;
	destination = NDPrinterDummy;
	applyMsg = MSG_ND_PRINTER_SET_PRINTER;
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_PLACE_MONIKER_ABOVE,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 60>
			SpecHeight <SST_LINES_OF_TEXT, 3>
			word	3
		}
	}
}

#if _NEWDESKBA
NDPrinterBannerInteraction = GenInteraction {
	moniker = "Banner";
	children = NDPrinterBannerOnOff, NDPrinterBannerText;
	hints = {
		HINT_LEFT_JUSTIFY_CHILDREN
	}
}

NDPrinterBannerOnOff = GenItemGroup {
	applyMsg = MSG_ND_PRINTER_SET_BANNER_ON_OFF;
	destination = NDPrinterDummy;
	numSelections = 1;
	selection = TRUE;
	children = NDPrinterBannerOn, NDPrinterBannerOff;
	hints = {
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word MSG_ND_PRINTER_BANNER_STATUS
		}
	}
}

NDPrinterBannerOn = GenItem {
	moniker = "On";
	identifier = TRUE;
}

NDPrinterBannerOff = GenItem {
	moniker = "Off";
	identifier = FALSE;
}
	
NDPrinterBannerText = GenText {
	textDisplayAttributes = default +singleLineText;
	maxLength = "size NetBannerText-2";
	moniker = "Banner text:";
	destination = NDPrinterDummy;
	applyMsg = MSG_ND_PRINTER_SET_BANNER_TEXT;
}
#endif

end NDPrinterOptionsUI;




