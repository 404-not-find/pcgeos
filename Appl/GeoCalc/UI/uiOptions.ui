/***********************************************************************
 *	Copyright (c) GeoWorks 1991 -- All Rights Reserved
 *
 * Project:	PC GEOS
 * Module:	
 * File:	uiOptions.ui
 *
 * Author:	John Wedgwood, Apr 29, 1991
 *
 * Revision History:
 *	Name	Date		Description
 *	----	----		-----------
 *	John	 4/29/91	Initial revision
 *
 * Description:
 *	Options menu for GeoCalc
 *
 *	$Id: uiOptions.ui,v 1.1 97/04/04 15:48:43 newdeal Exp $
 *
 ***********************************************************************/

start	Interface;

OptionsMenu = GenInteraction {
#if _TOOL_BAR

	children =

#if _LEVELS
		SetUserLevelDialog,
#endif
		OptionsGroup
		,GCToolControl
#if _LEVELS
		,SaveResetGroup
#endif
		;


#else					
	children =
#if _LEVELS
		SetUserLevelDialog,
#endif					/* if _LEVELS */
		OptionsGroup
#if _LEVELS
		,SaveResetGroup
#endif					/* if _LEVELS */
		;

#endif					/* if TOOL_BAR */
	visibility = popup;
	hints = {
		ATTR_GEN_INTERACTION_GROUP_TYPE
			{ GenInteractionGroupType GIGT_OPTIONS_MENU }
	}
}

end Interface;

start OptionsMenuUI;

OptionsGroup = GenInteraction {
	children =
#if _TOOL_BAR
		ShowToolsPopup,
#endif
		RulerSubMenu,
#if _CHARTS
		GCInstructionPopup,
#endif  				/* if _CHARTS */
		GCOptionsFoo,
		GCRecalcControl;
}

#if _LEVELS
SaveResetGroup = GenInteraction {
	children = SaveOptionsTrigger, ResetOptionsTrigger;
}
#endif

/* --- */

#if _TOOL_BAR
ShowToolsPopup = GenInteraction {
/* We want this for NIKE */
/* #ifndef	PRODUCT_NIKE */
	moniker = 'S', "Show Tools";
	visibility = popup;
/* #endif */
	children = ShowBarList;
}
#endif					/* if _TOOL_BAR */

/*****************************************************************************
 *	Ruler stuff
 *****************************************************************************/
RulerSubMenu = GenInteraction {
	moniker = RulerSubMenuPopupMoniker;
	children =
		GCRulerShowControl,
		GCRulerTypeControl;
	visibility = popup;
}

visMoniker RulerSubMenuPopupMoniker = 'R', "Rulers";

GCRulerTypeControl = RulerTypeControl {
	hints = {
		ATTR_GEN_CONTROL_REQUIRE_UI {
			word mask RTCF_SPREADSHEET
		},
		ATTR_GEN_CONTROL_REQUIRE_TOOLBOX_UI {
			word mask RTCTF_SPREADSHEET
		}
	}
}

GCRulerShowControl = RulerShowControl {
	message = "MSG_GEOCALC_DISPLAY_UPDATE_RULERS";
	hints = {
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word mask RSCCF_SHOW_VERTICAL or \
			     mask RSCCF_SHOW_HORIZONTAL
		},
		ATTR_GEN_CONTROL_REQUIRE_UI {
			word mask RSCCF_SHOW_RULERS
		}
	}
}

#if _CHARTS
GCInstructionPopup = GenInteraction {
	moniker = 'I', "Instructions";
	visibility = popup;
	children = GCInstructionControl;
}
GCInstructionControl = GrObjInstructionControl {
}
#endif	/* if _CHARTS */

/*****************************************************************************
 *	Spreadsheet Options
 *****************************************************************************/
GCOptionsFoo = GenInteraction {
	moniker = 'O', "Other Settings";
	visibility = popup;
	children =
		GCSSOptionsControl;
}

GCSSOptionsControl = SSOptionsControl {
	hints = {
	    HINT_GEN_CONTROL_SCALABLE_UI_DATA { GenControlScalableUIEntry <
		GCSUIC_SET_NORMAL_FEATURES_IF_APP_LEVEL,
		UIIL_INTRODUCTORY,
		0>,<
		GCSUIC_ADD_NORMAL_FEATURES_IF_APP_FEATURE_ON,
		mask GCF_SIMPLE_OPTIONS,
		mask SSOCF_DRAW_GRID or mask SSOCF_SHOW_FORMULAS>,<
		GCSUIC_ADD_NORMAL_FEATURES_IF_APP_FEATURE_ON,
		mask GCF_PAGE_SETUP,
		mask SSOCF_DRAW_HEADER_FOOTER_BUTTON>,<
		GCSUIC_ADD_NORMAL_FEATURES_IF_APP_FEATURE_ON,
		mask GCF_NOTES,
		mask SSOCF_DRAW_NOTE_BUTTON>}
	}
}

/*****************************************************************************
 *	"Save Options" & "Reset Options"
 *****************************************************************************/
#if _LEVELS
SaveOptionsTrigger = GenTrigger {
	SAVE_OPTIONS_TRIGGER_VALUES
	action = MSG_META_SAVE_OPTIONS, GCAppObj;
}

ResetOptionsTrigger = GenTrigger {
	RESET_OPTIONS_TRIGGER_VALUES
	action = MSG_GEOCALC_APPLICATION_QUERY_RESET_OPTIONS, GCAppObj;
}
#endif					/* if _LEVELS */

/****************************************************************************
 *	"Calculate Now" and "Calculation Order" DB
 ***************************************************************************/

GCRecalcControl = SSRecalcControl {
}

end OptionsMenuUI;

