/*
 * Project:	Studio
 * File:	UI/uiDocument.ui
 *
 * Author:	Paul DuBois   12 August 1994
 *
 *	This file contains the user interface description for the
 * platform emulation dialog box.
 *
 *	$Id: uiPlatform.ui,v 1.1 97/04/04 14:40:32 newdeal Exp $
 *
 */

start FileMenuUI;

StudioPlatformDialog = GenInteraction {
    genAttributes = default +notifyVisibility;
    moniker = 'E', "Platform Emulation";
    children = EmulationItemGroup, EmulationBooleanGroup,
    		CustomValueGroup, PlatformApplyTrigger;
    visibility = dialog;
    type = properties;
    hints = {
	ATTR_GEN_VISIBILITY_DATA { word 0, mask GV_PLATFORM_EMULATION },
	ATTR_GEN_HELP_CONTEXT { char "dbPlatforms", 0 }
    }
}

end FileMenuUI;

start PlatformUI;

EmulationBooleanGroup = GenBooleanGroup vardataReloc {
    children = ToolbarBoolean;
    destination = "TO_APP_MODEL";
}

ToolbarBoolean = GenBoolean {
    moniker = "Account for Toolbox and Title Bar";
    identifier = "mask PES_TOOLBOX";
}

EmulationItemGroup = GenItemGroup vardataReloc {
    behaviorType = exclusive;
    moniker = "Emulation:";
    children = StudioNoEmulation,
    		StudioZoomerEmulation,
    		StudioPT9000Emulation,
    		StudioDesktopEmulation,
		StudioCustomEmulation;
    destination = "TO_APP_MODEL";
    applyMsg = MSG_STUDIO_DOCUMENT_SET_PLATFORM;
    hints = {
	ATTR_GEN_SEND_APPLY_MSG_ON_APPLY_EVEN_IF_NOT_MODIFIED,
	ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass },
	ATTR_GEN_ITEM_GROUP_STATUS_MSG {
	    word MSG_STUDIO_DOCUMENT_PLATFORM_STATUS
	},
	HINT_ORIENT_CHILDREN_VERTICALLY
    }
}

StudioNoEmulation = GenItem {
    moniker = 'N', "None";
    identifier = "PET_NONE";
}
StudioDesktopEmulation = GenItem {
    moniker = 'D', "Desktop";
    identifier = "PET_DESKTOP";
}
StudioCustomEmulation = GenItem {
    moniker = 'C', "Custom";
    identifier = "PET_CUSTOM";
}
StudioZoomerEmulation = GenItem {
    moniker = 'Z', "Casio Z-7000, Tandy Z-PDA, and AST GridPad 2390";
    identifier = "PET_ZOOMER";
}
StudioPT9000Emulation = GenItem {
    moniker = 'S', "Sharp PT-9000";
    identifier = "PET_PT9000";
}

CustomValueGroup = GenInteraction {
    children = CustomWidthValue, CustomHeightValue;
    hints = {
	HINT_ORIENT_CHILDREN_VERTICALLY,
	HINT_CENTER_CHILDREN_ON_MONIKERS,
	HINT_LEFT_JUSTIFY_MONIKERS
    }
}
CustomWidthValue = GenValue {
    genStates = default -enabled;
    moniker = "Custom Width:";
    displayFormat = points;
/*  value = ;     inital value controlled by document */
}
CustomHeightValue = GenValue {
    genStates = default -enabled;
    moniker = "Custom Height:";
    displayFormat = points;
/*  value = ;     inital value controlled by document */
}

PlatformApplyTrigger = GenTrigger vardataReloc {
	action = MSG_STUDIO_DOCUMENT_SET_PLATFORM, "TO_APP_MODEL";
	genAttributes = default +signalInteractionComplete;
	hints = {
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND { word IC_APPLY },
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_DESTINATION_CLASS { UN_DD StudioDocumentClass }	
	}
}

end PlatformUI;
