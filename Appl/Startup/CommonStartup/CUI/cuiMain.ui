/*
 *
 * project:	PC GEOS
 * Module:	Startup -- UI description
 * File:	cuiMain.ui
 *
 * Author:	Doug Fults
 *
 *	This file contains the user Interface description for this
 * application.
 *
 *	$Id: cuiMain.ui,v 1.1 97/04/04 16:52:24 newdeal Exp $
 *
 */

#include "generic.uih"
#include "product.uih"

#ifndef _NEW_DEAL
#ifdef NEW_DEAL
       #define _NEW_DEAL -1
#else
       #define _NEW_DEAL 0
#endif
#endif

class StartupField = GenField {
}

class StartupApplication = GenApplication {
	returnField = optrComp : null;
}

class StartupPrimary = GenPrimary {
}

class StartupRoomTrigger = GenTrigger, master {
	title = nptrComp: null;
	pictureNumber = byteComp : 0;
}

#ifdef ISTARTUP

class IStartupField = StartupField {
}

#include "uiQuiz.ui"
#endif


/* ----------------------------------------------------------------------------
	Canned monikers
-----------------------------------------------------------------------------*/

					/* Overview monikers */
start OverviewRoomLCMonikerResource, data;
#ifdef WELCOME
/* No longer used - Don 9/23/99 */
/* #include "../CArt/mkrConsumerUI.ui" */
/* #include "../CArt/mkrStandardUI.ui" */
/* #include "../CArt/mkrHelpButton.ui" */
/* #include "../CArt/mkrShutDownButton.ui" */
#endif

#ifdef ISTARTUP
#include "../CArt/mkrCAssisted.ui"
#include "../CArt/mkrCSelfGuided.ui"
#include "../CArt/mkrCPrimary.ui"
#endif

end OverviewRoomLCMonikerResource;

start OverviewRoomLMMonikerResource, data;

#ifdef ISTARTUP
#include "../CArt/mkrBWAssisted.ui"
#include "../CArt/mkrBWSelfGuided.ui"
#include "../CArt/mkrBWPrimary.ui"
#endif
end OverviewRoomLMMonikerResource;

start OverviewRoomLCGAMonikerResource, data;

#ifdef ISTARTUP
#include "../CArt/mkrCGAAssisted.ui"
#include "../CArt/mkrCGASelfGuided.ui"
#include "../CArt/mkrCGAPrimary.ui"
#endif

end OverviewRoomLCGAMonikerResource;

#ifdef MOUSETUTORIAL
#include "../CArt/mkrMouseTutIcon.ui"
#endif

/* ----------------------------------------------------------------------------
	App obj
-----------------------------------------------------------------------------*/
start	AppResource;

#ifdef WELCOME
StartupApp = StartupApplication {
	children =
		StartupWindow;
	moniker = list {
		Text
	}
}
visMoniker Text = "Choose UI";
#endif

#ifdef ISTARTUP

StartupApp = StartupApplication {
	children =
		StartupWindow, StartupHelp, AlertInteraction,
		AutologInter, QuestionEditDialog, UpgradeInteraction;
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST) = StartupHelp;
	moniker = list {
		Text
	}
	hints = {
		ATTR_GEN_HELP_CONTEXT {
			char "dLevel", 0
		},
		ATTR_GEN_HELP_FILE {
			char "e help t", 0
		},
		ATTR_GEN_HELP_TYPE {byte HT_SIMPLE_HELP}
	}
}
visMoniker Text = "Startup";

StartupHelp = HelpControl ignoreDirty{
	moniker = "Help";
	helpType = "HT_SIMPLE_HELP";
	attributes = modal;
}

chunk DefaultPrinterQueue = "";

#endif
end	AppResource;



/* ----------------------------------------------------------------------------
	 Overview Resource & Primaries
		-- Keep everything in this resource that will be loaded
		   when Welcome is initialized
-----------------------------------------------------------------------------*/

/* notDetachable == NOT SAVED TO STATE */

start OverviewRoomResource, notDetachable;

#ifdef ISTARTUP

/**************************************************************************
 *	UI For displaying incoming messages
 **************************************************************************/ 

AlertInteraction = GenInteraction {
	moniker = "A Message has arrived";
	type = notification;
	visibility = dialog;
	attributes = default +sysModal, -modal;

	children = 	AlertText,
			AlertOKTrigger;

}

AlertText = GenText {
	genAttributes = default +readOnly;
	textDisplayAttributes = default +tailOriented, +initScrolling;

	hints = {
		HINT_DRAW_IN_BOX,
		HINT_TEXT_FRAME,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 60>
			SpecHeight <>
		}
	}
}

AlertOKTrigger = GenTrigger {
	genAttributes = default +signalInteractionComplete;
	moniker = "OK";
	hints = {
		HINT_DEFAULT_DEFAULT_ACTION,
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word	IC_OK
		}
	}
	action = MSG_STARTUP_CLOSE_ALERT_INTERACTION, process;
}

#endif

/******************************
 * All the children of the app
 ******************************/
StartupWindow = StartupPrimary {
#ifdef WELCOME
	children = RoomGroup;
#endif
#ifdef ISTARTUP
	moniker = "Choose Your Work Area";
	attributes = default -userDismissable;
#ifdef MOUSETUTORIAL
	children = RoomGroup, MouseTutInter, InstructionLine, ReplyBarInter;
#else
	children = RoomGroup, InstructionLine, ReplyBarInter;
#endif /* MOUSETUTORIAL */
#endif /* ISTARTUP */
	genStates = default -usable;
	hints = {
		ATTR_GEN_DISPLAY_NOT_MINIMIZABLE,
		ATTR_GEN_DISPLAY_NOT_RESTORABLE,
		HINT_CENTER_CHILDREN_HORIZONTALLY,	
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_DEFAULT_FOCUS,
		HINT_DEFAULT_TARGET,
#ifdef ISTARTUP
		HINT_GENERAL_CONSUMER_MODE {
		     GeneralConsumerModeFlags <>
		},
		ATTR_GEN_DISPLAY_MAXIMIZED_STATE,
#endif
#ifdef WELCOME
		ATTR_GEN_DISPLAY_NOT_MAXIMIZABLE,
		ATTR_GEN_WINDOW_CUSTOM_WINDOW,
		HINT_SIZE_WINDOW_AS_DESIRED,
		HINT_CENTER_WINDOW,
#endif
		HINT_PRIMARY_NO_FILE_MENU
	}
}


/******************************
 * Everything that will come in
 ******************************/


RoomGroup = GenInteraction {
#if WELCOME
	children = Room1, Room3, HelpButton, ShutDownButton;
#else
	children = Room1, Room2, Room3;
#endif
	hints = {
#if WELCOME
		HINT_FIXED_SIZE {
			SpecWidth <SST_PIXELS, 640-4>
			SpecHeight <SST_PIXELS, 440-4>
		},
#else
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_ORIENT_CHILDREN_HORIZONTALLY
#endif
	}
}

#ifdef WELCOME
Room1 = GenTrigger {
/* No longer used - Don 9/23/99 */
/*	moniker = list { ConsumerUIMoniker } */
#else
Room1 = StartupRoomTrigger {
	title = "Entry";
	kbdAccelerator = control 'e';
	pictureNumber = PN_ROOM_1;
#endif
	genAttributes = initiatesInputIgnore;
	action = MSG_STARTUP_OPEN_FIELD, process;
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	handle	Room1Field
			word	offset	Room1Field
		},
#ifdef WELCOME
		ATTR_GEN_POSITION {
			Point	<171, 207>
		},
#endif
#ifdef ISTARTUP
		HINT_ENSURE_TEMPORARY_DEFAULT,
#endif
		HINT_DRAW_SHORTCUT_BELOW
	}
}

#ifndef WELCOME
Room2 = StartupRoomTrigger {
#ifdef WELCOME
	title = "Beginner";
	kbdAccelerator = control '2';
#endif
#ifdef ISTARTUP
	title = "Guided";
	kbdAccelerator = control 'g';
#endif
	pictureNumber = PN_ROOM_2;
	genAttributes = initiatesInputIgnore;
	action = MSG_STARTUP_OPEN_FIELD, process;
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	handle	Room2Field
			word	offset	Room2Field
		},
#ifdef ISTARTUP
		HINT_ENSURE_TEMPORARY_DEFAULT,
#endif
		HINT_DRAW_SHORTCUT_BELOW
	}
}
#endif /* !WELCOME */

#ifdef WELCOME
Room3 = GenTrigger {
/* No longer used - Don 9/23/99 */
/*	moniker = list { StandardUIMoniker } */
#else
Room3 = StartupRoomTrigger {
	title = "Unguided";
	kbdAccelerator = control 'u';
	pictureNumber = PN_ROOM_3;
#endif
	genAttributes = initiatesInputIgnore;
	action = MSG_STARTUP_OPEN_FIELD, process;
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	handle	Room3Field
			word	offset	Room3Field
		},
#ifdef WELCOME
		ATTR_GEN_POSITION {
			Point	<171, 301>
		},
#endif
#ifdef ISTARTUP
		HINT_ENSURE_TEMPORARY_DEFAULT,
#endif
		HINT_DRAW_SHORTCUT_BELOW
	}
}

#ifdef WELCOME
HelpButton = GenTrigger {
/* No longer used - Don 9/23/99 */
/*	moniker = list { HelpButtonMoniker } */
        action = MSG_META_BRING_UP_HELP, "TO_SELF";
	hints = {
		ATTR_GEN_POSITION {
			Point <26, 375>
		},
		ATTR_GEN_HELP_CONTEXT {
			char "TOC", 0
		}
	}
}

ShutDownButton = GenTrigger {
/* No longer used - Don 9/23/99 */
/*	moniker = list { ShutDownButtonMoniker } */
	action = MSG_META_QUIT, StartupApp;
	hints = {
		ATTR_GEN_POSITION {
			Point <575, 375>
		}
	}
}
#endif

end OverviewRoomResource;

/*-----------------------------------------------------------------------------
	Welcome Demo dialog
-----------------------------------------------------------------------------*/

#ifdef PRODUCT_WIN_DEMO
start WelcomeResource, notDetachable;

/* added for the WinDemo version */
WelcomeDialog = GenInteraction {
	/* Add spaces to end of string so last character isn't cut off */
	moniker = "Welcome to the GlobalPC Demonstration ";
	children = WelcomeText;
	genStates = default -usable;
	type = notification;
	visibility = dialog;
	attributes = default +sysModal, +initiatedViaUserDoDialog;
	hints = {
		HINT_CUSTOM_EXTRA_MARGINS {
			Rectangle <10, 10, 10, 10>
		}
	}
}

WelcomeText = GenText {
	genAttributes = default +readOnly;
	text = "Thank you for installing the GlobalPC Demonstration Software Program.\\\r\\\rBecause this program is intended for demonstration purposes only, some functionality is limited.\\\r\\\rPlease visit our web site or call for more information about the GlobalPC:\\\rwww.GlobalPC.com\\\r1-877-MYTURN1";
	hints = {
		ATTR_GEN_TEXT_DEFAULT_PARA_ATTR {
			word DEF_PARA_ATTR_CENTER
		},
		ATTR_GEN_TEXT_NEVER_MAKE_SCROLLABLE
	}
}

end WelcomeResource;
#endif

start StaticRoom1Resource;

#ifdef ISTARTUP
Room1Field = IStartupField {
#else
Room1Field = StartupField {
#endif
	internalFlags = hasDefaultLauncher;
	hints = {
		ATTR_GEN_INIT_FILE_KEY,
#ifdef WELCOME
		ATTR_GEN_INIT_FILE_CATEGORY {
			char	"uiFeatures - intro",0
		}
#endif
#ifdef ISTARTUP
		ATTR_GEN_INIT_FILE_CATEGORY {
			char	"uiFeatures - k2Level",0
		}
#endif
	}
	notificationDestination = process;
}

end StaticRoom1Resource;

start StaticRoom2Resource;

#ifdef ISTARTUP
Room2Field = IStartupField {
#else
Room2Field = StartupField {
#endif
	internalFlags = needsWorkspaceMenu, hasDefaultLauncher;
	hints = {
		ATTR_GEN_INIT_FILE_KEY,
#ifdef WELCOME
		ATTR_GEN_INIT_FILE_CATEGORY {
			char	"uiFeatures - beginner",0
		}
#endif
#ifdef ISTARTUP
		ATTR_GEN_INIT_FILE_CATEGORY {
			char	"uiFeatures - entryLevel",0
		}
#endif
	}
	notificationDestination = process;
}

end StaticRoom2Resource;

start StaticRoom3Resource;

#ifdef ISTARTUP
Room3Field = IStartupField {
#else
Room3Field = StartupField {
#endif
	internalFlags = needsWorkspaceMenu, hasDefaultLauncher;
	hints = {
		ATTR_GEN_INIT_FILE_KEY,
#ifdef WELCOME
		ATTR_GEN_INIT_FILE_CATEGORY {
			char	"uiFeatures - advanced",0
		}
#endif
#ifdef ISTARTUP
		ATTR_GEN_INIT_FILE_CATEGORY {
			char	"uiFeatures - advanced",0
		}
#endif
	}
	notificationDestination = process;
}

end StaticRoom3Resource;

#ifdef ISTARTUP
start StaticLoginRoomResource;

LoginRoomField = StartupField {
	internalFlags = hasDefaultLauncher;
	hints = {
		ATTR_GEN_INIT_FILE_KEY,
		ATTR_GEN_INIT_FILE_CATEGORY {
			char	"uiFeatures - login",0
		}
	}
	notificationDestination = process;
}

end StaticLoginRoomResource;

#ifdef MOUSETUTORIAL
start StaticMouseRoomResource;

MouseRoomField = StartupField {
	internalFlags = hasDefaultLauncher;
	hints = {
		ATTR_GEN_INIT_FILE_KEY,
		ATTR_GEN_INIT_FILE_CATEGORY {
			char	"uiFeatures - mouseTutorial",0
		}
	}
	notificationDestination = process;
}

end StaticMouseRoomResource;
#endif /* MOUSETUTORIAL */
#endif /* STARTUP */

start OverviewRoomResource, notDetachable;

#ifdef MOUSETUTORIAL
MouseTutInter = GenInteraction {
	children = MouseTutTrigger, MouseTutName;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_NO_WIDER_THAN_CHILDREN_REQUIRE,
		HINT_FIXED_SIZE {
			SpecWidth <>
			SpecHeight <SST_PCT_OF_FIELD_HEIGHT, PCT_20>
			word 1
		}
	}
}
MouseTutTrigger = GenTrigger {
	genAttributes = default +initiatesInputIgnore;
	moniker = list {
		MouseTutIconLCMoniker,
		MouseTutIconLMMoniker,
		MouseTutIconLCGAMoniker
	}
	action  = MSG_STARTUP_MOUSE_TUTORIAL, process;
	hints = {
		HINT_ENSURE_TEMPORARY_DEFAULT
	}
}
MouseTutName = GenGlyph {
	moniker = "Mouse Tutorial";
}
#endif /* MOUSETUTORIAL */

#ifdef ISTARTUP

#ifdef MOUSETUTORIAL
InstructionLine = GenGlyph {
	moniker = "Press M to run the mouse tutorial.  Press ESC to Logout.  Press F1 for help.";
}
#else
InstructionLine = GenGlyph {
	moniker = "Press ESC to Logout.  Press F1 for help.";
}
#endif /* MOUSETUTORIAL */

ReplyBarInter = GenInteraction {
	children = LogoutTrigger, HelpTrigger;
	hints = {
		HINT_MAKE_REPLY_BAR,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING
	}
}

LogoutTrigger = GenTrigger {
	moniker = "Logout";
	action = MSG_STARTUP_APP_LOGOUT, StartupApp;
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	TRUE		/* query user before logout */
		}
	}
}

HelpTrigger = GenTrigger {
        action = MSG_META_BRING_UP_HELP, "TO_SELF";
	hints = {
		ATTR_GEN_DEFAULT_MONIKER {
			word	GDMT_HELP;
		},
                ATTR_GEN_HELP_CONTEXT {
                        char "dLevel", 0
                }
	}
}

/* dialog for autologin */
AutologInter = GenInteraction {
	visibility = dialog;
	attributes = default +sysModal, +notUserInitiatable; 
	children = AutologName, AutologText, AutologTrigger;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_NOT_MOVABLE
	}
}

AutologName = GenText {
	genAttributes = default +readOnly;
	hints = {
                ATTR_GEN_TEXT_DEFAULT_CHAR_ATTR {
			word DEF_CHAR_ATTR_FONT_SIZE(VTDF_BERKELEY, VTDS_36)
		},
		ATTR_GEN_TEXT_NEVER_MAKE_SCROLLABLE
	}
}

AutologText = GenText {
	genAttributes = default +readOnly;
	text = "has been autologged in to a class. Press Enter to continue.";
	hints = {
		ATTR_GEN_TEXT_NEVER_MAKE_SCROLLABLE
	}
}

AutologTrigger = GenTrigger {
	moniker = "Login Now";
	action = MSG_STARTUP_AUTOLOGIN_USER, process;
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

/**************************************************************************
 *	UI for informing user of upgrade process
 **************************************************************************/

UpgradeInteraction = GenInteraction {
	type = organizational;
	visibility = dialog;
	attributes = default +sysModal, +notUserInitiatable;
	children =	UpgradeText;
}

UpgradeText = GenText {
	genAttributes = default +readOnly;
	text = "SchoolView is setting up your desktop.  This one time process usually takes less than a minute, but may take up to seven minutes under certain conditions.";
}	


#endif /* ISTARTUP */

end OverviewRoomResource;

#include "cuiStrings.ui"


