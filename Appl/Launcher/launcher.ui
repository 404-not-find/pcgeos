/*---------------------------------------------------------------------------
	Copyright (c) GeoWorks 1991 -- All Rights Reserved

PROJECT:	LAUNCHER
MODULE:		Launcher
FILE:		launcher.asm

AUTHOR:		Andrew Wilson, Nov 21, 1990

ROUTINES:
	Name			Description
	----			-----------
	
REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	atw	11/21/90	Initial revision
	roger	11/22/91	Revised and Added multiple file checks.
	dlitwin	03/17/92	Updated to 2.0 for integration with Desktop

DESCRIPTION:
	This file contains the user Interface description for this
	application.

	$Id: launcher.ui,v 1.1 97/04/04 16:13:51 newdeal Exp $
---------------------------------------------------------------------------*/
#include "generic.uih"
#include "Art/mkrLauncher.ui"
#include "Art/mkrLauncherTiny.ui"



/*	IMPORTANT!!!!!!!!!!!!						*/
/*	The ordering of the AppUI resource is very important! It must	*/
/*	only have the GenApplication and VisMoniker in it, so the 	*/
/*	ordering of chunks goes:					*/
/*		Flags, GenApp, MonikerList, VisMoniker	*/
/*		  0       1        2            3       */

start	AppUI, notDetachable;
LauncherApp = GenApplication {
	moniker = list {
		LauncherTextMoniker,
		LauncherLCMoniker,
		LauncherLMMoniker,
		LauncherSCMoniker,
		LauncherSMMoniker,
		LauncherTCMoniker,
		LauncherTMMoniker
	}

	children = CommandStringPrompt, ConfirmArgs, AppendFileDialog,
	    AppOrDocPrompt; 
}
visMoniker LauncherTextMoniker = "DOS Program1";
end	AppUI;


start	Interface;
	/*  Command String edit dialog	*/

CommandStringPrompt = GenInteraction {
	type = multipleResponse;
	visibility = dialog;
	attributes = default +modal;
	children = CommandStringGlyph, CommandStringText, CommandStringOK,
			CommandStringCancel;
	hints = {	HINT_ORIENT_CHILDREN_VERTICALLY,
			HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}

CommandStringGlyph = GenGlyph {
	moniker = "Enter DOS command line arguments:";
}

CommandStringText = GenText {
	moniker = "Command String:";
	maxLength = "PATH_LENGTH";
	textDisplayAttributes = default +singleLineText, +allowTextOffEnd;
	hints = {
		HINT_DEFAULT_FOCUS,
		HINT_FIXED_SIZE {
			SpecWidth <SST_PIXELS, 2*72>
			SpecHeight <>
		  }
	}
}

CommandStringOK = GenTrigger {
	moniker = "Use this Command String";
	action = MSG_LAUNCHER_COMMAND_STRING_SET, process;
	hints = { 	HINT_DEFAULT_DEFAULT_ACTION, HINT_SEEK_REPLY_BAR }
}

CommandStringCancel = GenTrigger {
	moniker = "VMO_CANCEL", "Cancel Running Launcher";
	action = MSG_META_QUIT, LauncherApp;
	hints = { HINT_SEEK_REPLY_BAR }
}


	/*  Confirm Arguments dialog	*/

ConfirmArgs = GenInteraction {
	type = multipleResponse;
	visibility = dialog;
	attributes = default +modal;
	children = ConfirmArgsGlyph, ConfirmArgsText, ConfirmArgsOK,
			ConfirmArgsCancel;
	hints = {	HINT_ORIENT_CHILDREN_VERTICALLY,
			HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}

ConfirmArgsGlyph = GenGlyph {
	moniker = "Are these command line arguments correct?";
}

ConfirmArgsText = GenText {
	moniker = "Arguments:";
	maxLength = "PATH_LENGTH";
	textDisplayAttributes = default +singleLineText, +allowTextOffEnd;
	hints = {
		HINT_DEFAULT_FOCUS,
		HINT_FIXED_SIZE {
			SpecWidth <SST_PIXELS, 2*72>
			SpecHeight <>
		  }
	}
}

ConfirmArgsOK = GenTrigger {
	moniker = "Use these arguments";
	action = MSG_LAUNCHER_ARGS_CONFIRMED, process;
	hints = { 	HINT_DEFAULT_DEFAULT_ACTION, HINT_SEEK_REPLY_BAR }
}

ConfirmArgsCancel = GenTrigger {
	moniker = "VMO_CANCEL", "Cancel Running Launcher";
	action = MSG_META_QUIT, LauncherApp;
	hints = { HINT_SEEK_REPLY_BAR }
}


	/* AppendFile FileSelector dialog */

AppendFileDialog = GenInteraction {
	type = multipleResponse;
	visibility = dialog;
	attributes = default +modal;
	children = AppendFileGlyph, AppendFileFileSelector,
			AppendFileOK, AppendFileNoFile, AppendFileCancel;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}

AppendFileGlyph = GenGlyph {
	moniker = "Choose a file to use in DOS program";
}

AppendFileFileSelector = GenFileSelector {
	fileCriteria = dirs, nonGeosFiles;
	hints = {
		HINT_FILE_SELECTOR_SINGLE_ACTION,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS,20>
			SpecHeight <>
			word 	1
		},
		ATTR_GEN_PATH_DATA {
			GenFilePath <SP_DOCUMENT>
		}
	}
	action = MSG_LAUNCHER_FILE_CHECK, process;
}

AppendFileOK = GenTrigger {
	moniker = "Use this file";
	action = MSG_LAUNCHER_FILE_CHOSEN, process;
	hints = { HINT_DEFAULT_DEFAULT_ACTION, HINT_SEEK_REPLY_BAR }
}

AppendFileNoFile = GenTrigger {
	moniker = "Use no file";
	action = MSG_LAUNCHER_NO_FILE_CHOSEN, process;
	hints = { HINT_SEEK_REPLY_BAR }
}

AppendFileCancel = GenTrigger {
	moniker = "VMO_CANCEL", "Cancel Running Launcher";
	action = MSG_META_QUIT, LauncherApp;
	hints = { HINT_SEEK_REPLY_BAR }
}


AppOrDocPrompt = GenInteraction {
    moniker = list { LauncherTextMoniker }
    type = multipleResponse;
    visibility = dialog;
    attributes = default +modal;
    children = AppOrDocText, 
	AppOrDocAppTrigger, AppOrDocDocTrigger, AppOrDocCancelTrigger;
    hints = {
	HINT_ORIENT_CHILDREN_VERTICALLY,
	HINT_CENTER_CHILDREN_HORIZONTALLY
    }
}

AppOrDocText = GenText {
    genAttributes = readOnly;
    text = "This application contains additional documentation.  Would you like to run the application now or read the documentation first?  You may wish to print a copy of the documentation to reference while you use the application.";
    hints = {
	HINT_TEXT_CURSOR_AT_START,
	HINT_MINIMUM_SIZE {
	    SpecWidth <SST_AVG_CHAR_WIDTHS, 35>
	    SpecHeight <>
	},
	HINT_MAXIMUM_SIZE {
	    SpecWidth <SST_AVG_CHAR_WIDTHS, 35>
	    SpecHeight <SST_LINES_OF_TEXT, 6>
	},
	HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	HINT_EXPAND_HEIGHT_TO_FIT_PARENT
    }
}
AppOrDocAppTrigger = GenTrigger {
    moniker = "Run Application";
    action = MSG_LAUNCHER_RUN_APP, process;
    hints = { HINT_DEFAULT_DEFAULT_ACTION, HINT_SEEK_REPLY_BAR }
}

AppOrDocDocTrigger = GenTrigger {
    moniker = "Read User Guide";
    action = MSG_LAUNCHER_READ_DOC, process;
    hints = { HINT_SEEK_REPLY_BAR }
}

AppOrDocCancelTrigger = GenTrigger {
    moniker = "VMO_CANCEL", "Cancel";
    action = MSG_META_QUIT, LauncherApp;
    hints = { HINT_SEEK_REPLY_BAR }
}

end	Interface;


start	LauncherStrings, data;
chunk	launcherFlags = data {byte mask LDF_PROMPT_USER or mask LDF_NO_ARGS}
chunk	launcherFileName = "";
chunk	launcherCommandString = "";
chunk	launcherWorkingDirectory = "";
chunk	launcherCheckFile = "";
chunk	launcherCheckFile2 = "";
chunk	launcherDiskSave = data {}
chunk	launcherDocFile = "";		/* GPC: Added 9/24/99 JimG */
chunk	launcherDocReaderToken = data {GeodeToken <"",0>} /* GPC:
						     Added 10/6/99 JimG */
chunk	launcherAppOrDocCustomText = "This application contains additional " \
"documentation.  Would you like to run the application now or read the " \
"documentation first?"; /* GPC: Added 7/28/00 dhunter */
end	LauncherStrings;


start	LauncherErrorStrings, data;
chunk	DiskNotFoundSysModal = 
	"The disk \\\"\\1\\\" could not be found.  Please insert it in"
	" drive \\\"\\2\\\" and click Ok to continue, or click Cancel"
	" to exit.";

visMoniker SDRT_DiskNotFoundSysModal_OK = "OK";
visMoniker SDRT_DiskNotFoundSysModal_Cancel = "VMO_CANCEL", "Cancel";

chunk	NotFoundError =
	"The file could not be located.";
chunk	ParseError =
	"An error occured while parsing the Command String or the parsed"
	" Command String was too long.";
chunk	DosExecError = "An error occured while trying to run the DOS program.";
chunk	OutOfMemoryError =
	"There is currently not enough memory to execute the DOS Program.";
end	LauncherErrorStrings;



/* The launcher does not support codeword expansion and parsing at this */
/* point, so even though this is a currently useless resource, we need	*/
/* to keep it because it was in the original launcher, and so even 	*/
/* I'd like to nuke it now, I won't because it would mean the launcher	*/
/* only has 13 resources instead of 14.  GeoManager expects 14, and so	*/
/* I would have to change GeoManager to adapt to this.  This would not 	*/
/* be a problem if we hadn't already shipped the Upgrade, but because	*/
/* we did and that makes and edits launchers with 14 resources, we are	*/
/* stuck if we want to ever be able to edit old DOS Launchers. Sigh.	*/
/* 		dlitwin 8/31/93 					*/

start	LauncherCodewords, data;
chunk	FileCodeword		= {file}
chunk	PassedFileCodeword	= {passedfile}
chunk	DriveCodeword		= {drive}
chunk	DirectoryCodeword	= {directory}
chunk	CheckCodeword		= {check}
chunk	ArgumentsCodeword	= {arguments}
chunk	ListCodeword		= {list}
chunk	DollarCodeword		= {$}
end	LauncherCodewords;






