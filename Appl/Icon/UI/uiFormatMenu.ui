/*
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * Project:	PC GEOS
 * Module:	Icon editor
 * File:	uiFormatMenu.ui
 *
 * Author:	Steve Yegge, Feb 23, 1993
 *
 * Revision History:
 *	Name	Date		Description
 *	----	----		-----------
 *	stevey	2/23/93		Initial revision
 *
 * Description:
 *	
 *
 *	$Id: uiFormatMenu.ui,v 1.1 97/04/04 16:06:16 newdeal Exp $
 *
 */

start	DisplayTempUI;

/*-----------------------------------------------------------------------------
		Format Menu
-----------------------------------------------------------------------------*/

FormatMenu = GenInteraction {
	genStates = default +enabled;
	visibility = popup;
	moniker = 't', "Format";
	children = AddFormatDialog,
		   ResizeFormatDialog,
		   DeleteFormatTrigger,
		   TFDialog,
		   RotateFormatDialog,
		   FormatBooleanGroupGroup,
		   ResizeFormatInteraction;
}

DeleteFormatTrigger = GenTrigger {
	genStates = default + enabled;
	moniker = 'D', "Delete Format";
	action = MSG_DB_VIEWER_DELETE_FORMAT, "TO_APP_MODEL";
}

/*-----------------------------------------------------------------------------
		Add-format dialog
-----------------------------------------------------------------------------*/

AddFormatDialog = GenInteraction {
	attributes = default +modal;
	visibility = dialog;
	moniker = 'A', "Add Format";
	children = AddFormatSizeSelector,
		   AddFormatItemGroups,
		   AddFormatReplyBar;
}

AddFormatItemGroups = GenInteraction {
	type = organizational;
	children = AddFormatColorSchemeGroup,
		   AddFormatAspectRatioGroup,
		   AddFormatStyleGroup,
		   AddFormatDisplaySizeGroup;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
	}
}


AddFormatSizeSelector = GenInteraction {
	type = organizational;
	children = AddFormatWidth,
		   AddFormatHeight;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING,
	}
}

AddFormatWidth = GenValue {
	moniker = "Width";
	minimum = 1;
	maximum = 1024;
	value = 48;
	destination = "TO_APP_MODEL";
	applyMsg = MSG_META_DUMMY;
	hints = {
		HINT_PLACE_MONIKER_TO_LEFT,
	}
}

AddFormatHeight = GenValue {
	moniker = "Height";
	minimum = 1;
	maximum = 1024;
	value = 30;
	destination = "TO_APP_MODEL";
	applyMsg = MSG_META_DUMMY;
	hints = {
		HINT_PLACE_MONIKER_TO_LEFT,
	}
}

/*-----------------------------------------------------------------------------
		Color scheme selector
-----------------------------------------------------------------------------*/

AddFormatColorSchemeGroup = GenInteraction {
	type = organizational;
	moniker = "Color Scheme";
	children = AddFormatColorScheme;
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
	}
}

AddFormatColorScheme = GenItemGroup {
	numSelections = 1;
	behaviorType = exclusive;
	selection = BMF_4BIT;
	children = FormatColor8Item,
		   FormatColor4Item,
		   FormatColor1Item;
	hints = {
		HINT_PLACE_MONIKER_TO_LEFT,
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_ITEM_GROUP_CUSTOM_DOUBLE_PRESS {
			word 0
		}
	}
}

FormatColor8Item = GenItem {
	moniker = "256-color";
	identifier = BMF_8BIT;
}

FormatColor4Item = GenItem {
	moniker = "16-color";
	identifier = BMF_4BIT;
}

FormatColor1Item = GenItem {
	moniker = "Monochrome";
	identifier = BMF_MONO;
}

/*-----------------------------------------------------------------------------
			Aspect-ratio selector
-----------------------------------------------------------------------------*/

AddFormatAspectRatioGroup = GenInteraction {
	type = organizational;
	moniker = "Aspect Ratio";
	children = AddFormatAspectRatio;
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
	}
}

AddFormatAspectRatio = GenItemGroup {
	numSelections = 1;
	behaviorType = exclusive;
	selection = DAR_NORMAL;
	children = VGAItem,
/*		   EGAItem,	*/
		   CGAItem;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_ITEM_GROUP_CUSTOM_DOUBLE_PRESS {
			word 0
		}
	}
}

VGAItem = GenItem {
	moniker = "VGA";
	identifier = DAR_NORMAL;
}

/*
EGAItem = GenItem {
	moniker = "EGA";
	identifier = DAR_SQUISHED;
}*/

CGAItem = GenItem {
	moniker = "CGA";
	identifier = DAR_VERY_SQUISHED;
}

/*-----------------------------------------------------------------------------
		Add-format style selector group
-----------------------------------------------------------------------------*/

AddFormatStyleGroup = GenInteraction {
	type = organizational;
	moniker = "Format Type";
	children = AddFormatStyle;
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
	}
}

AddFormatStyle = GenItemGroup {
	numSelections = 1;
	behaviorType = exclusive;
	selection = VMS_ICON;
	children = IconStyleItem,
		   ToolStyleItem;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_ITEM_GROUP_CUSTOM_DOUBLE_PRESS {
			word 0
		}
	}
}

IconStyleItem = GenItem {
	moniker = "Icon";
	identifier = VMS_ICON;
}

ToolStyleItem = GenItem {
	moniker = "Tool";
	identifier = VMS_TOOL;
}

/*-----------------------------------------------------------------------------
		Add-format "size" field group.
-----------------------------------------------------------------------------*/

AddFormatDisplaySizeGroup = GenInteraction {
	type = organizational;
	moniker = "Display Size";
	children = AddFormatDisplaySize;
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
	}
}

AddFormatDisplaySize = GenItemGroup {
	numSelections = 1;
	behaviorType = exclusive;
	selection = DS_STANDARD;
	children = TinySizeItem,
		   StandardSizeItem,
		   LargeSizeItem,
		   HugeSizeItem;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_ITEM_GROUP_CUSTOM_DOUBLE_PRESS {
			word 0
		}
	}
}

TinySizeItem = GenItem {
	moniker = "Tiny";
	identifier = DS_TINY;
}

StandardSizeItem = GenItem {
	moniker = "Standard";
	identifier = DS_STANDARD;
}

LargeSizeItem = GenItem {
	moniker = "Large";
	identifier = DS_LARGE;
}

HugeSizeItem = GenItem {
	moniker = "Huge";
	identifier = DS_HUGE;
}

/*-----------------------------------------------------------------------------
		Add-format reply bar
-----------------------------------------------------------------------------*/

AddFormatReplyBar = GenInteraction {
	type = organizational;
	children = AddFormatOKTrigger,
		   AddFormatCancelTrigger;
	hints = {
		HINT_MAKE_REPLY_BAR,
	}
}

AddFormatOKTrigger = GenTrigger {
	genStates = default + enabled;
	genAttributes = default + signalInteractionComplete;
	moniker = 'O', "   OK   ";
	action = MSG_DB_VIEWER_ADD_FORMAT, "TO_APP_MODEL";
	hints = {HINT_DEFAULT_DEFAULT_ACTION}
}

AddFormatCancelTrigger = GenTrigger {
	genStates = default + enabled;
	genAttributes = default + signalInteractionComplete;
	moniker = "VMO_CANCEL", "Cancel";
}

/*-----------------------------------------------------------------------------
		ResizeFormatDialog
-----------------------------------------------------------------------------*/

ResizeFormatDialog = GenTrigger {
	moniker = 'R', "Resize Format";
	action = MSG_DB_VIEWER_INITIATE_RESIZE_FORMAT_DIALOG, "TO_APP_MODEL";
	hints = {
		HINT_TRIGGER_BRINGS_UP_WINDOW,
	}
}

ResizeFormatInteraction = GenInteraction {
	type = command;
	attributes = default +modal, +notUserInitiatable;
	visibility = dialog;
	children = ResizeFormatSizeGroup,
		   ResizeFormatOptionsGroup,
		   ResizeFormatOKTrigger;
}

ResizeFormatSizeGroup = GenInteraction {
	type = organizational;
	children = ResizeWidthValue,
		   ResizeHeightValue;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
	}
}

ResizeWidthValue = GenValue {
	moniker = "New Width";
	minimum = 1;
	maximum = MAX_FORMAT_WIDTH;
	destination = "TO_APP_MODEL";
	applyMsg = MSG_META_DUMMY;
}

ResizeHeightValue = GenValue {
	moniker = "New Height";
	minimum = 1;
	maximum = MAX_FORMAT_HEIGHT;
	destination = "TO_APP_MODEL";
	applyMsg = MSG_META_DUMMY;
}

ResizeFormatOptionsGroup = GenItemGroup {
	behaviorType = exclusive;
	numSelections = 1;
	selection = RFOT_SCALE_BITMAP;
	children = ResizeScaleBitmapItem,
		   ResizeKeepBitmapItem;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		ATTR_GEN_ITEM_GROUP_CUSTOM_DOUBLE_PRESS {
				word 0
		}
	}
}

ResizeScaleBitmapItem = GenItem {
	moniker = "Scale Bitmap to Fit";
	identifier = RFOT_SCALE_BITMAP;
}

ResizeKeepBitmapItem = GenItem {
	moniker = "Don't Scale Bitmap";
	identifier = RFOT_KEEP_BITMAP;
}

ResizeFormatOKTrigger = GenTrigger {
	moniker = 'R', "Resize";
	genAttributes = default +signalInteractionComplete;
	action = MSG_DB_VIEWER_RESIZE_FORMAT, "TO_APP_MODEL";
	hints = {
		HINT_SEEK_REPLY_BAR,
		HINT_DEFAULT_DEFAULT_ACTION,
	}
}

/*-----------------------------------------------------------------------------
		the floating-format option
-----------------------------------------------------------------------------*/

FormatBooleanGroupGroup = GenInteraction {
	type = organizational;
	children = FormatBooleanGroup;
}

FormatBooleanGroup = GenBooleanGroup {
	children = FloatingFormatList;
	destination = "TO_APP_MODEL";
	applyMsg = MSG_DB_VIEWER_SET_FORMAT_OPTIONS;
}

FloatingFormatList = GenBoolean {
	moniker = 'F', "Floating Format List";
	identifier = "mask FO_FLOATING_FORMATS";
}

/*-----------------------------------------------------------------------------
		Rotate-format dialog
-----------------------------------------------------------------------------*/

RotateFormatDialog = GenInteraction {
	visibility = dialog;
	type = command;
	moniker = 'F', "Flip Format";
	children = RotateItemGroup, RotateOKTrigger;
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
	}
}

RotateItemGroup = GenItemGroup {
	behaviorType = exclusive;
	selection = FRT_HORIZONTAL;
	numSelections = 1;
	children = FlipHorizontalItem,
		   FlipVerticalItem,
		   Rotate90Item,
		   Rotate180Item,
		   Rotate270Item;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
	}
}

FlipHorizontalItem = GenItem {
	moniker = "Flip Horizontally";
	identifier = FRT_HORIZONTAL;
}

FlipVerticalItem = GenItem {
	moniker = "Flip Vertically";
	identifier = FRT_VERTICAL;
}

Rotate90Item = GenItem {
	moniker = Moniker90DegreesCW;
	identifier = FRT_ROTATE_90;
}

Rotate180Item = GenItem {
	moniker = Moniker180Degrees;
	identifier = FRT_ROTATE_180;
}

Rotate270Item = GenItem {
	moniker = Moniker90DegreesCCW;
	identifier = FRT_ROTATE_270;
}

visMoniker Moniker90DegreesCW = data {
#ifdef DO_DBCS
	byte	7
	wchar	"Rotate 90", C_DEGREE_SIGN, " Clockwise", 0;
#else
	char	7, "Rotate 90", C_DEGREE, " Clockwise", 0;
#endif
}

visMoniker Moniker180Degrees = data {
#ifdef DO_DBCS
	byte	8
	wchar	"Rotate 180", C_DEGREE_SIGN, 0;
#else
	char	8, "Rotate 180", C_DEGREE, 0;
#endif
}

visMoniker Moniker90DegreesCCW = data {
#ifdef DO_DBCS
	byte	8
	wchar	"Rotate 90", C_DEGREE_SIGN, " Counterclockwise", 0;
#else
	char	8, "Rotate 90", C_DEGREE, " Counterclockwise", 0;
#endif
}

RotateOKTrigger = GenTrigger {
	moniker = "OK";
	action = MSG_DB_VIEWER_ROTATE_FORMAT, "TO_APP_MODEL";
	hints = {
		HINT_SEEK_REPLY_BAR,
		HINT_DEFAULT_DEFAULT_ACTION,
	}
}

end DisplayTempUI;
