/*
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * Project:	PC GEOS
 * Module:	Icon editor
 * File:	uiGraphics.ui
 *
 * Author:	Steve Yegge, Apr  8, 1993
 *
 * Revision History:
 *	Name	Date		Description
 *	----	----		-----------
 *	stevey	4/ 8/93	Initial revision
 *
 * Description:
 *	
 *
 *	$Id: uiGraphics.ui,v 1.1 97/04/04 16:06:13 newdeal Exp $
 *
 */

start	PrimaryUI;

IconGraphicsMenu = GenInteraction {
	visibility = popup;
	moniker = 'G', "Graphics";
	children = IconAreaAttrDialog,
		   IconLineAttrDialog,
		   FloatingToolbox,
  		   ToolMenu;
}

end	PrimaryUI;

/*-----------------------------------------------------------------------------
		Area and line controllers
-----------------------------------------------------------------------------*/

start	GraphicsMenuUI;

IconAreaAttrDialog = GenInteraction {
	visibility = dialog;
	type = properties;
	moniker = 'A', "Area Attributes";
	children = IconAreaColorSelector;
}

IconAreaColorSelector = GrObjAreaColorSelector vardataReloc {
	output = "TO_BITMAP";
	hints = {
		ATTR_GEN_OUTPUT_TRAVEL_START {
			UN_OPTR IconApp
		},
		ATTR_GEN_DESTINATION_CLASS {
			UN_DD IconBitmapClass
		},
	}
}

IconLineAttrDialog = GenInteraction {
	visibility = dialog;
	type = properties;
	moniker = 'L', "Line Attributes";
	children = IconLineColorSelector,
		   IconLineAttrControl;
}

IconLineColorSelector = GrObjLineColorSelector vardataReloc {
	output = "TO_BITMAP";
	hints = {
		ATTR_GEN_OUTPUT_TRAVEL_START {
			UN_OPTR IconApp
		},
		ATTR_GEN_DESTINATION_CLASS {
			UN_DD IconBitmapClass
		},
	}
}

IconLineAttrControl = GrObjLineAttrControl vardataReloc {
	output = "TO_BITMAP";
	hints = {
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word	mask GOLACF_STYLE or \
				mask GOLACF_ARROWHEAD_TYPE or \
				mask GOLACF_ARROWHEAD_WHICH_END
		},
		ATTR_GEN_OUTPUT_TRAVEL_START {
			UN_OPTR IconApp
		},
		ATTR_GEN_DESTINATION_CLASS {
			UN_DD IconBitmapClass
		},
	}
}

/*-----------------------------------------------------------------------------
			Tool Menu & Control
-----------------------------------------------------------------------------*/

ToolMenu = GenInteraction {
	moniker = 'C', "Customize Tool Bars";
	children = IconToolControl,
		   ToolMenuCloseTrigger;
	visibility = dialog;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}

ToolMenuCloseTrigger = GenTrigger {
	moniker = "VMO_CANCEL", "Close";
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

IconToolControl = GenToolControl {
	toolboxList = IconToolboxList;
	toolGroupList = IconToolGroupList;
	hints = { 
		HINT_SAME_CATEGORY_AS_PARENT
	}
}

chunk IconToolboxList = data {
	UN_OPTR TopToolbar
	UN_OPTR TopToolbarName
	UN_OPTR	LeftToolbar
	UN_OPTR	LeftToolbarName
	UN_OPTR RightToolbar
	UN_OPTR RightToolbarName
	UN_OPTR BottomToolbar
	UN_OPTR BottomToolbarName
	UN_OPTR	FloatingToolbox
	UN_OPTR	FloatingToolboxName	
}

chunk TopToolbarName = "Above Document";
chunk LeftToolbarName = "Left of Document";
chunk RightToolbarName = "Right of Document";
chunk BottomToolbarName = "Below Document";
chunk FloatingToolboxName = "Floating Toolbox";

chunk IconToolGroupList = data {
	UN_OPTR IconFileTools
	UN_OPTR IconEditTools
	UN_OPTR IconBitmapEditTools
	UN_OPTR	IconAreaColorTools
	UN_OPTR IconLineColorTools
	UN_OPTR IconLineAttrTools
}

end	GraphicsMenuUI;
