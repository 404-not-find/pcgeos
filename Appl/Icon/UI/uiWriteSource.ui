/*
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * Project:	GEOS
 * Module:	Icon editor
 * File:	uiSource.ui
 *
 * Author:	Steve Yegge, Feb 18, 1993
 *
 * Revision History:
 *	Name	Date		Description
 *	----	----		-----------
 *	stevey	2/18/93	Initial revision
 *
 * Description:
 *	
 *	This file contains the user-interface description of the
 *	dialog for writing out source code for an icon.
 *
 *	$Id: uiWriteSource.ui,v 1.1 97/04/04 16:06:15 newdeal Exp $
 *
 */

start DisplayTempUI;

/*-----------------------------------------------------------------------------
			Write-source dialog
-----------------------------------------------------------------------------*/

WriteSourceDialog = GenInteraction vardataReloc {
	genAttributes = default +notifyVisibility;
	genStates = default - enabled;
	visibility = dialog;
	type = properties;
	children = WriteSourceFileName,
		   WriteSourceItemGroups,
		   LargeBitmapGroup,
		   OtherOptionsGroup,
		   WriteSourceOKTrigger;
	moniker = 'W', "Write Source Code";
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
		ATTR_GEN_VISIBILITY_DESTINATION {
			I_OPTR	WriteSourceFileName
		},
		ATTR_GEN_VISIBILITY_MESSAGE {
			word	MSG_VIS_TEXT_SELECT_ALL
		},
	}
}

WriteSourceFileName = GenText {
	moniker = "Filename:";
	textDisplayAttributes = singleLineText, useTabForNavigation;
	maxLength = MAX_LENGTH;
	hints = {
		HINT_DEFAULT_FOCUS,
		HINT_PLACE_MONIKER_TO_LEFT,
		ATTR_GEN_TEXT_LEGAL_DOS_FILENAMES,
		HINT_TEXT_FRAME,
		ATTR_GEN_TEXT_MAKE_UPPERCASE,
		ATTR_GEN_TEXT_NO_SPACES,
		HINT_TEXT_WHITE_WASH_COLOR,	
		HINT_FIXED_SIZE {
			SpecWidth <SST_WIDE_CHAR_WIDTHS, MAX_LENGTH>
			SpecHeight <>
		}
	}
}

WriteSourceItemGroups = GenInteraction {
	type = organizational;
	children = WriteSourceLeftGroup,
		   WriteSourceRightGroup;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING,
	}
}


/*-----------------------------------------------------------------------------
	WriteSourceLeftGroup -- formats and language
-----------------------------------------------------------------------------*/

WriteSourceLeftGroup = GenInteraction {
	type = organizational;
	children = WriteSourceFormatGroup,
		   WriteSourceHeaderGroup;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
	}
}


WriteSourceFormatGroup = GenInteraction {
	type = organizational;
	children = WriteSourceFormatList;
	moniker = "Formats";
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	}
}

WriteSourceFormatList = GenItemGroup {
	children = WriteMonikerTrigger,
		   WriteMonikerListTrigger;
	behaviorType = exclusive;
	numSelections = 1;
	selection = WSFT_ALL_FORMATS;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
	}
}

WriteMonikerTrigger = GenItem {
	moniker = "Current Format";
	identifier = WSFT_CURRENT_FORMAT;
}

WriteMonikerListTrigger = GenItem {
	moniker = "All Formats";
	identifier = WSFT_ALL_FORMATS;
}

WriteSourceLanguageGroup = GenInteraction {
	type = organizational;
	children = WriteSourceLanguageList;
	moniker = "Language";
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	}
}

WriteSourceLanguageList = GenItemGroup {
	children = WriteCTrigger,
		   WriteAsmTrigger;
	behaviorType = exclusive;
	numSelections = 1;
	selection = WSLT_GOC;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
	}
}

WriteCTrigger = GenItem {
	moniker = "GOC Source";
	identifier = WSLT_GOC;
}

WriteAsmTrigger = GenItem {
	moniker = "UIC Source";
	identifier = WSLT_ASM;
}

/*-----------------------------------------------------------------------------
	WriteSourceRightGroup -- header type & compaction
-----------------------------------------------------------------------------*/

WriteSourceRightGroup = GenInteraction {
	type = organizational;
	children = WriteSourceLanguageGroup,
		   WriteSourceCompactionGroup;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
	}
}

WriteSourceHeaderGroup = GenInteraction {
	type = organizational;
	children = WriteSourceHeaderList;
	moniker = "Header";
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	}
}

WriteSourceHeaderList = GenItemGroup {
	children = WriteVisMonikerItem,
/*		   WriteBitmapItem,	*/
		   WritePtrImageItem;
	behaviorType = exclusiveNone;
	numSelections = 1;
	selection = WSHT_VIS_MONIKER;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
	}
}

WriteVisMonikerItem = GenItem {
	moniker = "VisMoniker";
	identifier = WSHT_VIS_MONIKER;
}

/*WriteBitmapItem = GenItem {
	moniker = "Bitmap";
	identifier = WSHT_BITMAP;
}*/

WritePtrImageItem = GenItem {
	moniker = "PointerDef";
	identifier = WSHT_PTR_IMAGE;
}

WriteSourceCompactionGroup = GenInteraction {
	type = organizational;
	children = WriteSourceCompactionList;
	moniker = "Compaction";
	hints = {
		HINT_DRAW_IN_BOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
	}
}

WriteSourceCompactionList = GenItemGroup {
	children = WriteSourceCompactItem,
		   WriteSourcePackbitsItem;
	behaviorType = exclusive;
	numSelections = 1;
	selection = WSCT_COMPACTED;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
	}
}

WriteSourceCompactItem = GenItem {
	moniker = "Compacted";
	identifier = WSCT_COMPACTED;
}

WriteSourcePackbitsItem = GenItem {
	moniker = "Uncompacted";
	identifier = WSCT_UNCOMPACTED;
}

/*-----------------------------------------------------------------------------
		WriteLargeBitmapGroup
-----------------------------------------------------------------------------*/

LargeBitmapGroup = GenInteraction {
	type = organizational;
	genStates = default -usable;
	children = WriteSourceLargeBoolean, WLOptionsDialog;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING,
		HINT_CENTER_CHILDREN_VERTICALLY,
	}
}

WriteSourceLargeBoolean = GenItemGroup {
	children = WriteLargeItem;
	behaviorType = exclusiveNone;
	numSelections = 0;
}

WriteLargeItem = GenItem {
	moniker = "Write Large Bitmap";
	identifier = 1;
}

WLOptionsDialog = GenInteraction {
	visibility = dialog;
	moniker = "Options";
	type = properties;
	children = ResourceSizeValue,
		   XOffsetValue,
		   YOffsetValue;
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
	}
}

ResourceSizeValue = GenValue {
	moniker = "Resource Size (KB):";
	value = 5;
	minimum = 1;
	maximum = 12;
}

XOffsetValue = GenValue {
	moniker = "X Offset:";
	value = 0;
	minimum = 0;
	maximum = 1000;
}

YOffsetValue = GenValue {
	moniker = "Y Offset:";
	value = 0;
	minimum = 0;
	maximum = 1000;
}

OtherOptionsGroup = GenInteraction {
	type = organizational;
	children = OtherOptionsBooleanGroup;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_CENTER_CHILDREN_HORIZONTALLY,
	}
}

OtherOptionsBooleanGroup = GenBooleanGroup {
	children = WriteMaskBoolean;
	selectedBooleans = "mask WSOO_WRITE_MASK";	
}

WriteMaskBoolean = GenBoolean {
	moniker = "Write Mask";
	identifier = "mask WSOO_WRITE_MASK";
}

/*-----------------------------------------------------------------------------
		OK trigger -- attaches itself to reply bar
-----------------------------------------------------------------------------*/

WriteSourceOKTrigger = GenTrigger {
	genStates = default + enabled;
	action = MSG_DB_VIEWER_WRITE_SOURCE, "TO_APP_MODEL";
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {word IC_APPLY},
		HINT_DEFAULT_DEFAULT_ACTION,
	}
}

end DisplayTempUI;
