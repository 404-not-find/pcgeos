;
; CompuServe automated logon 
; $Id: compu.mac,v 1.7.36.1 97/03/29 09:45:39 canavese Exp $
;
; CompuServe is a registered trademark of CompuServe Incorporated.
;
; We assume the modem is COM 1. If otherwise, change this to 2, 3, or 4.
;
PORT	1
;
; As of 11/1/90, CompuServe recommends "1200-7-E-1-FULL" for communication.
;
COMM	1200-7-E-1-FULL
;
; If the above setting do not work, use the following line instead:
; (Remove the semi-colon from the beginning of the line, and insert
; a semi-colon on the beginning of the COMM line above.
;
;COMM	1200-8-N-1-FULL
;
; set the terminal type
;
TERM	VT100
;
; clear the script display
;
CLEAR
:retry
PAUSE
PRINT	"This is a sample", CR
PRINT	"script for signing ", CR
PRINT	"on to CompuServe.", CR
PRINT	"You will need to edit", CR
PRINT	"the port, comm, term,", CR
PRINT	"phone number, account name", CR
PRINT	"and password fields.", CR
PRINT	"DIALING", CR
DIAL	"531-1820"
MATCH	"BUSY"		GOTO	retry
MATCH	"CARRIER"	GOTO	retry
MATCH	"CONNECT"	GOTO	connect
PROMPT	1800
PRINT	"TIMEOUT ON DIALING", CR
GOTO	done
:connect
PAUSE
SEND	^C
MATCH	"Hostname:"	GOTO	host
MATCH	"ID:"		GOTO	id
PROMPT	600
PRINT	"TIMEOUT ON PROMPT", CR
GOTO	done
:id
PAUSE
SEND	"ACCOUNT_NAME",CR
MATCH	"word:"		GOTO	password
PROMPT	600
PRINT	"TIMEOUT ON PASSWORD", CR
GOTO	done
:password
PAUSE
SEND	"PASSWORD",CR
GOTO	done
:host
PAUSE
SEND	"CIS",CR
GOTO	id
:done
PRINT	"DONE",CR
BELL
END

:ABORT
PRINT	"HANGUP PHONE?",CR
SEND	"+++"
MATCH	"OK"		GOTO 	dohup
PROMPT	300
PRINT	"Couldn't disconnect the modem",CR
GOTO	end
;
; Hangup the connection
;
:dohup
SEND	"ATH0",CR
MATCH	"OK"	GOTO	hangup
PROMPT	300
PRINT	"TIMEOUT ON hangup",CR
GOTO	end
:hangup
PRINT	"HUNG UP",CR
:end
BELL
END
