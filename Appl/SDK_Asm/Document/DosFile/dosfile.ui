/*
 * Project:	PC/GEOS
 * Module:	DosFile (Sample PC/GEOS application)
 * File:	dosfile.ui
 *
 * Author:	Tony Requist
 *
 *	This file contains the user Interface description for this
 * application. This file is written in a language called ESPIRE, which is
 * compiled by the UIC compiler. See Spec/User/uic.doc for info.
 *
 * IMPORTANT NOTE:
 *	This sample application is primarily intended to demonstrate a
 *	model for handling documents.  Basic parts of a PC/GEOS application
 *	are not documented heavily here.  See the "Hello" sample application
 *	for more detailed documentation on the standard parts of a PC/GEOS
 *	application.
 *
 * RCS STAMP:
 *	$Id: dosfile.ui,v 1.1 97/04/04 16:33:25 newdeal Exp $
 *
 */

/* ----------------------------------------------------------------------------
	Include files
-----------------------------------------------------------------------------*/

#include "generic.uih"

/* ----------------------------------------------------------------------------
	Application Object
-----------------------------------------------------------------------------*/

start	AppResource;

DFApp = GenApplication {
	children = DFPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) = DFPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_APP_STARTUP) =
		DFDocumentControl;
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST) =
		DFDocumentControl;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_SELF_LOAD_OPTIONS) =
		DFDocumentControl;
	moniker = list {
		DFTextMoniker
	}
	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY {
		char	"dosfile", 0
		}
	}
}

visMoniker DFTextMoniker = "DosFile Sample Application";

end	AppResource;

/* ----------------------------------------------------------------------------
	 Primary window.
-----------------------------------------------------------------------------*/

/*
 * The GenDocumentControl must be a child of the primary, and must be
 * on the active list.
 */

start	Interface;		/* this resource is for misc UI objects */

DFPrimary = GenPrimary {
	children = DFFileMenu, DFValueX, DFValueY;
	hints = { ATTR_GEN_DISPLAY_NOT_MINIMIZABLE, HINT_SIZE_WINDOW_AS_DESIRED }
}

DFFileMenu = GenInteraction {
	children = DFDocumentControl;
	visibility = popup;
	hints = { ATTR_GEN_INTERACTION_GROUP_TYPE { GenInteractionGroupType GIGT_FILE_MENU } }
}

/* ----------------------------------------------------------------------------
	 UI Document Control
-----------------------------------------------------------------------------*/

/*
 * The GenDocumentControl should be put in the same resource as the
 * GenPrimary (for efficiency).  The GenDocumentControl will build the
 * file menu.
 */

DFDocumentControl = GenDocumentControl {
	/*
	 * Special attributes for working with DOS files
	 */
	dcAttributes = default -vmFile, +native;
	features = default -supportsTemplates,
		   -supportsUserSettableEmptyDocument,
		   -supportsUserMakingSharedDocuments;
	/*
	 * Text to display in the header area of the primary when no
	 * document is open.
	 */
	noNameText = "No Document";
	/*
	 * Template directory
	 */
	templateDir = "SAMPLE";
	/*
	 * Pointer to the GenDocumentGroup
	 */
	appDC = DFDocumentGroup;
	/*
	 * List of objects to enable/disable based on whether or not a
	 * document exists
	 */
	enableDisableList = DFUIEnableList;
}

chunk DFUIEnableList = data {
	UN_OPTR	DFValueX
	UN_OPTR	DFValueY
}

end Interface;

/* ----------------------------------------------------------------------------
	 Document Group
-----------------------------------------------------------------------------*/

/*
 * The GenDocumentGroup should be put in a resource of its own run by
 * the application thread.
 */

start DocumentUI;

DFDocumentGroup = GenDocumentGroup {
	/*
	 * Special attributes for working with DOS files
	 */
	attributes = default -vmFile, +native, -supportsAutoSave,
		     -automaticDirtyNotification,
		     -automaticChangeNotification;
	/*
	 * Name for untitled documents
	 */
	defaultName = "blank";
	/*
	 * Class for document objects
	 */
	documentClass = "DFDocumentClass";
	/*
	 * Pointer to the GenDocumentControl
	 */
	uiDC = DFDocumentControl;
}

end DocumentUI;

/*-----------------------------------------------------------------------------
	UI objects to display the document
-----------------------------------------------------------------------------*/

start Interface;

DFValueX = GenValue vardataReloc {
	genStates = default -enabled;
	minimum = 0;
	maximum = "DOCUMENT_WIDTH-1";
	applyMsg = MSG_DF_DOCUMENT_X_CHANGE;
	destination = "TO_APP_MODEL";
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD GenDocumentClass }
	}
}

DFValueY = GenValue vardataReloc {
	genStates = default -enabled;
	minimum = 0;
	maximum = "DOCUMENT_HEIGHT-1";
	applyMsg = MSG_DF_DOCUMENT_Y_CHANGE;
	destination = "TO_APP_MODEL";
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD GenDocumentClass }
	}
}

end Interface;
