/*
 * Project:	PC/GEOS
 * Module:	MultView (Sample PC/GEOS application)
 * File:	multview.ui
 *
 * Author:	Tony Requist
 *
 *	This file contains the user Interface description for this
 * application. This file is written in a language called ESPIRE, which is
 * compiled by the UIC compiler. See Spec/User/uic.doc for info.
 *
 * IMPORTANT NOTE:
 *	This sample application is primarily intended to demonstrate a
 *	model for handling documents.  Basic parts of a PC/GEOS application
 *	are not documented heavily here.  See the "Hello" sample application
 *	for more detailed documentation on the standard parts of a PC/GEOS
 *	application.
 *
 * RCS STAMP:
 *	$Id: multview.ui,v 1.1 97/04/04 16:33:10 newdeal Exp $
 *
 */

/* ----------------------------------------------------------------------------
	Include files
-----------------------------------------------------------------------------*/

#include "generic.uih"

/* ----------------------------------------------------------------------------
	Application Object
-----------------------------------------------------------------------------*/

start	AppResource;

MVApp = GenApplication {
	children = MVPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) = MVPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_APP_STARTUP) =
		MVDocumentControl, MVDisplayControl;
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST) =
		MVDocumentControl, MVDisplayControl;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_SELF_LOAD_OPTIONS) =
		MVDocumentControl, MVDisplayControl;
	moniker = list {
		MVTextMoniker
	}
	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY {
		char	"multview", 0
		}
	}
}

visMoniker MVTextMoniker = "MultView Sample Application";

end	AppResource;

/* ----------------------------------------------------------------------------
	 Primary window.
-----------------------------------------------------------------------------*/

/*
 * The GenDocumentControl must be a child of the primary, and must be
 * on the active list.
 */

start	Interface;		/* this resource is for misc UI objects */

MVPrimary = GenPrimary {
	children = FileMenu, WindowMenu, MVDisplayGroup;
	hints = { ATTR_GEN_DISPLAY_NOT_MINIMIZABLE, HINT_SIZE_WINDOW_AS_DESIRED }
}

FileMenu = GenInteraction {
	children = MVDocumentControl;
	visibility = popup;
	hints = { ATTR_GEN_INTERACTION_GROUP_TYPE { GenInteractionGroupType GIGT_FILE_MENU } }
}

/* --- */

WindowMenu = GenInteraction {
	children = MVDisplayControl;
	visibility = popup;
	hints = { ATTR_GEN_INTERACTION_GROUP_TYPE { GenInteractionGroupType GIGT_WINDOW_MENU } }
}

MVDisplayControl = GenDisplayControl {
}

/* ----------------------------------------------------------------------------
	 UI Document Control
-----------------------------------------------------------------------------*/

/*
 * The GenDocumentControl should be put in the same resource as the
 * GenPrimary (for efficiency).  The GenDocumentControl will build the
 * file menu.
 */

MVDocumentControl = GenDocumentControl {
	/*
	 * Document file's token
	 */
	documentTokenChars = "'S', 'A', 'M', 'P'";
	documentTokenID = "MANUFACTURER_ID_GEOWORKS";
	/*
	 * Text to display in the header area of the primary when no
	 * document is open.
	 */
	noNameText = "No Document";
	/*
	 * Template directory
	 */
	templateDir = "SAMPLE";
	/*
	 * Special attributes: allow multiple open files, don't display
	 * name on primary (this will be handled via the display control)
	 */
	dcAttributes = default +multipleOpenFiles;
	features = default -displayNameOnPrimary;
	/*
	 * Pointer to the GenDocumentGroup
	 */
	appDC = MVDocumentGroup;
	/*
	 * Set one special moniker
	 */
	dialogOpenMoniker = MVOpenMoniker;
}

visMoniker MVOpenMoniker =   {
	cachedSize = 64, 40;
	gstring {
		GSBeginString
		GSRelMoveTo	0, 0
		GSDrawRectTo	64, 40
		GSRelMoveTo	10, 15
		GSDrawTextAtCP	<MV Open>
		GSEndString
	}
}

end Interface;

/* ----------------------------------------------------------------------------
	 Document Group
-----------------------------------------------------------------------------*/

/*
 * The GenDocumentGroup should be put in a resource of its own run by
 * the application thread.
 */

start DocumentUI;

MVDocumentGroup = GenDocumentGroup {
	/*
	 * Name for untitled documents
	 */
	defaultName = "MV Untitled";
	/*
	 * Class for document objects
	 */
	documentClass = "MVDocumentClass";
	/*
	 * GenDisplay to automatically duplicate, GenDisplayGroup to attach
	 * duplicated displays to and GenView to attach documents to
	 */
	genDisplayGroup = MVDisplayGroup;
	genDisplay = MVDisplay;
	genView = MVView;
	/*
	 * Document protocol
	 */
	protocolMajor = "DOCUMENT_PROTOCOL_MAJOR";
	protocolMinor = "DOCUMENT_PROTOCOL_MINOR";
	/*
	 * Pointer to the GenDocumentControl
	 */
	uiDC = MVDocumentControl;
}

end DocumentUI;

/*-----------------------------------------------------------------------------
	GenDisplayGroup object
-----------------------------------------------------------------------------*/

start Interface;

MVDisplayGroup = GenDisplayGroup {
	/*
	 * Tell the display control to put the name of the target display
	 * in the long term moniker of the primary
	 */

	hints = {
		HINT_DEFAULT_FOCUS, HINT_DEFAULT_TARGET,
		HINT_INITIAL_SIZE {
			SpecWidth <SST_PCT_OF_FIELD_WIDTH, PCT_70>,
			SpecHeight <SST_PCT_OF_FIELD_HEIGHT, PCT_55>,
			0
		}
	}
}

end Interface;

/*-----------------------------------------------------------------------------
	Block to duplicate for each document
-----------------------------------------------------------------------------*/

start DisplayUI;

MVDisplay = GenDisplay {
	/*
	 * The display starts not usable since it is not in the generic tree.
	 * It will be set usable after being added to the display control.
	 */
	genStates = default -usable;
	children = MVView;
	hints = { ATTR_GEN_DISPLAY_NOT_MINIMIZABLE }
}

MVView = GenView {
	docRight = "DOCUMENT_WIDTH";
	docBottom = "DOCUMENT_HEIGHT";
	horizAttributes = default +scrollable;
	vertAttributes = default +scrollable;
}

end DisplayUI;
