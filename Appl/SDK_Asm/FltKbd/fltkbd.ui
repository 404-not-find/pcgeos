/***********************************************************************
 *
 *	Copyright (c) Geoworks 1996 -- All Rights Reserved
 *
 * PROJECT:	  Floating Keyboard Sample App
 * MODULE:	  
 * FILE:	  fltkbd.ui
 *
 * AUTHOR:  	  Allen Yuen, Jun 26, 1996
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	AY	6/26/96   	Initial version
 *
 * DESCRIPTION:
 *	Sample app to demonstrate how to implement a floating keyboard outside
 *	of the UI
 *
 * 	$Id: fltkbd.ui,v 1.1 97/04/04 16:35:38 newdeal Exp $
 *
 ***********************************************************************/

#include "generic.uih"
#include "ark.uih"
#include "fltkbd.uih"

start	AppResource;

FltKbdApp = FltKbdApplication {
	children = FltKbdInteraction;
}

end	AppResource;

start	Interface;

FltKbdInteraction = FloatingKbdWindow {
	moniker = "Floating Keyboard";
	visibility = dialog;
	type = multipleResponse;
	children = FltKbdTempText, FltKbdMain; 
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}

FltKbdMain = GenInteraction {
	children = FltKbdView, FltKbdOther;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

FltKbdOther = GenInteraction {
	children = FltKbdButtons, FltKbdSetCharSet;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}

FltKbdButtons = GenInteraction {
	children = FltKbdButtonsCol1, FltKbdButtonsCol2;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

FltKbdButtonsCol1 = GenInteraction {
	children = FltKbdBksp, FltKbdCR, FltKbdConvert;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}

FltKbdButtonsCol2 = GenInteraction {
	children = FltKbdDel, FltKbdConfirm, FltKbdSpace;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}


FltKbdSetCharSet = GenItemGroup ignoreDirty {
	destination = process;
	applyMsg = MSG_FKP_SET_CHAR_GROUP;
	children = AGroup, BGroup, CGroup, DGroup;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

AGroup = GenItem ignoreDirty {
	moniker = "A";
	identifier = GROUP_A;
}
BGroup = GenItem ignoreDirty {
	moniker = "B";
	identifier = GROUP_B;
}
CGroup = GenItem ignoreDirty {
	moniker = "C";
	identifier = GROUP_C;
}
DGroup = GenItem ignoreDirty {
	moniker = "D";
	identifier = GROUP_D;
}


FltKbdTempText = FltKbdText {
	moniker = "Temp Text:";
	textDisplayAttributes = default +singleLineText;
	maxLength = 25;
	hints = {
		ATTR_GEN_TEXT_CURSOR_NO_FOCUS
	}
}

FltKbdBksp = GenTrigger {
	moniker = "Bksp";
	action = MSG_FKP_PROCESS_BUTTON_PRESS, process;
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
SBCS <			word	(CS_BSW shl 8) or (VC_BACKSPACE)	>
DBCS <			word	(C_SYS_BACKSPACE)			>
		}
	}
}

FltKbdCR = GenTrigger {
	moniker = "CR";
	action = MSG_FKP_PROCESS_BUTTON_PRESS, process;
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
SBCS <			word	(CS_BSW shl 8) or (VC_ENTER)		>
DBCS <			word	(C_SYS_FEP_ENTER)			>
		}
	}
}

FltKbdConvert = GenTrigger {
	moniker = "Trans";
	action = MSG_FKP_PROCESS_BUTTON_PRESS, process;
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
SBCS <			word	(CS_BSW shl 8) or (C_SPACE)		>
DBCS <			word	(C_SPACE)				>
		}
	}
}

FltKbdDel = GenTrigger {
	moniker = "Del";
	action = MSG_FKP_PROCESS_BUTTON_PRESS, process;
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
SBCS <			word	(CS_BSW shl 8) or (VC_DEL)		>
DBCS <			word	(C_SYS_DELETE)				>
		}
	}
}

FltKbdConfirm = GenTrigger {
	moniker = "Confirm";
	action = MSG_FKP_PROCESS_BUTTON_PRESS, process;
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
SBCS <			word	(CS_BSW shl 8) or (VC_ENTER)		>
DBCS <			word	(C_SYS_FEP_ENTER)			>
		}
	}
}

FltKbdSpace = GenTrigger {
	moniker = "Space";
	action = MSG_FKP_PROCESS_BUTTON_PRESS, process;
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
SBCS <			word	(CS_BSW shl 8) or (C_SPACE)		>
DBCS <			word	(C_SPACE)				>
		}
	}
}

FltKbdView = GenView {
	output = Keyboard;
	viewAttributes = default +viewSizeFollowsContentSize,
			 +dontSendPtrEvents;
}

Keyboard = FltKbdContent {
	right = KEYBOARD_WIDTH;
	bottom = KEYBOARD_HEIGHT;
	visCompGeoAttrs = default +customManageChildren;
	view = FltKbdView;
}

end	Interface;
