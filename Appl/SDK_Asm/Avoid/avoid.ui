/*
 * Project:	PC/GEOS
 * Module:	Avoid (Sample PC/GEOS application)
 * File:	avoid.ui
 *
 * Author:	Doug Fults
 *
 *	This file contains the user Interface description for this
 * application. This file is written in a language called ESPIRE, which is
 * compiled by the UIC compiler. See Spec/User/uic.doc for info.
 *
 * RCS STAMP:
 *	$Id: avoid.ui,v 1.1 97/04/04 16:33:59 newdeal Exp $
 *
 */

/* ----------------------------------------------------------------------------
	Include files
-----------------------------------------------------------------------------*/

/* first, read pre-defined class definitions. See the file
 * /staff/pcgeos/Include/generic.uih. */

#include "generic.uih"

class	AvoidApplication = GenApplication {
}

class	AvoidFloatingDialog = GenInteraction {
}

/* ----------------------------------------------------------------------------
	Application Object
-----------------------------------------------------------------------------*/
/* The very top-level generic object of an application MUST be a GenApplication
 * object. The avoid.gp file contains "appobj" statement which indicates
 * that this "AvoidApp" object is in fact the top-level UI object.
 * This object should be in its own resource so that GeoManager can load it
 * quickly to grab the icon for this application. GeoManager will store this
 * icon in its "Token Database", so that the icon image can be reused quickly.*/

start	AppResource;

AvoidApp = AvoidApplication {
	/* The geoManager uses this token information to store this
	 * application's icon in a database. */

					/* four letters used as index to
					 * refer to this application in the
					 * token database. */

					/* manufacturer ID number. Also used
					 * for accessing database, to ensure
					 * uniqueness. */

	children = AvoidPrimary;	/* The GenApplication object only
					 * has one child: the main application
					 * window. */

	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) = AvoidPrimary;
					/* The main window should appear
					 * on-screen when the application
					 * is launched. */

	moniker = list {		/* The application object has a list
					 * of visual monikers */
		AvoidTextMoniker	/* Used in the express menu and in
					 * the header for the primary window */
	}
}

visMoniker AvoidTextMoniker = "Avoid Sample App";

end	AppResource;


/* ----------------------------------------------------------------------------
	 Primary window.
-----------------------------------------------------------------------------*/
/* Every application has at least one GenPrimary object. This object serves
 * as the main window of the application. Inside this window, various UI
 * components, such as menus or lists, can be placed.
 */

start	Interface;		/* this resource is for misc UI objects */

AvoidPrimary = GenPrimary {
	children = AvoidTitle, AvoidText, AvoidOptionsBooleanGroup, AvoidSpace;

	/* At present, we are not supplying the icon image for this application,
	 * (just the text moniker given above), so we want to prevent the
	 * user from minimizing this window. */

	hints = {
		ATTR_GEN_DISPLAY_NOT_MINIMIZABLE,
		HINT_FULL_JUSTIFY_CHILDREN_VERTICALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}

end Interface;


/*-----------------------------------------------------------------------------
	GenBooleanGroup object
-----------------------------------------------------------------------------*/

start Interface;

AvoidTitle = GenGlyph {
	moniker = TitleMoniker;
}

visMoniker TitleMoniker = {
	gstring {
		GSBeginString
		GSSetFont 	FID_DTC_URW_SANS, 18, 0
		GSDrawText	0, 5, <Avoid>
		GSEndString
	}
}


AvoidText = GenText {
	text = "Welcome to Avoid, the sample application that demonstrates how to properly avoid being transparently detached -- not just by marking the application AS_AVOID_TRANSPARENT_DETACH, but also by putting up what appears to be a desk accessory representing the background activity.  The psuedo-desk-accessory gives the user the options to return to the full application, or to terminate the operation in progress.  The dialog presented here is simple, but could as elaborate or icon-oriented as desired.  To simulate a background operation, select the checkbox below, and switch to another application.";
	genAttributes = default +readOnly;
}

AvoidOptionsBooleanGroup = GenBooleanGroup {
	children = AvoidAvoidTransparentDetachBoolean;
	destination = AvoidApp;
	applyMsg = MSG_AVOID_APP_OPTIONS_CHANGE;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}

AvoidAvoidTransparentDetachBoolean = GenBoolean {
	moniker = "Operation in progress";
	identifier = "mask 	ABID_OPERATION_IN_PROGRESS";
}

AvoidSpace = GenGlyph {
	moniker = " ";
}

end Interface;


/*-----------------------------------------------------------------------------
	Active dialog
-----------------------------------------------------------------------------*/
start ActiveDialog, notDetachable;

/* Since this box should be unobtrusive & remain reasonably attractive while
 * hanging around screen, The "Go To" moniker should be replaced where
 * possible with the application's icon, & the "Quit operation" moniker with
 * a similar sized icon, as long as it's possible to design something that
 * conveys the right meaning (Perhaps two plugs being pulled apart for 
 * disconnect?)  It would also be wise to provide a confirmation dialog,
 * to make sure the user really wants to cease the operation.
 */

AvoidActiveDialog = AvoidFloatingDialog {
	moniker = "Avoid";
	genStates = default -usable;
	type = command;
	visibility = dialog;
	attributes = default +notUserInitiatable;
	children = AvoidGoToTrigger, AvoidQuitTrigger;
	hints = {
		ATTR_GEN_WINDOW_CUSTOM_LAYER_ID {
			word	0
		},
		ATTR_GEN_WINDOW_CUSTOM_LAYER_PRIORITY {
			byte	LAYER_PRIO_ON_TOP
		},
		HINT_KEEP_INITIALLY_ONSCREEN,
		HINT_POSITION_WINDOW_AT_RATIO_OF_PARENT {
		SpecWinSizePair < mask SWSS_RATIO or PCT_40, \
				  mask SWSS_RATIO or PCT_70>
		}
	}
}

AvoidGoToTrigger = GenTrigger {
	moniker = "Go to Avoid";
	action = MSG_AVOID_APP_GOTO_AVOID, AvoidApp;
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}
AvoidQuitTrigger = GenTrigger {
	moniker = "Quit operation";
	action = MSG_AVOID_APP_QUIT_OPERATION, AvoidApp;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word	IC_DISMISS
		}
	}
}

end ActiveDialog;

