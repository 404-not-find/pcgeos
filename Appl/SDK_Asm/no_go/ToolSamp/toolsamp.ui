/*
 * Project:	PC/GEOS
 * Module:	ToolSamp (Sample PC/GEOS application)
 * File:	toolsamp.ui
 *
 * Author:	Doug Fults
 *
 *	This file contains the user Interface description for this
 * application. This file is written in a language called ESPIRE, which is
 * compiled by the UIC compiler. See Spec/User/uic.doc for info.
 *
 * IMPORTANT NOTE:
 *	This sample application is primarily intended to demonstrate the
 *	GenToolControl object.  Basic parts of a PC/GEOS application
 *	are not documented heavily here.  See the "Hello" sample application
 *	for more detailed documentation on the standard parts of a PC/GEOS
 *	application.
 *
 * RCS STAMP:
 *	$Id: toolsamp.ui,v 1.1 97/04/04 16:34:51 newdeal Exp $
 *
 */

/* ----------------------------------------------------------------------------
	Include files
-----------------------------------------------------------------------------*/

#include "generic.uih"
#include "Objects/styles.uih"
#include "Objects/Text/tCtrl.uih"


/* ----------------------------------------------------------------------------
	Application Object
-----------------------------------------------------------------------------*/

start	AppResource;

TSApp = GenApplication {
	children = TSPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) = TSPrimary,
				TSDisplay1, TSDisplay2,
				TSToolbox;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_STARTUP_LOAD_OPTIONS) =
			TSToolControl; 
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST) =
			TSToolControl,
			TSDisplayControl;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_SELF_LOAD_OPTIONS) =
			TSEditControl,  /* TSToolControl, */
			TSFontControl, TSTextStyleControl, TSPointSizeControl,
			TSDisplayControl; 
	moniker = list {
		TSTextMoniker
	}
	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY {
		char	"toolsamp", 0
		}
	}
}

visMoniker TSTextMoniker = "GenToolControl Sample Application";

end	AppResource;

/* ----------------------------------------------------------------------------
	 Primary window, shell menu & primary structures
-----------------------------------------------------------------------------*/

/*
 * The GenUIDocumentControl must be a child of the primary, and must be
 * on the active list.
 */

start	Interface;		/* this resource is for misc UI objects */

TSPrimary = GenPrimary {
	children = EditMenu, OptionsMenu,
			TSFontControl, TSTextStyleControl, TSPointSizeControl,
			TSTopHorizBar, TSPrimaryMiddle, TSBottomHorizBar;

	hints = { ATTR_GEN_DISPLAY_NOT_MINIMIZABLE, HINT_SIZE_WINDOW_AS_DESIRED }
}

EditMenu = GenInteraction {
	children = TSEditControl;
	visibility = popup;
	hints = { ATTR_GEN_INTERACTION_GROUP_TYPE { GenInteractionGroupType GIGT_EDIT_MENU } }
}

OptionsMenu = GenInteraction {
	children = TSToolbox, TSToolControl, TSSaveOptionsButton;
	visibility = popup;
	hints = { ATTR_GEN_INTERACTION_GROUP_TYPE { GenInteractionGroupType GIGT_OPTIONS_MENU } }
}

TSSaveOptionsButton = GenTrigger {
	moniker = 'S', "Save options";
	action = MSG_META_SAVE_OPTIONS, TSApp;
}

TSPrimaryMiddle = GenInteraction {
	children = TSLeftVertBar, TSDisplayControl, TSRightVertBar;
	hints = { HINT_ORIENT_CHILDREN_HORIZONTALLY }
}


/* ----------------------------------------------------------------------------
	 Toolbox areas
-----------------------------------------------------------------------------*/

TSToolbox = GenInteraction {
	moniker = 'T', "Tools";
	visibility = dialog;
	hints = {
/*		HINT_WINDOW_MENU, */
		HINT_CENTER_WINDOW,
		HINT_INTERACTION_MAKE_RESIZABLE,
		HINT_TOOLBOX,
		HINT_ALLOW_CHILDREN_TO_WRAP,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY
	}
}
chunk TSToolboxName = "Floating Toolbox";

TSTopHorizBar = GenInteraction {
	hints = {
		HINT_TOOLBOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ALLOW_CHILDREN_TO_WRAP,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY
	}
}
chunk TSTopHorizBarName = "Top of Primary";

TSBottomHorizBar = GenInteraction {
	hints = {
		HINT_TOOLBOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ALLOW_CHILDREN_TO_WRAP,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY
	}
}
chunk TSBottomHorizBarName = "Bottom of Primary";


TSLeftVertBar = GenInteraction {
	hints = {
		HINT_TOOLBOX,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_ALLOW_CHILDREN_TO_WRAP,
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_FULL_JUSTIFY_CHILDREN_VERTICALLY
	}
}
chunk TSLeftVertBarName = "Left side of Primary";

TSRightVertBar = GenInteraction {
	hints = {
		HINT_TOOLBOX,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_ALLOW_CHILDREN_TO_WRAP,
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_FULL_JUSTIFY_CHILDREN_VERTICALLY
	}
}
chunk TSRightVertBarName = "Right side of Primary";


TSTopOfDisplayBar = GenInteraction {
	genStates = default -usable;
	hints = {
		HINT_TOOLBOX,
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}
chunk TSTopOfDisplayBarName = "Top of Display";

TSInScrollbar = GenInteraction {
	genStates = default -usable;
	hints = {
		HINT_TOOLBOX,
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}
chunk TSInScrollbarName = "Next to Scrollbar";


/*-----------------------------------------------------------------------------
	GenToolControl object
-----------------------------------------------------------------------------*/

TSToolControl = GenToolControl {
	toolboxList = TSToolboxList;
	hints = { HINT_SAME_CATEGORY_AS_PARENT }
}
chunk TSToolboxList = data {
	UN_OPTR	TSToolbox
	UN_OPTR	TSToolboxName
	UN_OPTR	TSTopHorizBar
	UN_OPTR TSTopHorizBarName
	UN_OPTR	TSBottomHorizBar
	UN_OPTR TSBottomHorizBarName
	UN_OPTR	TSLeftVertBar
	UN_OPTR	TSLeftVertBarName
	UN_OPTR	TSRightVertBar
	UN_OPTR	TSRightVertBarName
	UN_OPTR	TSTopOfDisplayBar
	UN_OPTR	TSTopOfDisplayBarName
	UN_OPTR	TSInScrollbar
	UN_OPTR	TSInScrollbarName
}


/*-----------------------------------------------------------------------------
	Other UI controller objects used in this application (to provide a
	source of tools for the GenToolControl object to manage)
-----------------------------------------------------------------------------*/


TSEditControl = GenEditControl {
	hints = { HINT_SAME_CATEGORY_AS_PARENT }
}


TSFontControl = FontControl {
	moniker = 'n', "Fonts";
	visibility = popup;
}

TSTextStyleControl = TextStyleControl {
	moniker = 'S', "Styles";
	visibility = popup;
}

TSPointSizeControl = PointSizeControl {
	moniker = 'z', "Sizes";
	visibility = popup;
}

/*-----------------------------------------------------------------------------
	GenDisplayControl object
-----------------------------------------------------------------------------*/

TSDisplayControl = GenDisplayControl {
	children = TSDisplay1, TSDisplay2;
	hints = {
		HINT_DEFAULT_FOCUS, HINT_DEFAULT_TARGET,
		HINT_INITIAL_SIZE {
			SpecWidth <SST_PCT_OF_FIELD_WIDTH, PCT_70>,
			SpecHeight <SST_PCT_OF_FIELD_HEIGHT, PCT_55>,
			0
		}
	}
}

/*-----------------------------------------------------------------------------
	Display #1
-----------------------------------------------------------------------------*/

TSDisplay1 = GenDisplay {
	moniker = "Display #1";
	children = TSText1, TSView1;
	hints = { ATTR_GEN_DISPLAY_NOT_MINIMIZABLE,
		  ATTR_GEN_DISPLAY_TRAVELING_OBJECTS {
			word offset TSTravelingObjects1
		  }
	}

}
chunk TSTravelingObjects1 = data {
	UN_OPTR	TSTopOfDisplayBar
	word	offset	TSDisplay1
	word	mask CCF_MARK_DIRTY or CCO_FIRST
	UN_OPTR	TSInScrollbar
	word	offset	TSInScrollbarToolArea1
	word	mask CCF_MARK_DIRTY or CCO_FIRST
}

TSText1 = GenText {
	genAttributes = default +targetable;
	textDisplayAttributes = default -useTabForNavigation, +initScrolling;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_DEFAULT_TARGET,
		HINT_DEFAULT_FOCUS,
		HINT_TEXT_WHITE_WASH_COLOR
	}
}

TSView1 = GenView {
	children = TSInScrollbarToolArea1;
	docBottom = 16;
	vertAttributes = default +noLargerThanContent;
	horizAttributes = default +scrollable;
}
TSInScrollbarToolArea1 = GenInteraction {
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_SEEK_X_SCROLLER_AREA
	}
}

/*-----------------------------------------------------------------------------
	Display #2
-----------------------------------------------------------------------------*/

TSDisplay2 = GenDisplay {
	moniker = "Display #2";
	children =  TSText2, TSView2;
	hints = { ATTR_GEN_DISPLAY_NOT_MINIMIZABLE,
		  ATTR_GEN_DISPLAY_TRAVELING_OBJECTS {
			word offset TSTravelingObjects2
		  }
	}

}
chunk TSTravelingObjects2 = data {
	UN_OPTR	TSTopOfDisplayBar
	word	offset	TSDisplay2
	word	mask CCF_MARK_DIRTY or CCO_FIRST
	UN_OPTR	TSInScrollbar
	word	offset	TSInScrollbarToolArea2
	word	mask CCF_MARK_DIRTY or CCO_FIRST
}

TSText2 = GenText {
	genAttributes = default +targetable;
	textDisplayAttributes = default -useTabForNavigation, +initScrolling;
	hints = {
		HINT_DEFAULT_TARGET,
		HINT_DEFAULT_FOCUS,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_TEXT_WHITE_WASH_COLOR
	}
}

TSView2 = GenView {
	children = TSInScrollbarToolArea2;
	docBottom = 16;
	vertAttributes = default +noLargerThanContent;
	horizAttributes = default +scrollable;
}
TSInScrollbarToolArea2 = GenInteraction {
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_SEEK_X_SCROLLER_AREA
	}
}

end Interface;
