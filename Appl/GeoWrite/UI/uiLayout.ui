/*
 * Project:	GeoWrite
 * File:	UI/uiLayout.ui
 *
 * Author:	Tony Requist
 *
 *	This file contains the user interface description for the page
 * layout dialog box.
 *
 *	$Id: uiLayout.ui,v 1.1 97/04/04 15:55:39 newdeal Exp $
 *
 */

#define GEOWRITE_SECTION_LIST(mon, addhints) GenDynamicList vardataReloc {\
	moniker = mon;\
	queryMsg = MSG_WRITE_DOCUMENT_QUERY_SECTION_LIST;\
	destination = "TO_APP_MODEL";\
	genAttributes = default +notifyVisibility;\
	hints = {\
		addhints,\
		HINT_PLACE_MONIKER_ABOVE,\
		ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass },\
		HINT_ITEM_GROUP_SCROLLABLE,\
		HINT_FIXED_SIZE {\
			SpecWidth <SST_AVG_CHAR_WIDTHS, 40> | \
			SpecHeight <SST_LINES_OF_TEXT, 5> | \
			word    5\
		},\
		ATTR_GEN_ITEM_GROUP_CUSTOM_DOUBLE_PRESS { word 0 },\
		ATTR_GEN_VISIBILITY_MESSAGE {\
			word MSG_WRITE_APPLICATION_INIT_SECTION_LIST\
		}\
	}\
}


/*-----------------------------------------------------------------------------
	Layout menu
-----------------------------------------------------------------------------*/

start PrimaryUI;

LayoutMenu = GenInteraction {
	genStates = default -enabled;
	moniker = 'L', "Layout";
	children =	HeaderFooterSubGroup, TitlePageSubGroup,
			CreateFrameSubGroup, RulerSubGroup,
			EditMasterPageTrigger, PageSubMenu

#if _SECTION_SUPPORT
		   ,SectionSubMenu
#endif
#ifdef DO_PIZZA
		  ,WriteFixedSpacingControl
#endif
		  ,RecalcTrigger;
	visibility = popup;
}

end PrimaryUI;

start LayoutMenuUI;

HeaderFooterSubGroup = GenInteraction {
	moniker = 'H', "Header/Footer";
	visibility = popup;
	children = EditHeaderTrigger, EditFooterTrigger;
}

EditHeaderTrigger = GenTrigger vardataReloc {
	moniker = 'H', "Edit Header";
	action = MSG_WRITE_DOCUMENT_EDIT_HEADER, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass } }
}

EditFooterTrigger = GenTrigger vardataReloc {
	moniker = 'F', "Edit Footer";
	action = MSG_WRITE_DOCUMENT_EDIT_FOOTER, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass } }
}

/***/

TitlePageSubGroup = GenInteraction {
	moniker = 'T', "Title Page";
	genAttributes = default +notifyVisibility;
	children = CreateTitlePageTrigger, GotoTitlePageTrigger,
		   DeleteTitlePageTrigger;
	visibility = popup;
	hints = {
		ATTR_GEN_VISIBILITY_DATA { word 0, mask GV_TITLE_PAGE_MENU }
	}
}

CreateTitlePageTrigger = GenTrigger vardataReloc {
	moniker = 'C', "Create Title Page";
	action = MSG_WRITE_DOCUMENT_CREATE_TITLE_PAGE, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass } }
}

GotoTitlePageTrigger = GenTrigger vardataReloc {
	moniker = 'G', "Go to Title Page";
	action = MSG_WRITE_DOCUMENT_GOTO_TITLE_PAGE, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass } }
}

DeleteTitlePageTrigger = GenTrigger vardataReloc {
	moniker = 'D', "Delete Title Page";
	action = MSG_WRITE_DOCUMENT_DELETE_TITLE_PAGE, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass } }
}

/***/

CreateFrameSubGroup = GenInteraction {
	children = CreateFrameTrigger;
}

CreateFrameTrigger = GenTrigger {
	moniker = 'r', "Create Graphic Frame";
	action = MSG_WRITE_APPLICATION_CREATE_GRAPHICS_FRAME, WriteApp;
}

/***/

RulerSubGroup = GenInteraction {
	children = WriteGridSpacingControl, WriteRulerGuideControl;
}

WriteGridSpacingControl = RulerGridControl {
	moniker = 'G', "Grids";
	type = properties;
	visibility = dialog;
}

WriteRulerGuideControl = RulerGuideControl {
	moniker = 'u', "Guides";
	type = properties;
	visibility = dialog;
}

/***/

EditMasterPageTrigger = GenTrigger vardataReloc {
	moniker = 'M', "Edit Master Page";
	action = MSG_WRITE_DOCUMENT_EDIT_MASTER_PAGE, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass } }
}

/***/

PageSubMenu = GenInteraction {
	moniker = 'P', "Page Layouts";
	children = InsertPageTrigger, AppendPageTrigger, DeletePageTrigger;
	visibility = popup;
}

end LayoutMenuUI;

start LayoutDialogUI;

InsertPageTrigger = GenTrigger vardataReloc {
	moniker = 'I', "Insert (Before This One)";
	action = MSG_WRITE_DOCUMENT_INSERT_PAGE, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass } }
}

AppendPageTrigger = GenTrigger vardataReloc {
	moniker = 'A', "Append (After This One)";
	action = MSG_WRITE_DOCUMENT_APPEND_PAGE, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass } }
}

DeletePageTrigger = GenTrigger vardataReloc {
	moniker = 'D', "Delete";
	action = MSG_WRITE_DOCUMENT_DELETE_PAGE, "TO_APP_MODEL";
	genAttributes = default +notifyVisibility;
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass },
		ATTR_GEN_VISIBILITY_DATA { word 0, mask GV_DELETE_PAGE }
	}
}

end LayoutDialogUI;

/***/

#if _SECTION_SUPPORT

start LayoutMenuUI;

SectionSubMenu = GenInteraction {
	moniker = 'S', "Section";
	children = InsertSectionDialog, AppendSectionDialog,
		   DeleteSectionDialog, SectionOpSubGroup,
		   ReapplyMasterPageDialog;
	visibility = popup;
	genAttributes = default +notifyVisibility;
	hints = { ATTR_GEN_VISIBILITY_DATA { word 0, mask GV_SECTION_MENU } }
}

end LayoutMenuUI;

start LayoutDialogUI;

InsertSectionDialog = GenInteraction {
	moniker = 'I', "Insert Section";
	children = InsertSectionList, InsertSectionText, DoInsertSectionTrigger;
	visibility = dialog;
	type = command;
	attributes = modal;
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbInsSec", 0 }
	}
}

InsertSectionList =
	GEOWRITE_SECTION_LIST("Section to Insert Before:",)

InsertSectionText = GenText {
	moniker = "New Section Name:";
	maxLength = "MAX_SECTION_NAME_SIZE";
	textDisplayAttributes = default +singleLineText;
	genAttributes = default +notifyVisibility;
	hints = {
		ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS {
			word	offset DoInsertSectionTrigger, 0
		},
		ATTR_GEN_VISIBILITY_MESSAGE {
			word MSG_WRITE_APPLICATION_INIT_TEXT_OBJECT
		},
		HINT_DEFAULT_FOCUS
	}
}

DoInsertSectionTrigger = GenTrigger vardataReloc {
	genStates = default -enabled;
	moniker = 'I', "Insert Section";
	genAttributes = default +signalInteractionComplete;
	action = MSG_WRITE_DOCUMENT_INSERT_SECTION, "TO_APP_MODEL";
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass },
		HINT_SEEK_REPLY_BAR, HINT_DEFAULT_DEFAULT_ACTION
	}
}

AppendSectionDialog = GenInteraction {
	moniker = 'A', "Append Section";
	children = AppendSectionList, AppendSectionText, DoAppendSectionTrigger;
	visibility = dialog;
	type = command;
	attributes = modal;
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbAppSec", 0 }
	}
}

AppendSectionList =
	GEOWRITE_SECTION_LIST("Section to Append After:",)

AppendSectionText = GenText {
	moniker = "New Section Name:";
	maxLength = "MAX_SECTION_NAME_SIZE";
	textDisplayAttributes = default +singleLineText;
	genAttributes = default +notifyVisibility;
	hints = {
		ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS {
			word	offset DoAppendSectionTrigger, 0
		},
		ATTR_GEN_VISIBILITY_MESSAGE {
			word MSG_WRITE_APPLICATION_INIT_TEXT_OBJECT
		},
		HINT_DEFAULT_FOCUS
	}
}

DoAppendSectionTrigger = GenTrigger vardataReloc {
	genStates = default -enabled;
	moniker = 'A', "Append Section";
	action = MSG_WRITE_DOCUMENT_APPEND_SECTION, "TO_APP_MODEL";
	genAttributes = default +signalInteractionComplete;
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass },
		HINT_SEEK_REPLY_BAR, HINT_DEFAULT_DEFAULT_ACTION
	}
}

DeleteSectionDialog = GenInteraction {
	moniker = 'D', "Delete Section";
	children = DeleteSectionList, DoDeleteSectionTrigger;
	visibility = dialog;
	type = command;
	attributes = modal;
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbDelSec", 0 }
	}
}

DeleteSectionList = GEOWRITE_SECTION_LIST("Section to Delete",)

DoDeleteSectionTrigger = GenTrigger vardataReloc {
	moniker = 'D', "Delete";
	action = MSG_WRITE_DOCUMENT_DELETE_SECTION, "TO_APP_MODEL";
	genAttributes = default +signalInteractionComplete;
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass },
		HINT_SEEK_REPLY_BAR, HINT_DEFAULT_DEFAULT_ACTION
	}
}

SectionOpSubGroup = GenInteraction {
	children = RenameSectionDialog, SetFirstSectionDialog;
}

RenameSectionDialog = GenInteraction {
	moniker = 'R', "Rename Section";
	children = RenameSectionList, RenameSectionText,
		   DoRenameSectionTrigger;
	visibility = dialog;
	type = command;
	attributes = modal;
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbRenSec", 0 }
	}
}

RenameSectionList =
	GEOWRITE_SECTION_LIST("Section to Rename",ATTR_GEN_ITEM_GROUP_STATUS_MSG {word MSG_WRITE_DOCUMENT_UPDATE_RENAME_SECTION })

RenameSectionText = GenText {
	moniker = "New Name:";
	maxLength = "MAX_SECTION_NAME_SIZE";
	textDisplayAttributes = default +singleLineText;
	hints = {
		ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS {
			word	offset DoRenameSectionTrigger, 0
		},
		HINT_DEFAULT_FOCUS
	}
}

DoRenameSectionTrigger = GenTrigger vardataReloc {
	genStates = default -enabled;
	moniker = 'R', "Rename";
	action = MSG_WRITE_DOCUMENT_RENAME_SECTION, "TO_APP_MODEL";
	genAttributes = default +signalInteractionComplete;
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass },
		HINT_SEEK_REPLY_BAR, HINT_DEFAULT_DEFAULT_ACTION
	}
}

SetFirstSectionDialog = GenInteraction {
	moniker = 'S', "Set Starting Section #";
	children = SetFirstSectionValue;
	type = properties;
	visibility = dialog;
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbSetFSec", 0 }
	}
}

SetFirstSectionValue = GenValue vardataReloc {
	moniker = "Starting Section Number:";
	destination = "TO_APP_MODEL";
	applyMsg = MSG_WRITE_DOCUMENT_SET_STARTING_SECTION;
	genAttributes = default +notifyVisibility;
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass },
		ATTR_GEN_VISIBILITY_DATA { word 0, mask GV_SET_FIRST_SECTION }
	}
}

ReapplyMasterPageDialog = GenInteraction {
	moniker = 'M', "Reapply Master Page";
	children = ReapplyText, ResetReapplyTrigger, ReapplyOnlyTrigger;
	attributes = modal;
	type = command;
	visibility = dialog;
	/*
	 * No help context here (since this is like a confirmation box)
	 */
}

ReapplyText = GenText {
	genAttributes = default +readOnly;
	text = "Do you wish to reset the master page before reapplying it"
	       " to all the page layouts in this section, or do you wish"
	       " to reapply the existing master page without resetting it?";
}

ResetReapplyTrigger = GenTrigger vardataReloc {
	moniker = 'R', "Reset, Then Reapply";
	action = MSG_WRITE_DOCUMENT_RESET_REAPPLY_MASTER_PAGE, "TO_APP_MODEL";
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass }
	}
}

ReapplyOnlyTrigger = GenTrigger vardataReloc {
	moniker = 'E', "Reapply Existing";
	action = MSG_WRITE_DOCUMENT_REAPPLY_EXISTING_MASTER_PAGE,
								"TO_APP_MODEL";
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass }
	}
}

end LayoutDialogUI;

#endif

/***/

start LayoutMenuUI;

#ifdef DO_PIZZA
WriteFixedSpacingControl = FixedCharLinePageControl {
	moniker = 'F', "Fixed Spacing" ;
	visibility = dialog;
	type = properties;
	output = "TO_APP_TARGET" ;
}
#endif

RecalcTrigger = GenTrigger vardataReloc {
	genStates = default -enabled;
	moniker = 'c', "Recalculate Layout";
	action = MSG_WRITE_DOCUMENT_RECALC_LAYOUT, "TO_APP_MODEL";
	hints = { ATTR_GEN_DESTINATION_CLASS { UN_DD WriteDocumentClass } }
}

end LayoutMenuUI;

/***/

#if 0

start LayoutMenuUI;

ArticlesDialog = GenInteraction {
	NOT_IMPLEMENTED
	moniker = 'A', "\\xa5Articles\\xa5";
	visibility = dialog;
	type = command;
}

end LayoutMenuUI;

#endif
