/*
 * Project:	GeoWrite
 * File:	UI/uiGraphics.ui
 *
 * Author:	Tony Requist
 *
 *	This file contains the user interface description for the graphics
 * menu.
 *
 *	$Id: uiGraphics.ui,v 1.1 97/04/04 15:55:47 newdeal Exp $
 *
 */

/*-----------------------------------------------------------------------------
	Graphics menu
-----------------------------------------------------------------------------*/

start PrimaryUI;

GraphicsMenu = GenInteraction {
	moniker = 'G', "Graphic";
	children = GrAttrGroup, GrEditPopup, GrOptionsPopup,
		   TransformPopup,
		   ArrangePopup, AttributesPopup;
	visibility = popup;
}

end PrimaryUI;

start GraphicsMenuUI;

GrAttrGroup = GenInteraction {
	children = AreaAttrDialog, LineAttrDialog, WriteGrObjObscureAttrControl;
}

/***/

AreaAttrDialog = GenInteraction {
	moniker = 'A', "Area Attributes";
	type = properties;
	visibility = dialog;
#if DO_PIZZA
	children = WriteAreaColorControl;
#else
	children = WriteAreaColorControl,WriteAreaAttrControl;
#endif
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbAreaAttr", 0 }
	}
}

WriteAreaColorControl = GrObjAreaColorSelector vardataReloc {
	hints = {
#if DO_PIZZA
		HINT_GEN_CONTROL_DESTROY_CHILDREN_WHEN_NOT_INTERACTABLE
#else
		HINT_GEN_CONTROL_DESTROY_CHILDREN_WHEN_NOT_INTERACTABLE,
		ATTR_COLOR_SELECTOR_DISABLE_OBJECT {
			UN_OPTR WriteAreaAttrControl
		}
#endif
	}
}

#if DO_PIZZA
#else
WriteAreaAttrControl = GrObjAreaAttrControl {
	hints = {
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word	mask GOAACF_TRANSPARENCY
		}
	}
}
#endif


/***/

LineAttrDialog = GenInteraction {
	moniker = 'L', "Line Attributes";
	type = properties;
	visibility = dialog;
	children = WriteLineColorControl, WriteLineAttrControl;
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbLineAttr", 0 }
	}
}

WriteLineColorControl = GrObjLineColorSelector vardataReloc {
	hints = {
		HINT_GEN_CONTROL_DESTROY_CHILDREN_WHEN_NOT_INTERACTABLE,
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word	mask CSF_PATTERN
		},
		ATTR_GEN_CONTROL_PROHIBIT_TOOLBOX_UI {
			word	mask CSTF_PATTERN
		},
		ATTR_COLOR_SELECTOR_DISABLE_OBJECT {
			UN_OPTR WriteLineAttrControl
		}
	}
}

WriteLineAttrControl = GrObjLineAttrControl {
}

/***/

WriteGrObjObscureAttrControl = GrObjObscureAttrControl {
	moniker = 'W', "Wrap type";
	visibility = popup;
	hints = {
	    ATTR_GEN_CONTROL_PROHIBIT_UI {
		word	mask GOOACF_INSERT_OR_DELETE_MOVE or \
			mask GOOACF_INSERT_OR_DELETE_RESIZE or \
			mask GOOACF_INSERT_OR_DELETE_DELETE or \
			mask GOOACF_INSTRUCTIONS
	    }
	}
}

/***/

GrEditPopup = GenInteraction {
	moniker = 'E', "Edit";
	children = DuplicatePopup, PasteInsidePopup,
		   WriteHideShowControl, CreatePopup;
	visibility = popup;
}

CreatePopup = GenInteraction {
	moniker = 'C', "Create";
	children = WriteGrObjCreateControl, WriteCustomShapeControl;
	visibility = popup;
}

WriteGrObjCreateControl = GrObjCreateControl {
}

WriteCustomShapeControl = GrObjCustomShapeControl {
}

/***/

TransformPopup = GenInteraction {
	moniker = 'T', "Transform";
	children = WriteFlipControl, WriteScaleControl, WriteRotateControl,
		   WriteSkewControl, WriteTransformControl, PolylinePopup,
		   WriteConvertControl;
	visibility = popup;
}

WriteFlipControl = GrObjFlipControl {
        moniker = 'F', "Flip";
        visibility = popup;
}

WriteScaleControl = GrObjScaleControl {
        moniker = 'S', "Scale";
        visibility = popup;

}

WriteRotateControl = GrObjRotateControl {
        moniker = 'R', "Rotate";
        visibility = popup;

}

WriteSkewControl = GrObjSkewControl {
        moniker = 'k', "Skew";
        visibility = popup;

}

WriteTransformControl = GrObjTransformControl {
}

PolylinePopup = GenInteraction {
	moniker = 'P', "Polyline";
	children =
		WriteSplinePointControl, WriteSplineOpenCloseControl,
		WriteSplineSmoothnessControl;
	visibility = popup;
}

WriteSplinePointControl = SplinePointControl {
	visibility = subGroup;
}

WriteSplineOpenCloseControl = SplineOpenCloseControl {
	visibility = subGroup;
}

WriteSplineSmoothnessControl = SplineSmoothnessControl {
	moniker = 'P', "Control Points";
	visibility = popup;
}

WriteConvertControl = GrObjConvertControl {
	moniker = 'C', "Convert";
	visibility = popup;
}

/***/

ArrangePopup = GenInteraction {
	moniker = 'g', "Arrange";
	children = WriteDepthControl, WriteNudgeControl,
		   WriteAlignDistributeControl, WriteAlignToGridControl,
		   WriteGroupControl;
	visibility = popup;
}

WriteDepthControl = GrObjDepthControl {
/*	moniker = 'D', "Depth";
	visibility = popup; */
}

WriteNudgeControl = GrObjNudgeControl {
	moniker = 'M', "Move";
	visibility = popup;
}

WriteAlignDistributeControl = GrObjAlignDistributeControl {
	moniker = 'D', "Align/Distribute";
}

WriteAlignToGridControl = GrObjAlignToGridControl {
	moniker = 'A', "Align to Grid";
}

WriteGroupControl = GrObjGroupControl {
}

/***/

AttributesPopup = GenInteraction {
	moniker = 'r', "Attributes";
	children = WriteBackgroundAttrDialog, WriteGrObjStyleSheetControl,
		   GradientDialog, WriteArcControl,
#if _BITMAP_EDITING
		   WriteBitmapFormatControl,
#endif
		   ExtAttrDialog, WriteDefaultAttributesControl;
	visibility = popup;
}

WriteBackgroundAttrDialog = GenInteraction {
	moniker = 'B', "Background Color";
	type = properties;
	visibility = dialog;
	children = WriteBackgroundColorControl, WriteBackgroundAttrControl;
	hints = {
		ATTR_GEN_HELP_CONTEXT {
			char "dbBGColor", 0
		}
	}
}

WriteBackgroundColorControl = GrObjBackgroundColorSelector {
	hints = {
		HINT_GEN_CONTROL_DESTROY_CHILDREN_WHEN_NOT_INTERACTABLE
	}
}

WriteBackgroundAttrControl = GrObjAreaAttrControl {
	hints = {
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word	mask GOAACFeatures and not mask GOAACF_TRANSPARENCY
		}
	}
}

WriteGrObjStyleSheetControl = GrObjStyleSheetControl {
	moniker = 'S', "Graphic Style Sheets";
	visibility = popup;
	hints = {
	    ATTR_GEN_CONTROL_PROHIBIT_UI {
		word	mask SSCF_LOAD
	    }
	}
}

GradientDialog = GenInteraction {
	moniker = 'G', "Gradient Fill";
	type = properties;
	visibility = dialog;
	children = WriteStartingGradientColorControl,
		   WriteEndingGradientColorControl,
		   WriteGradientFillControl;
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbGradient", 0 }
	}
}

WriteStartingGradientColorControl = GrObjStartingGradientColorSelector {
	moniker = "Start Color";
	hints = {
		HINT_GEN_CONTROL_DESTROY_CHILDREN_WHEN_NOT_INTERACTABLE,
		HINT_DRAW_IN_BOX,
		HINT_PLACE_MONIKER_ABOVE
	}
}

WriteEndingGradientColorControl = GrObjEndingGradientColorSelector {
	moniker = "End Color";
	hints = {
			HINT_DRAW_IN_BOX,
			HINT_PLACE_MONIKER_ABOVE
	}
}

WriteGradientFillControl = GrObjGradientFillControl {
}

WriteArcControl = GrObjArcControl {
	moniker = 'A', "Arcs";
	type = properties;
	visibility = dialog;
}

#if _BITMAP_EDITING
WriteBitmapFormatControl = VisBitmapFormatControl {
	moniker = 'F', "Bitmap Format";
	visibility = dialog;
	type = properties;
}
#endif

ExtAttrDialog = GenInteraction {
	moniker = 'E', "Extended Attributes";
	children = WriteGrObjObscureAttrControl2, WriteGrObjLocksControl;
	visibility = dialog;
	type = properties;
	hints = {
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		ATTR_GEN_HELP_CONTEXT { char "dbExtAttr", 0 }
	}
}

WriteGrObjObscureAttrControl2 = GrObjObscureAttrControl {
	hints = {
		HINT_GEN_CONTROL_DESTROY_CHILDREN_WHEN_NOT_INTERACTABLE,
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word	mask GrObjObscureAttrControlFeatures and \
				not mask GOOACF_INSTRUCTIONS
		}
	}
}

WriteGrObjLocksControl = GrObjLocksControl {
	moniker = "Locks";
	hints = { HINT_DRAW_IN_BOX }
}

WriteDefaultAttributesControl = GrObjDefaultAttributesControl {
}

/***/

DuplicatePopup = GenInteraction {
	children = WriteDuplicateControl,
		   WriteMultiDuplicateControl,
		   WriteCustomDuplicateControl;
	visibility = popup;
	moniker = 'D', "Duplicate";
}

PasteInsidePopup = GenInteraction {
	moniker = 'P', "Paste Inside";
	children = WritePasteInsideControl,
		   WriteMoveInsideControl;
	visibility = popup;
}

WriteDuplicateControl = GrObjDuplicateControl {
}

WritePasteInsideControl = GrObjPasteInsideControl {
}

WriteMoveInsideControl = GrObjMoveInsideControl {
	moniker = 'N', "Nudge Inside";
	visibility = popup;
}

WriteMultiDuplicateControl = GrObjCustomDuplicateControl {
	moniker = 'M', "Multi-Duplicate";
	visibility = dialog;
	hints = {
		ATTR_GEN_HELP_CONTEXT {
			char "dbMultiDup", 0
		},
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word	mask GOCDCF_SCALE or mask GOCDCF_SKEW
		}
	}
}

WriteCustomDuplicateControl = GrObjCustomDuplicateControl {
	moniker = 'C', "Custom Duplicate";
	visibility = dialog;
}

/***/
GrOptionsPopup = GenInteraction {
	moniker = 'O', "Options";
	children = WriteHandleControl,
		   WriteDraftModeControl;
	visibility = popup;
}

WriteHandleControl = GrObjHandleControl {
	moniker = 'a', "Handles";
	visibility = popup;
}

WriteHideShowControl = GrObjHideShowControl {
	moniker = 'H', "Hide";
	visibility = popup;
}

WriteDraftModeControl = GrObjDraftModeControl {
}
end GraphicsMenuUI;
