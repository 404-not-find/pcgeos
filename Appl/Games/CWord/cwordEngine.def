COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	Copyright (c) Geoworks 1994 -- All Rights Reserved

PROJECT:	Crossword
MODULE:		Engine
FILE:		cwordEngine.def

AUTHOR:		Jennifer Lew, May 11, 1994

MACROS:
	Name			Description
	----			-----------
	EngineGetCellArrayDSSI
	EngineGetCellSegmentDS
	EngineGetClueHeaderArrayDSSI
	EngineGetClueHeaderSegmentDS

REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	JL	5/11/94   	Initial revision

DESCRIPTION:
	This file contains the Engine Module's definitions
	for the Crossword Application.
		
	$Id: cwordEngine.def,v 1.1 97/04/04 15:13:45 newdeal Exp $

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@

;--------------------------------------------------------------------------
;	Flags used for drawing a cell
;--------------------------------------------------------------------------

CellFlags	record
	CF_HINTED:1		; letter was hinted
	CF_VERIFIED:1		; user letter was verified
	CF_WRONG:1		; user letter was wrong
	CF_HOLE:1		; cell is a hole - black square
	CF_EMPTY:1		; cell has no user letter
	CF_NON_EXISTENT:1	; for cells that are not squares
	CF_NUMBER:1		; cell has a number
	:1
CellFlags	end

;--------------------------------------------------------------------------
;	Structure used to store data needed for each cell
;--------------------------------------------------------------------------

CellData	struct
	CD_solutionLetter	byte	; solution letter for cell
	CD_userLetter		byte	; letter entered by user
	CD_number		byte	; number displayed by cell
	CD_acrossClueToken	ClueTokenType	; across clue for this cell
	CD_downClueToken	ClueTokenType	; down clue for this cell
	CD_flags		CellFlags	; drawing flags
CellData	ends

;--------------------------------------------------------------------------
;	Flags used for identifying a clue's type
;--------------------------------------------------------------------------

ClueTypeFlagsType	etype	byte
	CTF_TEXT	enum	ClueTypeFlagsType, 1
	CTF_PICTURE	enum	ClueTypeFlagsType, 2
	CTF_SOUND	enum	ClueTypeFlagsType, 3

;--------------------------------------------------------------------------
;	Structure used to store a clue data token
;--------------------------------------------------------------------------

ClueDataToken	struct
	CDT_blockHandle	hptr		; block handle to clue text
	CDT_element	word		; element number to clue text
ClueDataToken	ends

;--------------------------------------------------------------------------
;	Structure used to store information needed for each clue
;--------------------------------------------------------------------------

ClueHeader	struct
	CH_cellToken		CellTokenType	; cell of first letter
						; corresponding to clue
	CH_textClueDataToken	ClueDataToken	; clue data
	CH_altClueDataToken	ClueDataToken	; alternate  clue data
	CH_altClueTypeFlags	ClueTypeFlagsType	; type of alt clue
	CH_clueNumber		byte		; number of clue
ClueHeader	ends

;--------------------------------------------------------------------------
; 	Structure to store the actual clue
;--------------------------------------------------------------------------

ClueData	struct
	CD_size	word			; size of actual data
	CD_data	label	byte		; actual data
ClueData	ends

;--------------------------------------------------------------------------
;	Structure for new CellBlockHeader
;--------------------------------------------------------------------------

CellBlockHeader	struct
	CBH_lMemBlockHeader		LMemBlockHeader
	CBH_cellArrayChunkHandle	lptr
	CBH_acrossClueHeaderBlockHandle	hptr
	CBH_downClueHeaderBlockHandle	hptr
	CBH_rows			byte
	CBH_columns			byte
CellBlockHeader	ends
	
;--------------------------------------------------------------------------
;	Structure for new ClueHeaderBlockHeader 
;--------------------------------------------------------------------------

ClueHeaderBlockHeader	struct
	CHBH_lMemBlockHeader		LMemBlockHeader
	CHBH_clueHeaderChunkHandle	lptr
	CHBH_currentClueDataBlockHandle	hptr
ClueHeaderBlockHeader	ends


;--------------------------------------------------------------------------
;	Structure for new ClueDataBlockHeader
;--------------------------------------------------------------------------

ClueDataBlockHeader	struct
	CDBH_lMemBlockHeader		LMemBlockHeader
	CDBH_clueDataChunkHandle	lptr
ClueDataBlockHeader	ends


;--------------------------------------------------------------------------
;	Constants
;--------------------------------------------------------------------------

ACROSS			equ	offset CBH_acrossClueHeaderBlockHandle
DOWN			equ	offset CBH_downClueHeaderBlockHandle

ENGINE_HOLE			equ	C_PERIOD
ENGINE_NON_EXIST		equ	C_SPACE
ENGINE_DEFAULT_ALLOC_SIZE	equ	6000
ENGINE_DEFAULT_HEAP_SIZE	equ	5500
ENGINE_GRID_EDGE		equ	-1
ENGINE_LAST_CLUE		equ	-1

ENGINE_NO_NUMBER		equ	-1
ENGINE_MAX_LENGTH_FOR_CLUE_TEXT	equ	100

ENGINE_NO_CLUE			equ	-1
ENGINE_NO_EMPTY_CELL		equ	-1


;----------------------------------------------------------------------------
;	Macro definitions
;----------------------------------------------------------------------------

COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		EngineGetCellSegmentDS
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:	Given the engine token, return the cell block segment
		in DS

CALLED BY:	engine internal routines
PASS:		dx	- engine token

RETURN:		ds	- cell block segment

DESTROYED:	nothing

SIDE EFFECTS:	cell block is LOCKED

REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	JL	5/31/94    	Initial version
	PT	6/17/94		Changed Assert lmem to Assert EngineTokenType

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@
EngineGetCellSegmentDS	macro
	push	bx, ax
	Assert	EngineTokenType	dx
	mov	bx, dx
	call	MemLock
	mov	ds, ax
	pop	bx, ax
endm


COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		EngineGetCellArrayDSSI
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:	Given the engine token, return the cell array within 
		that block (specified by the engine token).

CALLED BY:	engine internal routines
PASS:		dx	- engine token

RETURN:		*ds:si	- cell chunk array

DESTROYED:	nothing

SIDE EFFECTS:	cell block is LOCKED

REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	JL	5/25/94    	Initial version

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@
EngineGetCellArrayDSSI	macro
	EngineGetCellSegmentDS
	mov	si, ds:[CBH_cellArrayChunkHandle]	; *ds:si = array
endm


COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		EngineGetClueHeaderSegmentDS
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:	Get the clue header segment in DS

PASS:		dx	- engine token
		bx	- clue header offset into cell block header

RETURN:		bx	- clue header handle
		ds	- clue header segment

PSEUDO CODE/STRATEGY:
KNOWN BUGS/SIDE EFFECTS/IDEAS:
	clue header block is locked

REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	JL	5/31/94   	Initial version

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@
EngineGetClueHeaderSegmentDS	macro
	EngineGetCellSegmentDS
	push	ax,cx
	mov	cx, ds:[bx]
	mov	bx, dx
	Assert	lmem	bx
	call	MemUnlock
	mov	bx, cx			; clue header block handle
	call	MemLock			; lock clue header block
	mov	ds, ax			; clue header segment
	pop	ax,cx
endm


COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		EngineGetClueHeaderArrayDSSI
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:	Get the clue header chunk array in *ds:si

PASS:		dx	- engine token
		bx	- clue header offset into cell block header

RETURN:		bx 	- handle to clue header
		*ds:si 	- clue header array
PSEUDO CODE/STRATEGY:
KNOWN BUGS/SIDE EFFECTS/IDEAS:
	clue header block is locked

REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	JL	5/31/94   	Initial version

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@
EngineGetClueHeaderArrayDSSI	macro
	EngineGetClueHeaderSegmentDS
	mov	si, ds:[CHBH_clueHeaderChunkHandle]
endm



COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		EngineUnlockBlockAndCallChunkErrorBX
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:	unlock the block in bx and call for a chunk array error.

PASS:		bx	- block handle

PSEUDO CODE/STRATEGY:
KNOWN BUGS/SIDE EFFECTS/IDEAS:
REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	JL	6/ 6/94   	Initial version

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@
EngineUnlockBlockAndCallChunkErrorBX		macro
	Assert	lmem	bx
	call	MemUnlock
	mov	di, CHUNK_ARRAY_ERR
	call	CwordHandleError
	stc
endm



COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		EngineUnlockBlockAndCallChunkErrorDX
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:	unlock the block in dx and call for a chunk array error.

PASS:		dx	- block handle

PSEUDO CODE/STRATEGY:
KNOWN BUGS/SIDE EFFECTS/IDEAS:
REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	JL	6/ 6/94   	Initial version

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@
EngineUnlockBlockAndCallChunkErrorDX	macro
	xchg	bx, dx
	EngineUnlockBlockAndCallChunkErrorBX
	xchg	bx, dx
endm


COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		EngineUnlockDX
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:	unlock the block pointed to by handle dx.

PASS:		dx	- handle of block to unlock

PSEUDO CODE/STRATEGY:
KNOWN BUGS/SIDE EFFECTS/IDEAS:
REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	JL	6/ 8/94   	Initial version

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@
EngineUnlockDX		macro
	xchg	bx, dx				; dx - engine token
	Assert	lmem	bx
	call	MemUnlock
	xchg	bx, dx				; dx - engine token
endm

