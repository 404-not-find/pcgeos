/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PC GEOS
 * MODULE:	  ResEdit
 * FILE:	  uiFile.ui
 *
 * AUTHOR:  	  Cassie Hartzog, Sep 22, 1992
 *
 * REVISION HISTORY:
 *	Name	  Date	    Description
 *	----	  ----	    -----------
 *	CH	9/22/92	  Initial version
 *
 * DESCRIPTION:
 *	
 *	This file contains the user interface description for the
 *	file menu.
 *
 * 	$Id: uiFile.ui,v 1.1 97/04/04 17:13:42 newdeal Exp $
 *
 ***********************************************************************/


start FileMenuUI;

FileMenuInteraction = GenInteraction {
	children =
	        CopyrightInteraction,
		NameAndNotesInteraction,
		ResetSourcePathInteraction,
		CreateTrigger,
		CreatePatchTrigger,
		CreateNullPatchTrigger,
		UpdateInteraction;
	visibility = subGroup;
}

CreateTrigger = GenTrigger vardataReloc {
	moniker = 't', "Create a New Executable";
	action = MSG_RESEDIT_DOCUMENT_CREATE_EXECUTABLE, "TO_APP_MODEL";
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD ResEditDocumentClass },
		ATTR_GEN_TRIGGER_ACTION_DATA {
			byte CET_TRANSLATED_GEODE
			byte CEU_UPDATE_IF_NECESSARY
			byte CEN_TRANSLATED_NAME
			byte CED_DESTINATION_DIR
		}
	}
}

CreatePatchTrigger = GenTrigger vardataReloc {
	moniker = 't', "Create a Patch File";
	action = MSG_RESEDIT_DOCUMENT_CREATE_PATCH_FILE, "TO_APP_MODEL";
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD ResEditDocumentClass }
	}
}

CreateNullPatchTrigger = GenTrigger vardataReloc {
	moniker = 't', "Create a Null Executable and Patch File";
	action = MSG_RESEDIT_DOCUMENT_CREATE_NULL_PATCH_FILE, "TO_APP_MODEL";
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD ResEditDocumentClass }
	}
}


/**************************************************************************
		Update Menu
 **************************************************************************/

UpdateInteraction = GenInteraction {
	moniker = 'U', "Update";
	visibility = popup;
	children =
		UpdateReport,
		UpdateNameInteraction,
		UpdateTrigger,
		CommitTrigger;
}

UpdateNameInteraction = GenInteraction {
	moniker = 'N', "Update Geode Name";
	type = command;
	visibility = dialog;
	children =
		UpdateNameExplanation,
		UpdateNameTextEntry,
		UpdateNameTrigger;
	hints = {
		ATTR_GEN_HELP_CONTEXT {char "dbUpdateGeodeName", 0},
		HINT_MAXIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 40>
			SpecHeight <>
		}
	}
}

UpdateNameExplanation = GenText {
	text = "If name of the source geode for this translation file has changed, enter the new name here. Without the correct source geode name, update will not work.";
	genAttributes = default +readOnly;
}

UpdateNameTextEntry = GenText vardataReloc {
	moniker = "New geode name:";
	textDisplayAttributes = default +singleLineText;
	maxLength = "FILE_LONGNAME_LENGTH";
	genAttributes = default +signalInteractionComplete;
	applyMsg = MSG_RESEDIT_DOCUMENT_UPDATE_GEODE_NAME;
	destination = "TO_APP_MODEL";
	hints = {
		HINT_DEFAULT_TARGET,
		HINT_DEFAULT_FOCUS,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, FILE_LONGNAME_LENGTH>
			SpecHeight <SST_LINES_OF_TEXT, 1>
		},
		ATTR_GEN_DESTINATION_CLASS { UN_DD ResEditDocumentClass }
	}
}
localize "Text object used to change name of original geode stored in translation file.";

UpdateNameTrigger = GenTrigger vardataReloc {
	OK_TRIGGER_VALUES
	action = MSG_RESEDIT_DOCUMENT_UPDATE_GEODE_NAME, "TO_APP_MODEL";
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND { word IC_OK },
		ATTR_GEN_DESTINATION_CLASS { UN_DD ResEditDocumentClass }
	}
}

UpdateTrigger = GenTrigger vardataReloc {
	moniker = 'U', "Update Translation File";
	action = MSG_RESEDIT_DOCUMENT_UPDATE_TRANSLATION, "TO_APP_MODEL";
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD ResEditDocumentClass }
	}
}
localize "Trigger used to start the updating of a translation file.";

CommitTrigger = GenTrigger vardataReloc {
	genStates = default -enabled;
	moniker = 'C', "Commit the Update";
	action = MSG_RESEDIT_DOCUMENT_COMMIT_UPDATE, "TO_APP_MODEL";
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD ResEditDocumentClass }
	}
}
localize "Trigger which removes update info, deleted chunks, from translation file.";

UpdateReport = GenInteraction {
	moniker = "Update Results";
	visibility = dialog;
	attributes = default +notUserInitiatable;
	type = notification; 
	children = 
		ReportExplanation,
		ChangedChunks,
		NewChunks,
		DeletedChunks;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_MAXIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 30>
			SpecHeight <>
		}
	}
}
		
ReportExplanation = GenText {
	text = "The update process has completed.  The numbers below reflect the differences between the new geode and the previous version.";
	genAttributes = default +readOnly;
}

ChangedChunks = GenValue {
	moniker = "Number of chunks that have changed: ";
	value = 0;
	genAttributes = default +readOnly;
}
NewChunks = GenValue {
	moniker = "Number of new chunks: ";
	value = 0;
	genAttributes = default +readOnly;
}
DeletedChunks = GenValue {
	moniker = "Number of chunks that have been deleted: ";
	value = 0;
	genAttributes = default +readOnly;
}

/**************************************************************************
                Copyright/Screen Saver Module Name
 ***********************************************************/

CopyrightInteraction = GenInteraction {
    moniker = 'y', "Copyright/Screen Saver Module Name";
    visibility = dialog;
    attributes = modal;
    type = command;
    children = CopyrightDescText,
	       NewCopyrightText,
	       ChangeCopyrightTrigger;
}

CopyrightDescText = GenText {
    text = "If you are localizing a screen saver options module, you will want to be sure that the field below EXACTLY matches the GEOS name of the Screen Saver geode which this module is associated with.  If you localize the GEOS name of the associated screen saver module, you will want to also be sure to edit the field below for the appropriate screen saver option module.";
    genAttributes = default +readOnly;
}

NewCopyrightText = GenText {
    moniker = "Copyright/Screen Saver Name:";
    textDisplayAttributes = default +singleLineText;
    maxLength = "GFH_NOTICE_SIZE";
    hints = {
	HINT_FIXED_SIZE {
	    SpecWidth <SST_AVG_CHAR_WIDTHS, GFH_NOTICE_SIZE>
	    SpecHeight <SST_LINES_OF_TEXT, 1>
	}
    }
}

ChangeCopyrightTrigger = GenTrigger vardataReloc {
    action = MSG_RESEDIT_DOCUMENT_SET_COPYRIGHT, "TO_APP_MODEL";
    genAttributes = default +signalInteractionComplete;
    hints = {
	HINT_SEEK_REPLY_BAR,
	ATTR_GEN_TRIGGER_INTERACTION_COMMAND { word IC_OK },
	ATTR_GEN_DESTINATION_CLASS { UN_DD ResEditDocumentClass }
    }
}

/**************************************************************************
		Name & User Notes 
 **************************************************************************/

NameAndNotesInteraction = GenInteraction {
	moniker = 'm', "New Name and User Notes";
	type = command;
	visibility = dialog;
	attributes = modal;
	children =
		ResEditFileName,
		ResEditUserNotes,
		NameAndNotesOKTrigger;
	hints = {
		ATTR_GEN_HELP_CONTEXT {char "dbNameAndNotes", 0}
	}
}

ResEditFileName = GenText {
	moniker = "Geode Name:";
	textDisplayAttributes = default +singleLineText;
	maxLength = "FILE_LONGNAME_LENGTH";
	hints = {
		  HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, FILE_LONGNAME_LENGTH>
			SpecHeight <SST_LINES_OF_TEXT, 1>
		  }
	}
}

ResEditUserNotes = GenText {
	moniker = "User Notes:";
	maxLength = "GFH_USER_NOTES_LENGTH";
	hints = {
		  HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 35>
			SpecHeight <>
		  }
	}
}
localize "Text object used to change the user notes stored in the geode's file header", 20;

NameAndNotesOKTrigger = GenTrigger vardataReloc {
	action = MSG_RESEDIT_DOCUMENT_SET_NAME_AND_NOTES, "TO_APP_MODEL";
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND { word IC_OK },
		ATTR_GEN_DESTINATION_CLASS { UN_DD ResEditDocumentClass }
	}
}


/**************************************************************************
		NewFileInteraction
**************************************************************************/

NewFileInteraction = GenInteraction {
	moniker	= "Create New Translation";
	children =
		NewFileInstruction,
		NewFileSelector,
		NewFileCurrentSourceGlyph,
		NewFileCurrentSourcePath,
		NewFileOKTrigger, 
		NewFileCancelTrigger; 
	visibility = dialog;
	attributes = default +modal, +notUserInitiatable, 
		+initiatedViaUserDoDialog;
	type = notification; 
}


NewFileInstruction = GenGlyph {
	moniker	= "Select the geode you want to translate:";
}

NewFileSelector = GenFileSelector {
	fileCriteria = dirs, geosExecutables; 
	hints = {
		ATTR_GEN_PATH_DATA { GenFilePath <SP_APPLICATION,> },
		HINT_FILE_SELECTOR_NUMBER_OF_FILES_TO_SHOW {word 10}
	}
}

NewFileCurrentSourceGlyph = GenGlyph {
	moniker = "Using current source directory:";
}

NewFileCurrentSourcePath = GenGlyph {
	moniker = "Not set yet!";
}

NewFileOKTrigger = GenTrigger {
	OK_TRIGGER_VALUES
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR, 
		HINT_DEFAULT_FOCUS,
		HINT_DEFAULT_TARGET,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND { word IC_OK }
	}
}

NewFileCancelTrigger = GenTrigger {
	CANCEL_TRIGGER_VALUES
	genAttributes = default +signalInteractionComplete;
	hints = {
		HINT_SEEK_REPLY_BAR, 
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND { word IC_DISMISS }
	}
}

end FileMenuUI;

