/*
===============================================================================

   Project:	Serial/IR Comminication Protocol( IRLAP )
   Module:	Irlap Address Dialog
   File:	irlap.ui
   Date:	9/21/94
   Author:	Steve Jang

  	This file contains the user Interface for Irlap address dialog.

  	$Id: irlap.ui,v 1.1 97/04/18 11:57:00 newdeal Exp $

===============================================================================
*/

#include "generic.uih"

class IrlapAddressDialog = GenInteraction {
	client	= wordComp : 0;
	irlapStation = wordComp : 0;
	discoveryLogs = wordComp : 0;
	selection = wordComp : 0;
}

start	IrlapUI;

IrlapAddressSelector = IrlapAddressDialog {
	moniker = "IRLAP Address Selector";
	visibility = dialog;
	type = organizational;
	genStates = default -usable;
	attributes = notUserInitiatable, sysModal;
	children = IrlapAddressDialogPurpose,
		   OriginalAddress,
		   IrlapAddressList,
		   SelectButton,
		   DiscoveryButton,
		   CancelButton;
}

IrlapAddressDialogPurpose	= GenGlyph {
	moniker = "Select new address for connection";
}

OriginalAddress = GenText {
	moniker = "Original address: ";
	textDisplayAttributes = default +singleLineText;
	genAttributes = readOnly;
}

IrlapAddressList = GenDynamicList {
	moniker = "Available Addresses";
	/* a proper destination will be set later */
	destination = IrlapAddressSelector;
	applyMsg = MSG_IRLAP_SET_ADDRESS;
	queryMsg = MSG_IRLAP_GET_ADDRESS_STR;
	hints = {
		HINT_PLACE_MONIKER_ABOVE,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ITEM_GROUP_SCROLLABLE
	}
}

SelectButton = GenTrigger {
	moniker = "Select";
	genStates = default -enabled;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {word IC_YES}
	}
}

DiscoveryButton = GenTrigger {
	moniker = "Retry";
	action	= MSG_IRLAP_DO_DISCOVERY, IrlapAddressSelector;
	hints = {
		HINT_SEEK_REPLY_BAR,
	}
}

CancelButton = GenTrigger {
	moniker = "Cancel";
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {word IC_NO}
	}
}

end	IrlapUI;
