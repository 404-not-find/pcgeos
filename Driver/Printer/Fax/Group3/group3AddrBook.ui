/***********************************************************************
 *
 *	Copyright (c) Geoworks 1994.  All rights reserved.
 *	GEOWORKS CONFIDENTIAL
 *
 * PROJECT:	Pasta
 * MODULE:	Group3 Printer Driver
 * FILE:	group3AddrBook.ui
 *
 * AUTHOR:  	  Andy Chiu, Nov 16, 1993
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	AC	11/16/93   	Initial version
 *
 * DESCRIPTION:
 *	UI for objects pertaining to the address book.
 *
 *
 * 	$Id: group3AddrBook.ui,v 1.1 97/04/18 11:52:57 newdeal Exp $
 *
 ***********************************************************************/

#include "group3AddrBook.uih"

/************************************************************************/
/*									*/
/*			 UI for the address book			*/
/*									*/
/************************************************************************/
AddrBookDialog = GenInteraction {
	moniker = "Address Book";
	children = AddrBookList,
		AddrBookChangeFileTrigger,
		AddrBookFileSelectDialog;
	type = properties;
	visibility = dialog;
	attributes = default +modal, +notUserInitiatable;
	hints = {
		HINT_IF_SYSTEM_ATTRS {
			SystemAttrs mask SA_HORIZONTALLY_TINY
		},
			HINT_SIZE_WINDOW_AS_RATIO_OF_FIELD {
				SpecWinSizePair	< mask SWSS_RATIO or PCT_100, \
						  mask SWSS_RATIO or PCT_100 >
			},
		HINT_ELSE,
		HINT_ENDIF,
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_DISMISS_WHEN_DISABLED,
		ATTR_GEN_HELP_CONTEXT {
			char	"dbFaxAddrBook",0
		},
	}
}

AddrBookList = AddressBookList {
	numItems = 0;
	applyMsg = MSG_ADDRESS_BOOK_LIST_SET_CURRENT_SELECTION;
	queryMsg = MSG_ADDRESS_BOOK_LIST_REQUEST_ITEM_MONIKER;
	hints = {
                HINT_ITEM_GROUP_SCROLLABLE,
		HINT_FIXED_SIZE {
			SpecWidth  < SST_AVG_CHAR_WIDTHS, 60>
			SpecHeight < SST_LINES_OF_TEXT, 14 >
			word 14
		},
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	}
}

AddrBookChangeFileTrigger = GenTrigger {
	moniker = "Change Address Book";	
	action = MSG_GEN_INTERACTION_INITIATE, AddrBookFileSelectDialog;
	hints = {
		HINT_TRIGGER_BRINGS_UP_WINDOW,
		}
}

AddrBookFileSelectDialog = GenInteraction {
	moniker = "Open";
	children = AddrBookFileSelector,
		   OpenFileTrigger;
	type = command;
	visibility = dialog;
	attributes = default +notUserInitiatable, +modal;
}


AddrBookFileSelector =  AddressBookFileSelector {
	action = MSG_ADDRESS_BOOK_FILE_SELECTOR_GET_FILE, "TO_SELF";
	hints = {
		HINT_TOOLBOX,
		ATTR_GEN_FILE_SELECTOR_TOKEN_MATCH {
			GeodeToken < "adbk", MANUFACTURER_ID_GEOWORKS >
/*			GeodeToken < "ADDR", MANUFACTURER_ID_PALM_COMPUTING >
*/
		},
		ATTR_GEN_PATH_DATA {
			GenFilePath < SP_DOCUMENT, "" >
		},
	}
}

/*-----------------------------------------------------------------------------
			Reply-bar triggers
-----------------------------------------------------------------------------*/

OpenFileTrigger = GenTrigger {
	moniker = 'O', "Open";
	action = MSG_ADDRESS_BOOK_FILE_SELECTOR_OPEN_PRESSED, AddrBookFileSelector;


	hints = {
		HINT_SEEK_REPLY_BAR,
		HINT_DEFAULT_DEFAULT_ACTION,
	}
}
