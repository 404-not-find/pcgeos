

COMMENT }%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	Copyright (c) GeoWorks 1990 -- All Rights Reserved


PROJECT:	PC GEOS
MODULE:		common buffer routines
FILE:		bufferCreateCanonRGB.asm

AUTHOR:		Dave Hunter

ROUTINES:
	Name		Description
	----		-----------

REVISION HISTORY:
	Name	Date	Description
	----	----	-----------
	DH	5/4/00	initial version

DESCRIPTION:

	$$

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%}
COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		PrCreatePrintBuffers
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:	
	Looks at the PSTATE to find out what graphics resolution this document
is printing at.  The routine then allocates a chunk of memory for a output
buffer for the graphic print routines.

CALLED BY:	PrintSwath

PASS:		es	- pointer to locked PState

RETURN:	
	PSTATE loaded with handle and segment of buffers
DESTROYED:	
	nothing

PSEUDO CODE/STRATEGY:
	create the buffer space used by the print driver in this mode.
	The buffer is created to hold a single scanline of compacted print
	data generated by the rb2cmyk library.  The scanline will be
	PackBits-compressed bitmap data for one of the four CMYK color
	components with a bit depth of one and a pixel length equal to the
	width in inches of the band times the printer raster image resolution
	in DPI.  To allow for better than worst-case expansion, 50% extra space
	is allocated:

		BufferLen = 1.5 * ceil ( bandWidth * DPI / 8 )

KNOWN BUGS/SIDE EFFECTS/IDEAS:
		

REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	DH	5/4/2000	Initial version

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@
PrCreatePrintBuffers	proc	near
	uses	ax,bx,cx,dx
	.enter
	mov	ax, HI_RES_RASTER_X_RES	; assume hi-resolution printing
	mov	cl, es:[PS_printerType]
	andnf	cl, mask PT_COLOR
	cmp	cl, BMF_MONO
	jne	gotRes
	cmp	es:[PS_mode], PM_GRAPHICS_HI_RES
	je	gotRes
	mov	ax, LOW_RES_MONO_X_RES	; using low-resolution
gotRes:
	mov	cx, ax			; cx <- printer resolution
	mov	ax, es:[PS_customWidth]	; ax <- width of the page in pts
	sub	ax, es:[PS_currentMargins].PM_left	; remove margins
	sub	ax, es:[PS_currentMargins].PM_right	; ax <- band width in pts
	mul	cx			; dxax <- band width in pts * raster DPI
	mov	cx, 72
	div	cx			; ax <- band width in inches * raster DPI
					;       = band width in pixels
	add	ax, 7			; round up to next byte
	mov	cl, 3
	shr	ax, cl			; ax <- band width in bytes
	mov	cx, ax
	shr	cx, 1			; cx = ax / 2
	add	ax, cx			; add 50% for expansion
	mov	cx,ALLOC_DYNAMIC_NO_ERR_LOCK or mask HF_SHARABLE ;mem flags
	call	MemAlloc	;allocate a buffer in memory.
	mov	es:[PS_bufHan],bx	;store handle in PSTATE.
	mov	es:[PS_bufSeg],ax	;store the segment in PSTATE.
	.leave
	ret
PrCreatePrintBuffers	endp

